namespace Nitra
{
  syntax module LangSpec
  {
    using Identifiers;
    using Outline;
    using PrettyPrint;
    using StandardSpanClasses;
    using CStyleComments;
    using Whitespaces;

    alias Name = Identifier;

    [StartRule]
    syntax Language = "language" Name "{" LanguageBody "}";

    syntax LanguageBody
    {
      | SpanClassDefinition = "span" sm "class" sm Name sm "=" sm DefaultForegroundColor=Color ";";
      | Extention           = "extention" sm "=" sm FileExtention ";";
      | StartRule           = "start" sm "syntax" sm "module" QualifiedIdentifier sm "rule" sm QualifiedIdentifier ";";
    }

    token Color = "#" R=Hex G=Hex B=Hex;
    regex Hex = ['0'..'9', 'A'..'F', 'a'..'f'];

    regex FileExtention = "." IdentifierPartCharacters+;

    [Identifier]
    token Identifier = !Keyword IdentifierBody;

    [ExplicitSpaces]
    syntax QualifiedIdentifier = Parts=(Identifier; s "." s)+;

    regex KeywordToken = "language" | "span" | "class" | "extention" | "start" | "syntax" | "rule";

    [Keyword]
    token Keyword = Name=KeywordToken !IdentifierPartCharacters;

    extend token IgnoreToken
    {
      | [SpanClass(Comment)] SingleLineComment;
      | [SpanClass(Comment)] MultiLineComment;
    }
  }
}
