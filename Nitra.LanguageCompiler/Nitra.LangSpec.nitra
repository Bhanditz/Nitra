using Nitra.LanguageCompiler.Utils;
using Nitra.ProjectSystem;

using WpfColor = System.Windows.Media.Color;

namespace Nitra
{
  syntax module LangSpec
  {
    using Identifiers;
    using Outline;
    using PrettyPrint;
    using StandardSpanClasses;
    using CStyleComments;
    using Whitespaces;

    alias Name = Identifier;

    [StartRule]
    syntax Language = "language" Name "{" Properties=LanguageBody* "}";

    syntax LanguageBody
    {
      | SpanClassDefinition = "span" sm "class" sm Name sm "=" sm DefaultForegroundColor=Color ";";
      | Extension           = "extension" sm "=" sm FileExtension ";";
      | SyntaxModule        = "syntax" sm "module" sm Module=QualifiedIdentifier RuleOpt=(sm "start" sm "rule" sm QualifiedIdentifier)? ";";
      | Company             = "company" sm "=" sm Name=(!";" Any)* ";";
    }

    syntax Color
    {
      Value(_compilerMessages : ICompilerMessages) : WpfColor;

      | Hex=HexColor { override Value = HexColor.Value(); }
      | Named = Identifier { override Value = StringToColor(Identifier, _compilerMessages); }
    }

    token HexColor = "#" R=Hex G=Hex B=Hex
    {
      regex Hex = ['0'..'9', 'A'..'F', 'a'..'f']{2};

      Value() : WpfColor = WpfColor.FromRgb(ParseHex(GetText(R)), ParseHex(GetText(G)), ParseHex(GetText(B)));
    }

    regex FileExtension = "." IdentifierPartCharacters+;

    [Identifier]
    token Identifier = !Keyword IdentifierBody;

    [ExplicitSpaces]
    syntax QualifiedIdentifier = Parts=(Identifier; s "." s)+;

    regex KeywordToken = "language" | "span" | "class" | "extention" | "start" | "syntax" | "rule";

    [Keyword]
    token Keyword = Name=KeywordToken !IdentifierPartCharacters;

    extend token IgnoreToken
    {
      | [SpanClass(Comment)] SingleLineComment;
      | [SpanClass(Comment)] MultiLineComment;
    }
  }
}
