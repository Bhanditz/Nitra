using Nitra.LanguageCompiler.Utils;

namespace Nitra
{
  syntax module LangSpec
  {
    using Identifiers;
    using Outline;
    using PrettyPrint;
    using StandardSpanClasses;
    using CStyleComments;
    using Whitespaces;

    alias Name = Identifier;

    [StartRule]
    syntax Language = "language" Name "{" Properties=LanguageBody* "}";

    syntax LanguageBody
    {
      | SpanClassDefinition = "span" "class" Name "=" DefaultForegroundColor=Color ";";
      | Extension           = "extension" "=" FileExtension ";";
      | StartRule           = "syntax" "module" Module=QualifiedIdentifier "start" "rule" Rule=QualifiedIdentifier ";";
      | SyntaxModule        = "syntax" "module" Module=QualifiedIdentifier ";";
    }

    token Color = "#" R=Hex G=Hex B=Hex
    {
      Value() : System.Drawing.Color = System.Drawing.Color.FromArgb(ParseHex(GetText(R)), ParseHex(GetText(G)), ParseHex(GetText(B)));
    }
    regex Hex = ['0'..'9', 'A'..'F', 'a'..'f']{2};

    regex FileExtension = "." IdentifierPartCharacters+;

    [Identifier]
    token Identifier = !Keyword IdentifierBody;

    [ExplicitSpaces]
    syntax QualifiedIdentifier = Parts=(Identifier; s "." s)+;

    regex KeywordToken = "language" | "span" | "class" | "extention" | "start" | "syntax" | "rule";

    [Keyword]
    token Keyword = Name=KeywordToken !IdentifierPartCharacters;

    extend token IgnoreToken
    {
      | [SpanClass(Comment)] SingleLineComment;
      | [SpanClass(Comment)] MultiLineComment;
    }
  }
}
