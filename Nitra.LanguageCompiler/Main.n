using System.Text;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra;
using Nitra.Declarations;
using Nitra.LanguageCompiler.Utils;

using System;
using System.Collections.Generic;
using System.Console;
using System.Diagnostics;
using System.IO;
using System.Linq;
using System.Reflection;

namespace Nitra.LanguageCompiler
{
  module Program
  {
    Main() : void
    {
      Start();
      _ = ReadLine();
    }
    
    Start() : void
    {
      def config = CommandPromptReader();
      
      unless (config.Success)
        return;
        
      def langSpecFile = config.LangSpecFile;
      def spec         = File.ReadAllText(langSpecFile);
    
      def source      = SourceSnapshot(spec);
      def lang = Parse(source, langSpecFile);
      when (lang == null)
        return;

      def (success, _stratRuleDescriptor) = Check(source, lang, config);
      
      unless (success)
        return;
        
      foreach (spanClasse in lang.SpanClasses)
        WriteLine("span class " + spanClasse.Name + " = " + ColorToString(spanClasse.DefaultForegroundColor.Value));
      
      foreach (m in lang.SyntaxModules)
        WriteLine("module: " + m.Module + if (m.RuleOpt.HasValue) " rule: " + m.RuleOpt.Value else "");
        
      GenerateSpanClasses(lang, config);
      
      when (config.GenerateProject)
        GenerateProject(lang, config);
        
      WriteLine("Output Path: " + config.OutputPath);
    }
    
    GenerateProject(lang : Language, config : CommandPromptReader) : void
    {
      def name = lang.Name.ToString();
      def prjName = name + "VsPackage";
      ExpandTemplates(@"Templates\XxxVsPackage",  Path.Combine(config.OutputPath, prjName), 
          [("XXNamespaceXX",    config.DefaultNamespace),
           ("Xxx",              name),
           ],
          config);
          
      def snkPath = Path.Combine(config.OutputPath, prjName, "Key.snk");
      unless (File.Exists(snkPath))
        CreateSnk(snkPath);
    }
    
    GenerateSpanClasses(lang : Language, config : CommandPromptReader) : void
    {
      def prjName = lang.Name + "VsPackage";

      foreach (spanClasse in lang.SpanClasses)
      {
        def color = spanClasse.DefaultForegroundColor.Value;
        def name = spanClasse.Name.ToString();
        ExpandTemplate(@"Templates\Highlighting\ClassificationDefinition.cs",  $@"$prjName\Highlighting\$(name)ClassificationDefinition.Generated.cs", 
          [("XXNamespaceXX",    config.DefaultNamespace),
           ("XXDisplay nameXX", name),
           ("Xxx",              name),
           ("Colors.Red",       ColorToString(color, "Colors.")),
           ("Red",              ColorToString(color)),
           ],
          config);
      }
    }
    
    Parse(source : SourceSnapshot, langSpecFile : string) : Language
    {
      def parserHost  = ParserHost();
      def parseResult = LangSpec.Language(source, parserHost);
      unless (parseResult.IsSuccess)
      {
        foreach(error in parseResult.GetErrors())
        {
          def (line, col) = error.Location.StartLineColumn;
          Debug.WriteLine($<#$langSpecFile($line:$col): error: $(error.Message)#>);
        }
        return null;
      }
    
      def parseTree = LangSpecParseTree.Language.CreateParseTree(parseResult);
      def ast       = DeclarationRoot.Create(null, parseTree);
      ast.EvalProperties();
      ast.Content
    }
    
    Check(source : SourceSnapshot, lang : Language, config : CommandPromptReader) : bool * RuleDescriptor
    {
      mutable success = true;
      def error(msg : string) : void { success = false; WriteLine(msg); }
      def langSpecFile = config.LangSpecFile;
      def descriptors  = config.ModuleDescriptors;
      def modules = HashSet();
      def stratRules = List();;
      mutable stratRuleDescriptor;

      def processModule(mod : QualifiedIdentifier, ruleOpt : QualifiedIdentifier.IAstOption) : void
      {
        def moduleFullName = mod.ToString();
        mutable descriptor;
        if (descriptors.TryGetValue(moduleFullName, out descriptor))
        {
          unless (modules.Add(moduleFullName))
            error($<#$langSpecFile($(Location(source, mod.Span).StartLineColumn)): error: Syntax module '$moduleFullName' defined twice.#>);
        
          when (ruleOpt.HasValue)
          {
            def ruleNameOpt = ruleOpt.Value.ToString();
            if (descriptor.Rules.Find(x => x.Name == ruleNameOpt) is Some(rule))
            {
              stratRules.Add(ruleOpt.Value);
              stratRuleDescriptor = rule;
            }
            else
              error($<#$langSpecFile($(Location(source, ruleOpt.Span).StartLineColumn)): error: Rule '$ruleNameOpt' not found in '$moduleFullName' syntax module.#>);
          }
        }
        else
          error($<#$langSpecFile($(Location(source, mod.Span).StartLineColumn)): error: Syntax module '$moduleFullName' not found.#>);
      }
      
      foreach (m in lang.SyntaxModules)
        processModule(m.Module, m.RuleOpt);
      
      if (stratRules.Count == 0)
        error($<#$langSpecFile($(Location(source, lang.Span).StartLineColumn)): error: No start rule defined.#>);
      else when (stratRules.Count > 1)
      {
        def loc1 = Location(source, stratRules[1].Span);
        def loc2 = Location(source, stratRules[0].Span);
        error($<#$langSpecFile($(loc1.StartLineColumn)): error: Start rule defined twice.#>);
        error($<#$langSpecFile($(loc2.StartLineColumn)): warning: Previous defined.#>);
      }
    
      //mutable stratRuleDescriptor;
        
      (success, stratRuleDescriptor)
    }
 
    ExpandTemplates(templateRootPath : string, outputPath : string, vars : Seq[string * string], config : CommandPromptReader) : void
    {
      unless (Directory.Exists(outputPath))
      {
        _ = Directory.CreateDirectory(outputPath);
        assert(true);
      }
        
      foreach (file in Directory.EnumerateFiles(templateRootPath))
      {
        def fileName = Path.GetFileName(file);
        def outFile = Path.Combine(outputPath, ExpandTemplateText(fileName, vars));
        ExpandTemplate(file, outFile, vars, config);
      }

      foreach (dir in Directory.EnumerateDirectories(templateRootPath))
      {
        def dirName = Path.GetFileName(dir);
        def outDir = Path.Combine(outputPath, ExpandTemplateText(dirName, vars));
        ExpandTemplates(Path.Combine(templateRootPath, dirName), outDir, vars, config);
      }
    }
    
    ExpandTemplateText(templateText : string, vars : Seq[string * string]) : string
    {
      def templateBuilder = StringBuilder(templateText, templateText.Length + templateText.Length / 10);
      
      foreach ((name, value) in vars)
        _ = templateBuilder.Replace(name, value);
      
      def result = templateBuilder.ToString();
      result
    }
    
    ExpandTemplate(templatePath : string, outputPath : string, vars : Seq[string * string], config : CommandPromptReader) : void
    {
      def outputPath2     = Path.GetFullPath(Path.Combine(config.OutputPath, outputPath));
      def outputDir       = Path.GetDirectoryName(outputPath2);

      unless (Directory.Exists(outputDir))
        _ = Directory.CreateDirectory(outputDir);
      
      unless (IsUtf8File(templatePath))
      {
        File.Copy(templatePath, outputPath2, overwrite=true);
        return;
      }
      
      def templateText    = File.ReadAllText(templatePath, Encoding.UTF8);
      def result          = ExpandTemplateText(templateText, vars);

        
      File.WriteAllText(outputPath2, result, Encoding.UTF8);
      
      when (result.Contains("Xxx") || result.Contains("XX"))
        WriteLine($<#$templatePath: error: The template contains unreplaced placeholders.#>);
    }
  }
}

namespace Nitra
{
  public abstract partial class QualifiedIdentifier : AstBase[IAst]
  {
    public sealed partial class Concrete : QualifiedIdentifier
    {
      public override ToString() : string
      {
        $<#..$(this.Parts; ".")#>
      }
    }
  }
}