using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra;
using Nitra.Declarations;
using Nitra.LanguageCompiler.Utils;

using System;
using System.Collections.Generic;
using System.Console;
using System.Diagnostics;
using System.IO;
using System.Linq;
using System.Reflection;

module Program
{
  Main(args : array[string]) : void
  {
    when (args.Length < 1 || !Path.GetExtension(args[0]).Equals(".nlang", StringComparison.InvariantCulture))
    {
      WriteLine("Usage: Nitra.LanguageCompiler.exe NitraLanguageSpecFile.nlang (NitraParserAssebly.dll)*");
      return;
    }
    
    def langSpecFile = Path.GetFullPath(args[0]);
    
    when (!File.Exists(langSpecFile))
    {
      WriteLine($"The '$langSpecFile' file does not exists.");
      return;
    }
    
    def asms = List();
    
    for (mutable i = 1; i < args.Length; i++)
    {
      def asmRef = Path.GetFullPath(args[i]);
      when (!File.Exists(asmRef))
      {
        WriteLine($"The '$asmRef' file does not exists.");
        return;
      }
      
      def asm = Assembly.LoadFrom(asmRef);
      asms.Add(asm);
    }
    
    def descriptors = Dictionary();
    foreach (asm in  asms)
    {
      def asmDescriptors = Nitra.GrammarDescriptor.GetDescriptors(asm);
      foreach (asmDescriptor in asmDescriptors)
        when (!asmDescriptor.IsSplicable)
          descriptors.Add(asmDescriptor.FullName, asmDescriptor);
    }
    
    def spec = File.ReadAllText(langSpecFile);
    
    def source      = SourceSnapshot(spec);
    def parserHost  = ParserHost();
    def parseResult = LangSpec.Language(source, parserHost);
    unless (parseResult.IsSuccess)
    {
      foreach(error in parseResult.GetErrors())
      {
        def (line, col) = error.Location.StartLineColumn;
        Debug.WriteLine($<#$langSpecFile($line:$col): error: $(error.Message)#>);
      }
      return;
    }
    
    def parseTree = LangSpecParseTree.Language.CreateParseTree(parseResult);
    def ast       = DeclarationRoot.Create(null, parseTree);
    ast.EvalProperties();
    def lang = ast.Content;
    WriteLine("Lang: " + lang.Name);
    def processModule(mod : QualifiedIdentifier, ruleOpt : QualifiedIdentifier) : void
    {
      def ruleNameOpt = ruleOpt?.ToString();
      def moduleFullName = mod.ToString();
      mutable descriptor;
      if (descriptors.TryGetValue(moduleFullName, out descriptor))
      {
        if (ruleNameOpt == null)
          WriteLine("module: " + moduleFullName);
        else if (descriptor.Rules.Find(x => x.Name == ruleNameOpt) is Some(rule))
          WriteLine("module: " + moduleFullName + " rule: " + rule.Name);
        else
          WriteLine($<#$langSpecFile($(Location(source, ruleOpt.Span).StartLineColumn)): error: Rule '$ruleNameOpt' not found in '$moduleFullName' syntax module.#>);
      }
      else
        WriteLine($<#$langSpecFile($(Location(source, mod.Span).StartLineColumn)): error: Syntax module '$moduleFullName' not found.#>);
    }
    
    match (lang.StartRules)
    {
      | x1 :: x2 :: _ => 
        def loc1 = Location(source, x1.Span);
        def loc2 = Location(source, x2.Span);
        WriteLine($<#$langSpecFile($(loc1.StartLineColumn)): error: Start rule defined twice.#>);
        WriteLine($<#$langSpecFile($(loc2.StartLineColumn)): warning: Previous defined.#>);
        
      | [] => 
        def loc = Location(source, lang.Span);
        WriteLine($<#$langSpecFile($(loc.StartLineColumn)): error: No start rule defined.#>);
        
      | _ =>
        foreach (startRule in lang.StartRules)
          processModule(startRule.Module, startRule.Rule);
    }

    foreach (m in lang.SyntaxModules)
      processModule(m.Module, null);
      
    foreach (spanClasse in lang.SpanClasses)
      WriteLine("span class " + spanClasse.Name + " = " + spanClasse.DefaultForegroundColor);
      
    _ = ReadLine();
  }
}

namespace Nitra
{
  public abstract partial class QualifiedIdentifier : AstBase[IAst]
  {
    public sealed partial class Concrete : QualifiedIdentifier
    {
      public override ToString() : string
      {
        $<#..$(this.Parts; ".")#>
      }
    }
  }
}