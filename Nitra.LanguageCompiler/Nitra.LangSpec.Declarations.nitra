using Nitra;
using Nitra.Declarations;
using Nitra.Runtime.Binding;

using Color = System.Drawing.Color;

namespace Nitra
{
  declaration QualifiedIdentifier
  {
    Parts : Reference*;
  }

  declaration Language
  {
    out FileExtentions : list<string> = Properties.FileExtentionsOut;

    Properties.FileExtentionsIn = [];

    Name : Reference;
    Properties : LanguageBody*;
  }

  declarations LanguageBody
  {
    inout FileExtentions : list<string>;

    | SpanClassDefinition { Name : Reference; DefaultForegroundColor : Color; }
    | Extention
      {
        FileExtentionsOut = FileExtention :: FileExtentionsIn;

        FileExtention : string;
      }
    | StartRule           { Module  : QualifiedIdentifier; Rule : QualifiedIdentifier; }
    | SyntaxModule        { Module  : QualifiedIdentifier; }
  }

  map syntax LangSpec.QualifiedIdentifier -> QualifiedIdentifier
  {
    Parts.Item1 -> Parts;
  }

  map syntax LangSpec.Language -> Language
  {
    Name       -> Name;
    Properties -> Properties;
  }

  map syntax LangSpec.LanguageBody -> LanguageBody
  {
    | SpanClassDefinition { Name -> Name; DefaultForegroundColor = DefaultForegroundColor.Value(); }
    | Extention           { FileExtention = GetText(FileExtention); }
    | StartRule           { Module -> Module; Rule -> Rule; }
    | SyntaxModule        { Module -> Module;  }
  }
}
