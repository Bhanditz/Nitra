using Nitra;
using Nitra.Declarations;
using Nitra.Runtime.Binding;

using Color = System.Drawing.Color;

namespace Nitra
{
  declaration QualifiedIdentifier
  {
    Parts : Reference*;
  }

  declaration Language
  {
    Name : Reference;
    Properties : LanguageBody*;
  }

  declarations LanguageBody
  {
    | SpanClassDefinition { Name : Reference; DefaultForegroundColor : Color; }
    | Extention           { FileExtention : string; }
    | StartRule           { SyntaxModule  : QualifiedIdentifier; Rule : QualifiedIdentifier; }
  }

  map syntax LangSpec.QualifiedIdentifier -> QualifiedIdentifier
  {
    Parts.Item1 -> Parts;
  }

  map syntax LangSpec.Language -> Language
  {
    Name       -> Name;
    Properties -> Properties;
  }

  map syntax LangSpec.LanguageBody -> LanguageBody
  {
    | SpanClassDefinition { Name -> Name; DefaultForegroundColor = DefaultForegroundColor.Value(); }
    | Extention           { FileExtention = GetText(FileExtention); }
    | StartRule           { SyntaxModule -> SyntaxModule; Rule -> Rule; }
  }
}
