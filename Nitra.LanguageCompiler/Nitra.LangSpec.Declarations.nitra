using Nitra;
using Nitra.Declarations;
using Nitra.LanguageCompiler.Utils;
using Nitra.Runtime.Binding;

using System.Collections.Generic;

using Color = System.Windows.Media.Color;

namespace Nitra
{
  declaration QualifiedIdentifier
  {
    Parts : Reference*;
  }

  declaration Language
  {
    Properties.StylesIn   = Dictionary();
    Properties.StyleScope = Properties.StylesOut;

    Name       : Reference;
    Properties : LanguageProperty*;

    decompose Properties
    {
      Company                   : LanguageProperty.Company;
      FileExtensions            : LanguageProperty.FileExtension+;
      SpanClassesInlines        : LanguageProperty.SpanClassDefinitionInline*;
      SpanClassDefinitionStyles : LanguageProperty.SpanClassDefinitionStyle*;
      StartSyntaxModule         : LanguageProperty.StartSyntaxModule;
      AdditionalSyntaxModules   : LanguageProperty.SyntaxModule*;
      Styles                    : LanguageProperty.Style*;
    }
  }

  declarations Format
  {
    | ForegroundColor { Color : Color; }
  }

  map syntax LangSpec.Format -> Format
  {
    | ForegroundColor { Color = ParsedValue(DefaultForegroundColor.Location.Span, DefaultForegroundColor.Value(IAst.AstContext.CompilerMessages)); }
  }

  declarations LanguageProperty
  {
    inout Styles     : Dictionary<int, LanguageProperty.Style>;
    in    StyleScope : Dictionary<int, LanguageProperty.Style>;

    | SpanClassDefinitionInline
      {
        Name    : Reference;
        Formats : Format*;

        decompose Formats
        {
          ForegroundColor : Format.ForegroundColor;
        }
      }

    | SpanClassDefinitionStyle
      {
        out StyleDef : LanguageProperty.Style = Lookup(StyleScope, StyleRef, IAst.AstContext.CompilerMessages);

        Name     : Reference;
        StyleRef : Reference;
      }

    | SyntaxModule
      {
        Module  : QualifiedIdentifier;
      }

    | StartSyntaxModule
      {
        Module    : QualifiedIdentifier;
        StartRule : QualifiedIdentifier;
      }

    | FileExtension
      {
        Ext : string;
      }

    | Company
      {
        Name  : string;
      }

    | Style
      {
        StylesOut = Add(StylesIn, this, IAst.AstContext.CompilerMessages);

        Name    : Reference;
        Formats : Format*;

        decompose Formats
        {
          ForegroundColor : Format.ForegroundColor;
        }
      }
  }

  map syntax LangSpec.QualifiedIdentifier -> QualifiedIdentifier
  {
    Parts.Item1 -> Parts;
  }

  map syntax LangSpec.Language -> Language
  {
    Name       -> Name;
    Properties -> Properties;
  }

  map syntax LangSpec.LanguageProperty -> LanguageProperty
  {
    | Style                     { Name -> Name; Formats -> Formats; }
    | SpanClassDefinitionStyle  { Name -> Name; Style   -> StyleRef; }
    | SpanClassDefinitionInline { Name -> Name; Formats -> Formats; }
    | Company                   { Name = ParsedValue(Name, GetText(Name)); }
    | FileExtension             { Ext = ParsedValue(Ext, GetText(Ext)); }
    | SyntaxModule(_, _, moduleName, None, _)                    -> SyntaxModule      { moduleName -> Module; }
    | SyntaxModule(_, _, moduleName, Some((_, _, startRule)), _) -> StartSyntaxModule { moduleName -> Module; startRule -> StartRule; }
  }
}
