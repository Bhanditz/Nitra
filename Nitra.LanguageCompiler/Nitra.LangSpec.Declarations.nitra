using Nitra;
using Nitra.Declarations;
using Nitra.Runtime.Binding;

using Color = System.Windows.Media.Color;

namespace Nitra
{
  declaration QualifiedIdentifier
  {
    Parts : Reference*;
  }

  declaration Language
  {
    out FileExtensions : list<ParsedValue<string>>              = Properties.FileExtensionsOut;
    out SpanClasses    : list<LanguageBody.SpanClassDefinition> = Properties.SpanClassesOut;
    out SyntaxModules  : list<LanguageBody.SyntaxModule>        = Properties.SyntaxModulesOut;
    out Companies      : list<ParsedValue<string>>              = Properties.CompaniesOut;

    Properties.FileExtensionsIn = [];
    Properties.SpanClassesIn    = [];
    Properties.SyntaxModulesIn  = [];
    Properties.CompaniesIn      = [];

    Name       : Reference;
    Properties : LanguageBody*;
  }

  declarations LanguageBody
  {
    inout FileExtensions : list<ParsedValue<string>>;
    inout SpanClasses    : list<LanguageBody.SpanClassDefinition>;
    inout SyntaxModules  : list<LanguageBody.SyntaxModule>;
    inout Companies      : list<ParsedValue<string>>;

    | SpanClassDefinition
      {
        SpanClassesOut = this :: SpanClassesIn;

        Name                   : Reference;
        DefaultForegroundColor : ParsedValue<Color>;
      }

    | SyntaxModule
      {
        SyntaxModulesOut = this :: SyntaxModulesIn;

        Module  : QualifiedIdentifier;
        RuleOpt : QualifiedIdentifier?;
      }

    | Extension
      {
        FileExtensionsOut = FileExtension :: FileExtensionsIn;

        FileExtension : ParsedValue<string>;
      }

    | Company
      {
        CompaniesOut = Name :: CompaniesIn;

        Name  : ParsedValue<string>;
      }
  }

  map syntax LangSpec.QualifiedIdentifier -> QualifiedIdentifier
  {
    Parts.Item1 -> Parts;
  }

  map syntax LangSpec.Language -> Language
  {
    Name       -> Name;
    Properties -> Properties;
  }

  map syntax LangSpec.LanguageBody -> LanguageBody
  {
    | SpanClassDefinition { Name -> Name; DefaultForegroundColor = ParsedValue(DefaultForegroundColor.Location.Span, DefaultForegroundColor.Value(context)); }
    | Extension           { FileExtension = ParsedValue(FileExtension, GetText(FileExtension)); }
    | SyntaxModule        { Module -> Module; RuleOpt -> RuleOpt; }
    | Company             { Name = ParsedValue(Name, GetText(Name)); }
  }
}
