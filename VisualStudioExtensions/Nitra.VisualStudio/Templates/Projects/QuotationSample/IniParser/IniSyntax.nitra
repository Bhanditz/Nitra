[Splicable]
syntax module IniSyntax
{
  using PrettyPrint;
  using Outline;
  using StandardSpanClasses;
  using Whitespaces;

  token StandardSingleLineComment = ';' (!NewLine Any)* NewLine?;
  token UnixSingleLineComment     = '#' (!NewLine Any)* NewLine?;

  extend token IgnoreToken
  {
    | [SpanClass(Comment)] StandardSingleLineComment;
    | [SpanClass(Comment)] UnixSingleLineComment;
  }

  syntax Section = outline_begin '[' SectionName ']' nl Variable* outline_end_before;

  token SectionName = (!']' Any)*;

  syntax Variable = VariableName '=' VariableValue nl;

  token VariableName = (!'=' !NewLine !'[' Any)*;

  token VariableValue = (!NewLine !';' !'#' Any)*;

  [StartRule]
  syntax Start = Section*;
}
