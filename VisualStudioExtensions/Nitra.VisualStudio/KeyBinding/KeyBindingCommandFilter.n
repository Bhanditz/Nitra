using Microsoft.VisualStudio.Text;
using Nitra.Runtime.Highlighting;
using Nitra.VisualStudio.Parsing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using System.Runtime.InteropServices;

using Microsoft.VisualStudio.OLE.Interop;
using Microsoft.VisualStudio;
using Microsoft.VisualStudio.Text.Editor;

namespace Nitra.VisualStudio.KeyBinding
{
  internal class KeyBindingCommandFilter : IOleCommandTarget
  {
    private _textView : IWpfTextView;
    internal mutable m_nextTarget : IOleCommandTarget;
    internal mutable m_added : bool;
    private _parseAgent : ParseAgent;

    public this(textView : IWpfTextView, textDocumentFactoryService : ITextDocumentFactoryService)
    {
      _textView = textView;
      _parseAgent = Utils.TryGetOrCreateParseAgent(textView.TextBuffer, textDocumentFactoryService, NitraPackage.Instance.Language);
    }

    public QueryStatus(pguidCmdGroup : ref Guid, cCmds : uint, prgCmds : array[OLECMD], pCmdText : IntPtr) : int
    {
      m_nextTarget.QueryStatus(ref pguidCmdGroup, cCmds, prgCmds, pCmdText)
    }

    public Exec(pguidCmdGroup : ref Guid, nCmdID : uint, nCmdexecopt : uint, pvaIn : IntPtr, pvaOut : IntPtr) : int
    {
      when (pguidCmdGroup == VSConstants.VSStd2K)
      {
        match (nCmdID :> VSConstants.VSStd2KCmdID)
        {
          | VSConstants.VSStd2KCmdID.GOTOBRACE => OnGoToBrace(); return VSConstants.S_OK;
          | _ => assert2(true);
        }

      }

      //mutable typedChar = char.MinValue;
      //when (pguidCmdGroup == VSConstants.VSStd2K && nCmdID == VSConstants.VSStd2KCmdID.TYPECHAR :> uint)
      //{
      //  typedChar = Marshal.GetObjectForNativeVariant(pvaIn) :> char;
      //  when (typedChar.Equals('+'))
      //  {
      //  }
      //}

      m_nextTarget.Exec(ref pguidCmdGroup, nCmdID, nCmdexecopt, pvaIn, pvaOut);
    }

    OnGoToBrace() : void
    {
      when (_parseAgent.LastResult == null)
        return;

      def walker = MatchBracketsWalker();
      def pos = _textView.Caret.Position.BufferPosition.Position;
      def context = MatchBracketsWalker.Context(_textView.Caret.Position.BufferPosition.Position);
      _ = walker.Walk(_parseAgent.LastResult, context);
      foreach (brackets in context.Brackets)
      {
        def newPos =
          if (brackets.CloseBracket.IntersectsWith(pos))
            brackets.OpenBracket.StartPos
          else if (brackets.OpenBracket.IntersectsWith(pos))
            brackets.CloseBracket.StartPos
          else
            pos;

        def point = SnapshotPoint(_textView.TextBuffer.CurrentSnapshot, newPos);
        _ = _textView.Caret.MoveTo(point);
        _textView.Caret.EnsureVisible();
        break;
      }
    }
  }
}
