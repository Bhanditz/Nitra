using Nemerle;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;

using Nitra.Internal.Recovery;
using Nitra.Runtime.Reflection;

namespace Nitra.Internal
{
  [Record]
  internal struct RecoveryModeSequenceParseTreeReaderContext[TLoader]
    where TLoader : IParseTree
  {
    public Loader     : ISequenceParseTreeLoader[TLoader];
    public SequenceId : int;
  }

  internal sealed class RecoveryModeSequenceParseTreeReader[TLoader] : RecoveryModeParseTreeReader[RecoveryModeSequenceParseTreeReaderContext[TLoader], TLoader]
    where TLoader : IParseTree
  {
    public static Instance : this;

    static this() { Instance = RecoveryModeSequenceParseTreeReader() }

    protected override MakeEmptyParseTree(parseResult : ParseResult, pos : int, context : RecoveryModeSequenceParseTreeReaderContext[TLoader]) : TLoader
    {
      _ = parseResult;
      context.Loader.ReadEmptySequence(context.SequenceId, pos)
    }

    protected override MakeAmbiguousParseTree(ambiguities : array[TLoader], context : RecoveryModeSequenceParseTreeReaderContext[TLoader]) : TLoader
    {
      context.Loader.CreateAmbiguousSequence(context.SequenceId, ambiguities)
    }

    protected override ReadParseTree(parseResult : ParseResult, startPos : int, endPos : int, recoveredSubruleIndex : int, context : RecoveryModeSequenceParseTreeReaderContext[TLoader]) : TLoader
    {
      _ = parseResult; _ = startPos; _ = endPos;
      context.Loader.ReadSequence(context.SequenceId, recoveredSubruleIndex);
    }

    protected override ReadParseTreeInStandardMode(parseResult : ParseResult, startPos : int, endPos : int, context : RecoveryModeSequenceParseTreeReaderContext[TLoader]) : TLoader
    {
      _ = parseResult;
      mutable pos = startPos;
      def result = context.Loader.ReadSequence(context.SequenceId, ref pos);
      assert(pos == endPos);
      result
    }
  }
}
