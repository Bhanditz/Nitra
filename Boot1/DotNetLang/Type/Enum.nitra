using Nemerle.Collections;

using Nitra;
using Nitra.Declarations;
using Nitra.Internal;

namespace DotNet
{
  abstract declaration Enum : DeclaredType, Container
  {
    symbol
    {
      Kind      = "enum";
      SpanClass = DotNetLang.TypeSpanClass;
      in UnderlyingType : TypeSymbol;
    }

    Members.DeclaredIn = Symbol;
    Members.Scope      = Scope.HideWith(Symbol.MemberTable);
    EnumBase.Scope     = Scope;

    EnumBase  : QualifiedReference?;
    Members   : EnumMember*;
  }

  declaration TopEnum : Enum, TopType
  {
    symbol
    {
      IsSameIdentity = AstUtils.IsSameIdentity(0, candidate);
      CanMerge       = candidate is TopEnum;
    }

    ContainingTable = DeclaredIn.MemberTable;
  }

  declaration NestedEnum : Enum, NestedType
  {
    symbol
    {
      IsSameIdentity = AstUtils.IsSameIdentity(0, candidate);
      CanMerge       = candidate is NestedEnum;
    }

    ContainingTable = DeclaredIn.MemberTable;
  }
}
