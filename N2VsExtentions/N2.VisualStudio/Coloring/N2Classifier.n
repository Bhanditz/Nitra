using N2;
using N2.Internal;
using N2.Runtime;
using N2.VisualStudio.Parsing;

using Nemerle;
using Nemerle.Late;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using Microsoft.VisualStudio.Text;
using Microsoft.VisualStudio.Text.Classification;

using System;
using System.Collections.Generic;
using System.Diagnostics;
using System.Linq;
using System.Text;

namespace N2.VisualStudio.Coloring
{
  /// <summary>
  /// Implements the coloring classification.
  /// </summary>
  internal sealed class N2Classifier : IClassifier
  {
    private N2GlobalConfig              : IN2GlobalConfig { get; }
    public  Language                    : Language        { get _parseAgent.Language }
    private _parseAgent                 : ParseAgent;
    private _buffer                     : ITextBuffer;
    private _classificationTypeRegistry : IClassificationTypeRegistryService;
    private _classificationCache        : ClassificationCache;

    public this(n2GlobalConfig : IN2GlobalConfig, parseAgent : ParseAgent, buffer : ITextBuffer, classificationTypeRegistry : IClassificationTypeRegistryService)
    {
      N2GlobalConfig              = n2GlobalConfig;
      _parseAgent                 = parseAgent;
      _buffer                     = buffer;
      _classificationTypeRegistry = classificationTypeRegistry;
      _classificationCache        = ClassificationCache(100);
      buffer.Changed += 
        fun(_, args)
        {
          _classificationCache.Predict(args.After, args.Changes);
        };
      parseAgent.ParseComplete +=
        fun(_, args)
        {
          match (_classificationCache.Update(args.Result))
          {
            | Nothing => ()
            | All     => OnClassificationChanged(SnapshotSpan(buffer.CurrentSnapshot, 0, buffer.CurrentSnapshot.Length));
            | Ranges(rangesToUpdate) =>
              foreach(range in rangesToUpdate)
                OnClassificationChanged(SnapshotSpan(buffer.CurrentSnapshot, range.StartPos, range.Length));
          }
        };
    }

    #region IClassifier Members

    // Use this event if a text change causes classifications on a line other the one on which the line occurred.
    public event ClassificationChanged : EventHandler[ClassificationChangedEventArgs];

    private OnClassificationChanged(span : SnapshotSpan) : void
    {
      this.ClassificationChanged?.Invoke(this, ClassificationChangedEventArgs(span));
    }

    //This is the main method of the classifier. It should return one ClassificationSpan per group that needs coloring.
    //It will be called with a span that spans a single line where the edit has been made (or multiple times in paste operations).
    public GetClassificationSpans(span : SnapshotSpan) : IList[ClassificationSpan]
    {
#if DEBUG
      // Debug staff
      def (startLine, startCol) = span.Snapshot.GetLineAndColumn(span.Start);
      def (endLine,   endCol)   = span.Snapshot.GetLineAndColumn(span.End);
      def text                  = span.GetText().TrimEnd('\r', '\n');
      //Diagnostics.Trace.WriteLine($"Classification ($startLine, $startCol, $endLine, $endCol): '$text'");
      ignore(text, startLine, startCol, endLine, endCol);
#endif

      def classificationSpans = List.[ClassificationSpan]();

      def classifier = N2GlobalConfig.ClassificationTypeRegistryService;
      def spanInfos  = _classificationCache.GetSpans(span, _parseAgent.LastResult);
      def start      = span.Start : int;
      def end        = span.End   : int;
      foreach (spanInfo in spanInfos)
      {
        def loc = spanInfo.Location;

        when (loc.StartPos >= end) break;
        when (loc.EndPos < start)  continue;

        def currentSpn = span.Overlap(Span(loc.StartPos, loc.EndPos - loc.StartPos));

        when (currentSpn.HasValue)
        {
          when (spanInfo.SpanClass == SpanClass.Default : object)
            continue;

          def classificationType = classifier.GetClassificationType(spanInfo.SpanClass.Name);

          if (classificationType != null)
          {
            def classificationSpan = ClassificationSpan(currentSpn.Value, classificationType);
            classificationSpans.Add(classificationSpan);
          }
          else
          {
          }
        }
      }


      //Create a parser to parse the regular expression, and return the classification spans defined by it.
      //foreach (nSpan in NSpans)
      //{
      //  classificationSpans.Add(ClassificationSpan(SnapshotSpan(span.Snapshot, nSpan.Start, nSpan.End - nSpan.Start), _classificationTypeRegistry.GetClassificationType(spanClassName)));
      //}

      classificationSpans
    }

    #endregion
  }
}
