using N2;
using N2.Internal;
using N2.Runtime;

using Nemerle;
using Nemerle.Imperative;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

using Microsoft.VisualStudio.Text;
using Microsoft.VisualStudio.Text.Classification;

namespace N2.VisualStudio.Coloring
{
  internal sealed class ClassificationCache
  {
    private         _linesToCache : int;
    private mutable _cachedSpan   : NToken;
    private         _cacheIndex   : array[int * int]; // array of tuple (StartPos, Index in _spanCache)
    private mutable _cache        : List[SpanInfo];

    public this(linesToCache : int)
      requires linesToCache > 0
    {
      _linesToCache = linesToCache;
      _cachedSpan   = NToken();
      _cacheIndex   = array(10);
      _cache        = List(linesToCache * 4);
    }

    public Predict([NotNull] changes : INormalizedTextChangeCollection) : void
    {
      def predictedCache = List(_cache.Count);
      def copySpan(span, delta)
      {
        def newStartPos = span.Location.StartPos + delta;
        def newEndPos   = span.Location.EndPos + delta;
        SpanInfo(NToken(newStartPos, newEndPos), span.SpanClass)
      }
      def loopOverChanges(changeIndex, spanIndex, currentDelta, spanPos) : void
      {
        if (changeIndex < changes.Count)
        {
          def change = changes[changeIndex];
          if (change.OldEnd <= spanPos)
            loopOverChanges(changeIndex + 1, spanIndex, currentDelta + change.Delta, spanPos);
          else
            loopOverCache(changeIndex, spanIndex, currentDelta, change);
        }
        else
          loopOverCacheTail(spanIndex, currentDelta);
      }
      and loopOverCache(changeIndex, spanIndex, currentDelta, change) : void
      {
        when (spanIndex < _cache.Count)
        {
          def span = _cache[spanIndex];
          if (span.Location.EndPos <= change.OldPosition)
          {
            predictedCache.Add(copySpan(span, currentDelta));
            loopOverCache(changeIndex, spanIndex + 1, currentDelta, change);
          }
          else if (span.Location.StartPos < change.OldEnd)
            loopOverCache(changeIndex, spanIndex + 1, currentDelta, change);
          else
            loopOverChanges(changeIndex + 1, spanIndex, currentDelta + change.Delta, span.Location.StartPos);
        }
      }
      and loopOverCacheTail(spanIndex, currentDelta)
      {
        when (spanIndex < _cache.Count)
        {
          predictedCache.Add(copySpan(_cache[spanIndex], currentDelta));
          loopOverCacheTail(spanIndex + 1, currentDelta);
        }
      }
      when (_cache.Count > 0)
        loopOverChanges(0, 0, 0, _cache[0].Location.StartPos);
      _cache = predictedCache;
      _cachedSpan = if (predictedCache.Count > 0) NToken(predictedCache[0].Location.StartPos, predictedCache[predictedCache.Count - 1].Location.EndPos) else NToken();
    }

    public GetSpans(span : SnapshotSpan, [NotNull] parseResult : Parser) : array[SpanInfo]
    {
      def startPos = span.Start.Position;
      def endPos   = span.End.Position;

      unless (IsCacheContainsSpan(startPos, endPos))
        FillCache(span, parseResult);
      def (index, count) = GetSpansFromCache(startPos, endPos);
      def result = array(count);
      when (count > 0)
        _cache.CopyTo(index, result, 0, count);
      result
    }

    private IsCacheContainsSpan(startPos : int, endPos : int) : bool
    {
      _cachedSpan.StartPos <= startPos && endPos <= _cachedSpan.EndPos
    }

    private GetSpansFromCache(startPos : int, endPos : int) : int * int
    {
      mutable index = FindSpanCacheIndex(startPos);
      while (index < _cache.Count && _cache[index].Location.EndPos < startPos)
        ++index;
      def startIndex = index;
      while (index < _cache.Count && _cache[index].Location.StartPos < endPos)
        ++index;
      (startIndex, index - startIndex)
    }

    private FindSpanCacheIndex(pos : int) : int
    {
      mutable index = _cacheIndex[0][1];
      for(mutable i = 1; i < _cacheIndex.Length; ++i)
      {
        def (startPos, nextIndex) = _cacheIndex[i];
        when (pos < startPos)
          return index;
        index = nextIndex;
      }
      index
    }

    private FillCache(span : SnapshotSpan, parseResult : Parser) : void
    {
      _cache.Clear();
      _cachedSpan = GetCacheWindow(span);
      parseResult.GetSpans(_cachedSpan.StartPos, _cachedSpan.EndPos, _cache);
      _cache.Sort(
        fun (a : SpanInfo, b : SpanInfo)
        {
          def startPos1 = a.Location.StartPos;
          def startPos2 = b.Location.StartPos;
          if (startPos1 < startPos2) -1
          else if (startPos1 > startPos2) +1
          else
          {
            def endPos1 = a.Location.EndPos;
            def endPos2 = b.Location.EndPos;
            if (endPos1 < endPos2) -1
            else if (endPos1 > endPos2) +1
            else 0
          }
        });
      FillCacheIndex();
    }

    private FillCacheIndex() : void
    {
      for(mutable i = 0; i < _cacheIndex.Length; ++i)
      {
        def index = _cache.Count * i / _cacheIndex.Length;
        _cacheIndex[i] = (_cache[index].Location.StartPos, index);
      }
    }

    private GetCacheWindow(span : SnapshotSpan) : NToken
    {
      def (startLineNumber, _) = span.Start.GetLineAndColumn();
      def endLineNumber        = Math.Min(startLineNumber + _linesToCache, span.Snapshot.LineCount) - 1;
      def startLine            = span.Snapshot.GetLineFromLineNumber(startLineNumber);
      def endLine              = span.Snapshot.GetLineFromLineNumber(endLineNumber);
      NToken(startLine.Start.Position, endLine.End.Position)
    }
  }
}
