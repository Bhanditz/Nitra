using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Console;
using System.Linq;

using N2.Test.Framework;
using N2.Test.Framework.Utils;

module Program
{
  Main(args : array[string]) : int
  {
    mutable createGold = false;
    mutable teamCityTestSuite = null;
    def fileMasks = List();
    def options =
    [
      Getopt.CliOption.Flag("-create-gold", [], "Create missing gold files", fun()
      {
        createGold = true;
      }),
      Getopt.CliOption.String("-team-city-test-suite", [], "TeamCity test suite name", fun(value)
      {
        teamCityTestSuite = value;
      }),
      Getopt.CliOption.NonOption("", [], "File mask", fun(value)
      {
        fileMasks.Add(value);
      })
    ];
    Getopt.Parse(Getopt.Error, options, args.ToNList());

    def consoleListener = ConsoleExecutionListener();
    def listener = if(string.IsNullOrEmpty(teamCityTestSuite)) consoleListener else TeamCityExecutionListener(teamCityTestSuite, consoleListener, false);
    def runner = Runner(listener);
    def testsToRun = FileSearcher.Search(fileMasks).Select(GoldTest(_, createGold));
    runner.Run(testsToRun);
    consoleListener.GetExitCode();
  }
}
