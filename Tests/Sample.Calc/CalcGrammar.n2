using N2;

syntax module CalcGrammar
{
  token field OpenBrace    = "(";
  token field CloseBrace   = ")";
  token field OpenBrace    = "{";
  token field CloseBrace   = "}";
  token field Op           = "+", "++", "-", "--", "*", "/", "?", ":", "^", "%", "??";

  span class Number;
  span class Operator     = ['+', '-', '/', '*', '^', '?', ':', '%']+;
  span class OpenBrace  = "(" | "{";
  span class CloseBrace = ")" | "}";

  braces "(", ")";
  braces "{", "}";

  option EmitDebugSources = true;

  using IncGrammar;
  using NumParser;

  token any = ['\u0000'..'\uFFFF'];
  void s = ' '*;

  [StartRule]
  syntax start       = s expr !any { out Value : double = Expr.Value; }

  [StartRule]
  syntax expr { out Value : double; }

  syntax rounds is expr = '('s expr ')'s              { set Value = Expr.Value; }

  [SpanClass(Number)]
  syntax num is expr = number s                       { set Value = Number.Value; }

  syntax neg is expr = '-'s expr  { precedence 100;     set Value = -Expr.Value; }

  // We can redefine names for fields in any time.
  token field Operator = "+", "++", "-", "--", "*", "/", "?", ":", "^", "%", "??";

  syntax add is expr = expr '+'s expr { precedence 10; set Value = Expr1.Value + Expr2.Value; }
  syntax sub is expr = expr '-'s expr { precedence 10; set Value = Expr1.Value - Expr2.Value; }
  syntax mul is expr = expr '*'s expr { precedence 20; set Value = Expr1.Value * Expr2.Value; }
  syntax div is expr = expr '/'s expr { precedence 20; set Value = Expr1.Value / Expr2.Value; }
  syntax mod is expr = expr '%'s expr { precedence 20; set Value = Expr1.Value % Expr2.Value; }
//syntax mod is expr = expr '%'s expr { precedence 20; set Expr1.In = mod.In; }
//syntax mod is expr = expr '%'s expr { precedence 20; set mod.In = Expr1.In; } // error
  syntax pow is expr = expr '^'s expr { precedence 30 right-associative; }

  syntax prefixDec is expr = "--"s expr  { precedence 200; set Value = Expr.Value - 1.0; }
  syntax postfixDec is expr = expr "--"s { precedence 200; set Value = Expr.Value - 1.0; }

  alias Condition = expr : 301;

  syntax cond is expr = Condition '?'s expr ':' as Colon s expr { set Value = if (Condition.Value != 0.0) Expr1.Value else Expr2.Value; }

  syntax coalescing  is expr = expr : 401 "??"s expr : 400 { set Value = if (Expr1.Value != 0.0) Expr1.Value else Expr2.Value; }
}

syntax module IncGrammar
{
  using cp = CalcGrammar;
  using CalcGrammar;

  syntax plus is cp.expr = '+'cp.s cp.expr : 100        { set Value = Expr.Value; }
  syntax prefixInc is cp.expr = "++"cp.s cp.expr : 200  { set Value = Expr.Value + 1; }
  syntax postfixInc is cp.expr = cp.expr : 200 "++"cp.s { set Value = Expr.Value + 1; }
}
