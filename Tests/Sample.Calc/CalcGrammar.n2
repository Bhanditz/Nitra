using N2;

syntax module CalcGrammar
{
  token field OpenBrace    = "(";
  token field CloseBrace   = ")";
  token field OpenBrace    = "{";
  token field CloseBrace   = "}";
  token field Op           = "+", "++", "-", "--", "*", "/", "?", ":", "^", "%", "??";

  span class Number;
  span class Operator     = ['+', '-', '/', '*', '^', '?', ':', '%']+;
  span class OpenBrace  = "(" | "{";
  span class CloseBrace = ")" | "}";

  braces "(", ")";
  braces "{", "}";

  option EmitDebugSources = true;

  using IncGrammar;
  using NumParser;

  token any = ['\u0000'..'\uFFFF'];
  void s = ' '*;

  [StartRule]
  syntax start       = s expr !any { Value() : double = Expr.Value(); }

  [StartRule]
  syntax expr { Value() : double = double.Epsilon; }

  syntax rounds is expr = '('s expr ')'s              { override Value() : double = Expr.Value(); }

  [SpanClass(Number)]
  syntax num is expr = number s                       { override Value() : double = Number.Value(); }

  syntax neg is expr = '-'s expr  { precedence 100;     override Value() : double = -Expr.Value(); }

  // We can redefine names for fields in any time.
  token field Operator = "+", "++", "-", "--", "*", "/", "?", ":", "^", "%", "??";

  syntax add is expr = expr '+'s expr { precedence 10; override Value() : double = Expr1.Value() + Expr2.Value(); }
  syntax sub is expr = expr '-'s expr { precedence 10; override Value() : double = Expr1.Value() - Expr2.Value(); }
  syntax mul is expr = expr '*'s expr { precedence 20; override Value() : double = Expr1.Value() * Expr2.Value(); }
  syntax div is expr = expr '/'s expr { precedence 20; override Value() : double = Expr1.Value() / Expr2.Value(); }
  syntax mod is expr = expr '%'s expr { precedence 20; override Value() : double = Expr1.Value() % Expr2.Value(); }
  syntax pow is expr = expr '^'s expr { precedence 30 right-associative; }

  syntax prefixDec is expr = "--"s expr  { precedence 200; override Value() : double = Expr.Value() - 1.0; }
  syntax postfixDec is expr = expr "--"s { precedence 200; override Value() : double = Expr.Value() - 1.0; }

  alias Condition = expr : 301;

  syntax cond is expr = Condition '?'s expr ':' as Colon s expr { override Value() : double = if (Condition.Value() != 0.0) Expr1.Value() else Expr2.Value(); }

  syntax coalescing  is expr = expr : 401 "??"s expr : 400 { override Value() : double = if (Expr1.Value() != 0.0) Expr1.Value() else Expr2.Value(); }
}

syntax module IncGrammar
{
  using cp = CalcGrammar;
  using CalcGrammar;

  syntax plus is cp.expr = '+'cp.s cp.expr : 100        { override Value() : double = Expr.Value(); }
  syntax prefixInc is cp.expr = "++"cp.s cp.expr : 200  { override Value() : double = Expr.Value() + 1; }
  syntax postfixInc is cp.expr = cp.expr : 200 "++"cp.s { override Value() : double = Expr.Value() + 1; }
}
