using System;
using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Parsetree.PExpr;
using Nemerle.Compiler.Token;

namespace Nemerle.Parser
{
  internal partial class GrammarParser
  {
    // OrderedChoice   = Sequence ( '/' Sequence)*
    private ParseRegularChoice(tok : Token) : RegularRule * Token
    {
      def parseOr(tok : Token) : Token
      {
        | Operator("|") =>
          if (tok.Next == null) ReportCriticalUnexpected(tok, "rule")
          else tok.Next

        | _             => null
      }

      def (seqs, nextTok) = ParseOneOrMany(tok, parseOr, ParseRegularSequence, "sequence");
      def loc = if (seqs.IsEmpty) tok.Location else tok.Location + seqs.Last.Location;
      (RegularRule.Choice(loc, seqs), nextTok)
    }
  }
}
