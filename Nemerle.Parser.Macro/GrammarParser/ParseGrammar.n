using System;
using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Parsetree.PExpr;
using Nemerle.Compiler.Token;

namespace Nemerle.Parser
{
  internal partial class GrammarParser
  {
    public ParseGrammar(rules : PExpr) : Grammar
    {
      match (rules)
      { // Match grammar { ... } RawToken macro (defined above)
        | MacroCall(name, _ns, [SyntaxElement.RawToken(LooseGroup(BracesGroup(tokens, _)))]) => // Name * NamespaceTree.Node * list [SyntaxElement]
          if (name.Id != "grammar")
            ReportCriticalUnexpected(rules, "grammar { ... }")
          else
            ParseGramar(Grammar(), tokens)
        | rule => ReportCriticalUnexpected(rule, "grammar { ... }")
      }
    }

    private ParseGramar(grammar : Grammar, token : Token) : Grammar
    {
      //assert2(!token.ToString().Contains("using"));
      def grammar2 = 
        match (token)
        {
          | LooseGroup(Keyword("using") as u) => 
            when (u.Next == null)
              _ = ReportCriticalUnexpected(u, "using perser-type;");

            def (expr, nextToken) = _parser.ParseExpr(Token.LooseGroup(u.Next), TokenStoppers.None);

            when (nextToken != null)
              ReportUnexpected(nextToken, "nothing");

            match (expr)
            {
              | <[ $(alias : name) = $parserType ]> => 
                grammar.Add(GrammarRef.Alias(u.Location, 
                  NameRef(alias.Location, alias.Id), _typer.BindFixedType(parserType)))
                  
              | _ => grammar.Add(GrammarRef.Ref(u.Location, _typer.BindFixedType(expr)))
            }

          | _ =>
            def ruleDefinition = ParseRule(token, grammar.Count);

            if (ruleDefinition.GetRule() == null)//TODO: WTF?!
              grammar
            else
              grammar.Add(ruleDefinition)
        };

      if (token.Next == null)
        grammar2
      else
        ParseGramar(grammar2, token.Next)
    }
  }
}
