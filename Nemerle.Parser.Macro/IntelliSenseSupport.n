using System;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Parsetree.PExpr;
using Nemerle.Compiler.Token;
using Nemerle.Imperative;

namespace Nemerle.Parser
{
  internal module IntelliSenseSupport
  {
    public AddIntelliSenseSupport(compiler : GrammarCompiler) : void
    {
      foreach (name in compiler.ParsedGrammar.Names)
        MakeGotoInfos(name, compiler.ParsedGrammar, compiler.GrammarTB, compiler.Typer);

      def gotoMap = IntelliSenseSupport.GetGotoMap(compiler.GrammarTB);
      foreach (kv in compiler.GetHandlerMethods())
      {
        gotoMap[kv.Key.Location]       = kv.Value.NameLocation;
        gotoMap[kv.Value.NameLocation] = kv.Key.Location;
      }
    }

    public GetGotoMap(tb : TypeBuilder) : Hashtable[Nemerle.Compiler.Location, Nemerle.Compiler.Location]
    {
      mutable gotoMap = tb.UserData["GoToInfoMap"] :> Hashtable[Nemerle.Compiler.Location, Nemerle.Compiler.Location];

      when (gotoMap == null)
      {
        gotoMap = Hashtable();
        tb.UserData["GoToInfoMap"] = gotoMap;
      }

      gotoMap
    }

    private MakeGotoInfos(ruleName : NameRef, grammar : Grammar, tb : TypeBuilder, _typer : Typer) : void
    {
      def gotoMap = GetGotoMap(tb);
      def makeGotoRuleDef(r : Rule) : void
      {
        | Choice(rules)                              => foreach (r in rules) makeGotoRuleDef(r)
        | Sequence(rules)                            => foreach (r in rules) makeGotoRuleDef(r)
        | Call(name, _) | PrefixCall(name, _)        => gotoMap[name.Location] = grammar.GetRule(name).Location
        | Repeat(_, _, rule)                         => makeGotoRuleDef(rule)
        | RepeatWithSeparator(_, _, rule, separator) => makeGotoRuleDef(rule); makeGotoRuleDef(separator)
        | Chars | Fsm                                => ()
        | Not(rule)                                  => makeGotoRuleDef(rule)
        | And(rule)                                  => makeGotoRuleDef(rule)
        | Capture(_, rule)                           => makeGotoRuleDef(rule)
        | Cut                                        => ()
        | Scope(name, rule)                          =>
          def scopeBeginName = name.AsName + GrammarCompiler.ScopeBeginSufix;
          def scopeEndName   = name.AsName + GrammarCompiler.ScopeEndSufix;
          def isScopeHendler(m : ClassMember)
          {
            | Function as f => f.Name == scopeBeginName || f.Name == scopeEndName
            | _ => false
          }
          def scopeHendlers = tb.GetParsedMembers(false).Filter(isScopeHendler).Map(_ :> ClassMember.Function);
          match (scopeHendlers)
          {
            x :: _ => gotoMap[name.Location] = x.Location;
            | _ => Message.Error(name.Location, $"You must define scope hendler[s] for this scope: $scopeBeginName() : void and/or $scopeEndName(isFial : bool) : void");
          }
          makeGotoRuleDef(rule)
      }

      when (grammar.GetRule(ruleName).GetRule() is Some(rule))
        makeGotoRuleDef(rule);
    }
  }
}
