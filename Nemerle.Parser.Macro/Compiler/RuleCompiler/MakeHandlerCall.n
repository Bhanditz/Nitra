using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;

using System;
using System.Linq;
using SCG = System.Collections.Generic;

namespace Nemerle.Parser
{
  partial internal class RuleCompiler
  {
    public MakeHandlerCall(rule : Rule) : list[string] * PExpr
    {
      def varNames     = MakeVariablesForRule(rule);

      def ty           = _grammarCompiller.GetRuleNType(_ruleDefinition);
      def omitLocation = _ruleDefinition.Attributes.Exists(_ is RuleAttribute.OmitLocation);

      def startName = if (_ruleDefinition is RuleDefinition.ExtentionPostfix) <[ startPos ]> else  <[ pos ]>;
      def isSimple  = _ruleDefinition is RuleDefinition.Simple;
      def isAst     = _ruleDefinition.IsAst;
      def setResultLocation =
        if (!omitLocation && ty.TryRequire(_locatedType))
          match (varNames)
          {
            | []  with (start = startName,
                        end   = <[ newPos ]>)
            | _   with (start = <[ Nemerle.Parser.Internal.EvalLocationStart($(varNames.Head : dyn), $startName) ]>,
                        end   = <[ Nemerle.Parser.Internal.EvalLocationEnd($(varNames.Last() : dyn), newPos) ]>)
              =>
              def parsingSource = if (isSimple)
                <[ _parsingSource ]>;
              else
                <[ _grammar._parsingSource ]>;
              <[ result.Location = Nemerle.Parser.Location($parsingSource, $start, $end); ]>
          }
        else
          <[ () ]>;

      def varRefs = varNames.Map(var => <[ $(var : dyn) ]>);
      def handlerCall()
      {
        if (isSimple)
          <[ $(_ruleDefinition.Name.Id : dyn) ]>;
        else
          <[ _grammar.$(_ruleDefinition.Name.Id : dyn) ]>;
      }
      def setResult =
        if (isAst)
        {
          def result = if (isSimple)
          {
            def typeName = _grammarCompiller.MakePublicRuleName(_ruleDefinition.Name.Id);
            <[ $(typeName : dyn)(..$varRefs);]>;
          }
          else
          {
            def (variantName, optionName) = _grammarCompiller.GetRuleAstNames(_ruleDefinition);
            <[ $(variantName : dyn).$(optionName : dyn)(..$varRefs) ]>;
          }
          if (_grammarCompiller.GetHandlerMethods().Contains(_ruleDefinition.Name))
            <[ def tmpResult = $result; result = tmpResult; $(handlerCall())(tmpResult); ]>;
          else
            <[ result = $result ]>
        }
        else if (_grammarCompiller.GetHandlerMethods().Contains(_ruleDefinition.Name))
          <[ result = $(handlerCall())(..$varRefs) ]>;
        else if (varNames.Count() == 1)
          <[ result = $(varNames.Head : dyn) ]>
        else
          PExpr.Error("Too many variables.");

      (varNames, <[ $setResult; $setResultLocation ]>)
    }
  }
}
