using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;

using System;
using System.Linq;
using SCG = System.Collections.Generic;

namespace Nemerle.Parser
{
  partial internal class RuleCompiler
  {
    private mutable _sequenseLevel : int = 0;
    private NeedGenerateSequenceOffsets : bool { get { _sequenseLevel == 1 && (_ruleDefinition is RuleDefinition.ExtentionPrefix || _ruleDefinition is RuleDefinition.ExtentionPostfix) } }

    public CompileRuleSequence(rule : Rule.Sequence, results : list[string]) : PExpr
    {
      def generate(rules : list[Rule], results : list[string], ofsPos : int) : PExpr
      {
        match (rules)
        {
          //| [rule]        => CompileRule(rule, results);
          | rule :: rules =>
            def elementCode(curResults, tailResults)
            {
              def saveOffset()
              {
                if (NeedGenerateSequenceOffsets)
                  <[ def $($"ofs$ofsPos" : dyn) = pos; ]>
                else
                  <[ () ]>
              }
              def checkResult()
              {
                if (NeedGenerateSequenceOffsets)
                  <[ pos >= 0 && (isBest || { isBest = bestOffsets[$(ofsPos : int)] < pos; isBest || bestOffsets[$(ofsPos : int)] == pos }) ]>
                else
                  <[ pos >= 0 ]>
              }
              def ruleCode = CompileRule(rule, curResults);
              <[
                {
                  def pos = $ruleCode;
                  if ($(checkResult()))
                  {
                    $(saveOffset());
                    $(generate(rules, tailResults, ofsPos + 1))
                  }
                  else -1
                }
              ]>
            }
            match (rule)
            {
              | Capture(ty, _) when !(ty is RuleType.None || ty is RuleType.Void) =>
                match (results)
                {
                  | result :: results => elementCode([result], results)
                  | _ => PExpr.Error("Empty result list")
                }
              | _ => elementCode([], results)
            }

          | [] =>
            assert(results.Length == 0);
            if (NeedGenerateSequenceOffsets)
            {
              def offsetSetters = $[0..ofsPos - 1].Map(i => <[ bestOffsets[$(i : int)] = $($"ofs$i" : dyn); ]>);
              <[ 
                { ..$offsetSetters }
                for (mutable i = $(ofsPos : int); i < bestOffsets.Length && bestOffsets[i] >= 0; ++i)
                  bestOffsets[i] = -1;
                pos
              ]>
            }
            else
              <[ { pos } ]>
        }
      }

      def generateNoResult(rules : list[Rule]) : PExpr
      {
        match (rules)
        {
          | []            => <[ { pos } ]>
          | [rule]        => CompileRule(rule, [])
          | rule :: rules =>
            <[
              def pos = $(CompileRule(rule, []));
              if (pos >= 0) $(generateNoResult(rules)) else -1
            ]>
        }
      }

      ++_sequenseLevel;
      def res = match (results)
      {
        | [] => generateNoResult(rule.rules);
        | _  => generate(rule.rules, results, 0);
      }
      --_sequenseLevel;
      res;
    }
  }
}
