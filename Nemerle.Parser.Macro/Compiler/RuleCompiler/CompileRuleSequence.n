using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;

using System;
using System.Linq;
using SCG = System.Collections.Generic;

namespace Nemerle.Parser
{
  partial internal class RuleCompiler
  {
    private mutable _sequenseLevel : int = 0;
    private NeedGenerateSequenceOffsets : bool { get { _sequenseLevel == 1 && (_ruleDefinition is RuleDefinition.ExtentionPrefix || _ruleDefinition is RuleDefinition.ExtentionPostfix) } }

    public CompileRuleSequence(rule : Rule.Sequence, results : list[string]) : PExpr
    {
      def generate(rules : list[Rule], results : list[string], ofsPos : int) : PExpr
      {
        match (rules)
        {
          //| [rule]        => CompileRule(rule, results);
          | rule :: rules =>
            def saveOffset()
            {
              if (NeedGenerateSequenceOffsets)
                <[ curOffsets.$($"Ofs$ofsPos" : dyn) = pos; ]>
              else
                <[ () ]>
            }
            match (rule)
            {
              | Capture(ty, _) when !(ty is RuleType.None || ty is RuleType.Void)
                =>
                match (results)
                {
                  | result :: results =>
                    def ruleCode = CompileRule(rule, [result]);
                    <[
                      {
                        def pos = $ruleCode;
                        $(saveOffset());
                        if (pos >= 0) $(generate(rules, results, ofsPos + 1)) else -1
                      }
                    ]>
                  | _ => PExpr.Error("Empty result list")
                }

              | _ =>
                def ruleCode = CompileRule(rule, []);
                <[
                  {
                    def pos = $ruleCode;
                    $(saveOffset());
                    if (pos >= 0) $(generate(rules, results, ofsPos + 1)) else -1
                  }
                ]>
            }

          | [] =>
            assert(results.Length == 0);
            <[ { pos } ]>;
        }
      }

      def generateNoResult(rules : list[Rule]) : PExpr
      {
        match (rules)
        {
          | []            => <[ { pos } ]>
          | [rule]        => CompileRule(rule, [])
          | rule :: rules =>
            <[
              def pos = $(CompileRule(rule, []));
              if (pos >= 0) $(generateNoResult(rules)) else -1
            ]>
        }
      }

      ++_sequenseLevel;
      def res = match (results)
      {
        | [] => generateNoResult(rule.rules);
        | _  => generate(rule.rules, results, 0);
      }
      --_sequenseLevel;
      res;
    }
  }
}
