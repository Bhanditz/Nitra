using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;
using Nemerle.Parser.Model;

using System;
using System.Linq;
using SCG = System.Collections.Generic;

namespace Nemerle.Parser
{
  internal partial class RuleCompiler
  {
    public CompileRuleScope(rule : Rule.Scope) : PExpr * list[string]
    {
      def (beginName, beginRes, endName, endRes) = _grammarCompiller.GetScopeMethods(rule.name.AsName);
      def (subRuesCode, results) = CompileRule(rule.rule);

      when (endRes.IsNone && beginRes.IsNone)
        Message.Error(rule.name.Location, $"You must specify at least one scope handler for scope $(rule.name). Scope handlers must have signatures: '$beginName() : bool/void', '$endName(isOk : bool) : bool/void'.");

      def (beginCall, endCall) = if (_ruleDefinition.IsExtention)
        (<[ _grammar.$(beginName : dyn) ]>, <[ _grammar.$(endName : dyn) ]>)
      else
        (<[ $(beginName : dyn) ]>, <[ $(endName : dyn) ]>);

      def code =
        match (endRes)
        {
          | Some(true) =>
            <[
              def newPos = $subRuesCode;
              if ($endCall(newPos >= 0))
                newPos
              else
                -1
            ]>

          | Some(false) =>
            <[
              def newPos = $subRuesCode;
              $endCall(newPos >= 0);
              newPos
            ]>

          | None => subRuesCode
        };

      def code = match (beginRes)
      {
        | Some(true) =>
          <[
            if ($beginCall())
              $code
            else
              -1
          ]>

        | Some(false) =>
          <[
            $beginCall();
            $code
          ]>

        | None => code
      }
      (code, results)
    }
  }
}
