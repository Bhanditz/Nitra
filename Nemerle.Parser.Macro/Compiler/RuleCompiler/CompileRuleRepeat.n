using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;

using System;
using System.Linq;
using SCG = System.Collections.Generic;

namespace Nemerle.Parser
{
  partial internal class RuleCompiler
  {
    public CompileRuleRepeat(rule : Rule.Repeat, results : list[string]) : PExpr
    {
      def setListResult(call : PExpr)
      {
        if (_ruleDefinition.IsAst)
        {
          match (results)
          {
            | [] => call
            | [result] =>
              <[
                def newPos = $call;
                $(result : dyn) = tmpList.NToList();
                newPos
              ]>
            | _ => assert(false)
          }
        }
        else
          call
      }
      match (rule)
      {
        | Repeat(0, None, _) =>
          def (initialising, addTokensToList, ruleCode) = MakeListCode(results, rule.rule);
          PrepandExprs(initialising, <[
            def rep(pos : int)
            {
              def newPos = $ruleCode;
              if (newPos >= 0)
              {
                $addTokensToList;
                rep(newPos)
              }
              else
                pos
            }
            $(setListResult(<[ rep(pos) ]>))
          ]>)

        | Repeat(minCount, None, _) =>
          def (initialising, addTokensToList, ruleCode) = MakeListCode(results, rule.rule);
          PrepandExprs(initialising, <[
            def rep(count, pos)
            {
              def newPos = $ruleCode;
              if (newPos >= 0)
              {
                $addTokensToList;
                rep(count + 1, newPos)
              }
              else if (count >= $(minCount : int))
                pos
              else
                -1
            }
            $(setListResult(<[ rep(0, pos) ]>))
          ]>)

        | Repeat(0, Some(1), subrule)    =>
          def (initialising, setExpr, setNoneExpr, ruleCode) = match (subrule)
          {
            | Capture(ty, _) when _grammarCompiller.IsOptimizableOptionType(ty) =>
              MakeSubruleCode(results, subrule,
                              _                      => <[ () ]>,
                              (varRef, variableRefs) => match (variableRefs)
                                                        {
                                                          | [variableRef] => <[ $varRef = $variableRef; ]>
                                                          | _ => assert(false);
                                                        },
                              _                      => <[ () ]>);

            | _ =>
              MakeSubruleCode(results, subrule,
                              _                      => <[ () ]>,
                              (varRef, variableRefs) => <[ $varRef = Some(..$variableRefs); ]>,
                              varRef                 => <[ $varRef = None(); ]>);
          }
          PrepandExprs(initialising, <[
            def newPos = $ruleCode;
            if (newPos >= 0)
            {
              $setExpr;
              newPos
            }
            else
            {
              $setNoneExpr;
              pos
            }
          ]>)

        | Repeat(minCount, Some(maxCount), subrule)    =>
          def (initialising, addTokensToList, ruleCode) = MakeListCode(results, subrule);
          PrepandExprs(initialising, <[
            def rep(count, pos)
            {
              if (count == $(maxCount : int))
                pos
              else
              {
                def newPos = $ruleCode;
                if (newPos >= 0)
                {
                  $addTokensToList;
                  rep(count + 1, newPos)
                }
                else if (count >= $(minCount : int))
                  pos
                else
                  -1
               }
            }
            $(setListResult(<[ rep(0, pos) ]>))
          ]>)
      }
    }
  }
}
