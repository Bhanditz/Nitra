using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;

using System;
using System.Linq;
using SCG = System.Collections.Generic;

namespace Nemerle.Parser
{
  partial internal class GrammarCompiler
  {
    public CompileGrammar() : void
    {
      def compileRule(name, useResult)
      {
        def rule = if (useResult) TypedGrammar.GetRule(name);
                   else           UntypedGrammar.GetRule(name);

        try
        {
          RuleCompiler(this, rule, useResult).CompileRule();
        }
        catch
        {
          | ex => Message.Error(name.Location, ex.Message);
        }
      }

      foreach (ruleDef in TypedGrammar.Rules)
      {
        | RuleDefinition.ExtentionPostfix
        | RuleDefinition.ExtentionPrefix
        | RuleDefinition.ExtentionPoint   => compileRule(ruleDef.Name, true);
        | RuleDefinition.Regular          => compileRule(ruleDef.Name, false);
        | RuleDefinition.ExternalRule     => ()
        | RuleDefinition.Simple           =>
          compileRule(ruleDef.Name, false);
          when (GetRuleType(ruleDef.Name) is RuleType.NType)
            compileRule(ruleDef.Name, true);
      }
    }
  }
}
