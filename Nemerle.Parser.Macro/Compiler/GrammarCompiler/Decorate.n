using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Assertions;

using System;
using System.Linq;
using SCG = System.Collections.Generic;

using Nemerle.Parser.Internal;

namespace Nemerle.Parser
{
  partial internal class GrammarCompiler
  {
    public DecorateRuleName(name : NameRef) : string
    {
      $"_#_$(name.Parts.Last())_"
    }

    public DecorateRuleName(rd : RuleDefinition) : string
    {
      DecorateRuleName(rd.Name)
    }

    public DecorateRuleClassName(rd : RuleDefinition) : string
    {
      match (rd)
      {
        | ExtentionPoint   => $"_#point#___$(rd.Name.AsName)_"
        | ExtentionPrefix  => $"_#prefix#__$(rd.Name.AsName)_"
        | ExtentionPostfix => $"_#postfix#_$(rd.Name.AsName)_"
        | Simple           => $"_#simple#__$(rd.Name.AsName)_"
        | ExternalRule     => assert(false)
      }
    }

    public ParseRuleClassName(name : string) : option[string * string]
    {
      match (name.Substring(0, 11))
      {
        | "_#point#___" with type = name.Substring(2, 5)
        | "_#prefix#__" with type = name.Substring(2, 6)
        | "_#postfix#_" with type = name.Substring(2, 7)
        | "_#simple#__" with type = name.Substring(2, 6)
          =>
          if (name.EndsWith("_"))
            Some((type, name.Substring(11, name.Length - 12)))
          else
            None()
        | _ => None()
      }
    }

    public DecorateGrammarRef(grammarRef : GrammarRef) : string
    {
      $"_#grammar#$(grammarRef.Id)"
    }

    public static ScopeBeginSufix = "ScopeBegin";
    public static ScopeEndSufix   = "ScopeEnd";
  }
}
