using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Parser.Model;

using System;
using System.Linq;
using SCG = System.Collections.Generic;
using Nemerle.Parser.Internal;

namespace Nemerle.Parser
{
  internal partial class GrammarCompiler
  {
    private DefineLoadExtensionRules() : void
    {
      def inits = if (Manager.IsIntelliSenseMode)
        []
      else
      {
        def findExtentionRules(baseRuleName)
        {
          def prefixRDs  = TypedGrammar.Rules
            .MapFiltered(_ is RuleDefinition.ExtentionPrefix, _ :> RuleDefinition.ExtentionPrefix)
            .Filter(rd => rd.baseRuleName == baseRuleName);
          def postfixRDs = TypedGrammar.Rules
            .MapFiltered(_ is RuleDefinition.ExtentionPostfix, _ :> RuleDefinition.ExtentionPostfix)
            .Filter(rd => rd.baseRuleName == baseRuleName);
          (prefixRDs, postfixRDs)
        }

        def extentionPoints = TypedGrammar.Rules
          .MapFiltered(_ is RuleDefinition.ExtentionPoint, _ :> RuleDefinition.ExtentionPoint)
          .Map(baseRD => (baseRD, findExtentionRules(baseRD.Name)));

        def externalExtentionPoints = TypedGrammar.Rules
          .MapFiltered(_ is RuleDefinition.ExternalRule, _ :> RuleDefinition.ExternalRule)
          .Filter(_.isExtentionPoint)
          .Map(baseRD => (baseRD, findExtentionRules(baseRD.Name)));

        def extentionPoints = extentionPoints.Map((extentionPoint, (prefixRDs, postfixRDs)) =>
        {
          def names = ExtensionPointMemoizeNames(extentionPoint.Name);
          [(prefixRDs. Map(rd => <[ $(DecorateRuleClassName(rd) : dyn) ]>), <[ $(names.PrefixRules : dyn) ]>)
          ,(postfixRDs.Map(rd => <[ $(DecorateRuleClassName(rd) : dyn) ]>), <[ $(names.PostfixRules : dyn) ]>)
          ]
        }).Flatten();

        def externalExtentionPoints = externalExtentionPoints.Map((extentionPoint, (prefixRDs, postfixRDs)) =>
        {
          def names = ExtensionPointMemoizeNames(extentionPoint.Name.LastPart);
          [(prefixRDs. Map(rd => <[ $(DecorateRuleClassName(rd) : dyn) ]>), <[ $(DecorateGrammarRef(extentionPoint.grammarRef) : dyn).$(names.PrefixRules : dyn) ]>)
          ,(postfixRDs.Map(rd => <[ $(DecorateRuleClassName(rd) : dyn) ]>), <[ $(DecorateGrammarRef(extentionPoint.grammarRef) : dyn).$(names.PostfixRules : dyn) ]>)
          ]
        }).Flatten();

        extentionPoints.Append(externalExtentionPoints).Map((constructors, rules) =>
        {
          if (constructors.IsEmpty)
            <[ () ]>
          else
          {
            def inits = constructors.MapI((index, constructor) => <[ $rules[prevLength + $index] = $constructor(this) ]>);
            <[
              prevLength = $rules?.Length;
              Array.Resize(ref $rules, prevLength + $(constructors.Length : int));
              { ..$inits }
            ]>
          }
        });
      }

      DefineImplementation(
        <[decl:
          private LoadExtensionRules() : void
          {
            mutable prevLength; _ = prevLength;
            { ..$inits }
          }
        ]>);
    }
  }
}
