using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;

using System;
using System.Linq;
using SCG = System.Collections.Generic;

namespace Nemerle.Parser
{
  partial internal class GrammarCompiler
  {
    private DefineParseMethods() : void
    {
      foreach (rd when rd.Attributes.Exists(_ is RuleAttribute.StartRule) in TypedGrammar.Rules)
      {
        def tyParam =
          match (TypedGrammar.GetRuleType(rd.Name))
          {
            | RuleType.NType(ty) => <[ $(ty : typed) ]>
            | _ =>
              Message.Error(_tb.Location,  "Start rule must be a capture rule");
              <[ object ]>
          };
        def name = rd.Name.Id.Substring(0, 1).ToUpper() + rd.Name.Id.Substring(1);

        DefineWithSource(<[decl:
          public $(("Parse" + name) : dyn)(text : string) : option[$tyParam]
          {
            def (pos, res) = $(("TryParse" + name) : dyn)(text);
            if (pos < 0) None() else Some(res)
          }
        ]>);
        DefineWithSource(<[decl:
          public $(("Parse" + name) : dyn)(source : Nemerle.Parser.SourceSnapshot) : option[$tyParam]
          {
            def (pos, res) = $(("TryParse" + name) : dyn)(source);
            if (pos < 0) None() else Some(res)
          }
        ]>);

        DefineWithSource(<[decl:
          public $(("TryParse" + name) : dyn)(text : string) : int * $tyParam
          {
            $(("TryParse" + name) : dyn)(Nemerle.Parser.SourceSnapshot(text))
          }
        ]>);

        if (_typer.Manager.IsIntelliSenseMode)
        {
          DefineWithSource(<[decl:
            public $(("TryParse" + name) : dyn)([Nemerle.Assertions.NotNull] source : Nemerle.Parser.SourceSnapshot) : int * $tyParam
            {
              _parsingSource = source;
              mutable result;
              result;
            }
          ]>);
        }
        else
        {
          def args = match (rd)
          {
            | RuleDefinition.Simple         => [<[ 0 ]>, <[ _parsingSource.Text ]>, <[ ref result ]>]
            | RuleDefinition.ExtentionPoint => [<[ 0 ]>, <[ _parsingSource.Text ]>, <[ 0 ]>, <[ ref result ]>]
            | _                             => [PExpr.Error($"$(rd.Name.Id) can't be start rule.")]
          }
          DefineWithSource(<[decl:
            public $(("TryParse" + name) : dyn)([Nemerle.Assertions.NotNull] source : Nemerle.Parser.SourceSnapshot) : int * $tyParam
            {
              InitExtensionRules();
              ResetMemoization();
              ResetMaxRollbackPos();

              _parsingSource = source;

              mutable result;
              def pos = $(DecorateRuleName(rd.Name.Id) : dyn)(..$args);

              (pos, result)
            }
          ]>);
        }
      }
    }
  }
}
