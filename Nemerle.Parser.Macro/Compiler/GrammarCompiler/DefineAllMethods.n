using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;

using System;
using System.Linq;
using SCG = System.Collections.Generic;

namespace Nemerle.Parser
{
  partial internal class GrammarCompiler
  {
    public DefineAllMethods() : void
    {
      DefineGetMaxRollbackPos();
      DefineGetRuleName();
      DefineResetMaxRollbackPos();
      DefineResetMemoization();
      DefineInitExtensionRules();

      DefineImplementation(<[decl: mutable _parsingSource : Nemerle.Parser.SourceSnapshot; ]>);
      DefineImplementation(<[decl:
        public ParsingSource : Nemerle.Parser.SourceSnapshot
        {
          get { _parsingSource }
        }
      ]>);

      DefineImplementation(<[decl:
        public override GetText(tok : Nemerle.Parser.NToken) : string
        {
          _parsingSource.OriginalText.Substring(tok.StartPos, tok.EndPos - tok.StartPos)
        }
      ]>);

      DefineImplementation(<[decl:
        public override GetLocation(tok : Nemerle.Parser.NToken) : Nemerle.Parser.Location
        {
          Nemerle.Parser.Location(_parsingSource, tok.StartPos, tok.EndPos)
        }
      ]>);

      foreach (tb in _externalGrammars with grammarId)
      {
        match (tb.GetAllSubTypes().Find(tb => tb.Name == "Grammar"))
        {
          | Some(subTB) =>
            DefineImplementation(<[decl:
              private mutable $($"_grammar$grammarId" : dyn) : $(subTB.GetMemType() : typed)
            ]>);

          | None() =>
            Message.Error("Invalid external grammar.");
        }
      }

      _implTB.Compile();
    }
  }
}
