using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;

using System;
using System.Linq;
using SCG = System.Collections.Generic;

namespace Nemerle.Parser
{
  partial internal class GrammarCompiler
  {
    public DefineAllMethods() : void
    {
      DefineGetMaxRollbackPos();
      DefineGetRuleName();
      DefineResetMaxRollbackPos();
      DefineResetMemoization();
      DefineParseMethods();
      DefineInitExtensionRules();

      _tb.Define(<[decl: mutable _parsingSource : Nemerle.Parser.SourceSnapshot; ]>);
      _tb.Define(<[decl:
        public ParsingSource : Nemerle.Parser.SourceSnapshot
        {
          get { _parsingSource }
        }
      ]>);
      
      DefineWithSource(<[decl:
        // should it be macro?
        protected GetText(tok : Nemerle.Parser.NToken) : string
        {
          _parsingSource.OriginalText.Substring(tok.StartPos, tok.EndPos - tok.StartPos)
        }
      ]>);
         
      DefineWithSource(<[decl:
        // should it be macro?
        protected GetLocation(tok : Nemerle.Parser.NToken) : Nemerle.Parser.Location
        {
          Nemerle.Parser.Location(_parsingSource, tok.StartPos, tok.EndPos)
        }
      ]>);
    }
  }
}
