using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Parsetree.PExpr;
using Nemerle.Compiler.Token;

using System;
using System.Collections.Generic;
using System.Linq;

using Nemerle.Compiler.Util;

namespace Nemerle.Parser
{
  internal partial class GrammarCompiler
  {
    public RuleTypeToNType(ruleType : RuleType, useNList : bool) : TypeVar
    {
      def ntokenType = Typer.Manager.LookupTypeInfo("Nemerle.Parser.NToken", 0);
      def optionType = Typer.Manager.LookupTypeInfo("Nemerle.Core.option", 1);
      def listType = if (useNList)
          Typer.Manager.LookupTypeInfo("Nemerle.Core.list", 1);
        else
          Typer.Manager.LookupTypeInfo("System.Collections.Generic.List", 1);

      def ruleTypeToNType(_ : RuleType)
      {
        | Option(ty) when IsOptimizableOptionType(ty)
        | Chars        => FixedType.Class(ntokenType, [])
        | NType(ty)    => ty
        | List(ty)     => FixedType.Class(listType, [ruleTypeToNType(ty)])
        | Option(ty)   => FixedType.Class(optionType, [ruleTypeToNType(ty)])
        | Tuple(types) =>
          match (types.MapFiltered(ty => !(ty is RuleType.Void), ruleTypeToNType))
          {
            | [ty]  => ty
            | types => FixedType.Tuple(types)
          }

        | Void         => FixedType.Void()
        | None         => assert(false)
      }
      ruleTypeToNType(ruleType)
    }
  }
}
