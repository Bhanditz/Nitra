using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Assertions;

using System;
using System.Linq;
using SCG = System.Collections.Generic;

using Nemerle.Parser.Internal;

namespace Nemerle.Parser
{
  partial internal class GrammarCompiler
  {
    [Accessor]
    private _typer                 : Typer;
    [Accessor]
    private _grammarTB             : TypeBuilder;
    [Accessor]
    private _implTB                : TypeBuilder;
    private _stateTB               : TypeBuilder;
    private _grammarDescriptorImpl : TypeBuilder;
    private _parsingErrorsTB       : TypeBuilder;
    private _isDebug               : bool;
    private _emitDebugSources      : bool;

    [Accessor]
    private mutable _allRules : bool;

    [Accessor]
    private mutable _parsedGrammar  : Grammar;
    [Accessor]
    private mutable _typedGrammar   : Grammar;
    [Accessor]
    private mutable _untypedGrammar : Grammar;

    public this(typer : Typer, tb : TypeBuilder, options : PExpr, rules : PExpr)
    {
      _typer                 = typer;
      _grammarTB             = tb;

      def impl = <[decl:
        public sealed class GrammarImpl : $(tb.GetMemType() : typed), IGrammar
        {
          public Parser : Parser { get; private set; }

          public this()
          {
          }

          public this([NotNull] parser : Parser)
          {
            Parser = parser;
          }

          private mutable _parsingErrors : ParsingErrorsImpl;

          private static _descriptor : GrammarDescriptor = GrammarDescriptorImpl();
          public static StaticDescriptor : GrammarDescriptor { get { _descriptor } }
          public Descriptor : GrammarDescriptor { get { _descriptor } }
        }
      ]>;
      _implTB                = _grammarTB.DefineNestedType(impl);

      def grammarDescriptorImpl  = <[decl:
        public sealed class GrammarDescriptorImpl : GrammarDescriptor
        {
          public override Name : string { get { $(tb.Name : string) } }

          public override NewGrammar(parser : Internal.Parser) : Internal.IGrammar
          {
            GrammarImpl(parser);//TODO: 2 VladD2 bug in compiler
            //$(_implTB.GetMemType() : typed)(parser);
          }

          public override NewParsingErrors() : Nemerle.Parser.Internal.ParsingErrors
          {
            ParsingErrorsImpl();
          }
        }
      ]>;

      _grammarDescriptorImpl = _grammarTB.DefineNestedType(grammarDescriptorImpl);
      _parsingErrorsTB       = _grammarTB.DefineNestedType(<[decl:
        private class ParsingErrorsImpl : Nemerle.Parser.Internal.ParsingErrors
        {
          public this()
          {
            Clear()
          }
        }
      ]>);

      def stateImpl = <[decl:
        private sealed class GrammarStateImpl : Nemerle.Parser.Internal.IGrammarState
        {
          public Grammar : $(_implTB.GetMemType() : typed) { get; }
        }
      ]>;
      _stateTB   = _implTB.DefineNestedType(stateImpl);

      _isDebug   = _typer.Manager.Options.IsConstantDefined("DEBUG");

      _emitDebugSources = match (options) { | <[ Options = EmitDebugSources ]> => true | _ => false }

      _parsedGrammar = GrammarParser(_typer, false).ParseGrammar(rules);

      DefineInterfaceMembers();
    }

    public IsDebug   : bool { get { _isDebug } }
    public IsRelease : bool { get { !_isDebug } }

    public Optimization : bool { get { !_isDebug } }

    public DecorateRuleName(name : string) : string
    {
      $"_#_$(name)_"
    }

    public ParseRuleName(name : string) : option[string]
    {
      if (name.StartsWith("_#_") && name.EndsWith("_"))
        Some(name.Substring(3, name.Length - 4))
      else
        None()
    }

    public DecorateRuleClassName(rd : RuleDefinition) : string
    {
      match (rd)
      {
        | ExtentionPoint   => $"_#point#___$(rd.Name.Id)_"
        | ExtentionPrefix  => $"_#prefix#__$(rd.Name.Id)_"
        | ExtentionPostfix => $"_#postfix#_$(rd.Name.Id)_"
        | Simple           => $"_#simple#__$(rd.Name.Id)_"
        | ExternalRule     => assert(false)
      }
    }

    public DecorateGrammarRef(grammarRef : GrammarRef) : string
    {
      $"_#grammar#$(grammarRef.Id)"
    }

    public static ScopeBeginSufix = "ScopeBegin";
    public static ScopeEndSufix   = "ScopeEnd";
  }
}
