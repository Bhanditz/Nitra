using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Assertions;
using Nemerle.Parser.Model;

using System;
using System.Linq;
using SCG = System.Collections.Generic;

using Nemerle.Parser.Internal;

namespace Nemerle.Parser
{
  internal partial class GrammarCompiler
  {
    public GrammarTB               : TypeBuilder;
    public GrammarImplTB           : TypeBuilder;
    public GrammarStateTB          : TypeBuilder;
    public GrammarDescriptorImplTB : TypeBuilder;
    public ParsingErrorsTB         : TypeBuilder;

    [Accessor]
    private _manager : ManagerClass;
    [Accessor]
    private _env : GlobalEnv;
    private _isDebug               : bool;
    private _emitDebugSources      : bool = true;//TODO:Fixme

    [Accessor]
    private mutable _allRules : bool;

    [Accessor]
    private mutable _parsedGrammar  : Grammar;
    [Accessor]
    private mutable _typedGrammar   : Grammar;

    private _astType : FixedType;
    private _ntokenType : FixedType;

    public this(manager : ManagerClass, env : GlobalEnv, tb : TypeBuilder, parsedGrammar : Grammar)
    {
      when (!(tb.Attributes %&& NemerleModifiers.Abstract))
        Message.FatalError(tb.NameLocation, "Class must be abstract.");
        
      GrammarTB               = tb;
      GrammarImplTB           = GrammarTB.DefineNestedType(MakeGrammarImpl());
      GrammarDescriptorImplTB = GrammarImplTB.DefineNestedType(MakeGrammarDescriptorImpl());
      ParsingErrorsTB         = GrammarImplTB.DefineNestedType(MakeParsingErrorsImpl());
      GrammarStateTB          = GrammarImplTB.DefineNestedType(MakeGrammarStateImpl());

      _manager                = manager;
      _env                    = env;
      _isDebug                = Manager.Options.IsConstantDefined("DEBUG");
      _parsedGrammar          = parsedGrammar;
      _astType                = FixedType.Class(Manager.LookupTypeInfo("Nemerle.Parser.Ast", 0), []);
      _ntokenType             = FixedType.Class(Manager.LookupTypeInfo("Nemerle.Parser.NToken", 0), []);

      Manager.ExecBeforeAddMembers(_ =>
      {
        DoTyping();
        DefineSpanClasses();
        DefineAst();
        DefineInterfaceMembers();
      });

      Grammars.AddGrammarCompiler(Manager, this);
    }

    public IsDebug   : bool { get { _isDebug } }
    public IsRelease : bool { get { !_isDebug } }

    public Optimization : bool { get { !_isDebug } }
  }
}
