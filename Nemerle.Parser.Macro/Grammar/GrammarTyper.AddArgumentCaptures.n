using Nemerle;
using Nemerle.Extensions;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Parsetree.PExpr;
using Nemerle.Compiler.Token;

using System;
using System.Collections.Generic;
using System.Linq;

using Nemerle.Compiler.Util;

namespace Nemerle.Parser
{
  public partial module GrammarTyper
  {
    public AddArgumentCaptures(grammar : Grammar, typer : Typer) : Grammar
    {
      def optimizeType(ty : RuleType)
      {
        | Option(Chars)
        | List(Chars)  => RuleType.Chars()

        | List(None)
        | Option(None)
        | Tuple([])    => RuleType.None()

        | List(Void)
        | Option(Void)
        | Tuple(types) when types.ForAll(_ is RuleType.Void)
                       => RuleType.Void()

        | Tuple([ty])  => ty
        | _            => ty
      }

      def makeRes(rule, ty : RuleType)
      {
        def ty = optimizeType(ty);
        (Rule.Capture(rule.Location, ty, rule), ty);
      }
      def addCaptures(rule : Rule) : Rule * RuleType
      {
        match (rule)
        {
          | Choice(rules)                =>
            assert(!rules.IsEmpty);
            def capturedResWithTypes = rules.Map(addCaptures(_));
            def (_, firstTy)         = capturedResWithTypes.Head;
            def firstNTy = GrammarTyper.RuleTypeToNType(firstTy, typer);

            foreach ((rule, ty) in capturedResWithTypes)
              unless (GrammarTyper.RuleTypeToNType(ty, typer).Equals(firstNTy))
              {
                //assert2(false);
                Message.Error(rule.Location, $"The return type of rule '$(RemoveAllCaptures(rule))' is '$ty', but require type is '$firstTy'. All entries in one OrderedChoice must have same return type.");
              }

            def capturedRules = capturedResWithTypes.Map((rule, _) => rule);

            makeRes(Rule.Choice(rule.Location, capturedRules), firstTy);

          | Sequence([rule])             => addCaptures(rule) // remove redundant
          | Sequence(rules)              =>
            def rules = rules.Map(addCaptures(_));
            def types = rules.Map(t => t[1]).Filter(ty => !(ty is RuleType.None));
            def rules = rules.Map(t => t[0]);
            def loc = if (rules.IsEmpty) Location.Default else rules.EnclosingLocation.[_]();
            def ty = RuleType.Tuple(loc, types);
            makeRes(Rule.Sequence(rule.Location, rules), ty);

          | Call(name, bp)               => makeRes(Rule.Call(rule.Location, name, bp), grammar.GetRuleType(name));
          | PrefixCall(name, bp)         => makeRes(Rule.PrefixCall(rule.Location, name, bp), grammar.GetRuleType(name));
          | Repeat(0, Some(1), rule)     =>
            def (rule, ty) = addCaptures(rule);
            makeRes(Rule.Repeat(rule.Location, 0, Some(1), rule), RuleType.Option(ty));

          | Repeat(min, max, rule)       =>
            def (rule, ty) = addCaptures(rule);
            makeRes(Rule.Repeat(rule.Location, min, max, rule), RuleType.List(ty));

          | Chars(chars)                 => makeRes(Rule.Chars(rule.Location, chars), RuleType.Chars());
          | Fsm(fsm, loop)               => makeRes(Rule.Fsm(rule.Location, fsm, loop), RuleType.Chars());
          | Scope(name, rule)            =>
            def (rule, ty) = addCaptures(rule);
            makeRes(Rule.Scope(rule.Location, name, rule), ty);

          | Not | And | Cut              => (rule, RuleType.None())
          | Capture                      => assert(false);
        }
      }

      def removeRedundant(rule : Rule)
      {
        | Capture(Chars, rule)     => Rule.Capture(rule.Location, RuleType.Chars(), RemoveAllCaptures(rule));
        | Capture(Void, rule)      => Rule.Capture(rule.Location, RuleType.Void(), RemoveAllCaptures(rule));
        | Capture(None, rule)      => Rule.Capture(rule.Location, RuleType.None(), RemoveAllCaptures(rule));
        | Capture(retType, rule)   => Rule.Capture(rule.Location, retType, removeRedundant(rule));
        | Choice(rules)            => Rule.Choice(rule.Location, rules.Map(removeRedundant))
        | Sequence(rules)          => Rule.Sequence(rule.Location, rules.Map(removeRedundant))
        | Repeat(min, max, rule)   => Rule.Repeat(rule.Location, min, max, removeRedundant(rule))
        | Not(rule)                => Rule.Not(rule.Location, RemoveAllCaptures(rule))
        | And(rule)                => Rule.And(rule.Location, RemoveAllCaptures(rule))
        | Scope(name, rule)        => Rule.Scope(rule.Location, name, removeRedundant(rule))
        | Call | PrefixCall
        | Chars | Fsm | Cut        => rule
      }

      def doAddCaptures(rule)
      {
        def (rule, _) = addCaptures(rule);
        removeRedundant(rule)
      }

      grammar.TransformRules
        ( rd => doAddCaptures(rd.rule)
        , rd => doAddCaptures(rd.rule)
        , rd => doAddCaptures(rd.rule)
        )
    }
  }
}
