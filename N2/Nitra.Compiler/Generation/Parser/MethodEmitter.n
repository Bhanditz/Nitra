using N2.Model;
using N2.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

namespace N2.Compiler
{
  [Record]
  internal abstract partial class MethodEmitter : IVariableEmitter
  {
    public Owner           : RuleParserEmitter  { get; }
    public RuleStructure   : RuleStructure.Ast  { get; }
    public TypeBuilder     : TypeBuilder        { get; }
    public Environment     : EmitterEnvironment { get { Owner.Environment } }
    public ConstantEmitter : IConstantEmitter   { get { Owner } }
    [RecordIgnore]
    protected LocalVariables : SCG.HashSet[string] = SCG.HashSet();

    public CreateVariable(name : string) : string
    {
      _ = LocalVariables.Add(name);
      name
    }

    public CompileFields(code : SCG.List[PExpr], structure : RuleStructure) : void
    {
      foreach (field in structure.Fields)
      {
        | Empty             as field => CompileEmpty            (code, field);
        | ExtensibleCall    as field => CompileExtensibleCall   (code, field);
        | SimpleCall        as field => CompileSimpleCall       (code, field);
        | RegularCall       as field => CompileRegularCall      (code, field);
        | List              as field => CompileList             (code, field); when (!field.IsSkipAnyPattern) CompileFields(code, field.Rule);
        | ListWithSeparator as field => CompileListWithSeparator(code, field); CompileFields(code, field.Rule); CompileFields(code, field.Separator);
        | Option            as field => CompileOption           (code, field); CompileFields(code, field.Rule);
        | TokenString       as field => CompileTokenString      (code, field);
        | Marker                     => ()
      }
    }

    public CompileFieldsNoneRecursive(code : SCG.List[PExpr], structure : RuleStructure) : void
    {
      foreach (field in structure.Fields)
      {
        | Empty             as field => CompileEmpty            (code, field);
        | ExtensibleCall    as field => CompileExtensibleCall   (code, field);
        | SimpleCall        as field => CompileSimpleCall       (code, field);
        | RegularCall       as field => CompileRegularCall      (code, field);
        | List              as field => CompileList             (code, field);
        | ListWithSeparator as field => CompileListWithSeparator(code, field);
        | Option            as field => CompileOption           (code, field);
        | TokenString       as field => CompileTokenString      (code, field);
        | Marker                     => ()
      }
    }

    public abstract CompileEmpty            (code : SCG.List[PExpr], field : RuleStructureField.Empty)             : void;
    public abstract CompileRegularCall      (code : SCG.List[PExpr], field : RuleStructureField.RegularCall)       : void;
    public abstract CompileExtensibleCall   (code : SCG.List[PExpr], field : RuleStructureField.ExtensibleCall)    : void;
    public abstract CompileSimpleCall       (code : SCG.List[PExpr], field : RuleStructureField.SimpleCall)        : void;
    public abstract CompileList             (code : SCG.List[PExpr], field : RuleStructureField.List)              : void;
    public abstract CompileListWithSeparator(code : SCG.List[PExpr], field : RuleStructureField.ListWithSeparator) : void;
    public abstract CompileOption           (code : SCG.List[PExpr], field : RuleStructureField.Option)            : void;
    public abstract CompileTokenString      (code : SCG.List[PExpr], field : RuleStructureField.TokenString)       : void;

    public static MakeRegularRuleCallFullName(ruleSymbol : RegularRuleSymbol) : PExpr
    {
      def grammarType = PExprUtils.FromQualifiedIdentifier(ruleSymbol.DeclaringModule.Node.FullNameParts);
      <[ $grammarType.$(AssemblyConstants.GrammarImplName : usesite).$(ruleSymbol.DecorateRuleName() : global) ]>
    }

    public static MakeRegularRuleCall(ruleSymbol : RegularRuleSymbol, pos : PExpr, text : PExpr) : PExpr
    {
      <[ $(MakeRegularRuleCallFullName(ruleSymbol))($pos, $text) ]>
    }
  }
}
