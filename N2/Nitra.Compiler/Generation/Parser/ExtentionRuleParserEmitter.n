using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using N2.Model;
using N2.Typing;

using System;
using System.Collections.Generic;
using System.Linq;

namespace N2.Compiler
{
  internal sealed class ExtensionRuleParserEmitter : RuleParserEmitter
  {
    public Rule : Rule.Sequence { get; }
    private _ruleStructure : RuleStructure.Ast;

    public this(owner : SyntaxModuleEmitter, rs : ExtensionRuleSymbol)
    {
      base(owner, rs);
      Rule = rs.Rule;
      _ruleStructure = rs.Structure;
    }

    [RecordIgnore] private mutable _parserTb : TypeBuilder;

    public override DefineParser() : void
    {
      assert(_parserTb == null);
      _parserTb = Owner.GrammarImplTB.DefineNestedType(<[decl:
        public class $(RuleSymbol.DecorateRuleClassName() : global) : Nitra.Internal.ExtensionRuleParser, Nitra.Internal.IRecoveryRuleParser
        {
          public this(grammar : Nitra.CompositeGrammar)
          {
            base(grammar, $(RuleSymbol.GetRuleDescriptorRef()), $(RuleSymbol.FirstCharLowerBound), $(RuleSymbol.FirstCharUpperBound));//TODO: Fix me.
          }
        }
      ]>);
    }

    public override DefineParserMethods() : void
    {
      assert(_parserTb != null);
      DefineParse(_parserTb, _ruleStructure);
      DefineInit(_parserTb, _ruleStructure);
      DefineReflection(_parserTb, _ruleStructure);
      DefineGetNextState(_parserTb, _ruleStructure);
      DefineStartState(_parserTb, _ruleStructure);
      DefinePatchAst(_parserTb, _ruleStructure);
      DefineUpdateMandatoryTokens(_parserTb, _ruleStructure);
      def count = _ruleStructure.NonMarkerFields.Last().Offset - _ruleStructure.NonMarkerFields[0].Offset + 1;
      _parserTb.Define(<[ decl: public override FieldsCount : int { get { $count } } ]>);
    }

    [Memoize]
    public override CreateConstant(name : string, value : int) : string
    {
      Util.CreateConstant(_parserTb, name, value)
    }

    public override Emit() : void
    {
      assert(_parserTb != null);
      _parserTb.Compile();
    }
  }
}
