using Nemerle;
using Nemerle.Imperative;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using Nitra.Model;

namespace Nitra.Compiler
{
  internal partial class TryParseMethodEmitter : MethodEmitter
  {
    public override CompileListWithSeparator(code : SCG.List[PExpr], field : RuleStructureField.ListWithSeparator) : void
    {
      def hangingSeparatorCode = if (field.HangingSeparator)
        <[
          when ($(ListEndPos(field)) == $CurTextPos)
            $(GotoContinueParseLabel(field));
          $(ListEndPos(field)) = $CurTextPos;
        ]>
      else
        <[ {} ]>;

      def headPredicateCode = CompilePredicate(field.HeadPredicates, GotoFailParseLabel(field));

      def firstRuleChild = field.Rule.NonMarkerFields[0];
      def firstSeparatorChild = field.Separator.NonMarkerFields[0];
      code.Add(<[//TODO: Fix counters
        $(ParseLabel(field));
          _ = $(field.ToString());
          $headPredicateCode;
          $(ListStartPos(field)) = $CurTextPos;
          $(ListEndPos(field))   = $CurTextPos;
          $(GotoParseLabel(firstRuleChild));

        $(OkLabel(field.Rule));
          when (!continueList && $(EndOfFieldLevel(field)))
            $(_endLabel.Goto());
          when ($(ListEndPos(field)) == $CurTextPos)
            $(GotoContinueParseLabel(field));
          $(ListEndPos(field)) = $CurTextPos;
          $(GotoParseLabel(firstSeparatorChild));

        $(FailLabel(field.Rule));
          if ($(EndOfFieldLevel(field)))
          {
            //if (continueList)
            //{
            //  $CurTextPos = $(ListEndPos(field));
            //  $(AddParsedStateInfo(field, <[ $(ListEndPos(field)) - $(ListStartPos(field)) ]>));
            //}
            //else
              $CurTextPos = -1;
            $(_endLabel.Goto());
          }
          else
          {
            $CurTextPos = $(ListEndPos(field));
            $(AddParsedStateInfo(field, <[ $(ListEndPos(field)) - $(ListStartPos(field)) ]>));
            $(GotoContinueParseLabel(field));
          }

        $(OkLabel(field.Separator));
          when (!continueList && $(EndOfFieldLevel(field)))
            $(_endLabel.Goto());
          $hangingSeparatorCode;
          $(GotoParseLabel(firstRuleChild));

        $(FailLabel(field.Separator));
          if ($(EndOfFieldLevel(field)))
          {
            //if (continueList)
            //{
            //  $CurTextPos = $(ListEndPos(field));
            //  $(AddParsedStateInfo(field, <[ $(ListEndPos(field)) - $(ListStartPos(field)) ]>));
            //}
            //else
              $CurTextPos = -1;
            $(_endLabel.Goto());
          }
          else
          {
            $CurTextPos = $(ListEndPos(field));
            $(AddParsedStateInfo(field, <[ $(ListEndPos(field)) - $(ListStartPos(field)) ]>));
            $(GotoContinueParseLabel(field));
          }
      ]>);
    }
  }
}
