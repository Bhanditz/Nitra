using Nemerle;
using Nemerle.Imperative;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using Nitra.Model;

namespace Nitra.Compiler
{
  internal partial class ParseMethodEmitter : MethodEmitter
  {
    public override CompileListWithSeparator(code : SCG.List[PExpr], field : RuleStructureField.ListWithSeparator) : void
    {
      CompileFuntion(field.Rule);
      CompileFuntion(field.Separator);
      def localVariableEmitter = LocalVariableEmitter();
      def stateData = ListStateData(localVariableEmitter, field);
      def ifCounterEqualsToMaxThenContinue()
      {
        if (field.MaxCount is Some(max))
          <[
            when ($(stateData.Counter) == $max)
              $(GotoContinueParseLabel(field));
          ]>
        else
          <[ {} ]>
      }
      def ifCounterLessThanMinThenFail()
      {
        if (field.MinCount > 0)
          <[
            when ($(stateData.Counter) < $(field.MinCount))
            {
              parseState = $(field.State);
              $(GotoFailParseLabel(field));
            }
          ]>
        else
          <[ {} ]>
      }
      def hangingSeparatorCode = if (field.HangingSeparator)
        <[
          $(stateData.ListEndPos) = $CurTextPos;
          $(AstPtr(field.Owner).Set(field, <[ $(stateData.ListEndPos) - $(stateData.ListStartPos) ]>));
        ]>
      else
        <[ {} ]>;

      def headPredicateCode = CompilePredicate(field.HeadPredicates, GotoFailParseLabel(field));

      def body = <[
        $(ParseLabel(field));
          _ = $(field.ToString());
          $headPredicateCode;
          $(AllocateParentAst(field));
        $(RecoveryLabel(field));
          $(stateData.Init());
          $(stateData.ListStartPos) = $CurTextPos;
          $(stateData.ListEndPos) = $CurTextPos;
          $(stateData.BodyEndPos) = -1;
          $(stateData.SeparatorEndPos) = -1;
          while (true)
          {
            $CurTextPos = $(FunctionName(field.Rule) : global)($CurTextPos, text, parseResult);
            if ($CurTextPos >= 0 && $CurTextPos > $(stateData.BodyEndPos))
            {
              $(stateData.Inc());
              $(stateData.ListEndPos) = $CurTextPos;
              $(stateData.BodyEndPos) = $CurTextPos;
              $(AstPtr(field.Owner).Set(field, <[ $(stateData.ListEndPos) - $(stateData.ListStartPos) ]>));
              $(ifCounterEqualsToMaxThenContinue());
            }
            else
            {
              $(ifCounterLessThanMinThenFail());
              $CurTextPos = $(stateData.ListEndPos);
              $(AstPtr(field.Owner).Set(field, <[ $(stateData.ListEndPos) - $(stateData.ListStartPos) ]>));
              $(GotoContinueParseLabel(field));
            }
            $CurTextPos = $(FunctionName(field.Separator) : global)($CurTextPos, text, parseResult);
            if ($CurTextPos >= 0 && $CurTextPos > $(stateData.SeparatorEndPos))
            {
              $hangingSeparatorCode;
              $(stateData.SeparatorEndPos) = $CurTextPos;
            }
            else
            {
              $(ifCounterLessThanMinThenFail());
              $CurTextPos = $(stateData.ListEndPos);
              $(AstPtr(field.Owner).Set(field, <[ $(stateData.ListEndPos) - $(stateData.ListStartPos) ]>));
              $(GotoContinueParseLabel(field));
            }
          }
      ]>;
      code.Add(<[ when (true) { ..$(localVariableEmitter.Define()); $body } ]>);
    }
  }
}
