using N2.Model;
using N2.ProjectSystem;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

using G = N2Syntax;

namespace N2.Typing
{
  public sealed class SyntaxModuleBodyContext : EnteredScopeContext, ITokenNamesMapContext
  {
    public this(parent : Context)
    {
      base(parent);
    }

    public override CurrentSymbols : list[Symbol] { get { [] } }

    public TokenNamesMap : Map[string, TokenFieldSymbol]
    {
      [Memoize] get
      {
        def appendSymbol(symbol : TokenFieldSymbol, map : Map[string, TokenFieldSymbol])
        {
          symbol.StringLiterals.FoldLeft(map, (lit, map) => map.Replace(lit, symbol))
        }
        def walk(baseContext)
        {
          match (baseContext.TryGetParent.[OpenNamespaceOrTypeContext]())
          {
            | null => Map()
            | OpenNamespaceOrTypeContext(OpenSymbol = Some(SyntaxModuleSymbol as moduleSymbol)) as context =>
              moduleSymbol.TokenFields.FoldLeft(walk(context), appendSymbol)

            | context => walk(context)
          }
        }
        walk(this)
      }
    }

    [Memoize]
    public GetSpaceRule(context : Context, ruleNme : string) : option[RuleSymbol]
    {
      def s = Location(SourceSnapshot(ruleNme, 0, ""), NToken(0, ruleNme.Length));
      def symbol = Utils.BindImpl(context.BindMany.[RuleSymbol](s));
      symbol
    }
    
    public override ToString() : string
    {
      "<SyntaxModuleBodyContext>"
    }
  }
}
