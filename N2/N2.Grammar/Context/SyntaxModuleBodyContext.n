using N2.Model;
using N2.ProjectSystem;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace N2.Typing
{
  public sealed class SyntaxModuleBodyContext : EnteredScopeContext, ITokenNamesMapContext
  {
    public this(parent : Context)
    {
      base(parent);
    }

    public override CurrentSymbols : list[Symbol] { get { [] } }

    public TokenNamesMap : Map[string, TokenFieldSymbol]
    {
      [Memoize] get
      {
        def appendSymbol(symbol : TokenFieldSymbol, map : Map[string, TokenFieldSymbol])
        {
          symbol.StringLiterals.FoldLeft(map, (lit, map) => map.Replace(lit, symbol))
        }
        def walk(baseContext)
        {
          match (baseContext.TryGetParent.[OpenNamespaceOrTypeContext]())
          {
            | null => Map()
            | OpenNamespaceOrTypeContext(OpenSymbol = Some(SyntaxModuleSymbol as moduleSymbol)) as context =>
              moduleSymbol.TokenFields.FoldLeft(walk(context), appendSymbol)

            | context => walk(context)
          }
        }
        walk(this)
      }
    }

    public GetSpaceRule(context : Context, ruleName : string, loc : Location) : option[RuleSymbol]
    {
      def result = GetSpaceRule(context, ruleName);
      when (result.IsNone && !_whitespaceHintShown)
      {
        N2Project.CurrentProject.Hint(loc, $"Declare whitespace handling rule '$ruleName' or import 'Whitespaces' syntax module from N2.Core library.", -1);
        _whitespaceHintShown = true;
      }
      result;
    }

    [Memoize]
    private GetSpaceRule(context : Context, ruleName : string) : option[RuleSymbol]
    {
      def s = Location(SourceSnapshot(ruleName, 0, ""), NToken(0, ruleName.Length));
      def symbol = Utils.BindImpl(context.BindMany.[RuleSymbol](s));
      symbol
    }

    private mutable _whitespaceHintShown : bool = false;

    public override ToString() : string
    {
      "<SyntaxModuleBodyContext>"
    }
  }
}
