using N2.ProjectSystem;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

using G = GrammarParser2;

namespace N2.Typing
{
  public class KeywordRegexContext : Context
  {
    public KeywordRegexes : list[G.TokenRule * RuleRefSymbol] { get; }

    public this(parent : Context, matchTokens : G.TokenRule, ruleName : G.QIdentifier)
    {
      base(parent);
      def rule = parent.Bind(ruleName);
      def parent     = TryGetParent.[KeywordRegexContext]();
      when (rule is Some(ruleSymbol))
        KeywordRegexes = (matchTokens, ruleSymbol) :: (parent?.KeywordRegexes ?? []);
        //matchTokens.MakeMatcher()
    }
    
    public Matchers : list[RegularRuleMatcher * RuleRefSymbol] { [Memoize] get { KeywordRegexes.Map((re, rule) => (re.MakeMatcher(), rule)) } }

    public FindRule(keyword : string) : option[RuleRefSymbol]
    {
      foreach ((matcher, rule) in Matchers)
        when (matcher(keyword))
          return Some(rule);
          
      None()
    }

    public IsMatch(keyword : string) : bool
    {
      foreach ((matcher, _) in Matchers)
        when (matcher(keyword))
          return true;
          
      false
    }
  }
}
