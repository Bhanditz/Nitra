using N2.ProjectSystem;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

using G = GrammarParser2;

namespace N2.Typing
{
  public class RuleAliasContext : Context
  {
    RuleAlias : RuleAliasSymbol { get; }
    
    public this(parent : Context, name : G.Identifier, rule : G.Rule)
    {
      base(parent);
      RuleAlias = RuleAliasSymbol(name, rule);
      N2Project.CurrentProject.AddSymbolDef(RuleAlias, name.Location);
    }

    public override BindMany[T](path : list[Location]) : BindingResult[T]
    {
      | [one] => BindMany(one)
      | _ => base.BindMany(path)
    }

    public override BindMany[T](name : Location) : BindingResult[T]
    {
      match (RuleAlias : Symbol)
      {
        | symbol is T when symbol.Name == name => BindingResult([symbol])
        | _ => base.BindMany(name)
      }
    }

    public override ToString() : string
    {
      "alias " + RuleAlias.Name + " = " + RuleAlias.Rule;
    }
  }
}
