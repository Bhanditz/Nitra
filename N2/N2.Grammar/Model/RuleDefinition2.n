using N2.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace N2.Model
{
  [Record]
  public variant RuleDefinition2 : N2.Located
  {
    public Id          : int                    { get; }
    public RuleSymbol  : RuleRefSymbol          { get; }
    public ExtraFields : list[ExtraField2]      { get; }
    private _options   : RuleDefinitionOptions  { get; }

    | ExtentionPoint
    | ExtentionPrefix  { BaseRule : RuleRef; Rule : Rule2.Sequence; }
    | ExtentionPostfix { BaseRule : RuleRef; Rule : Rule2.Sequence; BindingPower : int; }
    | Simple           {                     Rule : Rule2.Sequence; }
    | Regular          {                     Rule : RegularRule; }
    | Marker

    public Type                     : RuleType2  { get { RuleSymbol.Type } }
    public IsAst                    : bool       { get { !(this is Regular || IsVoid) } }
    public IsExtention              : bool       { get { this is ExtentionPrefix || this is ExtentionPostfix } }
    public IsVoid                   : bool       { get { RuleSymbol.Type is RuleType2.Void } }
    public IsStart                  : bool       { get { _options.IsStart } }
    public IsExplicitSpaces         : bool       { get { _options.IsExplicitSpaces } }
    public IsSuppressTrailingSpaces : bool       { get { _options.IsSuppressTrailingSpaces } }
    public SpanClass                : Identifier { get { _options.SpanClass } }

    public this(originalRuleDefinition : RuleDefinition2)
    {
      base(originalRuleDefinition.Location);
      Id              = originalRuleDefinition.Id;
      RuleSymbol      = originalRuleDefinition.RuleSymbol;
      ExtraFields     = originalRuleDefinition.ExtraFields;
      _options        = originalRuleDefinition._options;
    }

    public GetRule() : option[Rule2]
    {
      match (this)
      {
        | Marker
        | Regular
        | ExtentionPoint         => None()
        | ExtentionPrefix as rd  => Some(rd.Rule)
        | ExtentionPostfix as rd => Some(rd.Rule)
        | Simple as rd           => Some(rd.Rule)
      }
    }

    public GetSubRules() : list[Rule2]
    {
      match (this)
      {
        | Marker
        | Regular
        | ExtentionPoint         => []
        | ExtentionPrefix as rd  => rd.Rule.GetSubRules()
        | ExtentionPostfix as rd => rd.Rule.GetSubRules()
        | Simple as rd           => rd.Rule.GetSubRules()
      }
    }

    [OverrideObjectEquals]
    public Equals(that : RuleDefinition2) : bool
    {
      that.Id == that.Id
    }

    public override GetHashCode() : int { Id }
    
    public override ToString() : string
    {
      def name = RuleSymbol.Name;
      
      match (this)
      {
        | ExtentionPoint                           => $"syntax $name"
        | ExtentionPrefix(baseRule, rule)          => $"syntax $name is $baseRule = $rule"
        | ExtentionPostfix(baseRule, rule, _)      => $"syntax $name is $baseRule = $rule"
        | Simple(rule) when Type is RuleType2.Void => $"void $name = $rule"
        | Simple(rule)                             => $"syntax $name = $rule"
        | Regular(rule)                            => $"token $name = $rule"
        | Marker                                   => $"marker $name"
      }
    }
  }
}
