using N2;
using N2.Utils;
using N2.Model;
using N2.ProjectSystem;
using N2.Runtime;
using N2.Typing;

using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Compiler.Parsetree;

using System.Collections.Generic;
using System.Text;
using System.Linq;

syntax module N2Syntax
{
  using Outline;
  using PrettyPrint;
  using TokenNames;
  using StandardSpanClasses;
  using Whitespaces;
  using Identifiers;
  using CStyleComments;

  token field Prefix              = "\\u";
  token field DodDot              = "..";
  token field Op                  = ".";
  token field Pipe                = "|";

  braces "(", ")";
  braces "{", "}";
  braces "[", "]";

  keyword regex IdentifierStartCharacter (IdentifierPartCharacters | '-')* rule S;

  alias Name            = Identifier;
  alias QualifiedName   = QualifiedIdentifier;
  alias NamespaceOrType = QualifiedIdentifier;
  alias Char            = Any;
  alias GrammarName     = QualifiedIdentifier;
  alias BaseName        = QualifiedIdentifier;
  alias SeparatorToken  = TokenRuleExpression;
  alias SeparatorRule   = RuleExpression;

  token PredefinedType = "void" | "int" | "string";

  token Keyword = "as" | "syntax" | "marker" | "token" | "field" | "span" | "alias"
                | "in" | "out" | "rule" | "precedence" | "using" | "namespace" | PredefinedType;

  extend syntax IgnoreToken
  {
    | [SpanClass(Comment), ExplicitSpaces] SingleLineComment;
    | [SpanClass(Comment), ExplicitSpaces] MultiLineComment;
  }

  token HexDigit                  = ['0'..'9', 'a'..'f', 'A'..'F'];
  token ReservedStringChar        = "\"" | "\\";
  token ReservedCharChar          = "\'" | "\\";

  [SpanClass(String), ExplicitSpaces]
  syntax StringLiteral                    = "\""  StringPart* "\""
  {
    Value() : string = FoldThrough(StringParts, StringBuilder(), _.Value(_)).ToString();
  }

  [ExplicitSpaces]
  syntax StringPart
  {
    Value(sb : StringBuilder) : StringBuilder;

    | UnicodeEscapeSequence = "\\u" HexDigit HexDigit HexDigit HexDigit
      {
        override Value = sb.Append(HexToChar(this, HexDigit1.StartPos, HexDigit4.EndPos));
      }
    | EscapeSequence        = "\\"  Char
      {
        override Value = sb.Append(EscapeSequence(FirstChar(Char)));
      }
    | Simple                = Chars=(!ReservedStringChar !NewLine Any)+
      {
        override Value = sb.Append(this.GetText());
      }
  }

  [SpanClass(Char), ExplicitSpaces]
  syntax CharLiteral                      = "\'"  CharPart "\'" { Value() : char = CharPart.Value(); }

  [ExplicitSpaces]
  syntax CharPart
  {
    Value() : char;

    | Simple                = !ReservedCharChar Char
      {
        override Value = FirstChar(this.Char);
      }
    | UnicodeEscapeSequence = "\\u" HexDigit HexDigit HexDigit HexDigit
      {
        override Value = HexToChar(this, HexDigit1.StartPos, HexDigit4.EndPos);
      }
    | EscapeSequence        = "\\" Char
      {
        override Value = EscapeSequence(FirstChar(this.Char));
      }
  }

  token Digits = ['0'..'9']+;

  [SpanClass(Number), ExplicitSpaces]
  syntax Number = Digits { Value() : int = DecToInt(this, Digits.StartPos, Digits.EndPos); }

  [ExplicitSpaces]
  syntax Identifier                       = !(Keyword !IdentifierPartCharacters) IdentifierBody
  {
    Value() : string = GetText(IdentifierBody);
    Identifier() : N2.Model.Identifier = N2.Model.Identifier(this, IdentifierBody);
  }

  [ExplicitSpaces]
  syntax QualifiedIdentifier              = Names=(Identifier; s "." s)+
  {
    Identifiers() : list[N2.Located] = Names[0].Map(_.Identifier());
    Parts() : list[string] = Names[0].Map(_.Value());
    CalcLocation() : N2.Location
    {
      this.Location // At present QualifiedIdentifier have no 's' at the end of rule. This allows the use rule location.
    }
    CalcLocations() : list[N2.Location] = Names[0].Map(_.Location);
  }

  ///////////////////////////////////////////////////////////////////////////
  //

  [StartRule, ExplicitSpaces]
  syntax Start                = s NamespaceBody !Any
  {
    CollectGlobalSymbols(rootNs : NamespaceSymbol) : void = NamespaceBody.CollectGlobalSymbols(rootNs);
    [Cached] MakeContext(parent : Context) : Context = NamespaceBody.MakeContext(parent);
    Typing() : void = NamespaceBody.Typing();
  }

  syntax Usings = outline_hiden outline_begin Using* outline_end_before
  {
    MakeContext(parent : Context) : Context = FoldThrough(Usings, parent, _.MakeContext(_));
  }

  syntax NamespaceBody = Usings NamespaceMember*
  {
    CollectGlobalSymbols(rootNs : NamespaceSymbol) : void = foreach (topDecl in NamespaceMembers) topDecl.CollectGlobalSymbols(rootNs);
    [Cached] MakeContext(parent : Context) : Context
    {
      def context1 = Usings.MakeContext(parent);
      def context2 = PassThrough(NamespaceMembers, context1, _.MakeContext(_));
      context2
    }
    Typing() : void = foreach (topDecl in NamespaceMembers) topDecl.Typing();
  }

  syntax Using
  {
    [Cached] MakeContext(parent : Context) : Context;

    | UsingOpen  = "using" sm NamespaceOrType ";" nl
      {
        override MakeContext = OpenNamespaceOrTypeContext(parent, NamespaceOrType);
      }
    | UsingAlias = "using" Name sm "=" sm NamespaceOrType ";" nl
      {
        override MakeContext = NamespaceOrTypeAliasContext(parent, Name, NamespaceOrType);
      }
  }

  syntax NamespaceMember
  {
    CollectGlobalSymbols(ns : NamespaceSymbol) : void;
    [Cached] MakeContext(parent : Context) : Context;
    Typing() : void;

    | Namespace = "namespace" sm QualifiedName outline_begin_before nl "{" inl NamespaceBody d "}" outline_end_before nl
      {
        [Cached] private MakeSymbol(ns : NamespaceSymbol) : NamespaceSymbol = MakeNamespaceSymbol(ns, QualifiedName.CalcLocations());
        override CollectGlobalSymbols = NamespaceBody.CollectGlobalSymbols(MakeSymbol(ns));
        override MakeContext
        {
          def nsContext = EnteredNamespaceContext(parent, Symbol);
          _ = NamespaceBody.MakeContext(nsContext);
          parent
        }
        override Typing = NamespaceBody.Typing();
      }

    | SyntaxModule = "syntax" sm "module"sm Name outline_begin_before nl "{" inl Usings SyntaxModuleMember* d "}" outline_end_before nl
      {
        override CollectGlobalSymbols
        {
          def symbol = ParsedSyntaxModuleSymbol(this);
          _ = ns.Node.AddAndReport(symbol, Name.Location);
          foreach (decl in SyntaxModuleMembers)
            decl.CollectGlobalSymbols(symbol);
        }
        [Cached] private MakeInitialContext(contextToSave : SyntaxModuleContext) : SyntaxModuleContext = contextToSave;
        [Cached] private MakeFinalContext(contextToSave : Context) : Context = contextToSave;
        override MakeContext
        {
          def context1 = MakeInitialContext(SyntaxModuleContext(parent, Name));
          def context2 = Usings.MakeContext(context1);
          def context3 = SyntaxModuleBodyContext(context2);
          def finalContext = MakeFinalContext(FoldThrough(SyntaxModuleMembers, context3, _.MakeContext(_)));
          Utils.RegisterGlobalSpanClassSymbols(context1.SyntaxModuleSymbol, finalContext);
          parent
        }
        [Cached] private MakeTokenFieldNames() : Map[string, N2.Model.Identifier] = FoldThrough(SyntaxModuleMembers, Map(), _.GetTokenFieldNames(_));
        override Typing
        {
          _ = MakeTokenFieldNames();
          foreach (decl in SyntaxModuleMembers)
            decl.Typing();
        }
      }
  }

  syntax RuleAttribute
  {
    Options(inOptions : RuleDefinitionOptions) : RuleDefinitionOptions = inOptions;
    [Cached] MakeContext(_parent : Context) : Context = _parent;

    | Start                  = AttributeName="StartRule"
      {
        override Options = inOptions.SetStart(this, true);
      }
    | SpanClass              = AttributeName="SpanClass" "(" Class=Identifier ")"
      {
        override Options = Context.Bind.[SpanClassSymbol](Class).Map(inOptions.SetSpanClass(Class, _)) ?? inOptions;
      }
    | ExplicitSpaces         = AttributeName="ExplicitSpaces"
      {
        override Options = inOptions.SetExplicitSpaces(this, true);
      }
    | ImplicitSpaces         = AttributeName="ImplicitSpaces"
      {
        override Options = inOptions.SetExplicitSpaces(this, false);
      }
    | SuppressTrailingSpaces = AttributeName="SuppressTrailingSpaces"
      {
        override Options = inOptions.SetSuppressTrailingSpaces(this, true);
      }
    | AllowTrailingSpaces   = AttributeName="AllowTrailingSpaces"
      {
        override Options = inOptions.SetSuppressTrailingSpaces(this, false);
      }
  }

  syntax RuleAttributeList = "[" (RuleAttribute; ","sm)+ "]" sm
  {
    Options(inOptions : RuleDefinitionOptions) : RuleDefinitionOptions = FoldThrough(RuleAttributes[0], inOptions, _.Options(_));
    MakeContext(parent : Context) : Context = FoldThrough(RuleAttributes[0], parent, _.MakeContext(_));
  }

  syntax RuleAttributes    = RuleAttributeList*
  {
    [Cached] Options(inOptions : RuleDefinitionOptions) : RuleDefinitionOptions = FoldThrough(RuleAttributeLists, inOptions, _.Options(_));
    [Cached] MakeContext(parent : Context) : Context = FoldThrough(RuleAttributeLists, parent, _.MakeContext(_));
  }

  [StartRule]
  syntax SyntaxModuleMember
  {
    CollectGlobalSymbols(owner : IRuleDeclarationSite) : void = _ = owner;
    [Cached] MakeContext(parent : Context) : Context = parent;
    Typing() : void {}

    GetTokenFieldNames(result : Map[string, N2.Model.Identifier]) : Map[string, N2.Model.Identifier] = result;

    | TokenRule
      {
        override CollectGlobalSymbols = TokenRule.CollectGlobalSymbols(owner);
        override MakeContext          = TokenRule.MakeContext(parent);
        override Typing               = TokenRule.Typing();
      }
    | SimpleRule
      {
        override CollectGlobalSymbols = SimpleRule.CollectGlobalSymbols(owner);
        override MakeContext          = SimpleRule.MakeContext(parent);
        override Typing               = SimpleRule.Typing();
      }
    | VoidRule
      {
        override CollectGlobalSymbols = VoidRule.CollectGlobalSymbols(owner);
        override MakeContext          = VoidRule.MakeContext(parent);
        override Typing               = VoidRule.Typing();
      }
    | ExtensibleRule
      {
        override CollectGlobalSymbols = ExtensibleRule.CollectGlobalSymbols(owner);
        override MakeContext          = ExtensibleRule.MakeContext(parent);
        override Typing               = ExtensibleRule.Typing();
      }
    | Marker              =                "marker"  sm             Name                                      ";"nl
      {
        override CollectGlobalSymbols = _ = owner.Node.AddAndReport(ParsedMarkerSymbol(Name, owner), Location);
      }
    | ExtendSyntax        =                "extend"  sm "syntax" sm (Name sm "=" sm)? BaseName outline_begin_before nl "{" inl InlineExtentionRule* d "}" outline_end_before nl
      {
        [Cached] private MakeSymbol(owner : IRuleDeclarationSite) : ParsedExtendSyntaxSymbol = ParsedExtendSyntaxSymbol(this, owner);
        override CollectGlobalSymbols
        {
          def symbol = MakeSymbol(owner);
          _ = owner.Node.AddAndReport(symbol, Location);
          foreach (rule in InlineExtentionRules)
            rule.CollectGlobalSymbols(symbol);
        }
        override MakeContext
        {
          def context = RuleDeclarationSiteContext(parent, Symbol);
          _ = FoldThrough(InlineExtentionRules, context, _.MakeContext(_));
          parent
        }
        override Typing
        {
          def baseRule = ExtensibleRuleRef.Create(BaseName, Context.Bind(BaseName));
          foreach (m in InlineExtentionRules)
            m.Typing(baseRule);
        }
      }
    | RuleAlias
      {
        override MakeContext = RuleAlias.MakeContext(parent);
      }
    | TokenField          =                "token"   sm "field"  sm Name  sm "="  sm (StringLiteral; ","sm)+  ";"nl
      {
        override GetTokenFieldNames
        {
          def name = Name.Identifier();
          FoldThrough(StringLiterals[0], result, (strLit, result) => result.Replace(strLit.Value(), name))
        }
        override MakeContext = TokenFieldContext(parent, Name, StringLiterals[0]);
      }
    | SpanClass           =                "span"    sm "class"  sm Name MatchTokens=("=" TokenRuleExpression)?         ";"nl
      {
        [Cached] private MakeSymbol(context : Context) : SpanClassSymbol
        {
          def symbol = ParsedSpanClassSymbol(this, context);
          N2Project.CurrentProject.AddSymbolDef(symbol, Name.Location);
          symbol
        }
        [Cached] private MakeRegularRule() : option[RegularRule] = MatchTokens.Map((_, tr) =>
        {
          def rr = tr.Typing();
          rr.ValidateRegularRule();
          rr
        });
        override MakeContext
        {
          def context = SpanClassContext(parent, MakeSymbol(parent));
          _ = MatchTokens.Map((_, tokenRule) => tokenRule.MakeContext(context));
          context
        }
        override Typing
        {
          _ = MakeRegularRule();
        }
      }
    | BracePair           =                "braces"  sm Open=TokenRuleExpression "," sm Close=TokenRuleExpression                 ";"nl
      {
        override MakeContext
        {
          def context = BracePairContext(parent, Open, Close);
          _ = Open.MakeContext(context);
          _ = Close.MakeContext(context);
          context
        }
        override Typing
        {
          Open.Typing().ValidateRegularRule();
          Close.Typing().ValidateRegularRule();
        }
      }
    | KeywordRegex        =                "keyword" sm "regex"  sm MatchTokens=TokenRuleExpression sm "rule" sm QualifiedName ";"nl
      {
        override MakeContext
        {
          def context = KeywordRegexContext(parent, MatchTokens, QualifiedName);
          _ = MatchTokens.MakeContext(context);
          context
        }
      }
  }

  syntax RuleAlias = "alias" sm Name sm "=" sm RuleExpression ";"nl
  {
    [Cached] private MakeSymbol() : RuleAliasSymbol
    {
      def symbol = ParsedRuleAliasSymbol(this);
      N2Project.CurrentProject.AddSymbolDef(symbol, Name.Location);
      symbol
    }
    MakeContext(parent : Context) : Context
    {
      RuleAliasContext(parent, MakeSymbol());
    }
  }

  syntax TokenRule = RuleAttributes "token" sm Name sm "=" sm TokenRuleExpression TokenRuleBody
  {
    [Cached] private MakeSymbol(owner : IRuleDeclarationSite) : ParsedRegularRuleSymbol = ParsedRegularRuleSymbol(this, owner);
    CollectGlobalSymbols(owner : IRuleDeclarationSite) : void
    {
      def symbol = MakeSymbol(owner);
      _ = owner.Node.AddAndReport(symbol, Location);
      TokenRuleBody.CollectGlobalSymbols(symbol);
    }
    MakeContext(parent : Context) : Context
    {
      def context = RuleDeclarationSiteContext(parent, Symbol);
      _ = RuleAttributes.MakeContext(context);
      _ = TokenRuleExpression.MakeContext(context);
      _ = TokenRuleBody.MakeContext(context);
      parent
    }
    [Cached] private MakeRegularRule() : RegularRule      = TokenRuleExpression.Typing();
    Typing() : void
    {
      _ = MakeRegularRule();
      TokenRuleBody.Typing();
    }
  }

  syntax TokenRuleBody
  {
    CollectGlobalSymbols(ruleSymbol : RegularRuleSymbol) : void = _ = ruleSymbol;
    [Cached] MakeContext(parent : Context) : Context = parent;
    GetLastLocation() : N2.Location;
    Typing() : void { }

    | Empty = ";"nl
      {
        override GetLastLocation = MakeLastLocation(this, Semicolon);
      }
    | Block = outline_begin_before nl "{" inl TokenRuleBodyMember* d "}" outline_end_before nl
      {
        override CollectGlobalSymbols = foreach (member in TokenRuleBodyMembers) member.CollectGlobalSymbols(ruleSymbol);
        override MakeContext = FoldThrough(TokenRuleBodyMembers, parent, _.MakeContext(_));
        override GetLastLocation = MakeLastLocation(this, OpenBrace);
        override Typing = foreach (x in TokenRuleBodyMembers) x.Typing();
      }
  }

  syntax TokenRuleBodyMember
  {
    CollectGlobalSymbols(ruleSymbol : RegularRuleSymbol) : void = _ = ruleSymbol;
    [Cached] MakeContext(parent : Context) : Context = parent;
    Typing() : void { }

    | RuleAlias
      {
        override MakeContext = RuleAlias.MakeContext(parent);
      }
    | TokenRule
      {
        override CollectGlobalSymbols = TokenRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = TokenRule.MakeContext(parent);
        override Typing               = TokenRule.Typing();
      }
  }

  syntax SimpleRule = RuleAttributes "syntax" sm Name sm "=" sm RuleExpression SimpleRuleBody
  {
    [Cached] private MakeSymbol(owner : IRuleDeclarationSite) : ParsedSimpleRuleSymbol = ParsedSimpleRuleSymbol(this, owner);
    CollectGlobalSymbols(owner : IRuleDeclarationSite) : void
    {
      def symbol = MakeSymbol(owner);
      _ = owner.Node.AddAndReport(symbol, Location);
      SimpleRuleBody.CollectGlobalSymbols(symbol);
    }
    MakeContext(parent : Context) : Context
    {
      def context = RuleDeclarationSiteContext(parent, Symbol);
      _ = RuleAttributes.MakeContext(context);
      _ = SimpleRuleBody.MakeContext(context);
      parent
    }
    [Cached] private MakeTypedRule()   : Rule.Sequence = EnsureSequence(RuleExpression.Typing(None(), 0, None(), SimpleRuleBody.Context, RuleAttributes.Options(RuleDefinitionOptions.Empty)));
    Typing() : void
    {
      _ = MakeTypedRule();
      SimpleRuleBody.Typing();
    }
  }

  syntax VoidRule = RuleAttributes "void" sm Name sm "=" sm RuleExpression SimpleRuleBody
  {
    [Cached] private MakeSymbol(owner : IRuleDeclarationSite) : ParsedVoidRuleSymbol = ParsedVoidRuleSymbol(this, owner);
    CollectGlobalSymbols(owner : IRuleDeclarationSite) : void
    {
      def symbol = MakeSymbol(owner);
      _ = owner.Node.AddAndReport(symbol, Location);
      SimpleRuleBody.CollectGlobalSymbols(symbol);
    }
    MakeContext(parent : Context) : Context
    {
      def context = RuleDeclarationSiteContext(parent, Symbol);
      _ = RuleAttributes.MakeContext(context);
      _ = SimpleRuleBody.MakeContext(context);
      parent
    }
    [Cached] private MakeTypedRule()   : Rule.Sequence = EnsureSequence(RuleExpression.Typing(None(), 0, None(), SimpleRuleBody.Context, RuleAttributes.Options(RuleDefinitionOptions.Empty)));
    Typing() : void
    {
      _ = MakeTypedRule();
      SimpleRuleBody.Typing();
    }
  }

  syntax SimpleRuleBody
  {
    CollectGlobalSymbols(ruleSymbol : SimpleRuleSymbol) : void = _ = ruleSymbol;
    [Cached] MakeContext(parent : Context) : Context = parent;
    GetLastLocation() : N2.Location;
    Typing() : void { }

    | Empty = ";"nl
      {
        override GetLastLocation = MakeLastLocation(this, Semicolon);
      }
    | Block = outline_begin_before nl "{" inl SimpleRuleBodyMember* d "}" outline_end_before nl
      {
        override CollectGlobalSymbols = foreach (member in SimpleRuleBodyMembers) member.CollectGlobalSymbols(ruleSymbol);
        override MakeContext = FoldThrough(SimpleRuleBodyMembers, parent, _.MakeContext(_));
        override GetLastLocation = MakeLastLocation(this, OpenBrace);
        override Typing = foreach (x in SimpleRuleBodyMembers) x.Typing();
      }
  }

  syntax SimpleRuleBodyMember
  {
    CollectGlobalSymbols(ruleSymbol : SimpleRuleSymbol) : void = _ = ruleSymbol;
    [Cached] MakeContext(parent : Context) : Context = parent;
    Typing() : void { }

    | RuleAlias
      {
        override MakeContext = RuleAlias.MakeContext(parent);
      }
    | TokenRule
      {
        override CollectGlobalSymbols = TokenRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = TokenRule.MakeContext(parent);
        override Typing               = TokenRule.Typing();
      }
    | VoidRule
      {
        override CollectGlobalSymbols = VoidRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = VoidRule.MakeContext(parent);
        override Typing               = VoidRule.Typing();
      }
    | SimpleRule
      {
        override CollectGlobalSymbols = SimpleRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = SimpleRule.MakeContext(parent);
        override Typing               = SimpleRule.Typing();
      }
    | ExtensibleRule
      {
        override CollectGlobalSymbols = ExtensibleRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = ExtensibleRule.MakeContext(parent);
        override Typing               = ExtensibleRule.Typing();
      }
    | ExtraField
      {
        override CollectGlobalSymbols = ExtraField.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = ExtraField.MakeContext(parent);
        override Typing               = ExtraField.Typing();
      }
    | ExtraFieldOverride
      {
        override CollectGlobalSymbols = ExtraFieldOverride.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = ExtraFieldOverride.MakeContext(parent);
        override Typing               = ExtraFieldOverride.Typing();
      }
    | ExtraFieldMissing
      {
        override CollectGlobalSymbols = ExtraFieldMissing.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = ExtraFieldMissing.MakeContext(parent);
        override Typing               = ExtraFieldMissing.Typing();
      }
  }

  syntax ExtensibleRule = RuleAttributes "syntax"  sm Name ExtensibleRuleBody
  {
    [Cached] private MakeSymbol(owner : IRuleDeclarationSite) : ParsedExtensibleRuleSymbol = ParsedExtensibleRuleSymbol(this, owner);
    CollectGlobalSymbols(owner : IRuleDeclarationSite) : void
    {
      def symbol = MakeSymbol(owner);
      _ = owner.Node.AddAndReport(symbol, Location);
      ExtensibleRuleBody.CollectGlobalSymbols(symbol);
    }
    MakeContext(parent : Context) : Context
    {
      def context = RuleDeclarationSiteContext(parent, Symbol);
      _ = RuleAttributes.MakeContext(context);
      _ = ExtensibleRuleBody.MakeContext(context);
      parent
    }
    Typing() : void
    {
      _ = ExtensibleRuleBody.Typing(ExtensibleRuleRef.Some(Name.Location, false, Symbol));
    }
  }

  syntax ExtensibleRuleBody
  {
    CollectGlobalSymbols(ruleSymbol : ExtensibleRuleSymbol) : void = _ = ruleSymbol;
    [Cached] MakeContext(parent : Context) : Context = parent;
    GetLastLocation() : N2.Location;
    Typing(_baseRule : ExtensibleRuleRef) : void { }

    | Empty = ";"nl
      {
        override GetLastLocation = MakeLastLocation(this, Semicolon);
      }
    | Block = outline_begin_before nl "{" inl ExtensibleRuleBodyMember* d "}" outline_end_before nl
      {
        override CollectGlobalSymbols = foreach (member in ExtensibleRuleBodyMembers) member.CollectGlobalSymbols(ruleSymbol);
        override MakeContext = FoldThrough(ExtensibleRuleBodyMembers, parent, _.MakeContext(_));
        override GetLastLocation = MakeLastLocation(this, OpenBrace);
        override Typing = foreach (x in ExtensibleRuleBodyMembers) x.Typing(_baseRule);
      }
  }

  syntax ExtensibleRuleBodyMember
  {
    CollectGlobalSymbols(ruleSymbol : ExtensibleRuleSymbol) : void = _ = ruleSymbol;
    [Cached] MakeContext(parent : Context) : Context = parent;
    Typing(_baseRule : ExtensibleRuleRef) : void { }

    | RuleAlias
      {
        override MakeContext = RuleAlias.MakeContext(parent);
      }
    | TokenRule
      {
        override CollectGlobalSymbols = TokenRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = TokenRule.MakeContext(parent);
        override Typing               = TokenRule.Typing();
      }
    | VoidRule
      {
        override CollectGlobalSymbols = VoidRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = VoidRule.MakeContext(parent);
        override Typing               = VoidRule.Typing();
      }
    | SimpleRule
      {
        override CollectGlobalSymbols = SimpleRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = SimpleRule.MakeContext(parent);
        override Typing               = SimpleRule.Typing();
      }
    | ExtensibleRule
      {
        override CollectGlobalSymbols = ExtensibleRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = ExtensibleRule.MakeContext(parent);
        override Typing               = ExtensibleRule.Typing();
      }
    | InlineExtentionRule
      {
        override CollectGlobalSymbols = InlineExtentionRule.CollectGlobalSymbols(ruleSymbol);
        override Typing               = InlineExtentionRule.Typing(_baseRule);
        override MakeContext          = InlineExtentionRule.MakeContext(parent);
      }
    | ExtraField
      {
        override CollectGlobalSymbols = ExtraField.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = ExtraField.MakeContext(parent);
        override Typing               = ExtraField.Typing();
      }
    | ExtraFieldOverride
      {
        override CollectGlobalSymbols = ExtraFieldOverride.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = ExtraFieldOverride.MakeContext(parent);
        override Typing               = ExtraFieldOverride.Typing();
      }
    | ExtraFieldMissing
      {
        override CollectGlobalSymbols = ExtraFieldMissing.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = ExtraFieldMissing.MakeContext(parent);
        override Typing               = ExtraFieldMissing.Typing();
      }
  }

  syntax InlineExtentionRule = "|" sm RuleAttributes (Name "=")? RuleExpression ExtentionRuleBody?
  {
    [Cached] private MakeSymbol(owner : IRuleDeclarationSite) : ParsedInlineExtentionRule = ParsedInlineExtentionRule(this, owner);
    CollectGlobalSymbols(owner : IRuleDeclarationSite) : void
    {
      def symbol = MakeSymbol(owner);
      _ = owner.Node.AddAndReport(symbol, Location);
      when (ExtentionRuleBodyOpt is Some(ruleBody))
        ruleBody.CollectGlobalSymbols(symbol);
    }
    [Cached] MakeContext(parent : Context) : Context
    {
      def context = RuleDeclarationSiteContext(parent, Symbol);
      _ = RuleAttributes.MakeContext(context);
      when (ExtentionRuleBodyOpt is Some(ruleBody))
        _ = ruleBody.MakeContext(context);
      parent
    }
    [Cached] private MakeBaseRuleRef(baseRule : ExtensibleRuleRef)  : ExtensibleRuleRef     = baseRule;
    [Cached] private MakeOptions()                                  : RuleDefinitionOptions = RuleAttributes.Options(BaseRuleRef.Map(r => r.Options.InheritedCopy()) ?? RuleDefinitionOptions.Empty);
    [Cached] private MakeType()                                     : RuleType              = BaseRuleRef.Map(_.Type) ?? RuleType.Void();
    [Cached] private MakeTypedRule()                                : Rule.Sequence         = EnsureSequence(RuleExpression.Typing(BaseRuleRef, ExtentionRuleBodyOpt.Map(b => b.GetPrecedence()) ?? 0, None(), ExtentionRuleBodyOpt.Map(_.Context) ?? Context, Options));
    Typing(baseRule : ExtensibleRuleRef) : void
    {
      _ = MakeBaseRuleRef(baseRule);
      _ = MakeOptions();
      _ = MakeType();
      _ = MakeTypedRule();
      when (ExtentionRuleBodyOpt is Some(ruleBody))
        ruleBody.Typing(BaseRuleRef);
    }
  }

  syntax ExtentionRuleBody
  {
    CollectGlobalSymbols(ruleSymbol : ExtentionRuleSymbol) : void = _ = ruleSymbol;
    GetPrecedence() : int = 0;
    [Cached] MakeContext(parent : Context) : Context = parent;
    GetLastLocation() : N2.Location;
    Typing(_baseRule : ExtensibleRuleRef) : void { }

    | Empty = ";"nl
      {
        override GetLastLocation = MakeLastLocation(this, Semicolon);
      }
    | Block = outline_hiden outline_begin_before inl "{" inl ExtentionRuleBodyMember* d "}" outline_end_before nl  d
      {
        override CollectGlobalSymbols = foreach (member in ExtentionRuleBodyMembers) member.CollectGlobalSymbols(ruleSymbol);
        [Cached] private EvalPrecedence() : int
        {
          match (ExtentionRuleBodyMembers.Map(_.GetPrecedence()).Filter(_.IsSome))
          {
            | [VSome((p, _))] => p
            | [] => 0
            | VSome((p, loc)) :: tail =>
              using (rootMsg = Project.CurrentProject.RootError(loc, "Too many 'precedence' directives. You can define only one a 'precedence' directive.", -1))
                foreach (other in tail)
                  rootMsg.Hint(other.Value[1], "   other definition.");
              p
            | _ => assert3(false)
          }
        }
        override GetPrecedence = EvalPrecedence();
        override MakeContext = FoldThrough(ExtentionRuleBodyMembers, parent, _.MakeContext(_));
        override GetLastLocation = MakeLastLocation(this, OpenBrace);
        override Typing = foreach (x in ExtentionRuleBodyMembers) x.Typing(_baseRule);
      }
  }

  syntax ExtentionRuleBodyMember
  {
    CollectGlobalSymbols(ruleSymbol : ExtentionRuleSymbol) : void = _ = ruleSymbol;
    GetPrecedence() : ValueOption[int * N2.Location] = VNone();
    [Cached] MakeContext(parent : Context) : Context = parent;
    Typing(_baseRule : ExtensibleRuleRef) : void { }

    | RuleAlias
      {
        override MakeContext = RuleAlias.MakeContext(parent);
      }
    | TokenRule
      {
        override CollectGlobalSymbols = TokenRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = TokenRule.MakeContext(parent);
        override Typing               = TokenRule.Typing();
      }
    | VoidRule
      {
        override CollectGlobalSymbols = VoidRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = VoidRule.MakeContext(parent);
        override Typing               = VoidRule.Typing();
      }
    | SimpleRule
      {
        override CollectGlobalSymbols = SimpleRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = SimpleRule.MakeContext(parent);
        override Typing               = SimpleRule.Typing();
      }
    | ExtensibleRule
      {
        override CollectGlobalSymbols = ExtensibleRule.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = ExtensibleRule.MakeContext(parent);
        override Typing               = ExtensibleRule.Typing();
      }
    | ExtraField
      {
        override CollectGlobalSymbols = ExtraField.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = ExtraField.MakeContext(parent);
        override Typing               = ExtraField.Typing();
      }
    | ExtraFieldOverride
      {
        override CollectGlobalSymbols = ExtraFieldOverride.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = ExtraFieldOverride.MakeContext(parent);
        override Typing               = ExtraFieldOverride.Typing();
      }
    | ExtraFieldMissing
      {
        override CollectGlobalSymbols = ExtraFieldMissing.CollectGlobalSymbols(ruleSymbol);
        override MakeContext          = ExtraFieldMissing.MakeContext(parent);
        override Typing               = ExtraFieldMissing.Typing();
      }
    | Precedence
      {
        // TODO: make precedence table
        override GetPrecedence = VSome((Precedence.GetPrecedence(), this.Location));
      }
  }

  syntax ExtraFieldsParam = Name sm ":" sm Type;
  [ExplicitSpaces]
  syntax ExtraFieldPrivate = "private" sm;

  syntax ExtraFieldAttribute
  {
    IsCached() : bool;

    | Cached = AttributeName="Cached"
      {
        override IsCached = true;
      }
  }
  syntax ExtraFieldAttributeList = "[" (ExtraFieldAttribute; ",")+ "]" nl
  {
    IsCached() : bool
    {
      foreach(x when x.IsCached() in ExtraFieldAttributes[0])
        return true;
      false
    }
  }
  syntax ExtraFieldAttributes = ExtraFieldAttributeList*
  {
    IsCached() : bool
    {
      foreach(x when x.IsCached() in ExtraFieldAttributeLists)
        return true;
      false
    }
  }

  syntax ExtraField = ExtraFieldAttributes ExtraFieldPrivate? Name sm "(" (ExtraFieldsParam; "," sm)* ")" sm ":" sm Type ExtraFieldBody
  {
    [Cached] MakeContext(parent : Context) : Context = parent;
    [Cached] private MakeSymbol(ruleSymbol : SyntaxRuleSymbol) : ExtraField.DefinitionSymbol
    {
      if (ExtraFieldPrivateOpt.IsSome)
      {
        when (ExtraFieldBody.IsEmpty())
          Project.CurrentProject.Error(this.Location, "A private extra field must have a body.", -1);
        unless (ruleSymbol is SimpleRuleSymbol || ruleSymbol is ExtentionRuleSymbol)
          Project.CurrentProject.Error(this.Location, "A private extra field can be declared only in simple rules and extention rules.", -1);
        ExtraField.ParsedDefPrivateSymbol(this)
      }
      else if (ExtraFieldBody.IsEmpty())
      {
        unless (ruleSymbol is ExtensibleRuleSymbol)
          Project.CurrentProject.Error(this.Location, "An abstract extra field must be defined in an extention point rule only.", -1);
        ExtraField.ParsedAbstractSymbol(this)
      }
      else
      {
        unless (ruleSymbol is SimpleRuleSymbol || ruleSymbol is ExtensibleRuleSymbol)
          Project.CurrentProject.Error(this.Location, "A public extra field must be defined in an extention point rule or a simple rule only.", -1);
        ExtraField.ParsedDefSymbol(this)
      }
    }
    CollectGlobalSymbols(ruleSymbol : SyntaxRuleSymbol) : void = _ = ruleSymbol.Node.AddAndReport(MakeSymbol(ruleSymbol), this.Location);
    [Cached] private MakeSignature() : ExtraField.ExtraFieldSignature = ExtraField.ExtraFieldSignature(this.Location, NemerleParser.Parse(this.ExtraFieldsParams[0], Context), NemerleParser.Parse(this.Type, Context));
    [Cached] private MakeBody()      : ExtraField.ExtraFieldBody      = ExtraField.ExtraFieldBody(this.ExtraFieldBody.BodyLocation(), NemerleParser.Parse(this.ExtraFieldBody, Context));
    [Cached] private MakeIsCached()  : bool                           = ExtraFieldAttributes.IsCached();
    Typing() : void
    {
      _ = MakeSignature();
      _ = MakeBody();
      _ = MakeIsCached();
    }
  }

  syntax ExtraFieldOverride = ExtraFieldAttributes "override"     sm  Name ExtraFieldBody
  {
    [Cached] MakeContext(parent : Context) : Context = parent;
    [Cached] private MakeSymbol() : ExtraField.DefinitionSymbol
    {
      when (ExtraFieldBody.IsEmpty())
        Project.CurrentProject.Error(this.Location, "An overriding extra field must have a body.", -1);
      ExtraField.ParsedOverrideSymbol(this);
    }
    CollectGlobalSymbols(ruleSymbol : SyntaxRuleSymbol) : void = _ = ruleSymbol.Node.AddAndReport(MakeSymbol(), this.Location);
    [Cached] private MakeBaseExtraField() : SymbolRef[ExtraField.ExtraFieldSymbol]
    {
      match (this.Symbol.DeclaringRule)
      {
        | ExtentionRuleSymbol(BaseRule = SymbolRef.Some(baseRuleSymbol)) =>
          match (baseRuleSymbol.Node.BindMany.[ExtraField.DefinitionSymbol](Name.Location))
          {
            | [ExtraField.DefSymbol      as baseExtraFieldSymbol]
            | [ExtraField.AbstractSymbol as baseExtraFieldSymbol] =>
              SymbolRef.Some(Name.Location, false, baseExtraFieldSymbol)

            | [_] =>
              def name = Name.Value();
              Project.CurrentProject.Error(this.Location, $"Cannot override '$name' extra field.", -1); //TODO: rewrite error message!
              SymbolRef.None(Name.Location, false)

            | _ =>
              def name = Name.Value();
              Project.CurrentProject.Error(this.Location, $"No '$name' extra field to override.", -1);
              SymbolRef.None(Name.Location, false)
          }

        | _ =>
          Project.CurrentProject.Error(this.Location, "An extra field can be overridden only in extending rules.", -1);
          SymbolRef.None(Name.Location, false)
      }
    }
    [Cached] private MakeBody() : ExtraField.ExtraFieldBody = ExtraField.ExtraFieldBody(this.ExtraFieldBody.BodyLocation(), NemerleParser.Parse(this.ExtraFieldBody, Context));
    Typing() : void
    {
      _ = MakeBaseExtraField();
      _ = MakeBody();
    }
  }

  syntax ExtraFieldMissing = ExtraFieldAttributes "missing"        sm  Name ExtraFieldBody
  {
    [Cached] MakeContext(parent : Context) : Context = parent;
    [Cached] private MakeSymbol() : ExtraField.ExtraFieldMissingValueSymbol
    {
      when (ExtraFieldBody.IsEmpty())
        Project.CurrentProject.Error(this.Location, "A 'missing' handler must have a body.", -1);
      ExtraField.ParsedExtraFieldMissingValueSymbol(this);
    }
    CollectGlobalSymbols(ruleSymbol : SyntaxRuleSymbol) : void = _ = ruleSymbol.Node.AddAndReport(MakeSymbol(), this.Location);
    [Cached] private MakeHandlingExtraField() : SymbolRef[ExtraField.ExtraFieldSymbol]
    {
      match (this.Symbol.DeclaringRule)
      {
        | ExtensibleRuleSymbol as targetRuleSymbol
        | SimpleRuleSymbol     as targetRuleSymbol =>
          match (targetRuleSymbol.Node.BindMany.[ExtraField.DefinitionSymbol](Name.Location))
          {
            | [extraFieldSymbol] => SymbolRef.Some(Name.Location, false, extraFieldSymbol)
            | _ =>
              def name = Name.Value();
              Project.CurrentProject.Error(this.Location, $"No '$name' extra field to handle 'missing'.", -1);
              SymbolRef.None(Name.Location, false)
          }

        | _ =>
          Project.CurrentProject.Error(this.Location, "A 'missing' handler for a extra field can be defined in simple and extensible rules.", -1);
          SymbolRef.None(Name.Location, false)
      }
    }
    [Cached] private MakeBody() : ExtraField.ExtraFieldBody = ExtraField.ExtraFieldBody(this.ExtraFieldBody.BodyLocation(), NemerleParser.Parse(this.ExtraFieldBody, Context));
    Typing() : void
    {
      _ = MakeHandlingExtraField();
      _ = MakeBody();
    }
  }

  syntax Precedence = "precedence"   sm  Number RightAssociative=("right-associative")? ";" nl
  {
    GetPrecedence() : int = if (RightAssociative.IsSome) -Number.Value() else Number.Value();
  }

  syntax ExtraFieldBody
  {
    BodyLocation() : N2.Location;
    IsEmpty() : bool;

    | Block
      {
        override BodyLocation = Block.Location;
        override IsEmpty = false;
      }
    | Expression = sm "=" sm Expression ";" nl
      {
        override BodyLocation = Expression.Location;
        override IsEmpty = false;
      }
    | Empty = ";" nl
      {
        override BodyLocation = Location(this.Location.Source, this.Location.StartPos, this.Location.StartPos);
        override IsEmpty = true;
      }
  }

  syntax TypeParams = "[" (Type; ","sm)+ "]";
  syntax SimpleName = Identifier TypeParams?;

  syntax Type
  {
    | PredefinedType
    | SimpleName
    | Qualified = Type "." SimpleName { precedence 20; }
    | Pointer   = Type "*" Type       { precedence 10; }
  }

  syntax Block = outline_hiden outline_begin_before nl "{" inl (Expression; ";" nl)* ";"* nl d "}" outline_end_before nl;
  syntax Parentheses = "(" Expression? ")";

  syntax Content
  {
    | Block;
    | IgnoreToken+;
    | CharLiteral;
    | StringLiteral;
    | Parentheses;
    | "fold" sm Identifier "(" Expression ";" (Expression; ";"sm)+ ")";
  }

  syntax ExpressionContent
  {
    | Content1 = Content;
    | Content2 = (!";" !")" !"}" !Content Any)+;
  }

  syntax Expression = ExpressionContent+;

  syntax Range
  {
    RangeSet(inCharSet : RangeSet) : RangeSet;

    | SingleChar = CharLiteral
      {
        override RangeSet = inCharSet.AddSingleCharRange(CharLiteral.Value());
      }
    | CharRange       = CharLiteral ".." CharLiteral
      {
        override RangeSet = inCharSet.AddRange(CharLiteral1.Value(), CharLiteral2.Value());
      }
    | UnicodeRange    = Name
      {
        override RangeSet
        {
          def nameText = Name.Value();
          if (UnicodeCategoryTable.StrToUnicodeCategory(nameText) is Some(cat))
            inCharSet.AddRange(cat)
          else
          {
            Project.CurrentProject.Error(this, $"Invalid unicode category '$nameText'.", -1);
            inCharSet
          }
        }
      }
  }

  syntax TokenRuleExpression
  {
    [Cached] MakeContext(parent : Context) : Context = parent;
    [Cached] Typing() : RegularRule;

    | Choice                  = LeftRule=TokenRuleExpression ^ 10 sm RightRules=("|" TokenRuleExpression ^ 10)+
      {
        override MakeContext
        {
          def context1 = LeftRule.MakeContext(parent);
          def context2 = FoldThrough(RightRules, context1, ((_, r), c) => r.MakeContext(c));
          context2
        }
        override Typing = RegularRule.Choice(Location, LeftRule.Typing() :: RightRules.Map(r => r[1].Typing()));
      }
    | Sequence                = LeftRule=TokenRuleExpression ^ 20 sm RightRules=(TokenRuleExpression ^ 20)+
      {
        override MakeContext
        {
          def context1 = LeftRule.MakeContext(parent);
          def context2 = FoldThrough(RightRules, context1, _.MakeContext(_));
          context2
        }
        override Typing = RegularRule.Sequence(Location, LeftRule.Typing() :: RightRules.Map(_.Typing()));
      }
    | Optional                = TokenRuleExpression "?"
      {
        precedence 30;
        override MakeContext = TokenRuleExpression.MakeContext(parent);
        override Typing = RegularRule.Repeat(Location, 0, Some(1), this.TokenRuleExpression.Typing());
      }
    | ZeroOrMany              = TokenRuleExpression "*"
      {
        precedence 30;
        override MakeContext = TokenRuleExpression.MakeContext(parent);
        override Typing = RegularRule.Repeat(Location, 0, None(), this.TokenRuleExpression.Typing());
      }
    | OneOrMany               = TokenRuleExpression "+"
      {
        precedence 30;
        override MakeContext = TokenRuleExpression.MakeContext(parent);
        override Typing = RegularRule.Repeat(Location, 1, None(), this.TokenRuleExpression.Typing());
      }
    | Char                    = CharLiteral
      {
        override Typing = RegularRule.Chars(Location, [RangeSet().AddSingleCharRange(CharLiteral.Value())]);
      }
    | String                  = StringLiteral
      {
        override Typing = RegularRule.Chars(Location, RangeSet.MakeString(StringLiteral.Value()));
      }
    | Call                    = QualifiedName
      {
        override Typing
        {
          def ruleRef = SymbolRef.Create(QualifiedName, Context.Bind(QualifiedName));
          RegularRule.Call(Location, ruleRef)
        }
      }
    | Rounds                  = "(" TokenRuleExpression ")"
      {
        override MakeContext = TokenRuleExpression.MakeContext(parent);
        override Typing = TokenRuleExpression.Typing();
      }
    | Range                   = "[" (Range; ","sm)+ "]"
      {
        override Typing = RegularRule.Chars(Location, [FoldThrough(Ranges[0], RangeSet(), _.RangeSet(_))]);
      }
    | InvertedRange           = "[" "^" (Range; ","sm)+ "]"
      {
        override Typing = RegularRule.Chars(Location, InvertRange(FoldThrough(Ranges[0], RangeSet(), _.RangeSet(_))));
      }
    | ZeroOrManyWithSeparator = "(" TokenRuleExpression ";"sm SeparatorToken ")" "*"
      {
        override MakeContext  = parent |> TokenRuleExpression.MakeContext |> SeparatorToken.MakeContext;
        override Typing = RegularRule.RepeatWithSeparator(Location, 0, None(), TokenRuleExpression.Typing(), SeparatorToken.Typing());
      }
    | OneOrManyWithSeparator  = "(" TokenRuleExpression ";"sm SeparatorToken ")" "+"
      {
        override MakeContext = parent |> TokenRuleExpression.MakeContext |> SeparatorToken.MakeContext;
        override Typing = RegularRule.RepeatWithSeparator(Location, 1, None(), TokenRuleExpression.Typing(), SeparatorToken.Typing());
      }
  }

  syntax RuleExpression
  {
    Typing(_baseRule : option[ExtensibleRuleSymbol], _precedence : int, _fieldName : option[string], context : Context, options : RuleDefinitionOptions) : N2.Model.Rule;  // precedence < 0 if it is right-associative
    InferName() : string = "";

    | Sequence                        = LeftRule=RuleExpression ^ 10 RightRules=(sm RuleExpression ^ 10)+
      {
        override Typing
        {
          def bp1   = if (_precedence < 0) -_precedence + 1 else _precedence;
          def bp2   = if (_precedence < 0) -_precedence     else _precedence;
          def flat = List();
          def tryExtractSpaceSequence(rule : Rule) : void
          {
            // assume that 's' is generated whitespace rule
            match (rule)
            {
              | Sequence([String as call1, Call(s, 0) as call2])
              | Sequence([Call   as call1, Call(s, 0) as call2]) =>
                when (s.IsGenerated)
                {
                  flat.Add(call1);
                  flat.Add(call2);
                  return
                }
              | _ => ()
            }
            flat.Add(rule);
          }
          tryExtractSpaceSequence(LeftRule.Typing(_baseRule, bp1, None(), context, options));
          foreach (rule in RightRules)
            tryExtractSpaceSequence(rule.Typing(_baseRule, bp2, None(), context, options));
          def rules =  flat.NToList();
          def type  = MakeType(rules.Map(_.Type));
          Rule.Sequence(this.Location, type, MakeName(_fieldName, rules, context), rules);
        }
        override InferName = LeftRule.InferName();
      }
    | Not                             = "!" RuleExpression ^ 20
      {
        override Typing
        {
          def rule = this.RuleExpression.Typing(None(), 0, None(), context, options);
          Rule.Not(this.Location, RuleType.Void(), "", rule);
        }
      }
    | And                             = "&" RuleExpression ^ 20
      {
        override Typing = Rule.And(this.Location, RuleType.Void(), "", RuleExpression.Typing(None(), 0, None(), context, options));
      }
    | Optional                        = RuleExpression "?"
      {
        precedence 30;
        override Typing
        {
          def rule  = RuleExpression.Typing(None(), 0, None(), context, options);
          def type  = if (rule.Type is RuleType.Void) RuleType.Void() else RuleType.Option(rule.Type);
          Rule.Repeat(this.Location, type, _fieldName ?? (rule.FieldName + "Opt"), 0, Some(1), rule);
        }
        override InferName = RuleExpression.InferName() + "Opt";
      }
    | ZeroOrMany                      = RuleExpression "*"
      {
        precedence 30;
        override Typing
        {
          def rule  = RuleExpression.Typing(None(), 0, None(), context, options);
          def type  = if (rule.Type is RuleType.Void) RuleType.Void() else RuleType.List(rule.Type);
          Rule.Repeat(this.Location, type, MakePlural(_fieldName, rule, context), 0, None(), rule);
        }
        override InferName = MakePlural(RuleExpression.InferName());
      }
    | OneOrMany                       = RuleExpression "+"
      {
        precedence 30;
        override Typing
        {
          def rule  = RuleExpression.Typing(None(), 0, None(), context, options);
          def type  = if (rule.Type is RuleType.Void) RuleType.Void() else RuleType.List(rule.Type);
          Rule.Repeat(this.Location, type, MakePlural(_fieldName, rule, context), 1, None(), rule);
        }
        override InferName = MakePlural(RuleExpression.InferName());
      }
    | FieldName1                      = RuleExpression ^ 12 sm "as" sm Name
      {
        override Typing = RuleExpression.Typing(_baseRule, _precedence, Some(Name.Value()), context, options);
        override InferName = Name.GetText();
      }
    | FieldName2                      = Name sm "=" sm RuleExpression ^ 11
      {
        override Typing = RuleExpression.Typing(_baseRule, _precedence, Some(Name.Value()), context, options);
        override InferName = Name.GetText();
      }
    | Char                            = CharLiteral
      {
        override Typing = StringRuleTyping(CharLiteral.Location, CharLiteral.Value().ToString(), _fieldName, context, options);
        override InferName = CharLiteral.Value().ToString();
      }
    | String                          = StringLiteral
      {
        override Typing = StringRuleTyping(StringLiteral.Location, StringLiteral.Value().ToString(), _fieldName, context, options);
        override InferName = StringLiteral.Value().ToString();
      }
    | Rounds                          = "(" RuleExpression ")"
      {
        override Typing = RuleExpression.Typing(_baseRule, _precedence, _fieldName, context, options);
        override InferName = RuleExpression.InferName();
      }
    | Call                            = QualifiedName BindingPower=(sm "^" sm Number)?
      {
        override Typing = CallRuleTyping(_baseRule, _precedence, _fieldName, context, options, QualifiedName, BindingPower);
        override InferName = QualifiedName.Parts().LastOrDefault("");
      }
    | ZeroOrManyWithSeparator         = "(" RuleExpression ";"sm SeparatorRule ")" "*"
      {
        override Typing
        {
          def rule    = RuleExpression.Typing(None(), 0, None(), context, options);
          def sepRule = SeparatorRule.Typing(None(), 0, None(), context, options);
          def type    = MakeType(rule, sepRule);
          Rule.RepeatWithSeparator(this.Location, type, MakePlural(_fieldName, rule, sepRule, context), 0, None(), rule, sepRule, false);
        }
        override InferName = MakePlural(RuleExpression.InferName());
      }
    | ZeroOrManyWithHangingSeparator  = "(" RuleExpression ";"sm SeparatorRule ";"sm "?" ")" "*"
      {
        override Typing
        {
          def rule    = RuleExpression.Typing(None(), 0, None(), context, options);
          def sepRule = SeparatorRule.Typing(None(), 0, None(), context, options);
          def type    = MakeType(rule, sepRule);
          Rule.RepeatWithSeparator(this.Location, type, MakePlural(_fieldName, rule, sepRule, context), 0, None(), rule, sepRule, true);
        }
        override InferName = MakePlural(RuleExpression.InferName());
      }
    | OneOrManyWithSeparator          = "(" RuleExpression ";"sm SeparatorRule ")" "+"
      {
        override Typing
        {
          def rule    = RuleExpression.Typing(None(), 0, None(), context, options);
          def sepRule = SeparatorRule.Typing(_baseRule, 0, None(), context, options);
          def type    = MakeType(rule, sepRule);
          Rule.RepeatWithSeparator(this.Location, type, MakePlural(_fieldName, rule, sepRule, context), 1, None(), rule, sepRule, false);
        }
        override InferName = MakePlural(RuleExpression.InferName());
      }
    | OneOrManyWithHangingSeparator   = "(" RuleExpression ";"sm SeparatorRule ";"sm "?" ")" "+"
      {
        override Typing
        {
          def rule    = RuleExpression.Typing(None(), 0, None(), context, options);
          def sepRule = SeparatorRule.Typing(_baseRule, 0, None(), context, options);
          def type    = MakeType(rule, sepRule);
          Rule.RepeatWithSeparator(this.Location, type, MakePlural(_fieldName, rule, sepRule, context), 1, None(), rule, sepRule, true);
        }
        override InferName = MakePlural(RuleExpression.InferName());
      }
  } // extend syntax RuleExpression
} // syntax module
