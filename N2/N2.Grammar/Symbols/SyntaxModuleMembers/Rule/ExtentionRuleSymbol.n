using N2.Model;
using N2.Typing.ExtraField;
using N2.Utils;

using Nemerle;

using G = N2Syntax;

namespace N2.Typing
{
  [Record, ExportableSymbol]
  public abstract class ExtentionRuleSymbol : SyntaxRuleSymbol
  {
    public        abstract BaseRule         : SymbolRef[ExtensibleRuleSymbol] { get; }
    public        override Description      : string                          { get { "extention rule" } }

    public variant ExtentionRuleType
    {
      | Prefix
      | Postfix { bindingPower : int; rule : Rule.Sequence }
    }

    public ExtentionType : ExtentionRuleType
    {
      [Memoize]
      get
      {
        match (Rule)
        {
          | Sequence(Call(baseRule, bindingPower) :: rules) when BaseRule == baseRule => ExtentionRuleType.Postfix(bindingPower, Rule.Sequence(Rule, rules))
          | _                                                                         => ExtentionRuleType.Prefix()
        }
      }
    }
  }

  public sealed class ParsedInlineExtentionRule : ExtentionRuleSymbol
  {
    public this(ast : G.InlineExtentionRule.Ast, baseName : string)
    {
      def (loc, name) =
        match (ast.NameOpt)
        {
          | Some((name, _)) => (name.Location, name.GetText())
          | _          =>
            def name1 = ast.Rule.InferName();
            def name2 = if (IsIdentifier(name1)) ToPascalCase(name1) else "";

            when (name2 == "")
              ProjectSystem.N2Project.CurrentProject.Error(ast.Location, "Can't infer name for this rule.", -1);
            (Location(ast.Location.Source, ast.Pipe.EndPos, ast.Pipe.EndPos), name2);
        };

      base(loc, baseName + "_" + name);
      _ast = ast;
    }

    public _ast                      : G.InlineExtentionRule.Ast;

    public override BaseRule         : ExtensibleRuleRef     { get { _ast.BaseRuleRef } }
    public override Type             : RuleType              { get { _ast.Type } }
    public override Options          : RuleDefinitionOptions { get { _ast.Options } }
    public override Rule             : Rule.Sequence         { get { _ast.TypedRule } }
    public override LastLocation     : option[Location]
    {
      [Memoize]  get
      {
        match (_ast.RuleBodyOpt)
        {
          | Some(body) => Some(body.GetLastLocation())
          | _          => Some(Utils.MakeLastLocation(_ast))
        }
      }
    }
  }

  public sealed class ParsedExtentionRuleSymbol : ExtentionRuleSymbol
  {
    public this(ast : G.Declaration_ExtentionRule.Ast)
    {
      base(ast.Name.Location, ast.BaseName.Parts().LastOrDefault("") + "_" + ast.Name.GetText());
      _ast = ast;
    }

    public _ast                      : G.Declaration_ExtentionRule.Ast;

    public override BaseRule         : ExtensibleRuleRef     { get { _ast.BaseRuleRef } }
    public override Type             : RuleType              { get { _ast.Type } }
    public override Options          : RuleDefinitionOptions { get { _ast.Options } }
    public override Rule             : Rule.Sequence         { get { _ast.TypedRule } }
  }
}
