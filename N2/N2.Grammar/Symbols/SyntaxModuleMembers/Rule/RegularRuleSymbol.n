using N2.Model;
using N2.Typing.ExtraField;

using Nemerle;
using System;

using G = N2SyntaxAst;

namespace N2.Typing
{
  [Record, ExportableSymbol]
  public abstract class RegularRuleSymbol : RuleDefSymbol, IRuleDeclarationSite
  {
    public sealed override Type             : RuleType    { get { RuleType.Chars() } }
    public        abstract Rule             : RegularRule { get; }
    public                 Fsm              : FSM         { [Memoize] get { FSMTransform.MakeDeterministic(this.Rule.ConvertRegularRuleToFsm()) } }
    public        override Description      : string      { get { "regular rule" } }
  }

  public sealed class ParsedRegularRuleSymbol : RegularRuleSymbol
  {
    public this(ast : G.TokenRule.Ast, declarationSite : IRuleDeclarationSite)
    {
      base(ast.Name);
      _ast = ast;
      DeclarationSite = declarationSite;
    }

    public _ast                         : G.TokenRule.Ast;

    public override DeclarationSite     : IRuleDeclarationSite  { get; }
    public override Options             : RuleDefinitionOptions { get { _ast.RuleAttributes.Options(RuleDefinitionOptions.Empty) } }
    public override Rule                : RegularRule           { get { _ast.RegularRule } }
    public override LastLocation        : option[Location]      { [Memoize] get { Some(_ast.TokenRuleBody.GetLastLocation()) } }
    public override IdInGrammar         : int                   { [Memoize] get { DeclaringModule.GetNewRuleId() } }
    public override CanParseEmptyString : bool                  { [Memoize] get { Fsm.OkStates.Contains(Fsm.StartState) } }
    public override FirstCharLowerBound : char                  { get { FirstCharBounds[0] } }
    public override FirstCharUpperBound : char                  { get { FirstCharBounds[1] } }
    private FirstCharBounds : CharBounds
    {
      [Memoize] get
      {
        mutable totalRange = RangeSet();
        foreach (Symbol(Chars = range, From = from) when from == Fsm.StartState in Fsm.Transitions)
          totalRange = totalRange.Sum(range);
        match (totalRange.GetBounds())
        {
          | None => (char.MinValue, char.MaxValue)
          | Some(bounds) => bounds
        }
      }
    }
    public override IsToken             : bool                  { get { true } }
  }
}
