using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Extensions;
using Nemerle.Text;
using Nemerle.Utility;

using N2.ProjectSystem;
using N2.Serialization;

using System;
using System.IO;
using System.Collections.Generic;
using System.Linq;

using G = GrammarParser2;

namespace N2.Typing.ExtraField
{
  [Record, ExportableSymbol(ExternalDefSymbol)]
  public abstract class DefSymbol : DefinitionSymbol, ISignature, IBody, ICached
  {
    public abstract Signature : ExtraFieldSignature { get; }
    public virtual  Body      : ExtraFieldBody      { get { throw NotSupportedException() } } // implementation for external symbol
    public abstract IsCached  : bool                { get; }

    Serialize(_ : BinaryWriter, _ : IN2TypesWriter) : void implements IExportableSymbol.Serialize
    {
      // all data is written to .NET metadata
    }
  }

  public sealed class ParsedDefSymbol : DefSymbol
  {
    _ast : G.RuleBodyMember_ExtraField.Ast;

    public this(ast : G.RuleBodyMember_ExtraField.Ast)
    {
      base(ast.Name);
      _ast = ast;
    }

    public override Signature : ExtraFieldSignature { get { _ast.Signature } }
    public override Body      : ExtraFieldBody      { get { _ast.Body } }
    public override IsCached  : bool                { get { _ast.IsCached } }
  }

  public sealed class ExternalDefSymbol : DefSymbol
  {
    Deserialize(_ : BinaryReader, n2TypesReader : IN2TypesReader) : void implements IImportableSymbol.Deserialize
    {
      (Signature, IsCached) = ExtraFieldReflectionUtils.ReflectProperties(n2TypesReader.Project :> N2Project, this);
    }
  }
}
