using Nitra.Internal;
using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Collections.ObjectModel;
using System.Linq;

namespace Nitra.Runtime.Reflection
{
  public delegate RegularCallDelegate(pos : int, text : string) : int;

  [Record]
  public variant RuleInfo
  {
    public Offset      : int    { get; }
    public Description : string { get; }
    public State       : int    { get; }

    | Empty
    | RegularCall                { Call : RegularCallDelegate; Descriptor : RegularRuleDescriptor; }
    | ExtensibleCall             { RuleParser : ExtensibleRuleParser; IsToken : bool; }
    | SimpleCall                 { RuleParser : SimpleRuleParser; IsToken : bool; }
    | Option                     { Rule : RuleStructure.Option; }
    | List                       { Rule : RuleStructure.List; }
    | ListWithSeparator          { Rule : RuleStructure.ListWithSeparatorRule; Separator : RuleStructure.ListWithSeparatorSeparator; }
    | Predicate                  { Rule : RuleStructure.Predicate; }
    | TokenString                { Str : string; }
    | Marker                     { Name : string; }

    public VisitVoid(
      regularCall       : Action[RegularCallDelegate, RegularRuleDescriptor] = null,
      extensibleCall    : Action[ExtensibleRuleParser, bool] = null,
      simpleCall        : Action[SimpleRuleParser, bool] = null,
      option            : Action[RuleStructure.Option] = null,
      aList             : Action[RuleStructure.List] = null,
      listWithSeparator : Action[RuleStructure.ListWithSeparatorRule, RuleStructure.ListWithSeparatorSeparator] = null,
      predicate         : Action[RuleStructure.Predicate] = null,
      tokenString       : Action = null,
      noMatch           : Action = null) : void
    {
      match (this)
      {
        | RegularCall      (call, descriptor)             when regularCall       != null => regularCall(call, descriptor)
        | ExtensibleCall   (ruleParser, isToken)          when extensibleCall    != null => extensibleCall(ruleParser, isToken)
        | SimpleCall       (ruleParser, isToken)          when simpleCall        != null => simpleCall(ruleParser, isToken)
        | Option           (rule)                         when option            != null => option(rule)
        | List             (rule)                         when aList             != null => aList(rule)
        | ListWithSeparator(rule, sep)                    when listWithSeparator != null => listWithSeparator(rule, sep)
        | Predicate        (rule)                         when predicate         != null => predicate(rule)
        | TokenString                                     when tokenString       != null => tokenString()
        | _                                                                              => noMatch()
      }
    }

    public Visit[T](
      regularCall       : Func[RegularCallDelegate, RegularRuleDescriptor, T] = null,
      extensibleCall    : Func[ExtensibleRuleParser, bool, T] = null,
      simpleCall        : Func[SimpleRuleParser, bool, T] = null,
      option            : Func[RuleStructure.Option, T] = null,
      aList             : Func[RuleStructure.List, T] = null,
      listWithSeparator : Func[RuleStructure.ListWithSeparatorRule, RuleStructure.ListWithSeparatorSeparator, T] = null,
      predicate         : Func[RuleStructure.Predicate, T] = null,
      tokenString       : Func[T] = null,
      noMatch           : Func[T] = null) : T
    {
      match (this)
      {
        | RegularCall      (call, descriptor)             when regularCall       != null => regularCall(call, descriptor)
        | ExtensibleCall   (ruleParser, isToken)          when extensibleCall    != null => extensibleCall(ruleParser, isToken)
        | SimpleCall       (ruleParser, isToken)          when simpleCall        != null => simpleCall(ruleParser, isToken)
        | Option           (rule)                         when option            != null => option(rule)
        | List             (rule)                         when aList             != null => aList(rule)
        | ListWithSeparator(rule, sep)                    when listWithSeparator != null => listWithSeparator(rule, sep)
        | Predicate        (rule)                         when predicate         != null => predicate(rule)
        | TokenString                                     when tokenString       != null => tokenString()
        | _                                                                              => noMatch()
      }
    }

    public override ToString() : string
    {
      Description
    }
  }
}
