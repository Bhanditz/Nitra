using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using N2.Model;
using N2.Typing;

using System;
using System.Collections.Generic;
using System.Linq;

namespace N2.Compiler
{
  internal class AbstractWalkerEmitter : IVariableEmitter
  {
    public Owner      : SyntaxModuleEmitter { get; }
    public RuleSymbol : SyntaxRuleSymbol    { get; }
    public TB         : TypeBuilder         { get { assert(_tb != null); _tb; } }

    public this(owner : SyntaxModuleEmitter, ruleSymbol : SyntaxRuleSymbol)
    {
      Owner = owner;
      RuleSymbol = ruleSymbol;
    }

    mutable _tb : TypeBuilder;

    Environment : EmitterEnvironment implements IVariableEmitter.Environment { get { Owner.Environment } }

    CreateVariable(name : string) : PExpr implements IVariableEmitter.CreateVariable
    {
      <[ $(name : dyn) ]>
    }

    public DefineTypes() : void
    {
      assert(_tb == null);
      _tb = Owner.GrammarTB.DefineNestedType(<[ decl:
        public class $(RuleSymbol.Name + "Walker" : usesite) : $(GetWalkerBaseType(RuleSymbol))
        {
        }
      ]>);
    }

    public DefineMembers(rootAstStruct : option[RootAstStruct2]) : void
    {
      assert(_tb != null);

      def parms =
        <[ parameter: $("location" : usesite) : N2.Location ]>
        :: <[ parameter: $("errorInfo" : usesite) : list[N2.ErrorInfo] ]> //TODO: удалить этот параметр
        :: RuleSymbol.NonVoidSubRules.Map(r => <[ parameter: $(r.FieldName : usesite) : $(Owner.Environment.RuleTypeToNType(r.Type) : typed) ]>);
      _tb.DefineConditional(<[ decl:
        public Accept(..$parms) : $(Owner.Environment.RuleTypeToNType(RuleSymbol.Type) : typed)
        {
          $(Owner.Environment.NTypeOfRuleSymbol(RuleSymbol) : typed).Ast(..$(parms.Map(p => <[ $(p.Name : usesite) ]>)))
        }
      ]>);

      def referencedWalkers = GetDependencies(RuleSymbol);
      foreach(p in referencedWalkers)
        _tb.Define(<[ decl: private mutable $(p.Value.FieldName : usesite) : $(GetWalkerType(p.Key[0])) ]>);

      def initBody = List();
      initBody.Add(<[ ignore(compositeAstWalker) ]>);
      foreach (p in referencedWalkers)
      {
        def walkerInfo = p.Value;
        match (p.Key)
        {
          | (ExtensibleRuleSymbol as s, bp) =>
            initBody.Add(<[ this.$(walkerInfo.FieldName : usesite) = compositeAstWalker.GetWalker($(s.GetDescriptorClassRef()).StaticDescriptor, $(bp : int)) ]>);

          | (s, _) =>
            initBody.Add(<[ this.$(walkerInfo.FieldName : usesite) = compositeAstWalker.GetWalker($(s.GetDescriptorClassRef()).StaticDescriptor) ]>);
        }
      }
      _tb.DefineConditional(<[ decl:
        public override Init(compositeAstWalker : N2.Runtime.CompositeAstWalker) : void
        {
          ..$(initBody.ToNList())
        }
      ]>);

      DefineWalkMethod(rootAstStruct, referencedWalkers);
    }

    private DefineWalkMethod(rootAstStruct : option[RootAstStruct2], referencedWalkers : Hashtable[RuleSymbol * int, WalkerInfo]) : void
    {
      def rootAstMaker(arguments : list[PExpr]) : PExpr
      {
        def acceptArgs =
          <[ N2.Location(parser.Source, startPos, pos) ]>
          :: <[ [] ]>
          :: arguments;
        <[ this.Accept(..$acceptArgs) ]>
      }
      def internalAstMaker(_ : list[PExpr]) : PExpr
      {
        | []   => assert(false)
        | [x]  => x
        | many => <[ (..$many) ]>
      }
      def makeReader(structField : AstStruct2.StructField, astPointer : AstPointer2) : PExpr
      {
        match(structField.Field)
        {
          | RuleField2.TokenString
          | RuleField2.TokenCall =>
            <[
              def tokenPos = pos;
              pos += parser.ast[$(astPointer.SizeOffset(structField))];
              N2.NToken(tokenPos, pos)
            ]>

          | RuleField2.ExternalAst(RuleSymbol = ruleSymbol) =>
            def walkerFieldName = referencedWalkers[(ruleSymbol, 0)].FieldName;
            if (ruleSymbol.Type is RuleType.Void)
              <[ pos += parser.ast[$(astPointer.SizeOffset(structField))]; ]>
            else
              <[ this.$(walkerFieldName : usesite).Walk(ref pos, parser.ast[$(astPointer.AstOffset(structField))], ref parser); ]>

          | RuleField2.Extensible(RuleSymbol = ruleSymbol, BindingPower = bp) =>
            def walkerFieldName = referencedWalkers[(ruleSymbol, bp)].FieldName;
            if (ruleSymbol.Type is RuleType.Void)
              <[ pos += parser.ast[$(astPointer.SizeOffset(structField))]; ]>
            else
              <[ this.$(walkerFieldName : usesite).Walk(ref pos, ref parser); ]>

          | RuleField2.List(astStruct = itemStruct, NextRef = nextRef) =>
            def itemPointer   = AstPointer2(itemStruct, "astIndex", this);
            <[
              def astIndex = parser.ast[$(astPointer.AstOffset(structField))];
              if (astIndex > 0)
              {
                def astBuffer = System.Collections.Generic.List(16);
                def loop(astIndex)
                {
                  astBuffer.Add($(makeAst(itemStruct, itemPointer, internalAstMaker)));
                  def astIndex = parser.ast[$(itemPointer.AstOffset(nextRef))];
                  when (astIndex > 0)
                    loop(astIndex);
                }
                loop(astIndex);
                Nemerle.Collections.NList.ToList(astBuffer)
              }
              else []
            ]>

          | RuleField2.List2(astRuleStruct = itemStruct, astSeparatorStruct = separatorStruct, NextRuleRef = nextItemRef, NextSeparatorRef = nextSeparatorRef, hangingSeparator = hangingSeparator) as lst =>
            def itemPointer = AstPointer2(itemStruct, "astIndex", this);
            def separatorPointer = AstPointer2(separatorStruct, "separatorIndex", this);
            def readNextAst()
            {
              if (hangingSeparator)
                <[
                  separatorBuffer.Add($(makeAst(separatorStruct, separatorPointer, internalAstMaker)));
                  def nextAstIndex = parser.ast[$(separatorPointer.AstOffset(nextSeparatorRef))];
                  when (nextAstIndex > 0)
                    loop(nextAstIndex);
                ]>
              else
                <[
                  def nextAstIndex = parser.ast[$(separatorPointer.AstOffset(nextSeparatorRef))];
                  when (nextAstIndex > 0)
                  {
                    separatorBuffer.Add($(makeAst(separatorStruct, separatorPointer, internalAstMaker)));
                    loop(nextAstIndex);
                  }
                ]>
            }
            def ifEmpty =
              if (lst.Min > 0)
              {
                def getFirstRule(rule)
                {
                  | Rule.Sequence(first :: _) => getFirstRule(first)
                  | _ => rule
                }
                def rule = getFirstRule(lst.Rule.Rule);
                def name = rule.FieldName;
                <[ throw N2.Runtime.EmptyListException("Expecter '" + $name + "' rule.", pos) ]>
              }
              else <[ ([], []) ]>;
            <[
              def astIndex = parser.ast[$(astPointer.AstOffset(structField))];
              if (astIndex > 0)
              {
                def astBuffer = System.Collections.Generic.List(16);
                def separatorBuffer = System.Collections.Generic.List(16);
                def loop(astIndex)
                {
                  astBuffer.Add($(makeAst(itemStruct, itemPointer, internalAstMaker)));
                  def separatorIndex = parser.ast[$(itemPointer.AstOffset(nextItemRef))];
                  when (separatorIndex > 0)
                    $(readNextAst());
                }
                loop(astIndex);
                (Nemerle.Collections.NList.ToList(astBuffer), Nemerle.Collections.NList.ToList(separatorBuffer))
              }
              else $ifEmpty
            ]>

          | RuleField2.Option(astStruct = valueAstStruct) =>
            <[
              def astIndex = parser.ast[$(astPointer.AstOffset(structField))];
              if (astIndex > 0)
                Some($(makeAst(valueAstStruct, AstPointer2(valueAstStruct, "astIndex", this), internalAstMaker)))
              else
                None()
            ]>

          | _ => assert(false)
        }
      }
      and makeAst(astStruct : AstStruct2, astPointer : AstPointer2, maker : list[PExpr] -> PExpr) : PExpr
      {
        def statements = List();
        def arguments = List();
        when (astStruct is PostfixAstStruct2)
          arguments.Add(<[ prefix ]>);

        foreach (structField in astStruct.SizeFields)
          match (structField.Field)
          {
            | RuleField2.Ast
            | RuleField2.Predicate => ()
            | RuleField2.Void =>
              statements.Add(<[ pos += parser.ast[$(astPointer.SizeOffset(structField))]; ]>);

            | RuleField2.List
            | RuleField2.List2
            | RuleField2.Option
            | _                =>
              def varName = Util.tmpname("ast");
              statements.Add(<[ def $(varName : usesite) = $(makeReader(structField, astPointer)) ]>);
              arguments.Add(<[ $(varName : usesite) ]>);
          }

        statements.Add(maker(arguments.ToNList()));
        <[ { ..$(statements.ToNList()) } ]>
      }

      match (RuleSymbol)
      {
        | SimpleRuleSymbol as s
        | ExtentionRuleSymbol(ExtentionType = Prefix) as s =>
          def astPointer = AstPointer2(rootAstStruct.Value, "astPtr", this);
          _tb.DefineConditional(<[ decl:
            public override Walk(pos : ref int, astPtr : int, parser : ref N2.Internal.Parser) : $(Owner.Environment.RuleTypeToNType(s.Type) : typed)
            {
              ignore(pos, astPtr, parser);
              unchecked
              {
                def startPos = pos;
                $(makeAst(rootAstStruct.Value, astPointer, rootAstMaker))
              }
            }
          ]>);

        | ExtentionRuleSymbol(ExtentionType = Postfix) as s =>
          def astPointer = AstPointer2(rootAstStruct.Value, "astPtr", this);
          _tb.DefineConditional(<[ decl:
            public override Walk(prefix : $(Owner.Environment.RuleTypeToNType(s.Type) : typed), startPos : int, pos : ref int, astPtr : int, parser : ref N2.Internal.Parser) : $(Owner.Environment.RuleTypeToNType(s.Type) : typed)
            {
              ignore(prefix, startPos, pos, astPtr, parser);
              unchecked
              {
                $(makeAst(rootAstStruct.Value, astPointer, rootAstMaker))
              }
            }
          ]>);

        | _ => assert2(false);
      }
    }

    private static GetDependencies(rule : RuleSymbol) : Hashtable[RuleSymbol * int, WalkerInfo]
    {
      def refs = Hashtable();
      def collect(rule : Rule) : void
      {
        | String
        | Not
        | And => ()
        | Sequence            (rules)                    => foreach(r in rules) collect(r);
        | Repeat              (_, _, rule)               => collect(rule)
        | RepeatWithSeparator (_, _, rule, separator, _) => collect(rule); collect(separator)
        | Call                (ruleRef, bp) as callRule  =>
          assert2(bp != 31);
          def symbol = ruleRef.Value;
          // TODO: deal with 'void'
          unless (callRule.Type is RuleType.Chars || callRule.Type is RuleType.Void || symbol is MarkerSymbol)
            unless (refs.ContainsKey((symbol, bp)))
              _ = refs.Add((symbol, bp), WalkerInfo(symbol, bp));
      }
      match (rule)
      {
        | ExtentionRuleSymbol(ExtentionType = Prefix, Rule = r)
        | ExtentionRuleSymbol(ExtentionType = Postfix(_, r))
        | SimpleRuleSymbol   (Rule = r)
          => collect(r);
        | _ => assert2(false); assert(false)
      }
      refs
    }

    private struct WalkerInfo
    {
      public this(symbol : RuleSymbol, bindingPower : int)
      {
        FieldName    = Util.tmpname(symbol.Name + "Walker");
        BindingPower = bindingPower;
      }
      public FieldName : string;
      public BindingPower : int;
    }

    private GetWalkerType(rule : RuleSymbol) : PExpr
    {
      | SimpleRuleSymbol as s => <[ N2.Runtime.SimpleAstWalker[$(Owner.Environment.RuleTypeToNType(s.Type) : typed)] ]>
      | ExtensibleRuleSymbol as s => <[ N2.Runtime.ExtensibleAstWalker[$(Owner.Environment.RuleTypeToNType(s.Type) : typed)] ]>
      | _ => assert2(false); assert(false)
    }

    private GetWalkerBaseType(rule : RuleSymbol) : PExpr
    {
      | SimpleRuleSymbol                             as s => <[ N2.Runtime.SimpleAstWalker[$(Owner.Environment.RuleTypeToNType(s.Type) : typed)] ]>
      | ExtentionRuleSymbol(ExtentionType = Prefix)  as s => <[ N2.Runtime.PrefixAstWalker[$(Owner.Environment.RuleTypeToNType(s.Type) : typed)] ]>
      | ExtentionRuleSymbol(ExtentionType = Postfix) as s => <[ N2.Runtime.PostfixAstWalker[$(Owner.Environment.RuleTypeToNType(s.Type) : typed)] ]>
      | _ => assert2(false); assert(false)
    }

    public Emit() : void
    {
      assert(_tb != null);
      _tb.Compile();
    }
  }
}
