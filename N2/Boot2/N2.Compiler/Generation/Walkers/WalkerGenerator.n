using N2.Model;
using N2.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace N2.Compiler
{
  [Record]
  public sealed class WalkerSignature
  {
    public Parameters : list[PParameter];
    public ReturnType : PExpr;
  }

  public abstract class WalkerGenerator
  {
    private _calledWalkers : Hashtable[SyntaxRuleSymbol * int, string] = Hashtable();

    public    abstract Name                            : string { get; }
    public    abstract SupportsVoidRules               : bool { get; }
    public    abstract GetSignature(rule : RuleSymbol) : WalkerSignature;

    protected abstract GetSyntaxRuleValue(ruleSymbol : SyntaxRuleSymbol, astPointer : AstPointerAllocator) : PExpr;
    protected abstract GetRuleValue(field : RuleStructureField, parentAstPointer : AstPointerAllocator) : PExpr;
    protected abstract CombineValues(ruleStructure : RuleStructure, arguments : list[PExpr]) : PExpr;

    public virtual PrefixAmbiguity(ruleSymbol : ExtensibleRuleSymbol) : PExpr
    {
      _ = ruleSymbol;
      <[
        _ = parser;
        _ = endPos;
        throw N2.Runtime.PrefixAmbiguityException($"Ambiguity between prefix rules of '$RuleFullName'.", __startPos, Parser, ambiguities);
      ]>
    }

    public virtual PostfixAmbiguity(ruleSymbol : ExtensibleRuleSymbol) : PExpr
    {
      _ = ruleSymbol;
      <[
        _ = parser;
        _ = endPos;
        throw N2.Runtime.PostfixAmbiguityException($"Ambiguity between postfix rules of '$RuleFullName'.", __startPos, Parser, ambiguities);
      ]>
    }

    public virtual PrefixMissed(ruleSymbol : ExtensibleRuleSymbol) : PExpr
    {
      _ = ruleSymbol;
      <[
        _ = parser;
        throw N2.Runtime.PrefixMissedException($"Expected '$RuleFullName' rule.", pos, Parser);
      ]>
    }

    public Init() : void
    {
      _calledWalkers.Clear();
    }

    private GetFieldNameForWalkerCall(ruleSymbol : SyntaxRuleSymbol, bp : int) : string
    {
      def key = (ruleSymbol, bp);
      mutable value;

      if (_calledWalkers.TryGetValue(key, out value))
        value
      else
      {
        def name = $"_walker_$(_calledWalkers.Count)_$(ruleSymbol.Name)";
        _calledWalkers.Add(key, name);
        name
      }
    }

    protected GetRangeOf(field : RuleStructureField, parentAstPointer : AstPointerAllocator) : PExpr
    {
      def size = parentAstPointer.Allocate().GetSize(field);
      <[ N2.NToken(pos, pos + $size) ]>
    }

    public CallSyntaxRule(ruleSymbol : SyntaxRuleSymbol, bindingPower : int, params args : array[PExpr]) : PExpr
    {
      def allArgs = <[ ref pos ]> ::  <[ parser ]> :: args.NToList();
      <[ this.$(GetFieldNameForWalkerCall(ruleSymbol, bindingPower) : usesite).Walk(..$allArgs); ]>
    }

    public GetCalledWalkers() : IEnumerable[SyntaxRuleSymbol * int * string]
    {
      _calledWalkers.Select(pair => (pair.Key[0], pair.Key[1], pair.Value))
    }

    public WalkerCodeForSimpleRule(ruleSymbol : SyntaxRuleSymbol) : PExpr
    {
      def pointerAllocator = AstPointerAllocator(ruleSymbol.Structure, "astPtr");
      def code = GetSyntaxRuleValue(ruleSymbol, pointerAllocator);
      <[
        $(pointerAllocator.Declare());
        $code;
      ]>.FlattenPExpr()
    }

    public WalkerCodeForExtentionRule(ruleSymbol : SyntaxRuleSymbol) : PExpr
    {
      def astPointer = AstPointer.Existing(ruleSymbol.Structure, "astPtr");
      <[
        _ = astPtr;
        $(GetSyntaxRuleValue(ruleSymbol, AstPointerAllocator(astPointer)));
      ]>.FlattenPExpr()
    }

    protected Skip(field : RuleStructureField, parentAstPointer : AstPointerAllocator) : PExpr
    {
      <[ pos += $(parentAstPointer.Allocate().GetSize(field)); ]>
    }

    public GetRuleValue(ruleStructure : RuleStructure, name : string) : PExpr
    {
      def pointerAllocator = AstPointerAllocator(ruleStructure, name);
      def ast = GetSubrulesValues(ruleStructure, pointerAllocator);
      <[ $(pointerAllocator.Declare()); $ast ]>
    }

    public virtual GetSubrulesValues(ruleStructure : RuleStructure, astPointer : AstPointerAllocator) : PExpr
    {
      def statements = List();
      def arguments = List();

      when (ruleStructure is RuleStructure.Ast(Symbol = ExtentionRuleSymbol(ExtentionType = Postfix)))
        arguments.Add(<[ prefix ]>);

      statements.Add(<[ def __listStartPos = pos; ]>);

      foreach (field in ruleStructure.Fields)
      {
        | RuleStructureField.Marker => ()
        | RuleStructureField.SimpleCall(symbol)        when !SupportsVoidRules && symbol.Type is RuleType.Void
        | RuleStructureField.ExtensibleCall(symbol, _) when !SupportsVoidRules && symbol.Type is RuleType.Void => statements.Add(Skip(field, astPointer));
        | _  =>
          def varName = field.MakeVariableName("ast");
          statements.Add(<[ def $(varName : usesite) = $(GetRuleValue(field, astPointer)) ]>);
          arguments.Add(<[ $(varName : usesite) ]>);
      }

      statements.Add(CombineValues(ruleStructure, arguments.ToNList()));
      <[ { ..$(statements.ToNList()) } ]>
    }

    public GetTokenValue(field : RuleStructureField, parentAstPointer : AstPointerAllocator, code : PExpr) : PExpr
    {
      <[
        def (skip, size) = $(parentAstPointer.Allocate().GetSkipAndSize(field));
        pos += skip;
        def tokenPos = pos;
        pos += size;
        $code;
      ]>
    }

    public GetOptionValue
      ( field            : RuleStructureField.Option
      , parentAstPointer : AstPointerAllocator
      , hasValue         : PExpr
      , noValue          : PExpr
      ) : PExpr
    {
      <[
        if ($(parentAstPointer.Allocate().GetSize(field)) > 0)
          $hasValue;
        else
          $noValue;
      ]>
    }

    public GetListValue
      ( field            : RuleStructureField.List
      , parentAstPointer : AstPointerAllocator
      , init             : PExpr
      , item             : PExpr
      , returnCode       : PExpr
      , emptyCode        : PExpr
      ) : PExpr
    {
      def loopCode = <[
        def loop()
        {
          $item;
          if (listEnd > pos)
            loop();
          else
            assert(listEnd == pos);
        }
      ]>;
      GetListValueImpl
        (field             = field
        , parentAstPointer = parentAstPointer
        , init             = init
        , loopCode         = loopCode
        , returnCode       = returnCode
        , emptyCode        = emptyCode)
    }

    public GetListWithSeparatorValue
      ( field            : RuleStructureField.ListWithSeparator
      , parentAstPointer : AstPointerAllocator
      , init             : PExpr
      , item             : PExpr
      , separator        : PExpr
      , returnCode       : PExpr
      , emptyCode        : PExpr
      ) : PExpr
    {
      def loopCode = <[
        def loop()
        {
          $item;
          if (listEnd > pos)
          {
            $separator;
            if (listEnd > pos)
              loop();
            else
              assert(listEnd == pos);
          }
          else
            assert(listEnd == pos);
        }
      ]>;
      GetListValueImpl
        (field             = field
        , parentAstPointer = parentAstPointer
        , init             = init
        , loopCode         = loopCode
        , returnCode       = returnCode
        , emptyCode        = emptyCode)
    }

    private GetListValueImpl
      (field             : RuleStructureField
      , parentAstPointer : AstPointerAllocator
      , init             : PExpr
      , loopCode         : PExpr
      , returnCode       : PExpr
      , emptyCode        : PExpr
      ) : PExpr
    {
      def nonemptyCode = List();
      match(init)
      {
        | <[ { ..$exprs } ]> => nonemptyCode.AddRange(exprs)
        | e                  => nonemptyCode.Add(e)
      }
      nonemptyCode.Add(loopCode);
      nonemptyCode.Add(<[ loop() ]>);
      nonemptyCode.Add(returnCode);
      <[
        def listEnd = pos + $(parentAstPointer.Allocate().GetSize(field));
        if (listEnd > pos) { ..$(nonemptyCode.NToList()) }
        else $emptyCode
      ]>
    }
  }
}
