using N2.Model;
using N2.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace N2.Compiler
{
  public sealed partial class WalkerEmitter
  {
    [Record]
    private sealed class PostfixRuleWalkerEmitter : RuleWalkerEmitter[ExtentionRuleSymbol]
    {
      [RecordIgnore] public override WalkerTb : TypeBuilder { get; private set; }

      public override RuleParserBaseType : PExpr
      {
        get { <[ N2.Internal.ExtentionRuleParser ]> }
      }

      public override Define(walkersModule : TypeBuilder) : void
      {
        Generator.Init();

        def baseRule = Rule.BaseRule.Value;
        def signature = Generator.GetSignature(baseRule);
        def code = Generator.WalkerCodeForExtentionRule(Rule);

        def returnTypeIsVoid = signature.ReturnType is <[ void ]>; // TODO: сделать более точную проверку на void
        def postfixParameters =
        {
          def mandatoryParameters =
            <[ parameter: __startPos : int ]>
            :: <[ parameter: pos : ref int ]>
            :: <[ parameter: astPtr : int ]>
            :: <[ parameter: parser : ref N2.Internal.Parser ]>
            :: signature.Parameters;

          if (returnTypeIsVoid) mandatoryParameters
          else <[ parameter: prefix : $(signature.ReturnType) ]> :: mandatoryParameters;
        }

        WalkerTb = walkersModule.DefineNestedType(<[ decl:
          public sealed class $(Rule.Name + "_Walker" : usesite) : $(WalkerEmitter.GetRuleWalkerFullName(baseRule, Generator, "_PostfixWalker"))
          {
            private RuleParser : $(Rule.GetParserClassRef());

            public this(ruleParser : $(RuleParserBaseType))
            {
              RuleParser = ruleParser :> $(Rule.GetParserClassRef());
            }
          }
        ]>);

        DefineInitMethod();

        WalkerTb.DefineConditional(<[ decl:
          public override Walk(..$postfixParameters) : $(signature.ReturnType)
          {
            $code
          }
        ]>);
      }

      public override Emit() : void
      {
        WalkerTb.Compile();
      }
    }
  }
}
