using Nemerle;
using Nemerle.Imperative;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;
using N2.Internal;

namespace N2.Runtime
{
  public abstract class ExtensibleRuleWalkerBase
  {
    public PrefixId             : int;
    public PostfixId            : int;
    public FirstPostfixRule     : int;
    public FirstPostfixRuleId   : int;
    public PrefixWalkersOffset  : int;
    public PostfixWalkersOffset : int;
    public ParseResult               : ExtensibleRuleParser;

    public this(parseResult : ExtensibleRuleParser)
    {
      ParseResult                = parseResult;
      PrefixId              = parseResult.PrefixId;
      PostfixId             = parseResult.PostfixId;

      FirstPostfixRule      = parseResult.FirstPostfixRule;
      FirstPostfixRuleId    = parseResult.FirstPostfixRuleId;

      PrefixWalkersOffset   = if (parseResult.PrefixRules.Length > 0)  parseResult.PrefixRules[0].RuleId  else 0; //FIXME empty array
      PostfixWalkersOffset  = if (parseResult.PostfixRules.Length > 0) parseResult.PostfixRules[0].RuleId else 0; //FIXME empty array
    }

    public RuleFullName : string
    {
      get { this.ParseResult.Descriptor.Grammar.Name + "." + this.ParseResult.Descriptor.Name }
    }

    private SkipBadResults(mutable result : int, parseResult : ParseResult) : int
    {
      while (result > 0 && ((parseResult.ast[result] & ExtensibleRuleParser.AstMask.Flags) == ExtensibleRuleParser.AstFlags.Bad || parseResult.ast[result + ExtensibleRuleParser.AstOfs.State] != N2.Internal.ParseResult.AstParsedState))
        result = parseResult.ast[result + ExtensibleRuleParser.AstOfs.Next];
      result
    }

    protected FindFirstPrefix(pos : ref int, parseResult : ParseResult) : int
    {
      mutable ast = parseResult.TryGetAst(pos, PrefixId);
      when (ast > 0)
        return SkipBadResults(parseResult.TryGetPrefix(ref pos, ref ast), parseResult);
      0
    }

    protected FindNextPrefix(result : int, parseResult : ParseResult) : int
    {
      //если у нас лучшее значение то заканчиваем перебор
      when ((parseResult.ast[result] & ExtensibleRuleParser.AstMask.Flags) == ExtensibleRuleParser.AstFlags.Best)
        return 0;
      SkipBadResults(parseResult.ast[result + ExtensibleRuleParser.AstOfs.Next], parseResult)
    }

    protected FindFirstPostfix(pos : ref int, parseResult : ParseResult) : int
    {
      mutable ast = parseResult.TryGetAst(pos, PostfixId);
      when (ast > 0)
      {
        mutable result = parseResult.TryGetPostfix(ref pos, ref ast);
        // пропускаем правила с низкой силой связывания.
        while (result > 0 && (parseResult.ast[result] & ExtensibleRuleParser.AstMask.Id) < FirstPostfixRuleId)
          result = parseResult.ast[result + ExtensibleRuleParser.AstOfs.Next];
        return SkipBadResults(result, parseResult);
      }
      0
    }

    protected FindNextPostfix(result : int, parseResult : ParseResult) : int
    {
      //если у нас лучшее значение то заканчиваем перебор
      when ((parseResult.ast[result] & ExtensibleRuleParser.AstMask.Flags) == ExtensibleRuleParser.AstFlags.Best)
        return 0;
      SkipBadResults(parseResult.ast[result + ExtensibleRuleParser.AstOfs.Next], parseResult)
    }
  }
}
