using System.Reflection;
using N2.Internal;
using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Diagnostics;
using System.IO;
using System.Linq;

public module Utils
{
  public VisualizeFrame(frame : RecoveryStackFrame) : void
  {
    VisualizeFrames([frame])
  }
  
  public VisualizeFrames(stacks : Seq[RecoveryStackFrame]) : void
  {
    def dot = N2.Internal.RecoveryStackFrame.DotStacks(stacks);
    def dotFileName = Path.ChangeExtension(Path.GetTempFileName(), "dot");
    File.WriteAllText(dotFileName, dot);
    def svgFileName = Path.ChangeExtension(dotFileName, "svg");
    def process = Process.Start(FindDotExe(), $<#$dotFileName -o $svgFileName -Tsvg#>);
    process.WaitForExit();
    _ = Process.Start(svgFileName);
  }

  public VisualizeRecoveryResults(results : Seq[RecoveryResult]) : void
  {
    VisualizeFrames(results.Map(r => r.Stack))
  }

  static ToolsDir = "ExternalTools";
  
  private FindDotExe() : string
  {
    def dir = Path.GetDirectoryName(Path.GetFullPath(Assembly.GetExecutingAssembly().Location));
    def findDot(dir : string) : string
    {
      if (Directory.Exists(Path.Combine(dir, ToolsDir)))
        Path.Combine(dir, ToolsDir, @"DOT\dot.exe")
      else
      {
        def parent = Directory.GetParent(dir);
        if (parent == null)
          null
        else
          findDot(parent.FullName)
      }
    }
    
    findDot(dir)
  }
}
