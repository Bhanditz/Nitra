using N2.Runtime.Internal;
using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;
using Nemerle.Imperative.GoTo;

using System;
using System.Collections.Generic;

namespace N2.Internal
{
  public sealed partial class ExtensibleRuleParser
  {
    public BestPostfix(mutable bestResult : int, parser : Parser) : int
    {
      // пропускаем правила с низкой силой связывания.
      while (bestResult > 0 && (parser.ast[bestResult] & AstMask.Id) < FirstPostfixRuleId)
        bestResult = parser.ast[bestResult + AstOfs.Next];
      // ищем лучшее правило среди тех у кого подходящая сила связывания.
      while (bestResult > 0 && (parser.ast[bestResult] & AstMask.Flags) != AstFlags.Best)
        bestResult = parser.ast[bestResult + AstOfs.Next];
      bestResult;
    }

    public ParsePostfix(mutable curTextPos : int, text : string, parser : Parser) : int
    {
      unchecked
      {
        mutable postfixAst;
        mutable state = ExtentionRuleParserState();
        state.parser = parser;
        state.curTextPos = curTextPos;

        mutable i;
        mutable c; // временная переменная для отсечения правил по первой букве

        when (curTextPos >= text.Length) // постфиксное правило которое не съело ни одного символа игнорируется
          return curTextPos;// при достижении конца текста есть нечего
        //ищем запомненое
        postfixAst = parser.TryGetAst(curTextPos, PostfixId);
        when (postfixAst > 0)
        {
          def maxFailPos = parser.ast[postfixAst + PostfixOfs.MaxFailPos];
          when (parser.MaxFailPos < maxFailPos)
            parser.MaxFailPos = maxFailPos;
          state.lastResult = parser.TryGetPostfix(ref curTextPos, ref postfixAst);//список разобраных с этого места правил
          state.bestResult = state.lastResult;
          i = parser.ast[postfixAst + PostfixOfs.FirstRuleIndex] - 1;//индекс первого не разобранного правила
          if (i >= FirstPostfixRule)// не всё разобрано
          {
            state.bestResult = BestPostfix(state.bestResult, parser);
            if (state.bestResult > 0 && parser.ast[state.bestResult + AstOfs.State] == Parser.AstParsedState)//Убеждаемся что разбор успешный
              state.bestEndPos = curTextPos + GetPostfixSize(state.bestResult, parser);
            else
              state.bestEndPos = -1;
            goto postfix_parse;//парсим то что не распарсили раньше
          }
          else
          {
            state.bestResult = BestPostfix(state.bestResult, parser);
            if (state.bestResult > 0)
            {
              def astState = parser.ast[state.bestResult + AstOfs.State];
              if (astState == Parser.AstParsedState)//Убеждаемся что разбор успешный
                return curTextPos + GetPostfixSize(state.bestResult, parser);
              else if (astState < 0)
                assert3(false)
              else
                return curTextPos;//облом. Заканчиваем разбор.
            }
            else
              return curTextPos;//облом. Заканчиваем разбор.
          }
        }
        //нет мемоизации
        postfixAst = parser.Allocate(PostfixOfs.NodeSize, PostfixId);
        parser.ast[postfixAst + PostfixOfs.Next] = parser.memoize[curTextPos];
        parser.memoize[curTextPos] = postfixAst;
        state.bestResult = 0;
        state.bestEndPos = curTextPos;
        state.lastResult = 0;
        i = PostfixRules.Length - 1;
      label postfix_parse;
        parser.ast[postfixAst + PostfixOfs.FirstRuleIndex] = FirstPostfixRule;
        c = text[curTextPos];
        for (; i >= FirstPostfixRule; --i)
        {
          def postfixRule = PostfixRules[i];
          when (postfixRule.LowerBound <= c && c <= postfixRule.UpperBound)
          {
            state.newResult = -1;
            state.newEndPos = postfixRule.Parse(curTextPos, text, ref state.newResult, parser);
            when (state.newResult > 0)
              state.Append();
          }
        }

        parser.ast[postfixAst + PostfixOfs.List] = state.lastResult;

        when (state.bestResult <= 0 || state.bestEndPos < 0)// не смогли разобрать постфикс
        {
          when (curTextPos > parser.MaxFailPos)
            parser.MaxFailPos = curTextPos;
          parser.ast[postfixAst + PostfixOfs.MaxFailPos] = parser.MaxFailPos;
          return curTextPos;
        }

        parser.ast[postfixAst + PostfixOfs.MaxFailPos] = parser.MaxFailPos;

        when (state.bestEndPos <= curTextPos)
          return curTextPos;
        state.bestEndPos;
      }
    }
  }
}
