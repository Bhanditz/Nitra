using Nemerle.Imperative;

namespace N2.Internal
{
  public sealed partial class ExtensibleRuleParser
  {
    public override FindRecoveryPosition(mutable curTextPos : int, stack : list[RecoveryStackFrame], parser : N2.Internal.Parser) : void
    {
      curTextPos = FindRecoveryPositionPrefix(curTextPos, stack, parser);
      when (curTextPos > 0)
        do curTextPos = FindRecoveryPositionPostfix(curTextPos, stack, parser) while (curTextPos > 0);
    }

    private FindRecoveryPositionPrefix(mutable curTextPos : int, stack : list[RecoveryStackFrame], parser : N2.Internal.Parser) : int
    {
      mutable bestResult = 0;
      mutable prefixAst = parser.TryGetAst(curTextPos, PrefixId);
      when (prefixAst > 0)
      {
        bestResult = parser.TryGetPrefix(ref curTextPos, prefixAst);
        if (bestResult > 0)
        {
          def state = parser.ast[bestResult + AstOfs.State];
          assert (state == Parser.AstParsedState || state >= 0);
        }
        else when (bestResult < 0)
          return -1;
      }
      mutable newPos;
      def stack = RecoveryStackFrame(this, prefixAst, curTextPos, PrefixState, 0, 0, 0, false, FrameInfo.None) :: stack;
      if (bestResult == 0)
      {//0
        if (parser.MaxFailPos == curTextPos)
          parser.RecoveryStacks.Add(stack);
        else
        {
          foreach (prefixRule in PrefixRules)
          {
            _ = prefixRule.FindRecoveryPosition(curTextPos, bestResult, stack, parser);
          }
        }
        newPos = -1;
      }
      else if (parser.ast[bestResult + AstOfs.Next] == 0)
      {//1
        def prefixParser = PrefixRuleParser(bestResult, parser);
        prefixParser.FindRecoveryPosition(curTextPos, bestResult, stack, parser);
        def state = parser.ast[bestResult + AstOfs.State];
        if (state == Parser.AstParsedState)
          newPos = curTextPos + GetPrefixSize(bestResult, parser);
        else
          newPos = -1;
      }
      else
      {//many
        assert3(false);
      }
      newPos
    }

    private FindRecoveryPositionPostfix(mutable curTextPos : int, stack : list[RecoveryStackFrame], parser : N2.Internal.Parser) : int
    {
      mutable bestResult = 0;
      mutable postfixAst = parser.TryGetAst(curTextPos, PostfixId);
      when (postfixAst > 0)
      {
        bestResult = parser.TryGetPostfix(ref curTextPos, ref postfixAst);
        if (bestResult > 0)
        {
          def state = parser.ast[bestResult + AstOfs.State];
          assert (state == Parser.AstParsedState || state >= 0);
        }
        else when (bestResult < 0)
          return -1;
      }
      mutable newPos;
      def stack = RecoveryStackFrame(this, postfixAst, curTextPos, PostfixState, 0, 0, 0, false, FrameInfo.None) :: stack;
      if (bestResult == 0)
      {//0
        if (parser.MaxFailPos == curTextPos)
          parser.RecoveryStacks.Add(stack);
        else
        {
          foreach (postfixRule in PostfixRules)
          {
            _ = postfixRule.FindRecoveryPosition(curTextPos, bestResult, stack, parser);
          }
        }
        newPos = -1;
      }
      else if (parser.ast[bestResult + AstOfs.Next] == 0)
      {//1
        def postfixParser = PostfixRuleParser(bestResult, parser);
        postfixParser.FindRecoveryPosition(curTextPos, bestResult, stack, parser);
        def state = parser.ast[bestResult + AstOfs.State];
        if (state == Parser.AstParsedState)
          newPos = curTextPos + GetPostfixSize(bestResult, parser);
        else
          newPos = -1;
      }
      else
      {//many
        assert3(false);
      }
      newPos
    }
  }
}
