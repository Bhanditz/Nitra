using Nemerle;
using Nemerle.Collections;
using Nemerle.Extensions;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace N2.Internal
{
    /// <summary>
    /// Описывает одно пропарсивание. Для каждого RecoveryStackFrame сздается столько ParseAlternativeNode сколько в нем есть ParseAlternative.
    /// </summary>
    [StructuralEquality]
    public class ParseAlternativeNode
    {
      public Frame                 : RecoveryStackFrame;
      public ParseAlternativeIndex : int;

      [EqualsIgnore]
      [FlagAccessor(Best, IsVisited, IsMarked, IsClone, IsInsideToken, flags = WantSetter)]
      mutable _state : StateFlag;
      
      [EqualsIgnore]
      public MissedSeparator : ParseAlternativeNode { get; internal set; }

      [EqualsIgnore] public mutable Depth : int = 0;

      [EqualsIgnore] mutable _skipedMandatoryTokenCount         : int = -1;

      [EqualsIgnore] public mutable MinSkipedMandatoryTokenCount : int = int.MaxValue;
      
      [EqualsIgnore]
      protected _children : List[ParseAlternativeNode] = List();
      
      [EqualsIgnore]
      protected _parents     : List[ParseAlternativeNode] = List();

      private this(frame : RecoveryStackFrame, parseAlternativeIndex : int, nodeMap : Dictionary[int, ParseAlternativeNode])
      {
        assert3(frame.Best);
        assert3(frame.ParseAlternatives.Length > 0);
        assert3(parseAlternativeIndex >= 0);
        assert3(parseAlternativeIndex < frame.ParseAlternatives.Length);
        
        this.Frame = frame;
        
        when (frame is RecoveryStackFrame.Root)
          this.MinSkipedMandatoryTokenCount = 0;
          
        this.ParseAlternativeIndex = parseAlternativeIndex;
        this.Best = true;
        nodeMap.Add(MakeKey(frame, parseAlternativeIndex), this);
        
        def parseAlternative = frame.ParseAlternatives[parseAlternativeIndex];
        def fail  = parseAlternative.Fail;
        def start = parseAlternative.Start;
        def end   = parseAlternative.End;
        
        def getNode(frame : RecoveryStackFrame, parseAlternativeIndex : int) : ParseAlternativeNode
        {
          def key = MakeKey(frame, parseAlternativeIndex);
          mutable node;
          when (!nodeMap.TryGetValue(key, out node))
            node = ParseAlternativeNode(frame, parseAlternativeIndex, nodeMap);
          node
        }

        when (frame is ListBody as body when body.MissedSeparator != null)
        {
          def missed = body.MissedSeparator;
          def separatorParseAlternativeIndex = missed.ParseAlternatives.FindIndex(a => a.Start == end);
          assert3(missed.ParseAlternatives.Count(a => a.Start == start) == 1);
          assert3(body.StartPos == missed.StartPos);
          assert3(body.StartPos == end); // элемент должен быть продолбан
          missed.Best = true;
          this.MissedSeparator = getNode(missed, separatorParseAlternativeIndex);
        }
        
        def frameParents = frame.Parents;
        mutable parentCount = 0;
        
        foreach (parent when parent.Best in frameParents)
          parentCount += parent.ParseAlternatives.Length;

        def parents = _parents;
        
        foreach (parent when parent.Best in frameParents with i)
        {
          for (mutable index = 0; index < parent.ParseAlternatives.Length; index++)
          {
            def a = parent.ParseAlternatives[index];
            when (end >= 0 && end == a.Start 
              || end < 0 && fail == a.Start && a.End < 0 && a.Start == a.Fail) // в случае если дочерня альтернатива зафейлилась, мы ищем пустые фэйл-ветки
            {
              def node = getNode(parent, index);
              parents.Add(node);
            }
          }
        }
      }
      
      public static MakeKey(frame : RecoveryStackFrame, parseAlternativeIndex : int) : int
      {
        assert3(parseAlternativeIndex < 100);
        frame.Id * 100 + parseAlternativeIndex
      }
      
      public static IsAdded(nodeMap : Dictionary[int, ParseAlternativeNode], frame : RecoveryStackFrame, parseAlternativeIndex : int) : bool
      {
        nodeMap.ContainsKey(MakeKey(frame, parseAlternativeIndex))
      }
      
      public static MakeGraph(topFrames : Seq[RecoveryStackFrame]) : List[ParseAlternativeNode]
      {
        def nodeMap = Dictionary.[int, ParseAlternativeNode]();

        foreach (frame when frame.IsTop in topFrames)
          for (mutable i = 0; i < frame.ParseAlternatives.Length; i++)
            unless (IsAdded(nodeMap, frame, i))
              _ = ParseAlternativeNode(frame, i, nodeMap);

        def result = nodeMap.Values.ToList();
        result.Sort(fun(a, b) { def res = a.Frame.Depth - b.Frame.Depth;  if (res != 0)  res else a.ParseAlternativeIndex - b.ParseAlternativeIndex; });
        
        // fill children
        foreach (node in result)
        {
          assert3(node.Best);
          foreach (parent in node._parents)
            parent._children.Add(node);
        }
        
        result
      }

      
      public HasChildren : bool
      {
        get
        {
          foreach (node in _children)
            when (node.Best)
              return true;
            
          false
        }
      }
      
      public HasAtLeastTwoChildren : bool
      {
        get
        {
          when (_children.Count < 2)
            return false;

          mutable counter = 0;
          foreach (node in _children)
          {
            when (node.Best)
              ++counter;
            when (counter == 2)
              return true;
          }

          false
        }
      }
      
      public HasParents : bool
      {
        get
        {
          def count = _parents.Count;
          
          match (count)
          {
            | 0 => false
            | 1 => _parents[0].Best
            | _ =>
              foreach (node in _parents)
                when (node.Best)
                  return true;
              false
          }
        }
      }

      public HasAtLeastTwoParents : bool
      {
        get
        {
          when (_parents.Count < 2)
            return false;

          mutable counter = 0;
          foreach (node in _parents)
          {
            when (node.Best)
              ++counter;
            when (counter == 2)
              return true;
          }

          false
        }
      }

      public IsTop : bool { get { this.Best && !this.HasChildren } }
      
      public IsRoot : bool { get { this.Best && !this.HasParents } }
      
      public Id : int { get { MakeKey(this.Frame, this.ParseAlternativeIndex) } }

      public IsEmpty : bool
      {
        get
        {
          def frame = this.Frame;
          
          when (frame.StartPos != frame.TextPos)
            return false;
            
          def a = frame.ParseAlternatives[ParseAlternativeIndex];
          
          when (frame.TextPos != a.Stop)
            return false;
            
          def isFailOnFirstState =
            if (IsTop)
              frame.FailState2 == frame.FirstState 
            else
              frame.FailState == frame.FirstState;
            
          isFailOnFirstState && a.State == -1
        }
      }
      
      public Parents : Seq[ParseAlternativeNode]
      {
        get
        {
          foreach (node when node.Best in _parents)
            yield node;
        }
      }

      public AllParents : Seq[ParseAlternativeNode]
      {
        get { _parents }
      }
      
      public Children : Seq[ParseAlternativeNode]
      {
        get
        {
          foreach (node when node.Best in _children)
            yield node;
        }
      }
      
      public AllChildren : Seq[ParseAlternativeNode]
      {
        get
        {
          _children
        }
      }
      
      ResetSkipedMandatoryTokenCount() : void
      {
        _skipedMandatoryTokenCount   = -1;
        MinSkipedMandatoryTokenCount = int.MaxValue;
      }
      
      public SkipedMandatoryTokenCount : int
      {
        get
        {
          when (_skipedMandatoryTokenCount < 0)
            _skipedMandatoryTokenCount = CalcSkipedMandatoryTokenCount();
            
          _skipedMandatoryTokenCount
        }
      }

      //private RemoveOptionalTokens(childTokenCount : int) : int
      //{
      //  if (Frame is RecoveryStackFrame.ExtensiblePrefix || Frame is RecoveryStackFrame.ExtensiblePostfix || Frame is RecoveryStackFrame.Extensible)
      //    childTokenCount
      //  else
      //    Math.Min(Frame.GetRuleParser().GetMandatoryTokensForState(Frame.FailState), childTokenCount)
      //}

      public CalcSkipedMandatoryTokenCount() : int
      {
        def endState           = this.ParseAlternative.State;
        def frame              = this.Frame;
        
        when (frame is RecoveryStackFrame.Root)
          return 0;
        
        def parsingFailAtState = frame.FailState2;
        def recursionState     = frame.FailState;
        def ruleParser         = frame.GetRuleParser();
        def calcSkipedMandatoryTokenCount(startState : int, endState : int) : int
        {
          mutable count = 0;
          
          for (mutable i = startState; i != endState; i = frame.GetNextState(i))
            count += ruleParser.GetMandatoryTokensForState(i);
            
          count
        }
        
        mutable skippedPrefixMandatoryTokenCount  = 0;
        mutable skippedPostfixMandatoryTokenCount = 0;

        if (this.IsTop)
        {
          when (recursionState != endState)
            skippedPrefixMandatoryTokenCount = calcSkipedMandatoryTokenCount(parsingFailAtState, endState);
        }
        else
        {
          when (parsingFailAtState < recursionState)
            skippedPrefixMandatoryTokenCount = calcSkipedMandatoryTokenCount(parsingFailAtState, recursionState);

          def startState = frame.GetNextState(recursionState);

          when (startState >= 0 && (startState < endState || endState < 0))
            skippedPostfixMandatoryTokenCount = calcSkipedMandatoryTokenCount(startState, endState);
        }
        
        skippedPrefixMandatoryTokenCount + skippedPostfixMandatoryTokenCount
      }
      
      public GetFlatParseAlternatives() : List[list[ParseAlternativeNode]]
      {
        def total = List();

        foreach (parent in Parents)
        {
          def results = parent.GetFlatParseAlternatives();
          for (mutable index = 0; index < results.Count; index++)
            results[index] = this :: results[index];

          total.AddRange(results);
        }

        when (total.Count == 0)
          total.Add([this]);

        total
      }
      
      public ParseAlternative : ParseAlternative { get { Frame.ParseAlternatives[ParseAlternativeIndex] }}

      public static DownToTop(nodes : List[ParseAlternativeNode], task : Action[ParseAlternativeNode]) : void
      {
        for (mutable i = nodes.Count - 1; i >= 0; i--)
        {
          def node = nodes[i];
          when (node.Best)
            task(node);
        }
      }

      public static TopToDown(nodes : List[ParseAlternativeNode], task : Action[ParseAlternativeNode]) : void
      {
        foreach (node in nodes)
        {
          when (node.Best)
            task(node);
        }
      }
      
      public Remove() : void
      {
        def children = this.Children.ToList();
        
        foreach (child in children)
        {
          def parents = child.Parents.ToList();
          
          when (parents.Count == 1)
          {
            assert3(parents[0] == this);
            child.Remove();
          }
        }

        foreach (node in Parents)
          node.ResetSkipedMandatoryTokenCount();

        this.Best = false;
      }

      public MakeMissedSeparator(parseResult : ParseResult) : void
      {
        def node = this.MissedSeparator;
      
        when (node != null)
        {
          def frame = node.Frame;
          node.Best  = false;
          frame.Best = false;
          def rp = frame.GetRuleParser();
          _ = rp.PatchAst(frame.StartPos, -1, parseResult.ErrorData.Count - 1, frame, parseResult);
          this.MissedSeparator = null;
        }
      }

      public PatchAst(errorIndex : int, parseResult : ParseResult) : bool
      {
        //MakeMissedSeparator(parseResult);
        def pa = this.ParseAlternative;
        def (startPos, startState) = if (pa.End < 0)
        {
          if (pa.State == Frame.FailState)
            (pa.Fail, pa.State);
          else
            (pa.Start, pa.State);
        }
        else
          (pa.Start,  pa.State);
        Frame.GetRuleParser().PatchAst(startPos, startState, errorIndex, Frame, parseResult)
      }

      public ContinueParse(parseResult : ParseResult) : bool
      {
        MakeMissedSeparator(parseResult);
        Frame.GetRuleParser().PatchAst(-1, -2, -1, Frame, parseResult)
      }
      
      public this(other : this, frameMap : Hashtable[int, RecoveryStackFrame])
      {
        this.Frame                              =  frameMap[other.Frame.Id];
        this.ParseAlternativeIndex              =  other.ParseAlternativeIndex             ;
        this._state                             =  other._state                            ;
        this.MissedSeparator                    =  other.MissedSeparator                   ;
        this.Depth                              =  other.Depth                             ;
        this._skipedMandatoryTokenCount         =  other._skipedMandatoryTokenCount        ;
      }
      
      public static CloneGraph(graph : List[ParseAlternativeNode]) : List[ParseAlternativeNode]
      {
        def cloned   = List(graph.Count);
        def nodeMap  = Hashtable(graph.Count);
        def frameMap = Hashtable(graph.Count(n => n.ParseAlternativeIndex == 0));

        foreach (node in graph)
        {
          def frame = node.Frame;
          def id = frame.Id;
          unless (frameMap.Contains(id))
            frameMap.Add(id, frame.CloneInternal());
        }
        
        foreach (node in graph)
        {
          def clone = ParseAlternativeNode(node, frameMap);
          cloned.Add(clone);
          nodeMap.Add(node.Id, clone);
        }
      
        foreach (node in graph)
        {
          def clone    = nodeMap[node.Id];
          def children = clone._children;
        
          foreach (oldNode in node.Children)
          {
            mutable child;
            when (nodeMap.TryGetValue(oldNode.Id, out child))
              children.Add(child);
          }

          def parents = clone._parents;
        
          foreach (oldNode in node.Parents)
            _ = parents.Add(nodeMap[oldNode.Id]);
        }      
      
        cloned
      }

      public static DotNodes(stacks : Seq[ParseAlternativeNode]) : string
      {
        def allFrames = HashSet();
        def collectAllFrames(frame : ParseAlternativeNode) : void
        {
          unless (allFrames.Add(frame))
            return;
          
          foreach (parent in frame.Parents)
             collectAllFrames(parent);
        }
      
        foreach (frame in stacks)
          collectAllFrames(frame);
        
        def map = Hashtable();
        def colorIndex(frame)
        {
          mutable value;
          _ = map.TryGetValue(frame, out value);
          value
        }
        def fillMap(node : ParseAlternativeNode) : void
        {
          def frame = node.Frame;
          mutable counter;
          _ = map.TryGetValue(frame, out counter);
        
          map[frame] = counter + 1;

          when (node.MissedSeparator != null)
            fillMap(node.MissedSeparator);
        
          foreach (subFrame in node.Parents)
            fillMap(subFrame);
          
        }
        foreach (node in allFrames) 
          when (node.IsTop)
            fillMap(node);
      
        def name(node : ParseAlternativeNode) : string
        {
          node.Id.ToString()
        }
        def visited = HashSet();
        def nodes = List();
        def edges = List();
        def label(node : ParseAlternativeNode, index : int) : string
        {
          def frame = node.Frame;
          def label = 
            match (frame)
            {
              | Root => $<#Root PA=$(node.ParseAlternative)$(if (node.Best) " B" else "")#>
              | _ =>
                def ruleParser = frame.GetRuleParser();
                def makeCode()
                {
                  def str = ruleParser.CodeWithStates.Replace("\"", "\\\"");
                  def count = 30;
                  def sb = Text.StringBuilder();
                  def parts = str.Split(array[' '], StringSplitOptions.RemoveEmptyEntries);
                  mutable len = 0;
                  foreach (part in parts)
                  {
                    len += part.Length;
                    if (len > count)
                    {
                      len = 0;
                      _ = sb.Append("\n  ");
                    }
                    else
                      _ = sb.Append(" ");
                    _ = sb.Append(part);
                  }
          
                  sb.ToString();
                }
                def failRuleCode = "\n" + makeCode();
                def txt = frame.ParseResult.Text;
                def len(a : ParseAlternative) : int { (if (a.End < 0) a.Fail else a.End) - a.Start }
                def getText(a : ParseAlternative) : string
                {
                  txt.Substring(a.Start, len(a)).Replace("\"", "\\\"")
                }
                def a = node.ParseAlternative;
                def parse = $<#  '$(getText(a))'#>;
                def top = if (frame.IsTop) "T" else "";
                def pa = $" PA$(node.ParseAlternative)";
                def skip = if (a.Skip > 0) "'" + txt.Substring(a.Start - a.Skip, a.Skip).Replace("\"", "\\\"") + "'" + " " else null;
                def minMaxSkip = $"\n$(skip)MinST=$(node.MinSkipedMandatoryTokenCount)";
                def skipedTokens = $"$minMaxSkip ST=$(node.SkipedMandatoryTokenCount)";
                def label = $"$top«$(ruleParser.RuleName)»$(frame.Info(node.Id, pa, node.Best))$skipedTokens: $parse\n";
                $"$label$failRuleCode"
            };
          def color = if (colorIndex(frame) == 1 && index < RecoveryStackFrame._colors.Length) $" color=$(RecoveryStackFrame._colors[index]) style=filled" else "";
          name(node) + $<#[label="$label"$color shape=box]#>
        }
        def visitNodes(node : ParseAlternativeNode, index : int) : void
        {
          //def frame = node.Frame;
          when (visited.Add(node))
          {
            when (node.MissedSeparator != null)
            {
              edges.Add($"$(name(node)) -> $(name(node.MissedSeparator)) [dir=back color=red]");
              visitNodes(node.MissedSeparator, index);
            }
            nodes.Add(node, index);
            foreach (parent in node.AllParents)
            {
              def color = if (node.Best && parent.Best) " color=lime" else "";
              edges.Add($"$(name(node)) -> $(name(parent)) [dir=back$color]");
              visitNodes(parent, index);
            }
          }
        }
        foreach (node in stacks with i)
          visitNodes(node, i);
        $<#
  digraph ParseAlternativeNode
  {
    ..$(nodes; ";\n  "; label);
    ..$(edges; ";\n  ");
  }#>
      }
      
      public override ToString() : string
      {
        this.Frame.ToStringImpl(this.Id, $" PA$(this.ParseAlternative)")
      }
    }
}
