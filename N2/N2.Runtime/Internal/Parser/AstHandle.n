using N2.Runtime;
using N2.Runtime.Reflection;
using Nemerle;
using Nemerle.Imperative;
using Nemerle.Collections;
using Nemerle.Late;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Diagnostics;
using System.Linq;
using System.Collections.ObjectModel;

using SCG = System.Collections.Generic;

namespace N2.Internal
{
  public partial class Parser
  {
    private _extensibleHandleCache : SCG.Dictionary[ExtensibleRuleParser * int * bool, AstHandle.Extensible] = SCG.Dictionary();
    private GetExtensibleHandle(ruleParser : ExtensibleRuleParser, textPos : int, isPrefix : bool) : AstHandle.Extensible
    {
      mutable handle;
      def key = (ruleParser, textPos, isPrefix);
      unless (_extensibleHandleCache.TryGetValue(key, out handle))
      {
        handle = AstHandle.Extensible(0, textPos, ruleParser);
        _extensibleHandleCache.Add(key, handle)
      }
      handle
    }

    private _extentionHandleCache : SCG.Dictionary[ExtentionRuleParser * int, AstHandle.Extention] = SCG.Dictionary();
    public GetExtentionHandle(ruleParser : ExtentionRuleParser, astPtr : int, textPos : int) : AstHandle.Extention
    {
      mutable handle;
      def key = (ruleParser, textPos);
      unless (_extentionHandleCache.TryGetValue(key, out handle))
      {
        handle = AstHandle.Extention(astPtr, textPos, ruleParser);
        _extentionHandleCache.Add(key, handle)
      }
      handle
    }

    public GetPrefixAstHandle(ruleParser : ExtensibleRuleParser, textPos : int) : AstHandle.Extensible
    {
      def astHandle = GetExtensibleHandle(ruleParser, textPos, true);
      astHandle.AstPtr = TryGetAst(textPos, ruleParser.PrefixId);
      astHandle
    }

    public GetPostfixAstHandle(ruleParser : ExtensibleRuleParser, textPos : int) : AstHandle.Extensible
    {
      def astHandle = GetExtensibleHandle(ruleParser, textPos, false);
      astHandle.AstPtr = TryGetAst(textPos, ruleParser.PostfixId);
      astHandle
    }

    private _simpleHandleCache : SCG.Dictionary[int * int, AstHandle.Simple] = SCG.Dictionary();
    public GetSimpleAstHandle(ruleParser : SimpleRuleParser, textPos : int) : AstHandle.Simple
    {
      mutable handle;
      def key = (ruleParser.RuleId, textPos);
      unless (_simpleHandleCache.TryGetValue(key, out handle))
      {
        handle = AstHandle.Simple(0, textPos, ruleParser);
        _simpleHandleCache.Add(key, handle)
      }
      handle.AstPtr = TryGetAst(textPos, ruleParser.RuleId);
      handle
    }

    private _subruleHandleCache : SCG.Dictionary[int * int, AstHandle.Subrule] = SCG.Dictionary();
    public GetSubruleAstHandle(ruleParser : IRecoveryRuleParser, ruleId : int, textPos : int) : AstHandle.Subrule
    {
      mutable handle;
      def key = (ruleId, textPos);
      unless (_subruleHandleCache.TryGetValue(key, out handle))
      {
        handle = AstHandle.Subrule(0, textPos, ruleParser, ruleId);
        _subruleHandleCache.Add(key, handle)
      }
      handle.AstPtr = TryGetAst(textPos, ruleId);
      handle
    }
  }
}
