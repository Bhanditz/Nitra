using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nemerle.Parser
{
  /// <summary>
  /// Description of Util.
  /// </summary>
  public module Util
  {
    public Once[T](this source : IEnumerable[Nemerle.Parser.Ast]) : option[T]
      where T: Nemerle.Parser.Ast
    {
      def source2 = source.OfType.[T]().NToList();
      
      match (source2)
      {
        [x]           => Some(x)
        | null | []   => None()
        | x :: y :: _ => 
          Nemerle.Compiler.Message.Error(y.Location.ToNemerleLocation(), 
            "Acceptably only one entry.");
          Some(x)
      }
    }

    //TODO: fix the bug in the compiler
    //public Once[T](this source : list[Nemerle.Parser.Ast]) : ValueOption[T]
    //  where T: Nemerle.Parser.Ast
    //{
    //  def source2 = source.OfType.[T]();
      
    //  match (source2)
    //  {
    //    [x]           => VSome(x)
    //    | null | []   => VNone()
    //    | x :: y :: _ => 
    //      Nemerle.Compiler.Message.Error((y : Nemerle.Parser.Ast).Location.ToNemerleLocation(), 
    //      "Acceptably only one entry."); VSome(x)
    //  }
    //}
    
    public Flatten[T](this src : Seq[option[T]]) : list[T]
    {
      def dst = List();
      foreach (Some(item) in src)
        dst.Add(item);
      dst.NToList();
    }

    public Flatten[T](this src : list[option[T]]) : list[T]
    {
      def dst = List(src.Length);
      foreach (Some(item) in src)
        dst.Add(item);
      dst.NToList();
    }
    
    public GraphWalk[Key, Value]( start   : IEnumerable[Key]
                                , calc    : Key * (Key -> Value) -> Value
                                , onCycle : Key -> Value)
                                : Map[Key, Value]
    {
      mutable result = Map();
      def visited = Hashtable();
      def getValue(key)
      {
        if (result.Contains(key))
          result.Get(key);
        else
        {
          def value = if (visited.ContainsKey(key))
            onCycle(key);
          else
          {
            visited.Add(key, 0);
            def value = calc(key, getValue);
            visited.Remove(key);
            value;
          }
          result = result.Replace(key, value);
          value;
        }
      }
      foreach (key in start)
        _ = getValue(key);
      result;
    }
  }

  namespace Internal
  {
    using Nemerle.Compiler;
    using Nemerle.Macros;
    using PT = Nemerle.Compiler.Parsetree;

    using EvalLocationXImpl;

    /* Gets StartPos value of 'x' when x is NToken or Located, otherwise returns 'default' */
    macro EvalLocationStart(x, default)
    {
      EvalLocation(ImplicitCTX(), PT.Splicable.Name(PT.Name("StartPos")), x, default)
    }

    /* Gets EndPos value of 'x' when x is NToken or Located, otherwise returns 'default' */
    macro EvalLocationEnd(x, default)
    {
      EvalLocation(ImplicitCTX(), PT.Splicable.Name(PT.Name("EndPos")), x, default)
    }

    internal module EvalLocationXImpl
    {
      public EvalLocation(typer : Typer, prop : PT.Splicable, x : PT.PExpr, default : PT.PExpr) : PT.PExpr
      {
        def locatedType = FixedType.Class(typer.Manager.LookupTypeInfo("Nemerle.Parser.Located", 0), []);
        def ntokenType = FixedType.Class(typer.Manager.LookupTypeInfo("Nemerle.Parser.NToken", 0), []);
  
        def makeResult(tX)
        {
          if(tX.Type.TryRequire(locatedType))
            <[ $(tX : typed).Location.$prop ]>
          else if(tX.Type.TryRequire(ntokenType))
            <[ $(tX : typed).$prop ]>
          else
            default
        }

        def tX = typer.TypeExpr(x);
        match(tX.Type.Hint)
        {
          | Some(_) => makeResult(tX)
          | _ =>
            typer.DelayMacro(lastChance =>
              match(tX.Type.Hint)
              {
                | Some(_) => Some(makeResult(tX))
                | _ when lastChance => Some(default)
                | _ => None()
              }
            )
        }
      }
    }
  }
}