using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;

using N2.Internal;
using N2.Model;
using N2.Compiler;

using System;
using System.Diagnostics;
using System.Linq;
using SCG = System.Collections.Generic;

namespace N2
{
  internal partial class GrammarCompiler
  {
    public DefineCreateAst(rd : RuleDefinition, descriptorTB : TypeBuilder) : void
    {
      def rootAstMaker(arguments : list[PExpr]) : PExpr
      {
        def arguments = <[ N2.Location(source, startPos, endPos) ]> :: <[ [] ]> :: arguments;
        <[ $( PExpr.FromQualifiedIdentifier(Manager, DefineAstType(rd).FullName)).Ast(..$arguments) ]>
      }
      def internalAstMaker(_ : list[PExpr]) : PExpr
      {
        | []   => assert(false)
        | [x]  => x
        | many => <[ (..$many) ]>
      }
      def makeReader(structField : AstStruct.StructField, astPointer : AstPointer) : PExpr
      {
        match(structField.Field)
        {
          | RuleField.TokenString
          | RuleField.TokenCall =>
            <[ N2.NToken(startPos, endPos) ]>

          | RuleField.ExternalAst(ruleDefinition = rd) =>
            <[
              $(GetStaticDescriptorRef(rd))
                .CreateAst(source, parserHost, ast, ast[$(astPointer.AstOffset(structField))], startPos)
                :> $(GetRuleNType(rd) : typed)
            ]>

          | RuleField.ExtentionPoint(ruleDefinition = rd) =>
            <[
              def astIndex = ast[$(astPointer.AstOffset(structField))];
              parserHost.GetRuleDescriptorById(ast[astIndex])
                .CreateAst(source, parserHost, ast, astIndex, startPos)
                :> $(GetRuleNType(rd) : typed)
            ]>

          | RuleField.List(astStruct = itemStruct, NextRef = nextRef) =>
            def itemPointer = AstPointer(itemStruct, "astIndex");
            <[
              def astBuffer = System.Collections.Generic.List();
              def loop(astIndex)
              {
                when (astIndex > 0)
                {
                  astBuffer.Add($(makeAst(itemStruct, itemPointer, internalAstMaker)));
                  loop(ast[$(itemPointer.AstOffset(nextRef))])
                }
              }
              loop(ast[$(astPointer.AstOffset(structField))]);
              Nemerle.Collections.NList.ToList(astBuffer)
            ]>

          | RuleField.List2(astRuleStruct = itemStruct, astSeparatorStruct = separatorStruct, NextRuleRef = nextItemRef, NextSeparatorRef = nextSeparatorRef) =>
            def itemPointer = AstPointer(itemStruct, "astIndex");
            def separatorPointer = AstPointer(separatorStruct, "separatorIndex");
            <[
              def astBuffer = System.Collections.Generic.List();
              def separatorBuffer = System.Collections.Generic.List();
              def loop(astIndex)
              {
                when (astIndex > 0)
                {
                  astBuffer.Add($(makeAst(itemStruct, itemPointer, internalAstMaker)));
                  def separatorIndex = ast[$(itemPointer.AstOffset(nextItemRef))];
                  when (separatorIndex > 0)
                  {
                    separatorBuffer.Add($(makeAst(separatorStruct, separatorPointer, internalAstMaker)));
                    loop(ast[$(separatorPointer.AstOffset(nextSeparatorRef))]);
                  }
                }
              }
              loop(ast[$(astPointer.AstOffset(structField))]);
              (Nemerle.Collections.NList.ToList(astBuffer), Nemerle.Collections.NList.ToList(separatorBuffer))
            ]>

          | RuleField.Option(astStruct = valueAstStruct) =>
            <[
              def astIndex = ast[$(astPointer.AstOffset(structField))];
              if (astIndex > 0)
                Some($(makeAst(valueAstStruct, AstPointer(valueAstStruct, "astIndex"), internalAstMaker)))
              else
                None()
            ]>

          | _ => assert(false)
        }
      }
      and makeAst(astStruct : AstStruct, astPointer : AstPointer, maker : list[PExpr] -> PExpr) : PExpr
      {
        def statements = SCG.List();
        def arguments = SCG.List();
        when (astStruct is PostfixAstStruct)
        {
          def varName = Util.tmpname("ast");
          statements.Add(<[ mutable $(varName : usesite) ]>);
          arguments.Add(<[ $(varName : usesite) ]>);
        }
        foreach(structField in astStruct.SizeFields)
        {
          match(structField.Field)
          {
            | RuleField.Ast
            | RuleField.Predicate => ()
            | RuleField.Void =>
              statements.Add(<[ startPos = endPos + ast[$(astPointer.SizeOffset(structField))] ]>);
              statements.Add(<[ endPos = startPos ]>);

            | _ =>
              def varName = Util.tmpname("ast");
              statements.Add(<[ endPos = startPos + ast[$(astPointer.SizeOffset(structField))] ]>);
              statements.Add(<[ def $(varName : usesite) = $(makeReader(structField, astPointer)) ]>);
              statements.Add(<[ startPos = endPos ]>);
              arguments.Add(<[ $(varName : usesite) ]>);
          }
        }
        statements.Add(maker(arguments.ToNList()));
        <[ { ..$(statements.ToNList()) } ]>
      }
      match (rd)
      {
        | ExtentionPrefix  when rd.IsAst
        | ExtentionPostfix when rd.IsAst
        | Simple           when rd.IsAst =>
          assert2(rd.Name.AsName != "Add");
          def ruleCompiler = _ruleCompilers[rd];
          def astStruct = ruleCompiler.RootField.astStruct;
          def astPointer = AstPointer(astStruct, "index");
          _ = descriptorTB.DefineWithSource(<[decl:
            public override CreateAst(source : N2.SourceSnapshot, parserHost : N2.ParserHost, ast : array[int], index : int, pos : int) : N2.Ast
            {
              assert(ast[$(astPointer.HeaderOffset(astStruct.StateRef))] == -1);
              mutable startPos = pos;
              mutable endPos = pos;
              ignore(source, parserHost, ast, index, startPos, endPos);
              $(makeAst(astStruct, astPointer, rootAstMaker))
            }
          ]>);

        | ExtentionPoint when rd.IsAst =>
          def ruleCompiler = _ruleCompilers[rd];
          def astStruct = ExtensibleAstStruct(ruleCompiler, rd);
          def astPointer = AstPointer(astStruct, "index");
          _ = descriptorTB.DefineWithSource(<[decl:
            public override CreateAst(source : N2.SourceSnapshot, parserHost : N2.ParserHost, ast : array[int], index : int, pos : int) : N2.Ast
            {
              assert(ast[$(astPointer.HeaderOffset(astStruct.StateRef))] == -1);
              def astIndex = ast[$(astPointer.AstOffset(astStruct.AstRef))];
              parserHost.GetRuleDescriptorById(ast[astIndex]).CreateAst(source, parserHost, ast, astIndex, pos)
            }
          ]>);

        | _ =>
          _ = descriptorTB.DefineWithSource(<[decl:
            public override CreateAst(source : N2.SourceSnapshot, parserHost : N2.ParserHost, ast : array[int], index : int, pos : int) : N2.Ast
            {
              ignore(source, parserHost, ast, index, pos);
              throw System.NotImplementedException();
            }
          ]>);
      }
    }
  }
}
