using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;
using N2.Model;

using System;
using System.Linq;
using SCG = System.Collections.Generic;

namespace N2
{
  internal partial class GrammarCompiler
  {
    [Memoize]
    public GetHandlerMethods() : Hashtable[NameRef, ClassMember.Function]
    {
      def methodsMap = Hashtable();
      def allMethods = GrammarTB.GetParsedMembers(true).MapFiltered(_ is ClassMember.Function, _ :> ClassMember.Function);
      def getSignature(rd : RuleDefinition)
      {
        if (rd.IsAst)
        {
          Some((rd, [GetRuleNType(rd)], GrammarTB.InternalType.Void))
        }
        else
        {
          match (GetAstFieldsTypes(rd))
          {
            | Some(types) =>
              match (GetRuleType(rd.Name))
              {
                | NType(retType) when !retType.Equals(GrammarTB.InternalType.Void) =>
                  match (types)
                  {
                    | [ty] when RuleTypeToNType(ty).TryUnify(retType) => None()
                    | _                                               => assert(false)
                  }
                | _ => None()
              }
            | None => None()
          }
        }
      }
      foreach (Some((rd, types, retType)) in TypedGrammar.Rules.Map(getSignature))
      {
        mutable invalidHandler = false;
        def error(loc, msg)
        {
          Message.Error(loc, msg);
          invalidHandler = true;
        }
        match (allMethods.Filter(m => rd.Name.AsName + "Handler" == m.Name))
        {
          | []       => ()
          | [method] =>
            // check parameters types...
            def parms = method.header.Parameters;

            if (parms.Length == types.Length)
              foreach ((need, p) in types.ZipLazy(parms))
              {
                def real = BindFixedType(p.Type);
                unless (need.TryUnify(real))
                  error(p.Location, $<#The parameter "$(p.Name)" has wrong type $real (need type $need).#>);
              }
            else
              error(method.Location, $<#The method-handler of rule "$(rd.Name)" must have $(types.Length) parameters.#>);

            // check return type...
            def methodRetTy = BindFixedType(method.header.ReturnType);

            unless (methodRetTy.TryRequire(retType))
              error(method.header.ReturnType.Location, $<#The method-handler of rule "$(rd.Name)" has wrong return type (need type $retType).#>);

            unless (invalidHandler)
              methodsMap.Add(rd.Name, method);

          | methods  => methods.Iter(m => error(m.Location, "Overloading not supported for rule method-handler"));
        }

        when (invalidHandler)
          Message.Error(rd.Name.Location, $<#You need define method-handler with signature: $(rd.Name)(_ : ..$(types; ", _ : ")) : $retType#>);
      }
      methodsMap
    }
  }
}
