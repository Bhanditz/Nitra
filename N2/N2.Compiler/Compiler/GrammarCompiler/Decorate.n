using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Assertions;
using N2.Model;

using System;
using System.Linq;
using SCG = System.Collections.Generic;

using N2.Internal;

namespace N2
{
  internal partial class GrammarCompiler
  {
    public DecorateRuleName(name : NameRef) : string
    {
      $"_#_$(name.Parts.Last())_"
    }

    public DecorateRuleName(rd : RuleDefinition) : string
    {
      DecorateRuleName(rd.Name)
    }

    public DecorateRuleClassName(rd : RuleDefinition) : string
    {
      match (rd)
      {
        | ExtentionPoint   => $"_#point#___$(rd.Name.AsName)_"
        | ExtentionPrefix  => $"_#prefix#__$(rd.Name.AsName)_"
        | ExtentionPostfix => $"_#postfix#_$(rd.Name.AsName)_"
        | Simple           => $"_#simple#__$(rd.Name.AsName)_"
        | Regular          => $"_#regular#_$(rd.Name.AsName)_"
        | ExternalRule     => assert(false)
      }
    }

    public variant RuleDefinitionType
    {
      | ExtentionPoint
      | ExtentionPrefix
      | ExtentionPostfix
      | Simple
      | Regular
    }

    public ParseRuleClassName(name : string) : option[RuleDefinitionType * string]
    {
      match (name.Substring(0, 11))
      {
        | "_#point#___" with type = RuleDefinitionType.ExtentionPoint()
        | "_#prefix#__" with type = RuleDefinitionType.ExtentionPrefix()
        | "_#postfix#_" with type = RuleDefinitionType.ExtentionPostfix()
        | "_#simple#__" with type = RuleDefinitionType.Simple()
        | "_#regular#_" with type = RuleDefinitionType.Regular()
          =>
          if (name.EndsWith("_"))
            Some((type, name.Substring(11, name.Length - 12)))
          else
            None()
        | _ => None()
      }
    }

    public DecorateGrammarRef(grammarRef : GrammarRef) : string
    {
      $"_#grammar#$(grammarRef.Id)"
    }

    public static ScopeBeginSufix = "ScopeBegin";
    public static ScopeEndSufix   = "ScopeEnd";
  }
}
