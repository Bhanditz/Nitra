using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using N2.Model;

namespace N2.Compiler
{
  internal partial abstract class RuleField
  {
    [Record]
    public sealed class Option : RuleField
    {
      fields : array[RuleField];

      public override InitChildren(compiler : RuleCompiler, parent : RuleField, level : int, allFields : SCG.List[RuleField]) : void
      {
        foreach (field in fields)
          field.Init(compiler, parent, level, allFields);
      }

      [RecordIgnore] public SizeParentRef : AstStruct.SizeField { get; private set; }
      [RecordIgnore] public AstParentRef  : AstStruct.AstField  { get; private set; }

      [RecordIgnore] private mutable astStruct : AstStruct;
      public override CalculateAstInfo() : void
      {
        astStruct = AstStruct(this, "Option");
        when (ParentAstStruct.IsAst)
          SizeParentRef = AstStruct.SizeField(ParentAstStruct);
        AstParentRef = AstStruct.AstField(ParentAstStruct);
        CalculateAstInfo(astStruct, fields);
      }

      public override Compile() : void
      {
        ParseCode = <[
        $(ParseLabel.Label());
          $StartTextPos = $CurTextPos;
          $(fields[0].ParseLabel.Goto());

        $(astStruct.OkLabel.Label());
          $(SetSizeCode(SizeParentRef, <[ $CurTextPos - $StartTextPos ]>));
          $(Parent.AstPtr.Ref(AstParentRef)) = $(AstPtr.Var);
          $(ContinueParseLabel.Goto());

        $(astStruct.FailLabel.Label());
          $(SetSizeCode(SizeParentRef, <[ 0 ]>));
          if ($(IsAstAllocatetdCode(astStruct))) // АСТ создано
            $(Parent.AstPtr.Ref(AstParentRef)) = ~ $(AstPtr.Var);
          else
            $(Parent.AstPtr.Ref(AstParentRef)) = -1;
          $CurTextPos = $StartTextPos;
          $(ContinueParseLabel.Goto());
        ]>
      }

      public override PrintContent() : string * string { ("Option", $<#$(PrintChildren(fields))#>) }
    }
  }
}
