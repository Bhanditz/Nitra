using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using N2.Model;

namespace N2.Compiler
{
  internal partial abstract class RuleField
  {
    [Record]
    public sealed class Option : RuleField
    {
      fields : array[RuleField];

      public override InitChildren(compiler : RuleCompiler, parent : RuleField, level : int, allFields : SCG.List[RuleField]) : void
      {
        foreach (field in fields)
          field.Init(compiler, parent, level, allFields);
      }

      [RecordIgnore] public Field   : AstStruct.StructField { get; private set; }

      [RecordIgnore] public  mutable astStruct : AstStruct;
      [RecordIgnore] private mutable astPtr      : AstPointer;
      public override CalculateAstInfo() : void
      {
        astStruct = InternalAstStruct(this);
        astPtr    = AstPointer(astStruct, "astPtr", this);
        Field     = AstStruct.StructField(ParentAstStruct, this, true, true);
        CalculateAstInfo(astStruct, astPtr, fields);
      }

      public override Compile() : void
      {
        ParseCode = <[
        $(ParseLabel.Label());
          $(AllocateParentAst());
          $StartTextPos = $CurTextPos;
          $(fields[0].ParseLabel.Goto());

        $(astStruct.OkLabel.Label());
          $(ParentAstPtr.SizeRef(Field)) = $CurTextPos - $StartTextPos;
          $(ParentAstPtr.AstRef(Field)) = $(astPtr.Var);
          $(ContinueParseLabel.Goto());

        $(astStruct.FailLabel.Label());
          $(ParentAstPtr.SizeRef(Field)) = 0;
          if ($(IsAstAllocatetdCode(astStruct))) // АСТ создано
            $(ParentAstPtr.AstRef(Field)) = ~ $(astPtr.Var);
          else
            $(ParentAstPtr.AstRef(Field)) = -1;
          $CurTextPos = $StartTextPos;
          $(ContinueParseLabel.Goto());
        ]>
      }

      public override PrintContent() : string * string { ("Option", $<#$(PrintChildren(fields))#>) }
    }
  }
}
