using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using N2.Model;

namespace N2.Compiler
{
  internal partial abstract class RuleField
  {
    [Record]
    public sealed class Option : RuleField
    {
      fields : array[RuleField];

      public override InitChildren(compiler : RuleCompiler, parent : RuleField, level : int, allFields : SCG.List[RuleField]) : void
      {
        foreach (field in fields)
          field.Init(compiler, parent, level, allFields);
      }

      [RecordIgnore] public SizeParentRef : AstStruct.SizeField { get; private set; }
      [RecordIgnore] public AstParentRef  : AstStruct.AstField  { get; private set; }

      [RecordIgnore] private mutable astStruct : AstStruct;
      [RecordIgnore] private mutable astPtr      : AstPointer;
      public override CalculateAstInfo() : void
      {
        astStruct = InternalAstStruct(this);
        astPtr    = AstPointer(astStruct, "astPtr", this);
        SizeParentRef = AstStruct.SizeField(ParentAstStruct);
        AstParentRef  = AstStruct.AstField(ParentAstStruct, this);
        CalculateAstInfo(astStruct, astPtr, fields);
      }

      public override Compile() : void
      {
        ParseCode = <[
        $(ParseLabel.Label());
          $(AllocateParentAst());
          $StartTextPos = $CurTextPos;
          $(fields[0].ParseLabel.Goto());

        $(astStruct.OkLabel.Label());
          $(ParentAstPtr.Ref(SizeParentRef)) = $CurTextPos - $StartTextPos;
          $(ParentAstPtr.Ref(AstParentRef)) = $(astPtr.Var);
          $(ContinueParseLabel.Goto());

        $(astStruct.FailLabel.Label());
          $(ParentAstPtr.Ref(SizeParentRef)) = 0;
          if ($(IsAstAllocatetdCode(astStruct))) // АСТ создано
            $(ParentAstPtr.Ref(AstParentRef)) = ~ $(astPtr.Var);
          else
            $(ParentAstPtr.Ref(AstParentRef)) = -1;
          $CurTextPos = $StartTextPos;
          $(ContinueParseLabel.Goto());
        ]>
      }

      public override PrintContent() : string * string { ("Option", $<#$(PrintChildren(fields))#>) }
    }
  }
}
