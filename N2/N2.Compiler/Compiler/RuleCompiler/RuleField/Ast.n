using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using N2.Model;

namespace N2.Compiler
{
  internal partial abstract class RuleField
  {
    [Record]
    public sealed class Ast : RuleField
    {
      private fields : array[RuleField];
      private ruleDefinition : RuleDefinition;

      public IsPrefix    : bool { get { ruleDefinition is RuleDefinition.ExtentionPrefix } }
      public IsPostfix   : bool { get { ruleDefinition is RuleDefinition.ExtentionPostfix } }
      public IsExtension : bool { get { IsPrefix || IsPostfix } }

      public override InitChildren(compiler : RuleCompiler, parent : RuleField, level : int, allFields : SCG.List[RuleField]) : void
      {
        foreach (field in fields)
          field.Init(compiler, parent, level, allFields);
      }

      [RecordIgnore] public IdRef     : AstStruct.HeaderField { get; private set; }
      [RecordIgnore] public SizeRef   : AstStruct.HeaderField { get; private set; }
      [RecordIgnore] public StateRef  : AstStruct.HeaderField { get; private set; }
      [RecordIgnore] public PrefixRef : AstStruct.AstField    { get; private set; }

      [RecordIgnore] private mutable astStruct : AstStruct;
      public override CalculateAstInfo() : void
      {
        astStruct = AstStruct(this, "Ast");
        IdRef     = AstStruct.HeaderField(astStruct);
        SizeRef   = AstStruct.HeaderField(astStruct);
        StateRef  = AstStruct.HeaderField(astStruct);
        when (IsPostfix)
          PrefixRef = AstStruct.AstField(astStruct);
        CalculateAstInfo(astStruct, fields);
      }

      public override Compile() : void
      {
        def code = SCG.List();
when (IsExtension) code.Add(<[                  if (parser.ast[resultPos + 1] == -1)
                                                   isBest = true;
                                                 else
                                                   bestAstPos = parser.ast[resultPos + 1] + 3;

]>); code.Add(<[                                $(fields[0].ParseLabel.Goto());
                                              $(astStruct.OkLabel.Label());
                                                $(AstPtr.Ref(StateRef)) = -1; // состояние разбора -1 если правило полностью разобрано.

]>); if (IsPostfix) code.Add(<[                 $(AstPtr.Ref(PrefixRef)) = parser.ast[resultPos];
                                                parser.ast[resultPos + 1] = $(AstPtr.Var);

]>); else code.Add(<[                           parser.ast[resultPos] = $(AstPtr.Var);

]>); code.Add(<[                                $(Compiler.EndLabel.Goto());
                                              $(astStruct.FailLabel.Label());
                                                $(AstPtr.Ref(StateRef)) = parseState;
                                                when (isBest && parser.ast[resultPos] == -1)
                                                  parser.ast[resultPos] = $(AstPtr.Ref(StateRef));
                                                curEndPos = -1;
                                                $(Compiler.EndLabel.Goto());
]>);
        ParseCode = PExpr.Sequence(code.FlattenPExprSequence());
      }

      public override PrintContent() : string * string { ("Ast", $<#$(PrintChildren(fields))#>) }
    }
  }
}
