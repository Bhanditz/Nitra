using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

using N2.Model;

namespace N2.Compiler
{
  internal partial abstract class RuleField
  {
    [Record]
    public sealed class ExternalAst : RuleField
    {
      rd : RuleDefinition;

      [RecordIgnore] public SizeRef : AstStruct.SizeField { get; private set; }
      [RecordIgnore] public AstRef  : AstStruct.AstField  { get; private set; }

      public override CalculateAstInfo() : void
      {
        when (ParentAstStruct.IsAst)
          SizeRef = AstStruct.SizeField(ParentAstStruct);
        AstRef = AstStruct.AstField(ParentAstStruct);
      }

      public override Compile() : void
      {
        ParseCode = MakeRuleCall(<[ $(Compiler.MakeCall(rd))(curEndPos, text, $(Parent.AstPtr.Offset(AstRef)), ref parser) ]>, SizeRef)
      }

      public override PrintContent() : string * string { ("ExternalAst", $<#$(rd.Name)#>) }
    }
  }
}
