using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using N2.Model;

namespace N2.Compiler
{
  internal class AstPointer
  {
    public AstStruct : AstStruct;
    public Var : PExpr;
    public this(astStruct : AstStruct, name : string)
    {
      AstStruct = astStruct;
      Var       = AstStruct.Compiler.GetName(name);
    }
    public this(astStruct : AstStruct, name : string, field : RuleField)
    {
      this(astStruct, $"$name$(field.Level)");
    }

    public HeaderOffset(headerField : AstStruct.HeaderField) : PExpr
    {
      if (object.ReferenceEquals(AstStruct, headerField.Owner))
        <[ $Var + $(headerField.Offset) ]>
      else
        PExpr.Error("Type mismatch.")
    }
    public HeaderRef(headerField : AstStruct.HeaderField) : PExpr
    {
      <[ parser.ast[$(HeaderOffset(headerField))] ]>
    }

    public AstOffset(structField : AstStruct.StructField) : PExpr
    {
      if (object.ReferenceEquals(AstStruct, structField.Owner) && structField.AstOffset >= 0)
        <[ $Var + $(AstStruct.AstOffset + structField.AstOffset) ]>
      else
        PExpr.Error("Type mismatch.")
    }
    public AstRef(structField : AstStruct.StructField) : PExpr
    {
      <[ parser.ast[$(AstOffset(structField))] ]>
    }

    public SizeOffset(structField : AstStruct.StructField) : PExpr
    {
      if (object.ReferenceEquals(AstStruct, structField.Owner) && structField.SizeOffset >= 0)
        <[ $Var + $(AstStruct.SizesOffset + structField.SizeOffset) ]>
      else
        PExpr.Error("Type mismatch.")
    }
    public SizeRef(structField : AstStruct.StructField) : PExpr
    {
      <[ parser.ast[$(SizeOffset(structField))] ]>
    }
  }

  internal partial abstract class RuleField
  {
    public mutable ParseCode : PExpr;
    public abstract Compile() : void;

    public ContinueParseLabel : PExprLabel { get { if (NextSibling == null) ParentAstStruct.OkLabel else NextSibling.ParseLabel } }
    public FailParseLabel     : PExprLabel { get { ParentAstStruct.FailLabel } }

    public CurTextPos     : PExpr { get { <[ curEndPos ]> } }
    public StartTextPos   : PExpr { get { Compiler.GetName($"startTextPos$Level") } }
    public TextPos        : PExpr { get { Compiler.GetName($"textPos$Level") } }

    public AllocateParentAst() : PExpr
    {
      if (AllocateAst)
      {
        if (Parent is Ast)
          <[ $(ParentAstPtr.Var) = parser.Allocate($(ParentAstStruct.AstSize), RuleId) ]>
        else
          <[ $(ParentAstPtr.Var) = parser.Allocate($(ParentAstStruct.AstSize)) ]>
      }
      else
        <[ {} ]>
    }

    public IsAstAllocatetdCode(astStruct : AstStruct) : PExpr
    {
      <[ parseState > $(astStruct.AllocateState) ]>
    }

    public AddBestCheck(check : PExpr, sizeField : AstStruct.StructField, size : PExpr) : PExpr
    {
      if (ParentAstStruct.MultipleVariants)
        <[ $check && (isBest || ({ diff =  ($(sizeField.BestRef)) - ($size); isBest = diff < 0; diff <= 0 })) ]>;
      else
        check;
    }

    public MakeSyntaxCallCode(call : PExpr, field : AstStruct.StructField) : PExpr
    {
      MakeCallCodeImpl(<[ $call(curEndPos, text, ref tmpResult, ref parser) ]>, field)
    }

    public MakeCallCodeImpl(call : PExpr, field : AstStruct.StructField) : PExpr
    {
      def setAst = if (field.HaveAst)
        <[ $(ParentAstPtr.AstRef(field)) = tmpResult ]>
      else
        <[ () ]>;
      <[
      $(ParseLabel.Label());
        tmpResult = -1;
        newPos = $call;
        tmpSize = newPos - curEndPos;
        if ($(AddBestCheck(<[ newPos >= 0 ]>, field, <[ tmpSize ]>)))
        {
          $(AllocateParentAst());
          $setAst;
          $(ParentAstPtr.SizeRef(field)) = tmpSize;
          curEndPos = newPos;
          $(ContinueParseLabel.Goto());
        }
        else
        {
          parseState = $(State : int);//состояние
          $(FailParseLabel.Goto());
        }
      ]>
    }
  }
}
