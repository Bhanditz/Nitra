using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

using N2.Model;

namespace N2.Compiler
{
  internal partial abstract class RuleField
  {
    [Record]
    public sealed class TokenString : RuleField
    {
      public str : string;

      [RecordIgnore] public Field : AstStruct.StructField { get; private set; }

      public override CalculateAstInfo() : void
      {
        Field = AstStruct.StructField(ParentAstStruct, this, false, true);
      }

      public override Compile() : void
      {
        def astSize = <[ $(str.Length) ]>;
        def checkCode = if (str == "")
        {
          <[ true ]>
        }
        else
        {
          def len = str.Length;
          def check = if (len > 1) <[curEndPos + $((len - 1) : int)]> else <[ curEndPos ]>;

          def makeCharCheckCode(ch : char, i : int) : PExpr
          {
            def index = if (i <= 0) <[ curEndPos ]> else <[ curEndPos + $(i : int) ]>;
            <[ text[$index] == $(ch : char) ]>
          }
          def exprs = str.Select(makeCharCheckCode);
          def condExpr = exprs.Aggregate((e1, e2) => <[ $e1 && $e2 ]>);
          <[ $check < text.Length && $condExpr ]>
        }
        
        def (debugSetStart, debugSetEnd, fail) =
          if (this.Compiler.Manager.Options.EmitDebug)
            (<[ parser.LastParseStart = curEndPos; ]>,
             <[ parser.LastParseEnd = curEndPos; ]>,
             <[ parser.LastParseEnd = -1; ]>)
          else
            (<[ () ]>, <[ () ]>, <[ () ]>);

#if !PARSER_DEBUG
        when (!this.Location.IsEmpty && this.Location.IsSourceAvailable)
          checkCode.Location = this.Location;
#endif
        ParseCode =
        <[
          $(ParseLabel.Label());
          $debugSetStart;
          if ($checkCode)
          {
            $(AllocateParentAst());
            $(ParentAstPtr.SizeRef(Field)) = $astSize;
            curEndPos += $astSize;
            $debugSetEnd;
            $(ContinueParseLabel.Goto());
          }
          else
          {
            parseState = $(State : int);//состояние
            $fail;
            $(FailParseLabel.Goto());
          }
        ]>;
      }

      public override PrintContent() : string * string { ("TokenString", $<#"$str"#>) }
    }
  }
}
