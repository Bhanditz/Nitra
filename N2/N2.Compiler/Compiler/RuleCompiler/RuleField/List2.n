using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using N2.Model;

namespace N2.Compiler
{
  internal partial abstract class RuleField
  {
    [Record]
    public sealed class List2 : RuleField
    {
      fieldsRule : array[RuleField];
      fieldsSeparator : array[RuleField];
      min     : int;
      max     : option[int];

      public override InitChildren(compiler : RuleCompiler, parent : RuleField, level : int, allFields : SCG.List[RuleField]) : void
      {
        foreach (field in fieldsRule)
          field.Init(compiler, parent, level, allFields);
        foreach (field in fieldsSeparator)
          field.Init(compiler, parent, level, allFields);
      }

      [RecordIgnore] public SizeParentRef : AstStruct.SizeField { get; private set; }
      [RecordIgnore] public AstParentRef  : AstStruct.AstField  { get; private set; }
      [RecordIgnore] public NextRuleRef : AstStruct.AstField { get; private set; }
      [RecordIgnore] public NextSeparatorRef : AstStruct.AstField { get; private set; }

      [RecordIgnore] private mutable astRuleStruct : AstStruct;
      [RecordIgnore] private mutable astSeparatorStruct : AstStruct;
      public override CalculateAstInfo() : void
      {
        astRuleStruct = AstStruct(this, "List2_Rule");
        astSeparatorStruct = AstStruct(this, "List2_Seperator");
        when (ParentAstStruct.IsAst)
          SizeParentRef = AstStruct.SizeField(ParentAstStruct);
        AstParentRef = AstStruct.AstField(ParentAstStruct);
        NextRuleRef = AstStruct.AstField(astRuleStruct);
        NextSeparatorRef = AstStruct.AstField(astSeparatorStruct);
        CalculateAstInfo(astRuleStruct, fieldsRule);
        CalculateAstInfo(astSeparatorStruct, fieldsSeparator);
      }

      public ListTailPtr : AstPointer { [Memoize] get { AstPointer(this, "listTailPtr") } }

      public override Compile() : void
      {
        def useCounter = min > 0 || max is Some(_);
        def counterInit() { if (useCounter) <[ $Counter  = 0 ]> else <[ {} ]> }
        def counterInc()  { if (useCounter) <[ ++ $Counter   ]> else <[ {} ]> }
        def ifCounterEqualsToMaxThenContinue()
        {
          if (max is Some(max))
            <[
              when ($Counter == $max)
                $(ContinueParseLabel.Goto());
            ]>
          else
            <[ {} ]>
        }
        def ifCounterLessThanMinThenFail()
        {
          if (min > 0)
            <[
              when ($Counter < $min)
                $(FailParseLabel.Goto());
            ]>
          else
            <[ {} ]>
        }

        def firstRuleChild = fieldsRule[0];
        def firstSeparatorChild = fieldsRule[0];
        def code = SCG.List();
        code.Add(<[
          $(ParseLabel.Label());
            $(counterInit());
            $(ListTailPtr.Var) = -1;
            $StartTextPos = $CurTextPos;
            $TextPos = $CurTextPos;
            $(firstRuleChild.ParseLabel.Goto());

          $(astRuleStruct.OkLabel.Label());
            $(SetSizeCode(SizeParentRef, <[ $CurTextPos - $StartTextPos ]>));
            $(counterInc());
            $(AstPtr.Ref(NextRuleRef)) = -1;
            if ($(ListTailPtr.Var) == -1)
              $(Parent.AstPtr.Ref(AstParentRef)) = $(AstPtr.Var);
            else
              $(ListTailPtr.Ref(NextRuleRef)) = $(AstPtr.Var);
            $(ListTailPtr.Var) = $(AstPtr.Var);
            $(ifCounterEqualsToMaxThenContinue());
            $TextPos = $CurTextPos;
            $(firstSeparatorChild.ParseLabel.Goto());

          $(astRuleStruct.FailLabel.Label());
            when ($(IsAstAllocatetdCode(astRuleStruct))) // АСТ создано
            {
              $(AstPtr.Ref(NextRuleRef)) = -1;
              if ($(ListTailPtr.Var) == -1)
                $(Parent.AstPtr.Ref(AstParentRef)) = ~ $(AstPtr.Var);
              else
                $(ListTailPtr.Ref(NextRuleRef)) = ~ $(AstPtr.Var);
            }
            $(ifCounterLessThanMinThenFail());
            $CurTextPos = $TextPos;
            $(SetSizeCode(SizeParentRef, <[ $CurTextPos - $StartTextPos ]>));
            $(ContinueParseLabel.Goto());

          $(astSeparatorStruct.OkLabel.Label());
            $(AstPtr.Ref(NextSeparatorRef)) = -1;
            $(ListTailPtr.Ref(NextSeparatorRef)) = $(AstPtr.Var);
            $(ListTailPtr.Var) = $(AstPtr.Var);
            $(firstRuleChild.ParseLabel.Goto());

          $(astSeparatorStruct.FailLabel.Label());
            when ($(IsAstAllocatetdCode(astSeparatorStruct))) // АСТ создано
            {
              $(AstPtr.Ref(NextSeparatorRef)) = -1;
              if ($(ListTailPtr.Var) == -1)
                $(Parent.AstPtr.Ref(AstParentRef)) = ~ $(AstPtr.Var);
              else
                $(ListTailPtr.Ref(NextSeparatorRef)) = ~ $(AstPtr.Var);
            }
            $(ifCounterLessThanMinThenFail());
            $CurTextPos = $TextPos;
            $(SetSizeCode(SizeParentRef, <[ $CurTextPos - $StartTextPos ]>));
            $(ContinueParseLabel.Goto());
        ]>);
        ParseCode = PExpr.Sequence(code.FlattenPExprSequence());
      }

      public override PrintContent() : string * string { ("List2", $<#$(PrintChildren(fieldsRule)) $(PrintChildren(fieldsSeparator)) $min $max#>) }
    }
  }
}
