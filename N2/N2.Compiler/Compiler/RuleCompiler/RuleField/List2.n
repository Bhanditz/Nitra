using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using N2.Model;

namespace N2.Compiler
{
  internal partial abstract class RuleField
  {
    [Record]
    public sealed class List2 : RuleField
    {
      fieldsRule : array[RuleField];
      fieldsSeparator : array[RuleField];
      min     : int;
      max     : option[int];

      public override InitChildren(compiler : RuleCompiler, parent : RuleField, level : int, allFields : SCG.List[RuleField]) : void
      {
        foreach (field in fieldsRule)
          field.Init(compiler, parent, level, allFields);
        foreach (field in fieldsSeparator)
          field.Init(compiler, parent, level, allFields);
      }

      [RecordIgnore] public SizeParentRef    : AstStruct.SizeField { get; private set; }
      [RecordIgnore] public AstParentRef     : AstStruct.AstField  { get; private set; }
      [RecordIgnore] public NextRuleRef      : AstStruct.AstField  { get; private set; }
      [RecordIgnore] public NextSeparatorRef : AstStruct.AstField  { get; private set; }

      [RecordIgnore] private mutable astRuleStruct        : AstStruct;
      [RecordIgnore] private mutable astRulePtr           : AstPointer;
      [RecordIgnore] private mutable listTailRulePtr      : AstPointer;

      [RecordIgnore] private mutable astSeparatorStruct   : AstStruct;
      [RecordIgnore] private mutable astSeparatorPtr      : AstPointer;
      [RecordIgnore] private mutable listTailSeparatorPtr : AstPointer;

      public override CalculateAstInfo() : void
      {
        astRuleStruct        = InternalAstStruct(this, "Rule");
        astSeparatorStruct   = InternalAstStruct(this, "Separator");
        astRulePtr           = AstPointer(astRuleStruct,      "astPtr", this);
        listTailRulePtr      = AstPointer(astRuleStruct,      "listTailPtr", this);
        astSeparatorPtr      = AstPointer(astSeparatorStruct, "astPtr", this);
        listTailSeparatorPtr = AstPointer(astSeparatorStruct, "listTailPtr", this);

        SizeParentRef = AstStruct.SizeField(ParentAstStruct);
        AstParentRef = AstStruct.AstField(ParentAstStruct);
        NextRuleRef = AstStruct.AstField(astRuleStruct);
        NextSeparatorRef = AstStruct.AstField(astSeparatorStruct);
        CalculateAstInfo(astRuleStruct, astRulePtr, fieldsRule);
        CalculateAstInfo(astSeparatorStruct, astSeparatorPtr, fieldsSeparator);
      }

      public Counter : PExpr { get { Compiler.GetName($"counter$Level") } }

      public override Compile() : void
      {
        def useCounter = min > 0 || max is Some(_);
        def counterInit() { if (useCounter) <[ $Counter  = 0 ]> else <[ {} ]> }
        def counterInc()  { if (useCounter) <[ ++ $Counter   ]> else <[ {} ]> }
        def ifCounterEqualsToMaxThenContinue()
        {
          if (max is Some(max))
            <[
              when ($Counter == $max)
                $(ContinueParseLabel.Goto());
            ]>
          else
            <[ {} ]>
        }
        def ifCounterLessThanMinThenFail()
        {
          if (min > 0)
            <[
              when ($Counter < $min)
                $(FailParseLabel.Goto());
            ]>
          else
            <[ {} ]>
        }

        def firstRuleChild = fieldsRule[0];
        def firstSeparatorChild = fieldsSeparator[0];
        def code = SCG.List();
        code.Add(<[
          $(ParseLabel.Label());
            $(counterInit());
            $(listTailRulePtr.Var) = -1;
            $StartTextPos = $CurTextPos;
            $TextPos = $CurTextPos;
            $(firstRuleChild.ParseLabel.Goto());

          $(astRuleStruct.OkLabel.Label());
            $(counterInc());
            $(astRulePtr.Ref(NextRuleRef)) = -1;
            if ($(listTailRulePtr.Var) == -1)
            {
              $(AllocateParentAst());
              $(ParentAstPtr.Ref(AstParentRef)) = $(astRulePtr.Var);
            }
            else
              $(listTailRulePtr.Ref(NextRuleRef)) = $(astRulePtr.Var);
            $(ParentAstPtr.Ref(SizeParentRef)) = $CurTextPos - $StartTextPos;
            $(listTailRulePtr.Var) = $(astRulePtr.Var);
            $(ifCounterEqualsToMaxThenContinue());
            $TextPos = $CurTextPos;
            $(firstSeparatorChild.ParseLabel.Goto());

          $(astRuleStruct.FailLabel.Label());
            when ($(IsAstAllocatetdCode(astRuleStruct))) // АСТ создано
            {
              $(astRulePtr.Ref(NextRuleRef)) = -1;
              if ($(listTailRulePtr.Var) == -1)
                $(ParentAstPtr.Ref(AstParentRef)) = ~ $(astRulePtr.Var);
              else
                $(listTailRulePtr.Ref(NextRuleRef)) = ~ $(astRulePtr.Var);
            }
            $(ifCounterLessThanMinThenFail());
            $CurTextPos = $TextPos;
            $(ParentAstPtr.Ref(SizeParentRef)) = $CurTextPos - $StartTextPos;
            $(ContinueParseLabel.Goto());

          $(astSeparatorStruct.OkLabel.Label());
            $(astSeparatorPtr.Ref(NextSeparatorRef)) = -1;
            $(listTailSeparatorPtr.Ref(NextSeparatorRef)) = $(astSeparatorPtr.Var);
            $(listTailSeparatorPtr.Var) = $(astSeparatorPtr.Var);
            $(firstRuleChild.ParseLabel.Goto());

          $(astSeparatorStruct.FailLabel.Label());
            when ($(IsAstAllocatetdCode(astSeparatorStruct))) // АСТ создано
            {
              $(astSeparatorPtr.Ref(NextSeparatorRef)) = -1;
              if ($(listTailSeparatorPtr.Var) == -1)
                $(ParentAstPtr.Ref(AstParentRef)) = ~ $(astSeparatorPtr.Var);
              else
                $(listTailSeparatorPtr.Ref(NextSeparatorRef)) = ~ $(astSeparatorPtr.Var);
            }
            $(ifCounterLessThanMinThenFail());
            $CurTextPos = $TextPos;
            $(ParentAstPtr.Ref(SizeParentRef)) = $CurTextPos - $StartTextPos;
            $(ContinueParseLabel.Goto());
        ]>);
        ParseCode = PExpr.Sequence(code.FlattenPExprSequence());
      }

      public override PrintContent() : string * string { ("List2", $<#$(PrintChildren(fieldsRule)) $(PrintChildren(fieldsSeparator)) $min $max#>) }
    }
  }
}
