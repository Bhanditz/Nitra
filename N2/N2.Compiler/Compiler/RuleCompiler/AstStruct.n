using Nemerle;
using Nemerle.Compiler.Parsetree;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;
using N2.Model;

namespace N2.Compiler
{
  internal abstract class RootAstStruct : AstStruct
  {
    public IdRef     : AstStruct.HeaderField { get; private set; }
    public SizeRef   : AstStruct.HeaderField { get; private set; }
    public StateRef  : AstStruct.HeaderField { get; private set; }

    public this(compiler : RuleCompiler)
    {
      def okLabel   = compiler.MakeLabel("Ast_Ok");
      def failLabel = compiler.MakeLabel("Ast_Fail");
      base(compiler, okLabel, failLabel);
      IdRef     = AstStruct.HeaderField(this);
      SizeRef   = AstStruct.HeaderField(this);
      StateRef  = AstStruct.HeaderField(this);
    }
  }

  [Record]
  internal class SimpleAstStruct : RootAstStruct
  {
    public override HasTerminator : bool { get { false } }
  }

  [Record]
  internal class PrefixAstStruct : RootAstStruct
  {
    public override HasTerminator : bool { get { true } }
  }

  internal class PostfixAstStruct : RootAstStruct
  {
    public override HasTerminator : bool { get { true } }
    public PrefixRef : AstStruct.StructField;
    public this(compiler : RuleCompiler)
    {
      base(compiler);
      PrefixRef = AstStruct.StructField(this, null, true, false);//TODO: FIXME
    }
  }

  internal class ExtensibleAstStruct : RootAstStruct
  {
    public override HasTerminator : bool { get { false } }
    public AstRef  : AstStruct.StructField;
    public BestRef : AstStruct.StructField;
    public this(compiler : RuleCompiler, rd : RuleDefinition)
    {
      base(compiler);
      def field = RuleField.ExtentionPoint(rd, 0);
      AstRef  = AstStruct.StructField(this, field, true, false);
      BestRef = AstStruct.StructField(this, field, true, false);
    }
  }

  internal class InternalAstStruct : AstStruct
  {
    public override HasTerminator : bool { get { false } }
    public this(field : RuleField, name : string = "")
    {
      def okLabel   = field.Compiler.MakeLabel($"$(field.GetType().Name)_$(name)_$(field.State)_Ok");
      def failLabel = field.Compiler.MakeLabel($"$(field.GetType().Name)_$(name)_$(field.State)_Fail");
      base(field.Compiler, okLabel, failLabel);
    }
  }

  internal abstract class AstStruct
  {
    public Compiler  : RuleCompiler;
    public OkLabel   : PExprLabel;
    public FailLabel : PExprLabel;

    public this(compiler : RuleCompiler, okLabel : PExprLabel, failLabel : PExprLabel)
    {
      Compiler  = compiler;
      OkLabel   = okLabel;
      FailLabel = failLabel;
    }

    public mutable HeaderFields     : SCG.List[HeaderField] = SCG.List();
    public mutable SizeFields       : SCG.List[StructField] = SCG.List();
    public mutable AstFields        : SCG.List[StructField] = SCG.List();

    public mutable AllocateState    : int;
    public mutable MultipleVariants : bool = false;

    public SizesOffset            : int  { get { HeaderFields.Count } }
    public TerminatorOffset       : int  { get { HeaderFields.Count + SizeFields.Count } }
    public AstOffset              : int  { get { TerminatorOffset + TerminatorSize } }

    public TerminatorSize         : int  { get { if (HasTerminator) 1 else 0 } }
    public abstract HasTerminator : bool { get; }

    public AstSize : int { get { HeaderFields.Count + SizeFields.Count + TerminatorSize + AstFields.Count } }

    public class HeaderField
    {
      public Owner : AstStruct;
      public Offset : int;
      public this(owner : AstStruct)
      {
        Owner = owner;
        Offset = owner.HeaderFields.Count;
        owner.HeaderFields.Add(this);
      }
    }

    public class StructField
    {
      public Owner : AstStruct;
      public Field : RuleField;
      public SizeOffset : int;
      public AstOffset  : int;
      public HaveSize : bool { get { SizeOffset >= 0 } }
      public HaveAst  : bool { get { AstOffset >= 0 } }
      public this(owner : AstStruct, field : RuleField, ast : bool, size : bool)
      {
        Owner = owner;
        Field = field;
        if (size)
        {
          SizeOffset = owner.SizeFields.Count;
          owner.SizeFields.Add(this);
        }
        else
        {
          SizeOffset = -1;
        }
        if (ast)
        {
          AstOffset = owner.AstFields.Count;
          owner.AstFields.Add(this);
        }
        else
        {
          AstOffset = -1;
        }
      }
      public BestRef : PExpr { get { if (HaveSize) <[ parser.ast[bestAstPos + $(Owner.SizesOffset + SizeOffset : int)] ]> else PExpr.Error("BestRef") } }
    }
  }
}
