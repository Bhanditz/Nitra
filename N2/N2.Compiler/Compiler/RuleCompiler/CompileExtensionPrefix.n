using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;
using N2.Model;

using System;
using System.Linq;
using SCG = System.Collections.Generic;

using N2.Internal;

namespace N2
{
  internal partial class RuleCompiler
  {
    public CompileExtensionPrefix(rd : RuleDefinition.ExtentionPrefix) : void
    {
      def className = _grammarCompiler.DecorateRuleClassName(rd);
      def returnType = _grammarCompiler.GetBaseRuleNType(rd);
      def (subRulesCode, varNames) = CompileRule(rd.rule);
      def setResult = MakeHandlerCall(varNames);

      def implClass = <[decl:
        public class $(className : dyn) : ExtensionPrefixBase[$(returnType : typed)]
        {
          private _grammar : $(_grammarCompiler.GrammarImplTB.ParsedTypeName);
          public this(grammar : IGrammar)
          {
            base(char.MinValue, char.MaxValue);//TODO:FIXME
            _grammar = grammar :> $(_grammarCompiler.GrammarImplTB.ParsedTypeName);
          }

          public override Descriptor : RuleDescriptor { get { GrammarDescriptorImpl.$(className : dyn).StaticDescriptor } }
        }
      ]>;

      def tb = _grammarCompiler.GrammarImplTB.DefineNestedType(implClass);
      def body =
        <[
          mutable isBest = false;
          def newPos = $subRulesCode;
          when (newPos >= 0)
            $setResult;
          newPos
        ]>;
      _ = tb.DefineWithSource(
        <[decl:
          public override Parse(pos : int, text : string, bestOffsets : array[int], result : ref $(returnType : typed)) : int
          {
            unchecked // востановление после ошибок тут не нужно
            {         // оно будет произведено в точке расширения
              mutable c : char;
              _ = c; // eliminate warning "a local value c was never used"
              $(PrintRule(rd.rule));
              $(DefVars(_vars.ToListRev(), body));
            }
          }
        ]>);
      tb.Compile();
    }
  }
}
