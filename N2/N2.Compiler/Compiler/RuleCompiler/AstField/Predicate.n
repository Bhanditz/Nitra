using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using N2.Model;

namespace N2.Compiler
{
  internal partial abstract class AstField
  {
    [Record]
    public sealed class Predicate : AstField
    {
      fields : array[AstField];
      isAnd  : bool;

      public override InitChildren(compiler : RuleCompiler, parent : AstField, level : int, allFields : SCG.List[AstField]) : void
      {
        foreach (field in fields)
          field.Init(compiler, parent, level, allFields);
      }

      [RecordIgnore] private mutable astStruct : AstStruct;
      public override CalculateAstInfo() : void
      {
        astStruct            = AstStruct(this);
        astStruct.OkLabel    = Compiler.MakeLabel($"OkLabel$State");
        astStruct.FailLabel  = Compiler.MakeLabel($"FailLabel$State");
        CalculateAstInfo(astStruct, fields)
      }

      public override AllocateFieldOffsets(astStruct : AstStruct) : FieldOffsets
      {
        astStruct.AllocateField(None(), None());
      }

      public override Compile() : void
      {
        def save = <[ $AllocatedPExpr = parser.allocated ]>;
        def load = <[ parser.allocated = $AllocatedPExpr ]>;
        ParseCode = if (isAnd)
        <[
          $(ParseLabel.Label());
            $save;
            $(fields[0].ParseLabel.Goto());

          $(astStruct.OkLabel.Label());
            $load;
            $(ContinueParseLabel.Goto());

          $(astStruct.FailLabel.Label());
            $load;
            $(FailParseLabel.Goto());
        ]>
        else
        <[
          $(ParseLabel.Label());
            $save;
            $(fields[0].ParseLabel.Goto());

          $(astStruct.OkLabel.Label());
            $load;
            $(FailParseLabel.Goto());

          $(astStruct.FailLabel.Label());
            $load;
            $(ContinueParseLabel.Goto());
        ]>
      }

      public override PrintContent() : string * string { ("Predicate", $<#$(PrintChildren(fields)) $isAnd#>) }
    }
  }
}
