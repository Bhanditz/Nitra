using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;
using N2.Model;

using System;
using System.Linq;
using SCG = System.Collections.Generic;

using N2.Compiler;

namespace N2
{
  internal partial class RuleCompiler
  {
    private Convert(rule : Rule) : array[AstField]
    {
      def conv(rule : Rule) : array[AstField]
      {
        match (rule)
        {
          | And                   as rule => array[AstField.Predicate  (conv(rule.rule), true)]
          | Not                   as rule => array[AstField.Predicate  (conv(rule.rule), false)]
          | Repeat(0, Some(1), _) as rule => array[AstField.Option     (conv(rule.rule))]
          | Repeat                as rule => array[AstField.List       (conv(rule.rule),                       rule.minCount, rule.maxCount)]
          | RepeatWithSeparator   as rule => array[AstField.List2      (conv(rule.rule), conv(rule.separator), rule.minCount, rule.maxCount)]
          | String                as rule => array[AstField.TokenString(rule.str)]
          | Sequence              as rule => rule.rules.Map(conv(_)).Flatten().NToArray();
          | Call                  as rule =>
            def rd   = _grammarCompiler.TypedGrammar.GetRule(rule.name);
            def field = match (rd, _grammarCompiler.GetRuleType(rd))
            {
              | (ExtentionPoint         , _    ) => AstField.ExtentionPoint(rd, rule.bindingPower)
              | (ExternalRule(_, true)  , _    ) => AstField.ExtentionPoint(rd, rule.bindingPower)
              | (ExternalRule(_, false) , Void ) => AstField.Void(rd)
              | (ExternalRule(_, false) , _    ) => AstField.ExternalAst(rd)
              | (Simple                 , Void ) => AstField.Void(rd)
              | (Simple                 , _    ) => AstField.ExternalAst(rd)
              | (Regular                , _    ) => AstField.TokenCall(rd)
              | _     => assert(false);
            }
            array[field]
          | PrefixCall                  => assert(false)
        }
      }
      def rule = match (rule)
      {
        | Sequence(PrefixCall :: rules) as rule => Rule.Sequence(rule.Location, rule.Type, rule.Id, rules)
        | rule => rule
      }
      def ast = AstField.Ast(conv(rule), _ruleDefinition is RuleDefinition.ExtentionPostfix);
      def fields = SCG.List();
      ast.Init(this, null, 0, fields);
      ast.CalculateAstInfo();
      fields.NToArray();
    }
  }
}
