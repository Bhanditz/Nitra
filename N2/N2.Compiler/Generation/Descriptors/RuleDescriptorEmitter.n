using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using N2.Model;
using N2.Typing;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

namespace N2.Compiler
{
  [Record]
  internal sealed class RuleDescriptorEmitter
  {
    public Owner      : SyntaxModuleEmitter { get; }
    public RuleSymbol : RuleDefSymbol       { get; }

    [RecordIgnore] private mutable _descriptorTb : TypeBuilder;

    public NeedsAstInfo : bool { get { true } }

    public DefineDescriptor() : void
    {
      assert(_descriptorTb == null);
      def baseType = match(RuleSymbol)
      {
        | ExtensibleRuleSymbol                         => <[ ExtensibleRuleDescriptor ]>
        | ExtentionRuleSymbol(ExtentionType = Prefix)  => <[ PrefixRuleDescriptor ]>
        | ExtentionRuleSymbol(ExtentionType = Postfix) => <[ PostfixRuleDescriptor ]>
        | SimpleRuleSymbol                             => <[ SimpleRuleDescriptor ]>
        | RegularRuleSymbol                            => <[ RegularRuleDescriptor ]>
        | _                                            => assert(false)
      }
      _descriptorTb = Owner.GrammarDescriptorImplTB.DefineNestedType(<[decl:
        public sealed class $(RuleSymbol.DecorateRuleClassName() : dyn) : $baseType
        {
          private this() {}
        }
      ]>);
      _descriptorTb.DefineConditional(<[decl: public  override Name              : string               { get { $(RuleSymbol.Name : string) } } ]>);
      _descriptorTb.DefineConditional(<[decl: public  override Grammar           : N2.GrammarDescriptor { get { $(Owner.GrammarDescriptorImplTB.GetMemType() : typed).StaticDescriptor } } ]>);
      _descriptorTb.DefineConditional(<[decl: public  override IdInGrammar       : int                  { get { $(RuleSymbol.IdInGrammar : int) } } ]>);
      _descriptorTb.DefineConditional(<[decl: private static   _staticDescriptor : this = $(_descriptorTb.GetMemType() : typed)(); ]>);
      _descriptorTb.DefineConditional(<[decl: public  static   StaticDescriptor  : this { get { _staticDescriptor } } ]>);

      match (RuleSymbol.Type)
      {
        | Chars => ()
        | Void  => _descriptorTb.DefineConditional(<[decl: public ResultType() : void {} ]>)
        | t     => _descriptorTb.DefineConditional(<[decl: public ResultType() : $(Owner.Environment.GetAstType(t) : typed) { mutable res; res } ]>)
      }

      def defineNewParserMethod(returnType)
      {
        _descriptorTb.DefineConditional(<[decl:
          public override NewParser(grammar : N2.CompositeGrammar) : $returnType
          {
            $(Owner.GrammarImplTB.GetMemType() : typed).$(RuleSymbol.DecorateRuleClassName() : dyn)(grammar)
          }
        ]>);
      }
      def defineExtentionRuleMethods(baseRuleSymbol)
      {
        defineNewParserMethod(<[ N2.Internal.ExtentionRuleParser ]>);
        _descriptorTb.DefineConditional(<[decl:
          public override BaseRule : ExtensibleRuleDescriptor { get { $(baseRuleSymbol.GetDescriptorClassRef()).StaticDescriptor } }
        ]>);
      }
      def defineEntranceMethod()
      {
        Owner.GrammarTB.DefineConditional(<[ decl:
          public static $(RuleSymbol.Name : usesite)(source : N2.SourceSnapshot, parserHost : N2.ParserHost) : N2.ParseResult
          {
            parserHost.DoParsing(source, $(_descriptorTb.GetMemType() : typed).StaticDescriptor)
          }
        ]>);
      }
      def defineIsStartRuleProperty()
      {
        _descriptorTb.DefineConditional(<[decl:
          public override IsStartRule : bool { get { $(RuleSymbol.Options.IsStart : bool) } }
        ]>);
      }
      match (RuleSymbol)
      {
        | ExtensibleRuleSymbol =>
          defineIsStartRuleProperty();
          defineEntranceMethod();

        | ExtentionRuleSymbol(BaseRule = RuleRef.Some(baseRuleSymbol), ExtentionType = Prefix) =>
          defineExtentionRuleMethods(baseRuleSymbol);

        | ExtentionRuleSymbol(BaseRule = RuleRef.Some(baseRuleSymbol), ExtentionType = Postfix(bindingPower, _)) =>
          defineExtentionRuleMethods(baseRuleSymbol);
          _descriptorTb.DefineConditional(<[decl: public override BindingPower : int { get { $(bindingPower : int) } } ]>);

        | SimpleRuleSymbol  =>
          defineNewParserMethod(<[ N2.Internal.SimpleRuleParser ]>);
          defineIsStartRuleProperty();
          defineEntranceMethod();

        | RegularRuleSymbol => ()
        | _       => assert(false)
      }
    }

    public Emit() : void
    {
      assert(_descriptorTb != null);
      _descriptorTb.Compile();
    }
  }
}
