using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Parsetree.PExpr;
using Nemerle.Compiler.Token;

using N2.Model;
using N2.Typing;

using System;
using System.Collections.Generic;
using System.Linq;

using Nemerle.Compiler.Util;

namespace N2.Compiler
{
  internal partial class SyntaxModuleEmitter
  {
    [Memoize]
    public NTypeOfRuleSymbol(symbol : RuleDefSymbol) : TypeVar
    {
      BindFixedType(symbol.Node.Parent.FullNameParts + [MakePublicRuleName(symbol.Name)])
    }

    [Memoize]
    public RuleTypeToNType(ruleType : RuleType) : TypeVar
    {
      def ruleTypeToNType(_ : RuleType)
      {
        | Chars        => Environment.NTokenTypeVar
        | Ref(symbol)  => NTypeOfRuleSymbol(symbol)
        | List(ty)     => FixedType.Class(Environment.ListTypeInfo, [ruleTypeToNType(ty)])
        | Option(Void) => FixedType.Void()
        | Option(ty)   => FixedType.Class(Environment.OptionTypeInfo, [ruleTypeToNType(ty)])
        | Tuple(types) =>
          match (types.MapFiltered(ty => !(ty is RuleType.Void), ruleTypeToNType))
          {
            | [ty]  => ty
            | types => FixedType.Tuple(types)
          }

        | Void         => FixedType.Void()
      }
      ruleTypeToNType(ruleType)
    }

    private BindFixedType(typeName : list[string]) : FixedType
    {
      match (Env.LookupType(typeName))
      {
        | Some(ty) => FixedType.Class(ty, [])
        | None     => assert2(false); assert(false)
      }
    }
  }
}
