using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using N2.Model;

namespace N2.Compiler
{
  internal partial abstract class RuleField
  {
    [Record]
    public sealed class List2 : RuleField
    {
      public Rule : Rule.RepeatWithSeparator;
      fieldsRule : array[RuleField];
      fieldsSeparator : array[RuleField];
      public Min     : int;
      public Max     : option[int];
      public hangingSeparator : bool;

      public override InitChildren(compiler : RuleParserEmitter, level : int, allFields : SCG.List[RuleField]) : void
      {
        foreach (field in fieldsRule)
          field.Init(compiler, this, level, allFields);
        foreach (field in fieldsSeparator)
          field.Init(compiler, this, level, allFields);
      }

      [RecordIgnore] public Field            : AstStruct.StructField { get; private set; }

      [RecordIgnore] public  mutable astRuleStruct        : AstStruct;
      [RecordIgnore] private mutable astRulePtr           : AstPointer;

      [RecordIgnore] public  mutable astSeparatorStruct   : AstStruct;
      [RecordIgnore] private mutable astSeparatorPtr      : AstPointer;

      public override CalculateAstInfo() : void
      {
        astRuleStruct        = InternalAstStruct(this, Compiler, "Rule");
        astSeparatorStruct   = InternalAstStruct(this, Compiler, "Separator");
        astRulePtr           = AstPointer(astRuleStruct,      "astPtr", this);
        astSeparatorPtr      = AstPointer(astSeparatorStruct, "astPtr", this);

        Field                = AstStruct.StructField(ParentAstStruct, this);
        CalculateAstInfo(astRuleStruct, astRulePtr, fieldsRule);
        CalculateAstInfo(astSeparatorStruct, astSeparatorPtr, fieldsSeparator);
      }

      public Counter      : PExpr { get { (Compiler : IVariableEmitter).CreateVariable($"counter$Level") } }
      public ListStartPos : PExpr { get { (Compiler : IVariableEmitter).CreateVariable($"listStartPos$Level") } }
      public ListEndPos   : PExpr { get { (Compiler : IVariableEmitter).CreateVariable($"listEndPos$Level") } }

      public override Compile() : void
      {
        def useCounter = Min > 0 || Max is Some(_);
        def counterInit() { if (useCounter) <[ $Counter  = 0 ]> else <[ {} ]> }
        def counterInc()  { if (useCounter) <[ ++ $Counter   ]> else <[ {} ]> }
        def ifCounterEqualsToMaxThenContinue()
        {
          if (Max is Some(max))
            <[
              when ($Counter == $max)
                $(ContinueParseLabel.Goto());
            ]>
          else
            <[ {} ]>
        }
        def ifCounterLessThanMinThenFail()
        {
          if (Min > 0)
            <[
              when ($Counter < $Min)
                $(FailParseLabel.Goto());
            ]>
          else
            <[ {} ]>
        }
        def hangingSeparatorCode = if (hangingSeparator)
          <[
            $ListEndPos = $CurTextPos;
            $(ParentAstPtr.SizeRef(Field)) = $ListEndPos - $ListStartPos;
          ]>
        else
          <[ {} ]>;

        def firstRuleChild = fieldsRule[0];
        def firstSeparatorChild = fieldsSeparator[0];
        def code = SCG.List();
        code.Add(<[
          $(ParseLabel.Label());
            $(AllocateParentAst());
            $(counterInit());
            $ListStartPos = $CurTextPos;
            $ListEndPos = $CurTextPos;
            $(astRuleStruct.AstStart) = $CurTextPos;
            $(firstRuleChild.ParseLabel.Goto());

          $(astRuleStruct.OkLabel.Label());
            $(counterInc());
            $ListEndPos = $CurTextPos;
            $(ParentAstPtr.SizeRef(Field)) = $ListEndPos - $ListStartPos;
            $(ifCounterEqualsToMaxThenContinue());
            $(astSeparatorStruct.AstStart) = $CurTextPos;
            $(firstSeparatorChild.ParseLabel.Goto());

          $(astRuleStruct.FailLabel.Label());
            $(ifCounterLessThanMinThenFail());
            $CurTextPos = $ListEndPos;
            $(ParentAstPtr.SizeRef(Field)) = $ListEndPos - $ListStartPos;
            $(ContinueParseLabel.Goto());

          $(astSeparatorStruct.OkLabel.Label());
            $hangingSeparatorCode;
            $(astRuleStruct.AstStart) = $CurTextPos;
            $(firstRuleChild.ParseLabel.Goto());

          $(astSeparatorStruct.FailLabel.Label());
            $(ifCounterLessThanMinThenFail());
            $CurTextPos = $ListEndPos;
            $(ParentAstPtr.SizeRef(Field)) = $ListEndPos - $ListStartPos;
            $(ContinueParseLabel.Goto());
        ]>);
        ParseCode = PExpr.Sequence(code.FlattenPExprSequence());
      }

      public override PrintContent() : string * string
      {
        def hanging = if(hangingSeparator) "hanging" else "";
        ("List2", $<#$(PrintChildren(fieldsRule)) $(PrintChildren(fieldsSeparator)) $hanging $Min $Max#>)
      }
    }
  }
}
