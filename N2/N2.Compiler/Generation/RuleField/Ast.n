using N2.Typing;
using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using N2.Model;

namespace N2.Compiler
{
  internal partial abstract class RuleField2
  {
    [Record]
    public sealed class Ast : RuleField2
    {
      private isDebug : bool;
      private fields : array[RuleField2];
      public RuleSymbol : RuleDefSymbol;

      public IsSimple : bool { get { RuleSymbol is SimpleRuleSymbol } }

      public override InitChildren(compiler : RuleParserEmitter, parent : RuleField2, level : int, allFields : SCG.List[RuleField2]) : void
      {
        foreach (field in fields)
          field.Init(compiler, parent, level, allFields);
      }

      [RecordIgnore] public mutable astStruct : RootAstStruct2;
      [RecordIgnore] public mutable astPtr    : AstPointer2;
      public override CalculateAstInfo() : void
      {
        astStruct = match (RuleSymbol)
        {
          | ExtensibleRuleSymbol       => assert2(false); assert(false);
          | ExtentionRuleSymbol  as rs =>
            match (rs.ExtentionType)
            {
              | Prefix  => PrefixAstStruct2(Compiler, this)
              | Postfix => PostfixAstStruct2(Compiler, this)
            }
          | SimpleRuleSymbol           => SimpleAstStruct2(Compiler, this)
          | _                          => assert(false);
        }
        astPtr = AstPointer2(astStruct, "astPtr", this);
        CalculateAstInfo(astStruct, astPtr, fields);
      }

      public override Compile() : void
      {
        def code = SCG.List();
        code.Add(CheckAstMemoize());
        code.Add(DebugCode());
        code.Add(<[ $(fields[0].ParseLabel.Goto()); ]>);

        //parse ok
        code.Add(<[ $(astStruct.OkLabel.Label()); ]>);
        code.Add(<[ $(astPtr.HeaderRef(astStruct.StateRef)) = -1; ]>);// состояние разбора -1 если правило полностью разобрано.
        code.Add(<[ resultRef = $(astPtr.Var); ]>);
        code.Add(<[ $(Compiler.EndLabel.Goto()); ]>);

        //parse fail
        code.Add(<[ $(astStruct.FailLabel.Label()); ]>);
        code.Add(<[ when ($(IsAstAllocatetdCode(astStruct))) // АСТ создано
                    {
                      $(astPtr.HeaderRef(astStruct.StateRef)) = parseState;
                      resultRef = $(astPtr.Var);
                    }
        ]>);
        code.Add(<[ curEndPos = -1; $(Compiler.EndLabel.Goto()); ]>);
        ParseCode = PExpr.Sequence(code.FlattenPExprSequence());
      }

      private DebugCode() : PExpr
      {
        if (false)//isDebug)
          <[
            when (parser.ruleCalls[$CurTextPos] == null)
              parser.ruleCalls[$CurTextPos] = System.Collections.Generic.List();
            parser.ruleCalls[$CurTextPos].Add(parser.parserHost.GetRuleDescriptorById(RuleId));
          ]>
        else
          <[ () ]>
      }

      private CheckAstMemoize() : PExpr
      {
        <[
          when ($CurTextPos < text.Length)
          {
            for (mutable ast = parser.memoize[$CurTextPos]; ast > 0; ast = parser.ast[ast + 1])
            {
              when (parser.ast[ast] == RuleId)
              {
                if (parser.ast[ast + 2] == -1)
                {
                  resultRef = ast;
                  mutable i = ast + $(astStruct.SizesOffset);
                  mutable end = i + $(astStruct.SizeFields.Count);
                  for (; i < end; ++i)
                    curEndPos += parser.ast[i];
                  $(Compiler.EndLabel.Goto());
                }
                else 
                {
                  curEndPos = -1;
                  $(Compiler.EndLabel.Goto());
                }
              }
            }
          }
        ]>
      }

      public override PrintContent() : string * string { ("Ast", $<#$(PrintChildren(fields))#>) }
    }
  }
}
