using N2.Typing;
using Nemerle;
using Nemerle.Compiler.Parsetree;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;
using N2.Model;

namespace N2.Compiler
{
  internal abstract class RootAstStruct : AstStruct
  {
    public StateRef  : AstStruct.HeaderField { get; private set; }

    public this(variableEmitter : IVariableEmitter, ruleField : RuleField)
    {
      def okLabel   = variableEmitter.Environment.MakeLabel("Ast_Ok");
      def failLabel = variableEmitter.Environment.MakeLabel("Ast_Fail");
      base(variableEmitter, ruleField, okLabel, failLabel);
      StateRef  = AstStruct.HeaderField(this);
      AstId = "RuleId";
    }
  }

  [Record]
  internal class SimpleAstStruct : RootAstStruct
  {
    public override HasTerminator : bool { get { false } }
  }

  [Record]
  internal class PrefixAstStruct : RootAstStruct
  {
    public override HasTerminator : bool { get { true } }
  }

  [Record]
  internal class PostfixAstStruct : RootAstStruct
  {
    public override HasTerminator : bool { get { true } }
  }

  internal class InternalAstStruct : AstStruct
  {
    public override HasTerminator : bool { get { false } }
    public this(field : RuleField, ruleId : string, name : string = "")
    {
      def variableEmitter = field.Compiler : IVariableEmitter;
      def okLabel         = variableEmitter.Environment.MakeLabel($"$(field.GetType().Name)_$(name)_$(field.State)_Ok");
      def failLabel       = variableEmitter.Environment.MakeLabel($"$(field.GetType().Name)_$(name)_$(field.State)_Fail");
      base(variableEmitter, field, okLabel, failLabel);
      AstId = ruleId;
    }
    public this(field : RuleField, ruleParserEmitter : RuleParserEmitter, name : string = "")
    {
      this(field, "", name);
      AstId = ruleParserEmitter.MakeRuleId(this);
    }
  }

  internal abstract class AstStruct
  {
    public RuleField       : RuleField;
    public VariableEmitter : IVariableEmitter;
    public AstId           : string { get; protected set; }
    public OkLabel         : PExprLabel;
    public FailLabel       : PExprLabel;

    public IdRef           : AstStruct.HeaderField { get; private set; }
    public NextRef         : AstStruct.HeaderField { get; private set; }

    public this(variableEmitter : IVariableEmitter, ruleField : RuleField, okLabel : PExprLabel, failLabel : PExprLabel)
    {
      RuleField       = ruleField;
      IdRef           = AstStruct.HeaderField(this);
      NextRef         = AstStruct.HeaderField(this);
      VariableEmitter = variableEmitter;
      OkLabel         = okLabel;
      FailLabel       = failLabel;
    }

    public AstStart : PExpr { get { VariableEmitter.CreateVariable($"astStart$(RuleField.Level)") } }

    public mutable HeaderFields     : SCG.List[HeaderField] = SCG.List();
    public mutable SizeFields       : SCG.List[StructField] = SCG.List();

    public mutable AllocateState    : int;

    public SizesOffset            : int  { get { HeaderFields.Count } }
    public TerminatorOffset       : int  { get { HeaderFields.Count + SizeFields.Count } }

    public TerminatorSize         : int  { get { if (HasTerminator) 1 else 0 } }
    public abstract HasTerminator : bool { get; }

    public AstSize                : int { get { HeaderFields.Count + SizeFields.Count + TerminatorSize } }

    public class HeaderField
    {
      public Owner : AstStruct;
      public Offset : int;
      public this(owner : AstStruct)
      {
        Owner = owner;
        Offset = owner.HeaderFields.Count;
        owner.HeaderFields.Add(this);
      }
    }

    public class StructField
    {
      public Owner : AstStruct;
      public Field : RuleField;
      public Offset : int
      {
        get { Owner.SizesOffset + _sizeOffset }
      }
      private _sizeOffset : int;
      public this(owner : AstStruct, field : RuleField)
      {
        Owner = owner;
        Field = field;
        _sizeOffset = owner.SizeFields.Count;
        owner.SizeFields.Add(this);
      }
    }
  }
}
