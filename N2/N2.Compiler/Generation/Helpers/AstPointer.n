using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using N2.Model;

namespace N2.Compiler
{
  internal interface IVariableEmitter
  {
    Environment : EmitterEnvironment { get; }
    CreateVariable(name : string) : PExpr;
  }

  internal class AstPointer2
  {
    public AstStruct : AstStruct2;
    public Var       : PExpr;
    public this(astStruct : AstStruct2, name : string, variableEmitter : IVariableEmitter)
    {
      AstStruct = astStruct;
      Var       = variableEmitter.CreateVariable(name);
    }
    public this(astStruct : AstStruct2, name : string, field : RuleField2)
    {
      this(astStruct, $"$name$(field.Level)", field.Compiler);
    }

    public HeaderOffset(headerField : AstStruct2.HeaderField) : PExpr
    {
      if (object.ReferenceEquals(AstStruct, headerField.Owner))
        <[ $Var + $(headerField.Offset) ]>
      else
        PExpr.Error("Type mismatch.")
    }
    public HeaderRef(headerField : AstStruct2.HeaderField) : PExpr
    {
      <[ parser.ast[$(HeaderOffset(headerField))] ]>
    }

    public AstOffset(structField : AstStruct2.StructField) : PExpr
    {
      if (object.ReferenceEquals(AstStruct, structField.Owner) && structField.AstOffset >= 0)
        <[ $Var + $(AstStruct.AstOffset + structField.AstOffset) ]>
      else
        PExpr.Error("Type mismatch.")
    }
    public AstRef(structField : AstStruct2.StructField) : PExpr
    {
      <[ parser.ast[$(AstOffset(structField))] ]>
    }

    public SizeOffset(structField : AstStruct2.StructField) : PExpr
    {
      if (object.ReferenceEquals(AstStruct, structField.Owner) && structField.SizeOffset >= 0)
        <[ $Var + $(AstStruct.SizesOffset + structField.SizeOffset) ]>
      else
        PExpr.Error("Type mismatch.")
    }
    public SizeRef(structField : AstStruct2.StructField) : PExpr
    {
      <[ parser.ast[$(SizeOffset(structField))] ]>
    }
  }
}
