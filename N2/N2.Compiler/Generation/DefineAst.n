using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;

using N2.Internal;
using N2.Model;
using N2.Typing;

using System;
using System.Diagnostics;
using System.Linq;
using SCG = System.Collections.Generic;

namespace N2.Compiler
{
  internal partial class SyntaxModuleEmitter
  {
    [Memoize]
    public DefineAstType(rd : RuleDefinition2) : TypeBuilder
    {
      assert(rd.IsAst);
      match (rd)
      {
        | ExtentionPoint(Type = RuleType2.Ref as baseType)
        | Simple(Type = RuleType2.Ref as baseType)
        | ExtentionPrefix(BaseRule = RuleRef(Ref = Some(RuleRefSymbol(Type = RuleType2.Ref as baseType))))
        | ExtentionPostfix(BaseRule = RuleRef(Ref = Some(RuleRefSymbol(Type = RuleType2.Ref as baseType)))) =>
          def mods = NemerleModifiers.Abstract %| NemerleModifiers.Public %| NemerleModifiers.Partial;
          def attrsAndMods = AttributesAndModifiers(mods, []);

          Util.locate(rd.RuleSymbol.FirstLocation.NLocation(),
          GrammarTB.DefineNestedType(<[decl:
            ..$attrsAndMods class $(MakePublicRuleName(rd.RuleSymbol) : dyn) : $(RuleTypeToNType(baseType) : typed)
            {
              public this(location : Location, errors : list[ErrorInfo])
              {
                base(location, errors)
              }
            }
          ]>))

        | _ => assert(false)
      }
    }

    private DefineErrorAst(rd : RuleDefinition2, baseAstTb : TypeBuilder) : TypeBuilder
    {
      def errorTb = Util.locate(rd.RuleSymbol.FirstLocation.NLocation(), 
        baseAstTb.DefineNestedType(<[decl: 
        public partial new class Error : $(baseAstTb.GetMemType() : typed)
        {
          public this(location : Location, errors : list[ErrorInfo])
          {
            base(location, errors)
          }
        }
      ]>));
      DefineErrorGetDump(errorTb, baseAstTb.FullName);
      errorTb
    }

    private DefineSpliceAst(rd : RuleDefinition2, baseAstTb : TypeBuilder) : TypeBuilder
    {
      def spliceTb = Util.locate(rd.RuleSymbol.FirstLocation.NLocation(),
        baseAstTb.DefineNestedType(<[decl: 
        public partial new class Splice : $(baseAstTb.GetMemType() : typed)
        {
          public this(location : Location, errors : list[ErrorInfo])
          {
            base(location, errors)
          }
        }
      ]>));
      DefineSpliceGetDump(spliceTb, baseAstTb.FullName);
      spliceTb
    }

    public DefineAst() : void
    {
      foreach (rd when rd.IsAst in SyntaxModule.RuleDefs)
      {
        def tb = DefineAstType(rd);

        def (errorTb, spliceTb) =
          if (rd is RuleDefinition2.ExtentionPoint || rd is RuleDefinition2.Simple)
            (DefineErrorAst(rd, tb), DefineSpliceAst(rd, tb))
          else
            (null, null);

        match (rd)
        {
          | RuleDefinition2.ExtentionPoint => Util.locate(rd.RuleSymbol.FirstLocation.NLocation(), DefineExtraFields(tb, null, errorTb, spliceTb, rd));
          | RuleDefinition2.ExtentionPostfix
          | RuleDefinition2.ExtentionPrefix
          | RuleDefinition2.Simple =>
            def args = GetAstFieldsTypes(rd).WithDefault([]);

            def astTb = Util.locate(rd.RuleSymbol.FirstLocation.NLocation(),
              tb.DefineNestedType(<[decl: 
              public partial new class Ast : $(tb.GetMemType() : typed)
              {
              }
            ]>));

            def names = rd.GetNames(_tokenFieldNames).Map(_.AsName);
            assert2(names.Length == args.Length);
            def members = names.Zip(args);

            DefineAstFields(astTb, members);
            DefineAstCtor(astTb, members);
            DefineAstGetErrors(astTb, members);
            when (rd.GetRule() is Some(Rule2.Sequence(rules)))
              DefineToString(astTb, names, rules);
            DefineAstGetDump(astTb, tb.FullName, members);
            when (rd.GetRule() is Some(rule))
              DefineAstGetSpans(astTb, rule, rd);

            Util.locate(rd.RuleSymbol.FirstLocation.NLocation(), DefineExtraFields(tb, astTb, errorTb, spliceTb, rd));

            astTb.Compile();

          | RuleDefinition2.Marker
          | RuleDefinition2.Regular => assert(false);
        }

        when (errorTb != null)
          errorTb.Compile();
        when (spliceTb != null)
          spliceTb.Compile();
        tb.Compile();
      }
    }

    private DefineAstFields(astTb : TypeBuilder, members : list[string * RuleType2]) : void
    {
      def fields = members.Map((name, argType) => <[decl: public $(name : dyn) : $(RuleTypeToNType(argType) : typed); ]>);
      foreach (field in fields)
        astTb.Define(field);
    }

    private DefineAstCtor(astTb : TypeBuilder, members : list[string * RuleType2]) : void
    {
      def locationName = <[ $(Nemerle.Compiler.Parsetree.Name("location") : name) ]>;
      def errorsName   = <[ $(Nemerle.Compiler.Parsetree.Name("error") : name) ]>;
      def args
        =  <[ $locationName : Location ]>
        :: <[ $errorsName : list[ErrorInfo] ]>
        :: members.Map((name, argType) => <[ $(name : dyn) : $(RuleTypeToNType(argType) : typed) ]>)
        ;
      _ = astTb.DefineWithSource(<[decl:
        public this(..$(args.Map(PParameter)))
        {
            base($locationName, $errorsName);
            { ..$(members.Map((name, _) => <[ this.$(name : dyn) = $(name : dyn); ]>)) }
        }
      ]>);
    }
  }
}
