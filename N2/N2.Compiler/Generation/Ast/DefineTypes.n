using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;

using N2.Internal;
using N2.Model;
using N2.Typing;

using System;
using System.Diagnostics;
using System.Linq;
using SCG = System.Collections.Generic;

namespace N2.Compiler
{
  internal partial class RuleAstEmitter
  {
    public DefineTypes() : void
    {
      CreateBaseAstTypeBuilder();
      match (RuleDefinition)
      {
        | RuleDefinition2.ExtentionPoint =>
          CreateErrorAstTypeBuilder();
          CreateSpliceAstTypeBuilder();

        | RuleDefinition2.Simple =>
          CreateErrorAstTypeBuilder();
          CreateSpliceAstTypeBuilder();
          CreateAstTypeBuilder();

        | RuleDefinition2.ExtentionPostfix
        | RuleDefinition2.ExtentionPrefix =>
          CreateAstTypeBuilder();

        | _ => assert(false)
      }
    }
    
    private CreateBaseAstTypeBuilder() : void
    {
      assert(_baseTb == null);
      match (RuleDefinition)
      {
        | ExtentionPoint with baseType = Owner.Environment.AstTypeVar
        | Simple         with baseType = Owner.Environment.AstTypeVar
        | ExtentionPrefix(BaseRule = RuleRef(Ref = Some(RuleRefSymbol(Type = RuleType2.Ref as baseTypeRef))))
            with baseType = Owner.RuleTypeToNType(baseTypeRef)
        | ExtentionPostfix(BaseRule = RuleRef(Ref = Some(RuleRefSymbol(Type = RuleType2.Ref as baseTypeRef))))
            with baseType = Owner.RuleTypeToNType(baseTypeRef) =>
          def mods = NemerleModifiers.Abstract %| NemerleModifiers.Public %| NemerleModifiers.Partial;
          def attrsAndMods = AttributesAndModifiers(mods, []);

          _baseTb = Util.locate(RuleDefinition.RuleSymbol.FirstLocation.NLocation(),
            Owner.GrammarTB.DefineNestedType(<[decl:
            ..$attrsAndMods class $(Owner.MakePublicRuleName(RuleDefinition.RuleSymbol) : dyn) : $(baseType : typed)
            {
              public this(location : N2.Location, errors : list[N2.ErrorInfo])
              {
                base(location, errors)
              }
            }
          ]>))

        | _ => assert2(false); assert(false)
      }
    }

    private CreateAstTypeBuilder() : void
    {
      assert(_astTb == null);
      _astTb = Util.locate(RuleDefinition.RuleSymbol.FirstLocation.NLocation(),
        _baseTb.DefineNestedType(<[decl: 
        public partial new class Ast : $(_baseTb.GetMemType() : typed)
        {
        }
      ]>));
    }

    private CreateErrorAstTypeBuilder() : void
    {
      assert(_errorTb == null);
      _errorTb = Util.locate(RuleDefinition.RuleSymbol.FirstLocation.NLocation(), 
        _baseTb.DefineNestedType(<[decl: 
        public partial new class Error : $(_baseTb.GetMemType() : typed)
        {
          public this(location : N2.Location, errors : list[N2.ErrorInfo])
          {
            base(location, errors)
          }
        }
      ]>));
    }

    private CreateSpliceAstTypeBuilder() : void
    {
      assert(_spliceTb == null);
      _spliceTb = Util.locate(RuleDefinition.RuleSymbol.FirstLocation.NLocation(),
        _baseTb.DefineNestedType(<[decl: 
        public partial new class Splice : $(_baseTb.GetMemType() : typed)
        {
          public this(location : N2.Location, errors : list[N2.ErrorInfo])
          {
            base(location, errors)
          }
        }
      ]>));
    }
  }
}
