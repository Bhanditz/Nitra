using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using N2.Model;
using N2.Typing;

using System;
using System.Collections.Generic;
using System.Linq;

namespace N2.Compiler
{
  internal class AbstractWalkerEmitter : IVariableEmitter
  {
    public Owner      : SyntaxModuleEmitter { get; }
    public RuleSymbol : RuleDefSymbol       { get; }

    public this(owner      : SyntaxModuleEmitter, ruleSymbol : RuleDefSymbol)
    {
      Owner = owner;
      RuleSymbol = ruleSymbol;
    }

    mutable _tb : TypeBuilder;

    Environment : EmitterEnvironment implements IVariableEmitter.Environment { get { Owner.Environment } }

    CreateVariable(name : string) : PExpr implements IVariableEmitter.CreateVariable
    {
      <[ $(name : dyn) ]>
    }

    public DefineTypes() : void
    {
      assert(_tb == null);
      _tb = Owner.GrammarImplTB.DefineNestedType(<[ decl:
        public class $(RuleSymbol.Name + "Walker" : usesite) : $(GetWalkerBaseType(RuleSymbol))
        {
        }
      ]>);
    }

    public DefineMembers(rootAstStruct : option[RootAstStruct2]) : void
    {
      assert(_tb != null);

      def parms =
        <[ parameter: $("location" : usesite) : N2.Location ]>
        :: <[ parameter: $("errorInfo" : usesite) : list[N2.ErrorInfo] ]> //TODO: удалить этот параметр
        :: RuleSymbol.GetSubRules().Map(r => <[ parameter: $(r.FieldName : usesite) : $(Owner.RuleTypeToNType(r.Type) : typed) ]>);
      _tb.DefineConditional(<[ decl:
        public Accept(..$parms) : $(Owner.RuleTypeToNType(RuleSymbol.Type) : typed)
        {
          $(Owner.NTypeOfRuleSymbol(RuleSymbol) : typed).Ast(..$(parms.Map(p => <[ $(p.Name : usesite) ]>)))
        }
      ]>);

      def referencedWalkers = GetDependencies(RuleSymbol);
      foreach(p in referencedWalkers)
        _tb.Define(<[ decl: private mutable $(p.Value.FieldName : usesite) : $(GetWalkerType(p.Key)) ]>);

      def initBody = List();
      foreach (p in referencedWalkers)
        initBody.Add(<[ $(p.Value.FieldName : usesite) = compositeAstWalker.GetWalker($(p.Key.GetDescriptorClassRef()).StaticDescriptor) ]>);
      _tb.DefineConditional(<[ decl: 
        public override Init(compositeAstWalker : N2.Runtime.CompositeAstWalker) : void
        {
          ..$(initBody.ToNList())
        }
      ]>);

      DefineWalkMethod(rootAstStruct.Value, referencedWalkers);
    }

    private DefineWalkMethod(rootAstStruct : RootAstStruct2, referencedWalkers : Hashtable[RuleSymbol, WalkerInfo]) : void
    {
      def rootAstMaker(arguments : list[PExpr]) : PExpr
      {
        def acceptArgs =
          <[ N2.Location(parser.Source, startPos, pos) ]>
          :: <[ [] ]>
          :: arguments;
        <[ this.Accept(..$acceptArgs) ]>
      }
      def internalAstMaker(_ : list[PExpr]) : PExpr
      {
        | []   => assert(false)
        | [x]  => x
        | many => <[ (..$many) ]>
      }
      def makeReader(structField : AstStruct2.StructField, astPointer : AstPointer2) : PExpr
      {
        match(structField.Field)
        {
          | RuleField2.TokenString
          | RuleField2.TokenCall =>
            <[
              def tokenPos = pos;
              pos += parser.ast[$(astPointer.SizeOffset(structField))];
              N2.NToken(tokenPos, pos)
            ]>

          | RuleField2.ExternalAst(RuleSymbol = ruleSymbol) =>
            def walkerFieldName = referencedWalkers[ruleSymbol].FieldName;
            if (ruleSymbol.Type is RuleType.Void)
              <[ pos += parser.ast[$(astPointer.SizeOffset(structField))]; ]>
            else
              <[ this.$(walkerFieldName : usesite).Walk(ref pos, parser.ast[$(astPointer.AstOffset(structField))], ref parser); ]>

          | RuleField2.Extensible(RuleSymbol = ruleSymbol) =>
            def walkerFieldName = referencedWalkers[ruleSymbol].FieldName;
            if (ruleSymbol.Type is RuleType.Void)
              <[ pos += parser.ast[$(astPointer.SizeOffset(structField))]; ]>
            else
              <[ this.$(walkerFieldName : usesite).Walk(ref pos, ref parser); ]>

          | RuleField2.List(astStruct = itemStruct, NextRef = nextRef) =>
            def itemPointer   = AstPointer2(itemStruct, "astIndex", this);
            <[
              def astIndex = parser.ast[$(astPointer.AstOffset(structField))];
              if (astIndex > 0)
              {
                def astBuffer = System.Collections.Generic.List(16);
                def loop(astIndex)
                {
                  astBuffer.Add($(makeAst(itemStruct, itemPointer, internalAstMaker)));
                  def astIndex = parser.ast[$(itemPointer.AstOffset(nextRef))];
                  when (astIndex > 0)
                    loop(astIndex);
                }
                loop(astIndex);
                Nemerle.Collections.NList.ToList(astBuffer)
              }
              else []
            ]>

          | RuleField2.List2(astRuleStruct = itemStruct, astSeparatorStruct = separatorStruct, NextRuleRef = nextItemRef, NextSeparatorRef = nextSeparatorRef, hangingSeparator = hangingSeparator) =>
            def itemPointer = AstPointer2(itemStruct, "astIndex", this);
            def separatorPointer = AstPointer2(separatorStruct, "separatorIndex", this);
            def readNextAst()
            {
              if (hangingSeparator)
                <[
                  separatorBuffer.Add($(makeAst(separatorStruct, separatorPointer, internalAstMaker)));
                  def nextAstIndex = parser.ast[$(separatorPointer.AstOffset(nextSeparatorRef))];
                  when (nextAstIndex > 0)
                    loop(nextAstIndex);
                ]>
              else
                <[
                  def nextAstIndex = parser.ast[$(separatorPointer.AstOffset(nextSeparatorRef))];
                  when (nextAstIndex > 0)
                  {
                    separatorBuffer.Add($(makeAst(separatorStruct, separatorPointer, internalAstMaker)));
                    loop(nextAstIndex);
                  }
                ]>
            }
            <[
              def astIndex = parser.ast[$(astPointer.AstOffset(structField))];
              if (astIndex > 0)
              {
                def astBuffer = System.Collections.Generic.List(16);
                def separatorBuffer = System.Collections.Generic.List(16);
                def loop(astIndex)
                {
                  astBuffer.Add($(makeAst(itemStruct, itemPointer, internalAstMaker)));
                  def separatorIndex = parser.ast[$(itemPointer.AstOffset(nextItemRef))];
                  when (separatorIndex > 0)
                    $(readNextAst());
                }
                loop(astIndex);
                (Nemerle.Collections.NList.ToList(astBuffer), Nemerle.Collections.NList.ToList(separatorBuffer))
              }
              else ([], [])
            ]>

          | RuleField2.Option(astStruct = valueAstStruct) =>
            <[
              def astIndex = parser.ast[$(astPointer.AstOffset(structField))];
              if (astIndex > 0)
                Some($(makeAst(valueAstStruct, AstPointer2(valueAstStruct, "astIndex", this), internalAstMaker)))
              else
                None()
            ]>

          | _ => assert(false)
        }
      }
      and makeAst(astStruct : AstStruct2, astPointer : AstPointer2, maker : list[PExpr] -> PExpr) : PExpr
      {
        def statements = List();
        def arguments = List();
        when (astStruct is PostfixAstStruct2)
          arguments.Add(<[ prefix ]>);

        foreach (structField in astStruct.SizeFields)
          match (structField.Field)
          {
            | RuleField2.Ast
            | RuleField2.Predicate => ()
            | RuleField2.Void =>
              statements.Add(<[ pos += parser.ast[$(astPointer.SizeOffset(structField))]; ]>);

            | RuleField2.List  
            | RuleField2.List2 
            | RuleField2.Option
            | _                =>
              def varName = Util.tmpname("ast");
              statements.Add(<[ def $(varName : usesite) = $(makeReader(structField, astPointer)) ]>);
              arguments.Add(<[ $(varName : usesite) ]>);
          }

        statements.Add(maker(arguments.ToNList()));
        <[ { ..$(statements.ToNList()) } ]>
      }

      match (RuleSymbol)
      {
        | SimpleRuleSymbol as s
        | ExtentionRuleSymbol(ExtentionType = Prefix) as s =>
          def astPointer = AstPointer2(rootAstStruct, "astPtr", this);
          _tb.DefineConditional(<[ decl:
            public override Walk(pos : ref int, astPtr : int, parser : ref N2.Internal.Parser) : $(Owner.RuleTypeToNType(s.Type) : typed)
            {
              unchecked
              {
                def startPos = pos;
                $(makeAst(rootAstStruct, astPointer, rootAstMaker))
              }
            }
          ]>);

        | ExtentionRuleSymbol(ExtentionType = Postfix) as s =>
          def astPointer = AstPointer2(rootAstStruct, "astPtr", this);
          _tb.DefineConditional(<[ decl:
            public override Walk(prefix : $(Owner.RuleTypeToNType(s.Type) : typed), pos : ref int, astPtr : int, parser : ref N2.Internal.Parser) : $(Owner.RuleTypeToNType(s.Type) : typed)
            {
              unchecked
              {
                def startPos = pos;
                $(makeAst(rootAstStruct, astPointer, rootAstMaker))
              }
            }
          ]>);

        | _ => assert2(false);
      }
    }

    private static GetDependencies(rule : RuleSymbol) : Hashtable[RuleSymbol, WalkerInfo]
    {
      def refs = Hashtable();
      def collect(rule : Rule) : void
      {
        | String
        | Not
        | And => ()
        | Sequence            (rules)                    => foreach(r in rules) collect(r);
        | Repeat              (_, _, rule)               => collect(rule)
        | RepeatWithSeparator (_, _, rule, separator, _) => collect(rule); collect(separator)
        | Call                (ruleRef, _) as callRule   =>
          def symbol = ruleRef.Ref.Value;
          // TODO: deal with 'void'
          unless (callRule.Type is RuleType.Chars || callRule.Type is RuleType.Void || symbol is MarkerSymbol)
            unless (refs.ContainsKey(symbol))
            _ = refs.Add(symbol, WalkerInfo(symbol));
      }
      match (rule)
      {
        | ExtentionRuleSymbol(Rule = r)
        | SimpleRuleSymbol   (Rule = r) => collect(r);
        | _ => assert2(false); assert(false)
      }
      refs
    }

    private struct WalkerInfo
    {
      public this(symbol : RuleSymbol)
      {
        FieldName = Util.tmpname(symbol.Name + "Walker");
      }
      public FieldName : string;
    }

    private GetWalkerType(rule : RuleSymbol) : PExpr
    {
      | SimpleRuleSymbol as s => <[ N2.Runtime.SimpleAstWalker[$(Owner.RuleTypeToNType(s.Type) : typed)] ]>
      | ExtensibleRuleSymbol as s => <[ N2.Runtime.ExtensibleAstWalker[$(Owner.RuleTypeToNType(s.Type) : typed)] ]>
      | _ => assert2(false); assert(false)
    }

    private GetWalkerBaseType(rule : RuleSymbol) : PExpr
    {
      | SimpleRuleSymbol                             as s => <[ N2.Runtime.SimpleAstWalker[$(Owner.RuleTypeToNType(s.Type) : typed)] ]>
      | ExtentionRuleSymbol(ExtentionType = Prefix)  as s => <[ N2.Runtime.PrefixAstWalker[$(Owner.RuleTypeToNType(s.Type) : typed)] ]>
      | ExtentionRuleSymbol(ExtentionType = Postfix) as s => <[ N2.Runtime.PostfixAstWalker[$(Owner.RuleTypeToNType(s.Type) : typed)] ]>
      | _ => assert2(false); assert(false)
    }

    public Emit() : void
    {
      _tb.Compile();
    }
  }
}
