using N2.Model;
using N2.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace N2.Compiler
{
  [Record]
  public sealed class WalkerSignature
  {
    public Parameters : list[PParameter];
    public ReturnType : PExpr;
  }

  public abstract class WalkerGenerator
  {
    private _calledWalkers : Hashtable[SyntaxRuleSymbol * int, string] = Hashtable();

    protected          ConstantEmitter                 : IConstantEmitter { get; private set; }
    public    abstract Name                            : string           { get; }
    public    abstract SupportsVoidRules               : bool             { get; }
    public    abstract GetSignature(rule : RuleSymbol) : WalkerSignature;

    protected abstract GetSyntaxRuleValue(ruleSymbol : SyntaxRuleSymbol, astPointer : AstPointerScope) : PExpr;
    protected abstract GetRuleValue(field : RuleStructureField, parentAstPointer : AstPointerScope) : PExpr;
    protected abstract CombineValues(ruleStructure : RuleStructure, arguments : list[PExpr]) : PExpr;

    public virtual PrefixAmbiguity(ruleSymbol : ExtensibleRuleSymbol) : PExpr
    {
      def message = $"Ambiguity between prefix rules of '$(ruleSymbol.FullName)'.";
      <[
        _ = parser;
        _ = endPos;
        throw N2.Runtime.PrefixAmbiguityException($message, __startPos, Parser, ambiguities);
      ]>
    }

    public virtual PostfixAmbiguity(ruleSymbol : ExtensibleRuleSymbol) : PExpr
    {
      def message = $"Ambiguity between postfix rules of '$(ruleSymbol.FullName)'.";
      <[
        _ = parser;
        _ = endPos;
        throw N2.Runtime.PostfixAmbiguityException($message, __startPos, Parser, ambiguities);
      ]>
    }

    public virtual PrefixMissed(ruleSymbol : ExtensibleRuleSymbol) : PExpr
    {
      def message = $"Expected '$(ruleSymbol.FullName)' rule.";
      <[
        _ = parser;
        throw N2.Runtime.PrefixMissedException($message, pos, Parser);
      ]>
    }

    public virtual SimpleMissed(ruleSymbol : SimpleRuleSymbol) : PExpr
    {
      def message = $"Expected '$(ruleSymbol.FullName)' rule.";
      <[
        _ = parser;
        throw N2.Runtime.SimpleMissedException($message, pos);
      ]>
    }

    public Init(constantEmitter : IConstantEmitter) : void
    {
      ConstantEmitter = constantEmitter;
      _calledWalkers.Clear();
    }

    private GetFieldNameForWalkerCall(ruleSymbol : SyntaxRuleSymbol, bp : int) : string
    {
      def key = (ruleSymbol, bp);
      mutable value;

      if (_calledWalkers.TryGetValue(key, out value))
        value
      else
      {
        def name = $"_walker_$(_calledWalkers.Count)_$(ruleSymbol.Name)";
        _calledWalkers.Add(key, name);
        name
      }
    }

    protected GetRangeOf(field : RuleStructureField, parentAstPointer : AstPointerScope) : PExpr
    {
      def size = parentAstPointer.Access().GetSize(field);
      <[ N2.NToken(pos, pos + $size) ]>
    }

    public CallSimpleRule(field : RuleStructureField.SimpleCall, parentAstPointer : AstPointerScope, params args : array[PExpr]) : PExpr
    {
      def walkCode = <[
        pos += $(parentAstPointer.Access().GetSkip(field));
        this.$(GetFieldNameForWalkerCall(field.Symbol, 0) : usesite).Walk(ref pos, parser, ..$args);
      ]>;
      if (field.CanParseEmptyString)
        walkCode
      else
        <[
          pos += $(parentAstPointer.Access().GetSkip(field));
          if ($(parentAstPointer.Access().GetSize(field)) > 0)
            $walkCode
          else
            this.$(GetFieldNameForWalkerCall(field.Symbol, 0) : usesite).OnSimpleMissed(pos, parser, ..$args);
        ]>
    }

    public CallExtensibleRule(field : RuleStructureField.ExtensibleCall, parentAstPointer : AstPointerScope, params args : array[PExpr]) : PExpr
    {
      <[
        pos += $(parentAstPointer.Access().GetSkip(field));
        if ($(parentAstPointer.Access().GetSize(field)) > 0)
          this.$(GetFieldNameForWalkerCall(field.Symbol, field.BindingPower) : usesite).Walk(ref pos, parser, ..$args);
        else
          this.$(GetFieldNameForWalkerCall(field.Symbol, field.BindingPower) : usesite).OnPrefixMissed(pos, parser, ..$args);
      ]>
    }

    public GetCalledWalkers() : IEnumerable[SyntaxRuleSymbol * int * string]
    {
      _calledWalkers.Select(pair => (pair.Key[0], pair.Key[1], pair.Value))
    }

    public WalkerCodeForSimpleRule(ruleSymbol : SimpleRuleSymbol) : PExpr
    {
      def pointerAllocator = AstPointerScope(ruleSymbol.Structure, "astPtr", ConstantEmitter);
      def code             = GetSyntaxRuleValue(ruleSymbol, pointerAllocator);
      def astMissingCode   = SimpleMissed(ruleSymbol);
      pointerAllocator.Compile(astMissingCode, code).FlattenPExpr();
    }

    public WalkerCodeForExtentionRule(ruleSymbol : ExtentionRuleSymbol) : PExpr
    {
      def astPointer = AstPointer.Existing(ruleSymbol.Structure, ConstantEmitter, "astPtr");
      <[
        _ = astPtr;
        $(GetSyntaxRuleValue(ruleSymbol, AstPointerScope(astPointer)));
      ]>.FlattenPExpr()
    }

    protected Skip(field : RuleStructureField, parentAstPointer : AstPointerScope) : PExpr
    {
      <[ pos += $(parentAstPointer.Access().GetSize(field)); ]>
    }

    public GetRuleValue(ruleStructure : RuleStructure, name : string) : PExpr
    {
      def pointerScope = AstPointerScope(ruleStructure, name, ConstantEmitter);
      def ast = GetSubrulesValues(ruleStructure, pointerScope);
      pointerScope.Compile(ast)
    }

    public virtual GetSubrulesValues(ruleStructure : RuleStructure, astPointer : AstPointerScope) : PExpr
    {
      def statements = List();
      def arguments = List();

      when (ruleStructure is RuleStructure.Ast(Symbol = ExtentionRuleSymbol(ExtentionType = Postfix)))
        arguments.Add(<[ prefix ]>);

      statements.Add(<[ def __listStartPos = pos; ]>);

      foreach (field in ruleStructure.Fields)
      {
        | RuleStructureField.Marker => ()
        | RuleStructureField.Empty
        | RuleStructureField.SimpleCall(symbol)        when !SupportsVoidRules && symbol.Type is RuleType.Void
        | RuleStructureField.ExtensibleCall(symbol, _) when !SupportsVoidRules && symbol.Type is RuleType.Void => statements.Add(Skip(field, astPointer));
        | _  =>
          def varName = field.MakeVariableName("ast");
          statements.Add(<[ def $(varName : usesite) = $(GetRuleValue(field, astPointer)) ]>);
          arguments.Add(<[ $(varName : usesite) ]>);
      }

      statements.Add(CombineValues(ruleStructure, arguments.ToNList()));
      <[ { ..$statements } ]>
    }

    public GetTokenValue(field : RuleStructureField, parentAstPointer : AstPointerScope, code : PExpr) : PExpr
    {
      <[
        def (skip, size) = $(parentAstPointer.Access().GetSkipAndSize(field));
        pos += skip;
        def tokenPos = pos;
        pos += size;
        $code;
      ]>
    }

    public GetOptionValue
      ( field            : RuleStructureField.Option
      , parentAstPointer : AstPointerScope
      , hasValue         : PExpr
      , noValue          : PExpr
      ) : PExpr
    {
      <[
        if ($(parentAstPointer.Access().GetSize(field)) > 0)
          $hasValue;
        else
          $noValue;
      ]>
    }

    public GetListValue
      ( field            : RuleStructureField.List
      , parentAstPointer : AstPointerScope
      , init             : PExpr
      , item             : PExpr
      , returnCode       : PExpr
      , emptyCode        : PExpr
      ) : PExpr
    {
      assert3(!field.IsSkipAnyPattern);

      def loopCode = <[
        def loop()
        {
          $item;
          if (pos < listEnd)
            loop();
          else
            assert(pos == listEnd);
        }
      ]>;
      GetListValueImpl
        (field             = field
        , parentAstPointer = parentAstPointer
        , init             = init
        , loopCode         = loopCode
        , returnCode       = returnCode
        , emptyCode        = emptyCode)
    }

    public GetListWithSeparatorValue
      ( field            : RuleStructureField.ListWithSeparator
      , parentAstPointer : AstPointerScope
      , init             : PExpr
      , item             : PExpr
      , separator        : PExpr
      , returnCode       : PExpr
      , emptyCode        : PExpr
      ) : PExpr
    {
      def continueLoop =
        if (field.HangingSeparator)
          <[
            if (pos < listEnd)
              loop();
            else
              assert(pos == listEnd);
          ]>
        else
          <[ loop(); ]>;
      def loopCode = <[
        def loop()
        {
          $item;
          if (pos < listEnd)
          {
            $separator;
            $continueLoop;
          }
          else
            assert(pos == listEnd);
        }
      ]>;
      GetListValueImpl
        (field             = field
        , parentAstPointer = parentAstPointer
        , init             = init
        , loopCode         = loopCode
        , returnCode       = returnCode
        , emptyCode        = emptyCode)
    }

    private GetListValueImpl
      (field             : RuleStructureField
      , parentAstPointer : AstPointerScope
      , init             : PExpr
      , loopCode         : PExpr
      , returnCode       : PExpr
      , emptyCode        : PExpr
      ) : PExpr
    {
      def nonemptyCode = List();
      if (init is <[ { ..$exprs } ]>)
        nonemptyCode.AddRange(exprs);
      else
        nonemptyCode.Add(init);
      nonemptyCode.Add(loopCode);
      nonemptyCode.Add(<[ loop() ]>);
      nonemptyCode.Add(returnCode);
      <[
        def listEnd = pos + $(parentAstPointer.Access().GetSize(field));
        if (listEnd > pos) { ..$nonemptyCode }
        else $emptyCode
      ]>
    }
  }
}
