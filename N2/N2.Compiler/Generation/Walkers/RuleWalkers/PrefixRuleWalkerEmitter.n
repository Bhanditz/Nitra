using N2.Model;
using N2.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace N2.Compiler
{
  public sealed partial class WalkerEmitter
  {
    [Record]
    private sealed class PrefixRuleWalkerEmitter : RuleWalkerEmitter[ExtentionRuleSymbol]
    {
      [RecordIgnore] public override WalkerTb : TypeBuilder { get; private set; }

      public override RuleParserBaseType : PExpr
      {
        get { <[ N2.Internal.ExtentionRuleParser ]> }
      }

      public override Define(walkersModule : TypeBuilder) : void
      {
        def baseRule = Rule.BaseRule.Value;
        def signature = Generator.GetSignature(baseRule);
        def code = Generator.MakeGenerator(Rule.Structure);
        def prefixParameters =
          <[ parameter: pos : ref int ]>
          :: <[ parameter: astPtr : int ]>
          :: <[ parameter: parser : ref N2.Internal.Parser ]>
          :: signature.Parameters;

        WalkerTb = walkersModule.DefineNestedType(<[ decl:
          public sealed class $(Rule.Name + "_Walker" : usesite) : $(WalkerEmitter.GetExtensibleRuleWalkerFullName(baseRule, Generator, "_PrefixWalker"))
          {
            private RuleParser : $(Rule.GetParserClassRef());
            public this(ruleParser : $(RuleParserBaseType))
            {
              RuleParser = ruleParser :> $(Rule.GetParserClassRef());
            }
          }
        ]>);
        WalkerTb.DefineConditional(<[ decl:
          public override Init(compositeAstWalker : N2.Runtime.CompositeAstWalker) : void
          {

          }
        ]>);
        WalkerTb.DefineConditional(<[ decl:
          public override Walk(..$prefixParameters) : $(signature.ReturnType)
          {
            def startPos = pos;
            _ = startPos;
            $(code.Code)
          }
        ]>);
      }

      public override Emit() : void
      {
        WalkerTb.Compile();
      }
    }
  }
}
