using N2.Model;
using N2.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace N2.Compiler
{
  public sealed class AstPointerAllocator
  {
    private         _ruleStructure : RuleStructure;
    private         _baseName      : string;
    private mutable _astPointer    : AstPointer;
    private mutable _declared      : bool;

    internal this(ruleStructure : RuleStructure, baseName : string)
    {
      _ruleStructure = ruleStructure;
      _baseName      = baseName;
    }

    internal this(astPointer : AstPointer)
    {
      _astPointer = astPointer;
    }

    public Allocate() : AstPointer
    {
      assert(!_declared);

      when (_astPointer == null)
        _astPointer = AstPointer.New(_ruleStructure, _baseName);
      _astPointer
    }

    internal Declare() : PExpr
    {
      assert(!_declared);
      assert(_ruleStructure != null);

      _declared = true;
      if (_astPointer == null)
        <[ () ]>
      else
        <[
          def $(_astPointer.Name : dyn) = {
            astPtr:
            {
              def ruleId = this.RuleParser.$(_ruleStructure.AstId : dyn);
              def ast = parser.ast;
              
              for (mutable current = parser.memoize[pos]; current > 0; current = ast[current + 1])
                when (ast[current] == ruleId)
                  astPtr(current);
             
              assert(false);
            }
          }
        ]>
    }
  }
}
