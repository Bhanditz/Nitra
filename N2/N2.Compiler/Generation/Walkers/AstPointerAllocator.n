using N2.Model;
using N2.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace N2.Compiler
{
  public sealed class AstPointerAllocator : IVariableEmitter
  {
    private         _ruleStructure   : RuleStructure;
    private         _baseName        : string;
    private mutable _astPointer      : AstPointer;
    private mutable _declared        : bool;

    // stuff for AstPointer.New
    Environment : EmitterEnvironment implements IVariableEmitter.Environment { get { throw NotImplementedException() } }
    CreateVariable(name : string) : string implements IVariableEmitter.CreateVariable { name }

    internal this(ruleStructure : RuleStructure, baseName : string)
    {
      _ruleStructure   = ruleStructure;
      _baseName        = baseName;
    }

    internal this(astPointer : AstPointer)
    {
      _astPointer = astPointer;
    }

    public Allocate() : AstPointer
    {
      assert(!_declared);

      when (_astPointer == null)
        _astPointer = AstPointer.New(_ruleStructure, this, _baseName);
      _astPointer
    }

    public IsAllocated() : bool { _astPointer != null }

    internal Declare(bodyCode : PExpr) : PExpr
    {
      assert(!_declared);
      assert(_ruleStructure != null);

      _declared = true;
      if (_astPointer == null)
        bodyCode
      else
        <[
          def $(_astPointer.Name : dyn) = parser.GetAst(pos, this.RuleParser.$(_ruleStructure.AstId : dyn));
          $bodyCode;
        ]>
    }

    internal Declare(noAstCode : PExpr, bodyCode : PExpr) : PExpr
    {
      assert(!_declared);
      assert(_ruleStructure != null);

      _declared = true;
      if (_astPointer == null)
        bodyCode
      else
        <[
          def $(_astPointer.Name : dyn) = parser.TryGetAst(pos, this.RuleParser.$(_ruleStructure.AstId : dyn));
          if ($(_astPointer.Name : dyn) > 0)
            $bodyCode;
          else
            $noAstCode;
        ]>
    }
  }
}
