using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using N2.Model;
using N2.Typing;

using System;
using System.Collections.Generic;
using System.Linq;

namespace N2.Compiler
{
  internal sealed partial class TryParseMethodEmitter
  {
    [Record]
    private sealed class RuleStructureData : RuleStructureDataBase[TryParseMethodEmitter]
    {
      [RecordIgnore] public OkLabel   : PExprLabel { get; private set; }
      [RecordIgnore] public FailLabel : PExprLabel { get; private set; }

      public override Init() : void
      {
        match (Structure)
        {
          | RuleStructure.Ast =>
            OkLabel   = Emitter.Environment.MakeLabel("Ast_Ok");
            FailLabel = Emitter.Environment.MakeLabel("Ast_Fail");

          | RuleStructure.Predicate =>
            def id = ManagerClass.Instance.GetNewId();
            OkLabel   = Emitter.Environment.MakeLabel($"$(Structure.GetType().Name)_$(id)_Ok");
            FailLabel = Emitter.Environment.MakeLabel($"$(Structure.GetType().Name)_$(id)_Fail");

          | _ =>
            OkLabel   = Emitter.Environment.MakeLabel($"$(Structure.GetType().Name)_$(Structure.Owner.State)_Ok");
            FailLabel = Emitter.Environment.MakeLabel($"$(Structure.GetType().Name)_$(Structure.Owner.State)_Fail");
        }
      }
    }

    [Record]
    private sealed class RuleStructureFieldData : RuleStructureFieldDataBase[TryParseMethodEmitter]
    {
      [RecordIgnore] public ParseLabel         : PExprLabel { get; private set; }

      public FailParseLabel     : PExprLabel { get { Emitter._data.RuleStructureData[Field.Owner].FailLabel } }
      public ContinueParseLabel : PExprLabel
      {
        get
        {
          def getSibling(field : RuleStructureField) : RuleStructureField
          {
            | Marker => getSibling(field.NextSibling)
            | _      => field
          }
          def nextSibling = getSibling(Field.NextSibling);
          if (nextSibling == null)
            Emitter._data.RuleStructureData[Field.Owner].OkLabel
          else
            Emitter._data.RuleStructureFieldData[nextSibling].ParseLabel
        }
      }

      public override Init() : void
      {
        ParseLabel    = Emitter.Environment.MakeLabel($"Parse_$LabelSuffix");
      }
    }

    _data : RuleStructureDataContainer[TryParseMethodEmitter, RuleStructureData, RuleStructureFieldData];

#pragma warning disable 10003
    private OkLabel      (ruleStructure : RuleStructure) : PExpr { _data.RuleStructureData[ruleStructure].OkLabel.Label()   }
    private GotoOkLabel  (ruleStructure : RuleStructure) : PExpr { _data.RuleStructureData[ruleStructure].OkLabel.Goto()    }
    private FailLabel    (ruleStructure : RuleStructure) : PExpr { _data.RuleStructureData[ruleStructure].FailLabel.Label() }
    private GotoFailLabel(ruleStructure : RuleStructure) : PExpr { _data.RuleStructureData[ruleStructure].FailLabel.Goto()  }

    private ListStartPos          (ruleStructureField : RuleStructureField) : PExpr { <[ $(CreateVariable($"listStartPos$(ruleStructureField.Level)") : dyn) ]>   }
    private ListEndPos            (ruleStructureField : RuleStructureField) : PExpr { <[ $(CreateVariable($"listEndPos$(ruleStructureField.Level)") : dyn) ]>     }
    private OptionStartPos        (ruleStructureField : RuleStructureField) : PExpr { <[ $(CreateVariable($"optionStartPos$(ruleStructureField.Level)") : dyn) ]> }
    private ParseLabel            (ruleStructureField : RuleStructureField) : PExpr { _data.RuleStructureFieldData[ruleStructureField].ParseLabel.Label()         }
    private GotoParseLabel        (ruleStructureField : RuleStructureField) : PExpr { _data.RuleStructureFieldData[ruleStructureField].ParseLabel.Goto()          }
    private ContinueParseLabel    (ruleStructureField : RuleStructureField) : PExpr { _data.RuleStructureFieldData[ruleStructureField].ContinueParseLabel.Label() }
    private GotoContinueParseLabel(ruleStructureField : RuleStructureField) : PExpr { _data.RuleStructureFieldData[ruleStructureField].ContinueParseLabel.Goto()  }
    private FailParseLabel        (ruleStructureField : RuleStructureField) : PExpr { _data.RuleStructureFieldData[ruleStructureField].FailParseLabel.Label()     }
    private GotoFailParseLabel    (ruleStructureField : RuleStructureField) : PExpr { _data.RuleStructureFieldData[ruleStructureField].FailParseLabel.Goto()      }
#pragma warning restore
  }
}
