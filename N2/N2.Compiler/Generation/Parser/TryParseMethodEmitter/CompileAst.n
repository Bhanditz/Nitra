using N2.Typing;
using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using N2.Model;

namespace N2.Compiler
{
  internal sealed partial class TryParseMethodEmitter : MethodEmitter
  {
    public CompileAst(ruleStructure : RuleStructure.Ast) : PExpr
    {
      def cases = SCG.List();
      foreach (field when !(field is RuleStructureField.Marker) in ruleStructure.AllFields)
      {
        def comment = field.SrcRule.ToString();
        if (field.IsPredicate)
          cases.Add(<[ case: | $(field.State) => assert(false, $($"Cannot parse from predicate state: '$comment'.")) ]>);
        else
          cases.Add(<[ case: | $(field.State) => _ = $comment; startLevel = $(field.Level); $(GotoParseLabel(field)) ]>);
      }
      cases.Add(<[ case: | x => assert(false, $"Unknown parse state '$x'.") ]>);

      def code = SCG.List();
      code.Add(<[ ignore(stackFrame, curTextPos, parser); ]>);
      code.Add(<[ _ = $(ruleStructure.SrcRule.ToString()) ]>);
      code.Add(<[ $(AstStart(ruleStructure)) = $CurTextPos; ]>);
      code.Add(<[ match (startState) { ..$cases } ]>);

      def (predicateCode, predicateFailLabel) = CompilePredicate(ruleStructure.TailPredicates);

      //parse ok
      code.Add(OkLabel(ruleStructure));
      code.Add(predicateCode);
      code.Add(_endLabel.Goto());

      //parse fail
      code.Add(FailLabel(ruleStructure));
      code.Add(predicateFailLabel);
      code.Add(<[ $CurTextPos = -1; ]>);
      code.Add(_endLabel.Goto());

      CompileFields(code, ruleStructure);

      code.Add(_endLabel.Label());
      code.Add(<[ $CurTextPos; ]>);

      def codeWithVariables = SCG.List();
      codeWithVariables.Add(<[
        mutable newPos     : int; _ = newPos;
      ]>);
      codeWithVariables.AddRange(LocalVariables.Map(name => <[ mutable $(name : dyn) ]>));
      codeWithVariables.AddRange(code);

      codeWithVariables.FlattenPExpr();
    }
  }
}
