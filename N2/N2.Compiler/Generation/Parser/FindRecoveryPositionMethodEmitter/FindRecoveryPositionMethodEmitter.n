using N2.Typing;
using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using N2.Model;

using Nemerle.Compiler;

namespace N2.Compiler
{
  internal partial class FindRecoveryPositionMethodEmitter : MethodEmitter
  {
    public Owner : RuleParserEmitter { get; }
    public RuleStructure : RuleStructure.Ast;
    private _tb : TypeBuilder;
    public override Environment     : EmitterEnvironment { get { Owner.Environment } }
    public override ConstantEmitter : IConstantEmitter   { get { Owner } }
    private EndLabel : PExprLabel;

    public this(owner : RuleParserEmitter, tb : TypeBuilder, ruleStructure : RuleStructure.Ast)
    {
      Owner                       = owner;
      _tb                         = tb;
      RuleStructure               = ruleStructure;
      _data                       = RuleStructureDataContainer(ruleStructure, this, RuleStructureData, RuleStructureFieldData);
      EndLabel                    = Environment.MakeLabel("EndParse");
    }

    public Emit() : void
    {
      CompileFuntion(RuleStructure)
    }

    public CurTextPos : PExpr { get { <[ curTextPos ]> } }

    public MakeRecoveryStackFrame(field : RuleStructureField) : PExpr
    {
      def getCounter()
      {
        match (field.Owner.Owner)
        {
          | List              as field when field.UseCounter => <[ counter ]>
          | ListWithSeparator as field when field.UseCounter => <[ counter ]>
          | List                                             => <[ 0 ]>
          | ListWithSeparator                                => <[ 0 ]>
          | _                                                => assert(false);
        }
      }
      match (field.Owner)
      {
        | RuleStructure.Ast
        | RuleStructure.Option                    => <[ parser.RecoveryStackFrame_Rule(stack, $CurTextPos, astHandle, $(field.State)); ]>
        | RuleStructure.List
        | RuleStructure.ListWithSeparatorRule     => <[ parser.RecoveryStackFrame_ListBody(stack, $CurTextPos, astHandle, $(field.State), $(getCounter())); ]>
        | RuleStructure.ListWithSeparatorSeparator=> <[ parser.RecoveryStackFrame_ListSeparator(stack, $CurTextPos, astHandle, $(field.State), $(getCounter())); ]>
        | RuleStructure.Predicate                 => assert(false);
      }
    }
  }
}
