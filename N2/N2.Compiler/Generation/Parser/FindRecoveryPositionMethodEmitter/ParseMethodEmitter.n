using N2.Typing;
using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using N2.Model;

using Nemerle.Compiler;

namespace N2.Compiler
{
  internal partial class FindRecoveryPositionMethodEmitter : MethodEmitter
  {
    public Owner : RuleParserEmitter { get; }
    public RuleStructure : RuleStructure.Ast;
    private _tb : TypeBuilder;
    public override Environment : EmitterEnvironment { get { Owner.Environment } }
    private EndLabel : PExprLabel;

    public this(owner : RuleParserEmitter, tb : TypeBuilder, ruleStructure : RuleStructure.Ast)
    {
      Owner                       = owner;
      _tb                         = tb;
      RuleStructure               = ruleStructure;
      _data                       = RuleStructureDataContainer(ruleStructure, this, RuleStructureData, RuleStructureFieldData);
      EndLabel                    = Environment.MakeLabel("EndParse");
    }

    public Emit() : void
    {
      if (RuleStructure.Symbol is ExtentionRuleSymbol)
      {
        _tb.DefineConditional(<[decl:
          public override FindRecoveryPosition(mutable curTextPos : int, text : string, astPtr : int, parser : N2.Internal.Parser) : int
          {
            $(if (Owner.Environment.IsIdeMode)
              <[ _ = curTextPos; _ = text; _ = astPtr; _ = parser; -1 ]>
            else
              CompileAst(RuleStructure))
          }
        ]>);
      }
      else
      {
        _tb.DefineConditional(<[decl:
          public override FindRecoveryPosition(mutable curTextPos : int, text : string, parser : N2.Internal.Parser) : int
          {
            $(if (Owner.Environment.IsIdeMode)
              <[ _ = curTextPos; _ = text; _ = parser; -1 ]>
            else
              CompileAst(RuleStructure))
          }
        ]>);
      }
    }

    public CurTextPos : PExpr { get { <[ curTextPos ]> } }

    public RecoveryStackFrameScope(field : RuleStructureField, code : PExpr) : PExpr
    {
      def frames = SCG.List();
      def collectFrames(field : RuleStructureField)
      {
        when (field !=  null)
        {
          def (counter, listStartPos, listEndPos, frameInfo) =
            match (field)
            {
              | List              as field when field.UseCounter => def data = ListStateData(this, field); (data.Counter, data.ListStartPos, data.ListEndPos, data.FrameInfo)
              | ListWithSeparator as field when field.UseCounter => def data = ListStateData(this, field); (data.Counter, data.ListStartPos, data.ListEndPos, data.FrameInfo)
              | List              as field                       => def data = ListStateData(this, field); (<[ 0 ]>,      data.ListStartPos, data.ListEndPos, data.FrameInfo)
              | ListWithSeparator as field                       => def data = ListStateData(this, field); (<[ 0 ]>,      data.ListStartPos, data.ListEndPos, data.FrameInfo)
              | _                                                => (<[ 0 ]>, <[ 0 ]>, <[ 0 ]>, <[ N2.Internal.FrameInfo.None ]>)
            };

          def isRootAst = field.Owner is RuleStructure.Ast;
          frames.Add(<[ parser.RecoveryStack.Push(N2.Internal.RecoveryStackFrame(this, $(AstPtr(field.Owner).Var), $(AstStart(field.Owner)), $(field.State), $counter, $listStartPos, $listEndPos, $isRootAst, $frameInfo)); ]>);
          collectFrames(field.Owner.Owner)
        }
      }
      collectFrames(field);
      frames.Reverse();
      <[
        ..$frames;
        $code;
        if (parser.ParsingMode == N2.Internal.ParsingMode.EndRecovery)
          $(EndLabel.Goto());
        else
        {
          ..$(frames.Map(_ => <[ _ = parser.RecoveryStack.Pop() ]> ));
        }
      ]>
    }

    public RecoveryCode(field : RuleStructureField) : PExpr
    {
      <[
        when (parser.MaxFailPos == curTextPos)
          $(RecoveryStackFrameScope(field, <[ parser.StartRecovery(curTextPos) ]>))
      ]>
    }
  }
}
