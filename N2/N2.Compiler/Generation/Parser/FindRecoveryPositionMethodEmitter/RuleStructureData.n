using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using N2.Model;
using N2.Typing;

using System;
using System.Collections.Generic;
using System.Linq;

namespace N2.Compiler
{
  internal partial class FindRecoveryPositionMethodEmitter
  {
    [Record]
    private sealed class RuleStructureData : RuleStructureDataBase[FindRecoveryPositionMethodEmitter]
    {
      [RecordIgnore] public AstPtr : AstPointer { get; private set; }
      public FunctionName : string
      {
        get
        {
          match (Structure)
          {
            | Ast                        => assert2(false); assert(false);
            | Predicate                  => assert2(false); assert(false);
            | ListWithSeparatorSeparator => $"FindRecoveryPosition$(Structure.Owner.State)_Separator"
            | ListWithSeparatorRule      => $"FindRecoveryPosition$(Structure.Owner.State)_Item"
            | List                       => $"FindRecoveryPosition$(Structure.Owner.State)_Item"
            | Option                     => $"FindRecoveryPosition$(Structure.Owner.State)_Item"
          }
        }
      }

      public override Init() : void
      {
        AstPtr = AstPointer.New(Structure, this.Emitter.ConstantEmitter, this.Emitter, "astPtr");
      }
    }

    [Record]
    private sealed class RuleStructureFieldData : RuleStructureFieldDataBase[FindRecoveryPositionMethodEmitter]
    {
      [RecordIgnore] public SelectStartStateLabel : PExprLabel { get; private set; }
      public override Init() : void
      {
        SelectStartStateLabel = Emitter.Environment.MakeLabel($"SelectStartStateLabel_$LabelSuffix");
      }
    }

    _data : RuleStructureDataContainer[FindRecoveryPositionMethodEmitter, RuleStructureData, RuleStructureFieldData];
#pragma warning disable 10003
    private FunctionName (structure : RuleStructure) : string     { _data.RuleStructureData[structure].FunctionName      }
    private AstPtr       (structure : RuleStructure) : AstPointer { _data.RuleStructureData[structure].AstPtr            }

    private SelectStartStateLabel    (ruleStructureField : RuleStructureField) : PExpr { _data.RuleStructureFieldData[ruleStructureField].SelectStartStateLabel.Label()         }
    private GotoSelectStartStateLabel(ruleStructureField : RuleStructureField) : PExpr { _data.RuleStructureFieldData[ruleStructureField].SelectStartStateLabel.Goto()          }
#pragma warning restore
  }
}
