using N2.Typing;
using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using N2.Model;

using N = Nemerle.Compiler;

namespace N2.Compiler
{
  internal partial class FindRecoveryPositionMethodEmitter : MethodEmitter
  {
    public MakeTokenRuleCall(ruleSymbol : RegularRuleSymbol, curTextPos : PExpr, text : PExpr) : PExpr
    {
      def grammarType = PExprUtils.FromQualifiedIdentifier(ruleSymbol.DeclaringModule.Node.FullNameParts);
      <[ $grammarType.$(AssemblyConstants.GrammarImplName : usesite).$(ruleSymbol.DecorateRuleName() : dyn)($curTextPos, $text) ]>
    }

    public MakeSimpleRuleCall(ruleSymbol : SimpleRuleSymbol, curTextPos : PExpr, text : PExpr) : PExpr
    {
      <[ $(Owner.MakeParserName(ruleSymbol) : dyn).FindRecoveryPosition($curTextPos, $text, ref parser) ]>
    }

    public MakeExtensibleRuleCall(ruleSymbol : ExtensibleRuleSymbol, bindingPower : int, curTextPos : PExpr, text : PExpr) : PExpr
    {
      <[ $(Owner.MakeParserName(ruleSymbol, bindingPower) : dyn).FindRecoveryPosition($curTextPos, $text, ref parser) ]>
    }

    public override CompileTokenCall(code : SCG.List[PExpr], field : RuleStructureField.TokenCall) : void
    {
      CompileCallImpl(code, MakeTokenRuleCall(field.Symbol, CurTextPos, <[ text ]>), field)
    }

    public override CompileExtensibleCall(code : SCG.List[PExpr], field : RuleStructureField.ExtensibleCall) : void
    {
      CompileCallImpl(code, MakeExtensibleRuleCall(field.Symbol, field.BindingPower, CurTextPos, <[ text ]>), field)
    }

    public override CompileSimpleCall(code : SCG.List[PExpr], field : RuleStructureField.SimpleCall) : void
    {
      CompileCallImpl(code, MakeSimpleRuleCall(field.Symbol, CurTextPos, <[ text ]>), field)
    }

    public CompileCallImpl(code : SCG.List[PExpr], call : PExpr, field : RuleStructureField) : void
    {
      code.Add(<[
      $(ParseLabel(field));
        _ = $(field.SrcRule.ToString());
        $(RecoveryStackFrameScope(field, <[ newPos = $call ]>));
        if (newPos >= 0)
        {
          $CurTextPos = newPos;
          $(GotoContinueParseLabel(field));
        }
        else
        {
          $(RecoveryCode(field));
          $(GotoFailParseLabel(field));
        }
      $(RecoveryLabel(field));
        $(GotoParseLabel(field));
      ]>)
    }
  }
}
