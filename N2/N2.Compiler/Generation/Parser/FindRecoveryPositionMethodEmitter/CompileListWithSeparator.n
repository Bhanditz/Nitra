using Nemerle;
using Nemerle.Imperative;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using N2.Model;

namespace N2.Compiler
{
  internal partial class FindRecoveryPositionMethodEmitter : MethodEmitter
  {
    public override CompileListWithSeparator(code : SCG.List[PExpr], field : RuleStructureField.ListWithSeparator) : void
    {
      CompileFuntion(field.Rule);
      CompileFuntion(field.Separator);
      def localVariableEmitter = LocalVariableEmitter();
      def stateData = ListStateData(localVariableEmitter, field);
      def itemPtr = AstPointer.New(field.Rule, ConstantEmitter, this, "itemPtr");
      def separatorPtr = AstPointer.New(field.Separator, ConstantEmitter, this, "separatorPtr");
      def body = <[
        $(stateData.Init());
        $(stateData.ListStartPos) = $CurTextPos;
        mutable lastItemStart = -1;
        mutable itemEndPos = $CurTextPos;
        mutable itemNotEmpty = true;
        mutable separatorNotEmpty = true;
        while (itemNotEmpty || separatorNotEmpty)
        {
          def $(itemPtr.Var) = parseResult.TryGetAst(itemEndPos, $(field.Rule.AstId : global));
          if ($(itemPtr.Var) > 0 && $(itemPtr.Get(itemPtr.Structure.HeaderState)) == N2.Internal.ParseResult.AstParsedState)
          {
            $(stateData.Inc());
            lastItemStart = itemEndPos;
            itemEndPos += parseResult.AstSize($(itemPtr.Var), $(field.Rule.SizesStart), $(field.Rule.SizesEnd));
            itemNotEmpty = lastItemStart < itemEndPos;
          }
          else
          {
            when (lastItemStart >= 0 && lastItemStart < itemEndPos)
            {
              $(stateData.ListEndPos) = lastItemStart;
              def stack = $(MakeRecoveryStackFrame(field, <[ lastItemStart ]>));
              when (stack != null)
                $(FunctionName(field.Separator) : global)(lastItemStart, ..$(stateData.TryGetCounter()), stack, parseResult);
            }
            $(stateData.Inc());
            $(stateData.ListEndPos) = itemEndPos;
            def stack = $(MakeRecoveryStackFrame(field, <[ itemEndPos ]>));
            when (stack != null)
              $(FunctionName(field.Rule) : global)(itemEndPos, ..$(stateData.TryGetCounter()), stack, parseResult);
            break;
          }
          def $(separatorPtr.Var) = parseResult.TryGetAst(itemEndPos, $(field.Separator.AstId : global));
          if ($(separatorPtr.Var) > 0 && $(separatorPtr.Get(separatorPtr.Structure.HeaderState)) == N2.Internal.ParseResult.AstParsedState)
          {
            lastItemStart = itemEndPos;
            itemEndPos += parseResult.AstSize($(separatorPtr.Var), $(field.Separator.SizesStart), $(field.Separator.SizesEnd));
            separatorNotEmpty = lastItemStart < itemEndPos;
          }
          else
          {
            when (lastItemStart >= 0 && lastItemStart < itemEndPos)
            {
              $(stateData.ListEndPos) = lastItemStart;
              def stack = $(MakeRecoveryStackFrame(field, <[ lastItemStart ]>));
              when (stack != null)
                $(FunctionName(field.Rule) : global)(lastItemStart, ..$(stateData.TryGetCounter()), stack, parseResult);
            }
            $(stateData.ListEndPos) = itemEndPos;
            def stack = $(MakeRecoveryStackFrame(field, <[ itemEndPos ]>));
            when (stack != null)
              $(FunctionName(field.Separator) : global)(itemEndPos, ..$(stateData.TryGetCounter()), stack, parseResult);
            break;
          }
        }
      ]>;
      code.Add(CheckState(field, <[
        ..$(localVariableEmitter.Define());
        $body;
      ]>))
    }
  }
}
