using Nemerle;
using Nemerle.Imperative;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using N2.Model;

namespace N2.Compiler
{
  internal partial class FindRecoveryPositionMethodEmitter : MethodEmitter
  {
    public override CompileListWithSeparator(code : SCG.List[PExpr], field : RuleStructureField.ListWithSeparator) : void
    {
      CompileFuntion(field.Rule);
      CompileFuntion(field.Separator);
      def localVariableEmitter = LocalVariableEmitter();
      def stateData = ListStateData(localVariableEmitter, field);
      def itemPtr = AstPointer.New(field.Rule, ConstantEmitter, this, "itemPtr");
      def separatorPtr = AstPointer.New(field.Separator, ConstantEmitter, this, "separatorPtr");
      def body = <[
        $(stateData.Init());
        $(stateData.ListStartPos) = $CurTextPos;
        mutable lastItemStart = -1;
        mutable itemEndPos = $CurTextPos;
        mutable itemNotEmpty = true;
        mutable separatorNotEmpty = true;
        while (itemNotEmpty || separatorNotEmpty)
        {
          def $(itemPtr.Var) = parser.TryGetAst(itemEndPos, $(field.Rule.AstId : global));
          if ($(itemPtr.Var) > 0 && $(itemPtr.Get(itemPtr.Structure.HeaderState)) == N2.Internal.Parser.AstParsedState)
          {
            $(stateData.Inc());
            lastItemStart = itemEndPos;
            itemEndPos += parser.AstSize($(itemPtr.Var), $(field.Rule.SizesStart), $(field.Rule.SizesEnd));
            itemNotEmpty = lastItemStart < itemEndPos;
          }
          else
          {
            when (lastItemStart >= 0 && lastItemStart < itemEndPos)
            {
              $(stateData.ListEndPos) = lastItemStart;
              $(FunctionName(field.Separator) : global)(lastItemStart, ..$(stateData.TryGetCounter()), $(MakeRecoveryStackFrame(field)), parser);
            }
            $(stateData.Inc());
            $(stateData.ListEndPos) = itemEndPos;
            $(FunctionName(field.Rule) : global)(itemEndPos, ..$(stateData.TryGetCounter()), $(MakeRecoveryStackFrame(field)), parser);
            break;
          }
          def $(separatorPtr.Var) = parser.TryGetAst(itemEndPos, $(field.Separator.AstId : global));
          if ($(separatorPtr.Var) > 0 && $(separatorPtr.Get(separatorPtr.Structure.HeaderState)) == N2.Internal.Parser.AstParsedState)
          {
            lastItemStart = itemEndPos;
            itemEndPos += parser.AstSize($(separatorPtr.Var), $(field.Separator.SizesStart), $(field.Separator.SizesEnd));
            separatorNotEmpty = lastItemStart < itemEndPos;
          }
          else
          {
            when (lastItemStart >= 0 && lastItemStart < itemEndPos)
            {
              $(stateData.ListEndPos) = lastItemStart;
              $(FunctionName(field.Rule) : global)(lastItemStart, ..$(stateData.TryGetCounter()), $(MakeRecoveryStackFrame(field)), parser);
            }
            $(stateData.ListEndPos) = itemEndPos;
            $(FunctionName(field.Separator) : global)(itemEndPos, ..$(stateData.TryGetCounter()), $(MakeRecoveryStackFrame(field)), parser);
            break;
          }
        }
      ]>;
      code.Add(CheckState(field, <[
        ..$(localVariableEmitter.Define());
        $body;
      ]>))
    }
  }
}
