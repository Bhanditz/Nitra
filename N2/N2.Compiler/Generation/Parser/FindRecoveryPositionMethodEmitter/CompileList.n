using Nemerle;
using Nemerle.Imperative;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using N2.Model;

namespace N2.Compiler
{
  internal partial class FindRecoveryPositionMethodEmitter : MethodEmitter
  {
    public override CompileList(code : SCG.List[PExpr], field : RuleStructureField.List) : void
    {
      def counter = ListItemCounter(this, field);
      def ifCounterEqualsToMaxThenContinue()
      {
        if (field.MaxCount is Some(max))
          <[
            when ($(counter.Ref()) == $max)
              $(GotoContinueParseLabel(field));
          ]>
        else
          <[ {} ]>
      }
      def ifCounterLessThanMinThenFail()
      {
        if (field.MinCount > 0)
          <[
            when ($(counter.Ref()) < $(field.MinCount))
              $(GotoFailParseLabel(field));
          ]>
        else
          <[ {} ]>
      }

      def firstChild = field.Rule.NotMarkerFields[0];
      code.Add(<[
        $(ParseLabel(field));
          _ = $(field.SrcRule.ToString());
          $(counter.Init());
          $(GotoParseLabel(firstChild));

        $(OkLabel(field.Rule));
          $(counter.Inc());
          $(ifCounterEqualsToMaxThenContinue());
          $(GotoParseLabel(firstChild));

        $(FailLabel(field.Rule));
          $(ifCounterLessThanMinThenFail());
          $(GotoContinueParseLabel(field));

        $(RecoveryLabel(field));
          $(counter.Init());
          when (true)
          {
            def listEndPos = $CurTextPos + $(AstPtr(field.Owner).GetSize(field));
            when ($CurTextPos == listEndPos)
              $(GotoParseLabel(firstChild));
            mutable itemEndPos = $CurTextPos;
            while (itemEndPos != listEndPos)
            {
              $(counter.Inc());
              $CurTextPos = itemEndPos;
              $(AstPtr(field.Rule).Var) = parser.GetAst($CurTextPos, $(field.Rule.AstId : dyn));
              itemEndPos += parser.AstSize($(AstPtr(field.Rule).Var), $(field.Rule.SizesStart), $(field.Rule.SizesEnd));
            }
            $(counter.Dec());
            $(GotoParseLabel(firstChild));
          }
      ]>);
    }
  }
}
