using N2.Typing;
using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using N2.Model;

using Nemerle.Compiler;

namespace N2.Compiler
{
  internal partial class ParseMethodEmitter : MethodEmitter
  {
    public Owner : RuleParserEmitter { get; }
    public RuleStructure : RuleStructure.Ast;
    private _tb : TypeBuilder;
    public override Environment : EmitterEnvironment { get { Owner.Environment } }

    public this(owner : RuleParserEmitter, tb : TypeBuilder, ruleStructure : RuleStructure.Ast)
    {
      Owner         = owner;
      _tb           = tb;
      RuleStructure = ruleStructure;
      InitRuleStructure(ruleStructure);
    }

    public Emit() : void
    {
      if (RuleStructure.Symbol is ExtentionRuleSymbol)
      {
        _tb.DefineConditional(<[decl:
          public override Parse(mutable curTextPos : int, text : string, resultRef : ref int, parser : ref N2.Internal.Parser) : int
          {
            $(if (Owner.Environment.IsIdeMode)
              <[ _ = curTextPos; _ = text; _ = resultRef; _ = parser; -1 ]>
            else
              CompileAst(RuleStructure))
          }
        ]>);
      }
      else
      {
        _tb.DefineConditional(<[decl:
          public override Parse(mutable curTextPos : int, text : string, parser : ref N2.Internal.Parser) : int
          {
            $(if (Owner.Environment.IsIdeMode)
              <[ _ = curTextPos; _ = text; _ = parser; -1 ]>
            else
              CompileAst(RuleStructure))
          }
        ]>);
      }
    }

    public CurTextPos : PExpr { get { <[ curTextPos ]> } }
    public CompileFields(code : SCG.List[PExpr], fields : array[RuleStructureField]) : void
    {
      foreach (field in fields)
      {
        | TokenString       as field => CompileTokenString      (code, field)
        | Option            as field => CompileOption           (code, field)
        | List              as field => CompileList             (code, field)
        | ListWithSeparator as field => CompileListWithSeparator(code, field)
        | Predicate         as field => CompilePredicate        (code, field)
        | TokenCall         as field => CompileTokenCall        (code, field)
        | ExtensibleCall    as field => CompileExtensibleCall   (code, field)
        | SimpleCall        as field => CompileSimpleCall       (code, field)
      }
    }

    public AllocateParentAst(field : RuleStructureField) : PExpr
    {
      def structure = field.Owner;
      if (field.AllocateAst)
      {
        match (structure)
        {
          | RuleStructure.Predicate =>
            <[
              unless (parser.IsRecoveryMode)
                $(structure.ParserExtention().AstPtr.Var) = parser.Allocate($(structure.Size), $(structure.AstId : dyn));
            ]>

          | RuleStructure.Ast(Symbol = N2.Typing.ExtentionRuleSymbol) =>
            <[
              unless (parser.IsRecoveryMode)
              {
                $(structure.ParserExtention().AstPtr.Var) = parser.Allocate($(structure.Size), $(structure.AstId : dyn));
                parser.ast[$(structure.ParserExtention().AstPtr.Var) + $(structure.TerminatorOffset)] = -1;
              }
            ]>

          | _ =>
            <[
              unless (parser.IsRecoveryMode)
              {
                $(structure.ParserExtention().AstPtr.Var) = parser.Allocate($(structure.Size), $(structure.AstId : dyn));
                $(structure.ParserExtention().AstPtr.Set(structure.HeaderNext, <[ parser.memoize[$(structure.ParserExtention().AstStart)] ]>));
                parser.memoize[$(structure.ParserExtention().AstStart)] = $(structure.ParserExtention().AstPtr.Var);
              }
            ]>
        }
      }
      else
        <[ {} ]>
    }

    public RecoveryCode() : PExpr
    {
      <[
        when (parser.MaxTextPos <= curTextPos)
        {
          if (parser.MaxTextPos == curTextPos && parser.IsRecoveryMode)
          {
            assert2(false);
          }
          else
          {
            parser.MaxTextPos = curTextPos;
          }
        }
      ]>
    }
  }
}
