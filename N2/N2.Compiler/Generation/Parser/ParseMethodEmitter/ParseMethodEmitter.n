using N2.Typing;
using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using N2.Model;

using Nemerle.Compiler;

namespace N2.Compiler
{
  internal partial class ParseMethodEmitter : MethodEmitter
  {
    public Owner : RuleParserEmitter { get; }
    public RuleStructure : RuleStructure.Ast;
    private _tb : TypeBuilder;
    public override Environment     : EmitterEnvironment { get { Owner.Environment } }
    public override ConstantEmitter : IConstantEmitter   { get { Owner } }

    public this(owner : RuleParserEmitter, tb : TypeBuilder, ruleStructure : RuleStructure.Ast)
    {
      Owner         = owner;
      _tb           = tb;
      RuleStructure = ruleStructure;
      _data         = RuleStructureDataContainer(ruleStructure, this, RuleStructureData, RuleStructureFieldData);
    }

    public Emit() : void
    {
      CompileFuntion(RuleStructure);
    }

    public CurTextPos : PExpr { get { <[ curTextPos ]> } }

    public MakeAstForSingleCallExtension(field : RuleStructureField) : PExpr
    {
      match (field, field.Owner)
      {
        | (RuleStructureField.SimpleCall as field, RuleStructure.Ast(Symbol = N2.Typing.ExtensionRuleSymbol) as structure) when field.AllocateAst && structure.Fields.Length == 1 =>
          <[
            when ($(AstPtr(structure).Var) == 0 && parser.TryGetAst($(AstStart(structure)), $(Owner.MakeParserName(field.Symbol) : global).RuleId) != 0)
              $(AstPtr(structure).Var) = parser.Allocate($(structure.Size), $(structure.AstId : global));
          ]>

        | (RuleStructureField.ExtensibleCall as field, RuleStructure.Ast(Symbol = N2.Typing.ExtensionRuleSymbol) as structure) when field.AllocateAst && structure.Fields.Length == 1 =>
          <[
            when ($(AstPtr(structure).Var) == 0 && parser.TryGetAst($(AstStart(structure)), $(Owner.MakeParserName(field.Symbol, field.BindingPower) : global).PrefixId) != 0)
              $(AstPtr(structure).Var) = parser.Allocate($(structure.Size), $(structure.AstId : global));
          ]>

        | _ => <[ {} ]>
      }
    }

    public AllocateParentAst(field : RuleStructureField) : PExpr
    {
      def structure = field.Owner;
      if (field.AllocateAst)
      {
        match (structure)
        {
          | RuleStructure.Predicate
          | RuleStructure.Ast(Symbol = N2.Typing.ExtensionRuleSymbol) =>
            <[
              when ($(AstPtr(structure).Var) == 0)
                $(AstPtr(structure).Var) = parser.Allocate($(structure.Size), $(structure.AstId : global));
            ]>

          | _ =>
            <[
              when ($(AstPtr(structure).Var) == 0)
              {
                $(AstPtr(structure).Var) = parser.Allocate($(structure.Size), $(structure.AstId : global));
                $(AstPtr(structure).Set(structure.HeaderNext, <[ parser.memoize[$(AstStart(structure))] ]>));
                parser.memoize[$(AstStart(structure))] = $(AstPtr(structure).Var);
              }
            ]>
        }
      }
      else
        <[ {} ]>
    }

    public RecoveryCode() : PExpr
    {
      <[
        when (parser.MaxFailPos < curTextPos)
          parser.MaxFailPos = curTextPos;
      ]>
    }
  }
}
