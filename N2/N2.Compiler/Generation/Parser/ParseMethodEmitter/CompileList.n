using Nemerle;
using Nemerle.Imperative;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using N2.Model;

namespace N2.Compiler
{
  internal partial class ParseMethodEmitter : MethodEmitter
  {
    public override CompileList(code : SCG.List[PExpr], field : RuleStructureField.List) : void
    {
      def counter = ListItemCounter(this, field);
      def listStartPos() { <[ $(CreateVariable($"listStartPos$(field.Level)") : dyn) ]> }
      def ifCounterEqualsToMaxThenContinue()
      {
        if (field.MaxCount is Some(max))
          <[
            when ($(counter.Ref()) == $max)
              $(Data(field).ContinueParseLabel.Goto());
          ]>
        else
          <[ {} ]>
      }
      def ifCounterLessThanMinThenFail()
      {
        if (field.MinCount > 0)
          <[
            when ($(counter.Ref()) < $(field.MinCount))
              $(Data(field).FailParseLabel.Goto());
          ]>
        else
          <[ {} ]>
      }

      def firstChild = field.Rule.Fields[0];
      code.Add(<[
        $(Data(field).ParseLabel.Label());
          _ = $(field.SrcRule.ToString());
          $(AllocateParentAst(field));
          $(counter.Init());
          $(listStartPos()) = $CurTextPos;
          $(Data(field.Rule).AstStart) = $CurTextPos;
          $(Data(firstChild).ParseLabel.Goto());

        $(Data(field.Rule).OkLabel.Label());
          $(counter.Inc());
          $(Data(field.Owner).AstPtr.Set(field, <[ $CurTextPos - $(listStartPos()) ]>));
          $(ifCounterEqualsToMaxThenContinue());
          $(Data(field.Rule).AstStart) = $CurTextPos;
          $(Data(firstChild).ParseLabel.Goto());

        $(Data(field.Rule).FailLabel.Label());
          $(ifCounterLessThanMinThenFail());
          $CurTextPos = $(Data(field.Rule).AstStart);
          $(Data(field.Owner).AstPtr.Set(field, <[ $CurTextPos - $(listStartPos()) ]>));
          $(Data(field).ContinueParseLabel.Goto());

        $(Data(field).RecoveryLabel.Label());
          $(counter.Init());
          $(listStartPos()) = $CurTextPos;
          $(Data(field.Rule).AstStart) = $CurTextPos;
          when (true)
          {
            def listEndPos = $CurTextPos + $(Data(field.Owner).AstPtr.GetSize(field));
            when ($CurTextPos == listEndPos)
              $(Data(firstChild).ParseLabel.Goto());
            mutable itemEndPos = $CurTextPos;
            while (itemEndPos != listEndPos)
            {
              $(counter.Inc());
              $CurTextPos = itemEndPos;
              $(Data(field.Rule).AstPtr.Var) = parser.GetAst($CurTextPos, $(field.Rule.AstId : dyn));
              itemEndPos += parser.AstSize($(Data(field.Rule).AstPtr.Var), $(field.Rule.SizesStart), $(field.Rule.SizesEnd));
            }
            $(counter.Dec());
            _ = "Мы перед последним элеменом цикла. Начинаем парсить хвост...";
            $(Data(firstChild).ParseLabel.Goto());
          }
      ]>);
    }
  }
}
