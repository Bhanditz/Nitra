using N2.Model;
using N2.Typing;

using Nemerle;
using Nemerle.Imperative;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;


namespace N2.Compiler
{
  internal partial class ParseMethodEmitter : MethodEmitter
  {
    public CompileSkipAny(code : SCG.List[PExpr], field : RuleStructureField.List) : void
    {
      assert3(field.Rule.Fields.Length == 1);
      
      def headPredicateCode = CompilePredicate(field.HeadPredicates, GotoFailParseLabel(field));
      def firstChild        = field.Rule.NonMarkerFields[0];
      def fail              = PExprLabel(this.Environment.Manager, "childFail");
      def childPredicateCode = CompilePredicate(firstChild.HeadPredicates, fail.Goto());
      def stateData = ListStateData(this, field);
      
      code.Add(<[
        $(ParseLabel(field));
          _ = $(field.ToString());
          $(CheckOwnerMemoize(field));
          $headPredicateCode;
          $(AllocateParentAst(field));
        $(RecoveryLabel(field));
          $(stateData.ListStartPos) = $CurTextPos;
          while ($(CurTextPos) < text.Length)
          {
            $childPredicateCode;
            ++ $(CurTextPos);
          }
          $(fail.Label());
          $(AstPtr(field.Owner).Set(field, <[ $CurTextPos - $(stateData.ListStartPos) ]>));
          $(GotoContinueParseLabel(field));
      ]>);
    }

    public override CompileList(code : SCG.List[PExpr], field : RuleStructureField.List) : void
    {
      when (field.IsSkipAnyPattern)
      {
        CompileSkipAny(code, field);
        return;
      }

      def stateData = ListStateData(this, field);
      def ifCounterEqualsToMaxThenContinue()
      {
        if (field.MaxCount is Some(max))
          <[
            when ($(stateData.Counter) == $max)
              $(GotoContinueParseLabel(field));
          ]>
        else
          <[ {} ]>
      }
      def ifCounterLessThanMinThenFail()
      {
        if (field.MinCount > 0)
          <[
            when ($(stateData.Counter) < $(field.MinCount))
            {
              parseState = $(field.State);
              $(GotoFailParseLabel(field));
            }
          ]>
        else
          <[ {} ]>
      }

      def headPredicateCode = CompilePredicate(field.HeadPredicates, GotoFailParseLabel(field));

      def firstChild = field.Rule.NonMarkerFields[0];
      code.Add(<[
        $(ParseLabel(field));
          _ = $(field.ToString());
          $(CheckOwnerMemoize(field));
          $headPredicateCode;
          $(AllocateParentAst(field));
        $(RecoveryLabel(field));
          $(stateData.Init());
          $(stateData.ListStartPos) = $CurTextPos;
          $(stateData.ListEndPos) = $CurTextPos;
          $(AstStart(field.Rule)) = $CurTextPos;
          $(GotoParseLabel(firstChild));

        $(OkLabel(field.Rule));
          when ($(stateData.ListEndPos) == $CurTextPos)
            $(GotoFailLabel(field.Rule));
          $(stateData.ListEndPos) = $CurTextPos;
          $(stateData.Inc());
          $(AstPtr(field.Rule).Set(field.Rule.HeaderState, <[ N2.Internal.Parser.AstParsedState ]>));// правило полностью разобрано.
          $(AstPtr(field.Owner).Set(field, <[ $CurTextPos - $(stateData.ListStartPos) ]>));
          $(ifCounterEqualsToMaxThenContinue());
          $(AstStart(field.Rule)) = $CurTextPos;
          $(GotoParseLabel(firstChild));

        $(FailLabel(field.Rule));
          when ($(AstPtr(field.Rule).Var) > 0) // АСТ создано
            $(AstPtr(field.Rule).Set(field.Rule.HeaderState, <[ parseState ]>));
          $(ifCounterLessThanMinThenFail());
          $CurTextPos = $(AstStart(field.Rule));
          $(AstPtr(field.Owner).Set(field, <[ $CurTextPos - $(stateData.ListStartPos) ]>));
          $(GotoContinueParseLabel(field));
      ]>);
    }
  }
}
