using Nemerle;
using Nemerle.Imperative;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using N2.Model;

namespace N2.Compiler
{
  internal partial class ParseMethodEmitter : MethodEmitter
  {
    public override CompileListWithSeparator(code : SCG.List[PExpr], field : RuleStructureField.ListWithSeparator) : void
    {
      def stateData = ListStateData(this, field);
      def ifCounterEqualsToMaxThenContinue()
      {
        if (field.MaxCount is Some(max))
          <[
            when ($(stateData.Counter) == $max)
              $(GotoContinueParseLabel(field));
          ]>
        else
          <[ {} ]>
      }
      def ifCounterLessThanMinThenFail()
      {
        if (field.MinCount > 0)
          <[
            when ($(stateData.Counter) < $(field.MinCount))
            {
              parseState = $(field.State);
              $(GotoFailParseLabel(field));
            }
          ]>
        else
          <[ {} ]>
      }
      def hangingSeparatorCode = if (field.HangingSeparator)
        <[
          $(stateData.ListEndPos) = $CurTextPos;
          $(AstPtr(field.Owner).Set(field, <[ $(stateData.ListEndPos) - $(stateData.ListStartPos) ]>));
        ]>
      else
        <[ {} ]>;

      def headPredicateCode = CompilePredicate(field.HeadPredicates, GotoFailParseLabel(field));

      def firstRuleChild = field.Rule.NonMarkerFields[0];
      def firstSeparatorChild = field.Separator.NonMarkerFields[0];
      code.Add(<[
        $(ParseLabel(field));
          _ = $(field.ToString());
          $(CheckOwnerMemoize(field));
          $headPredicateCode;
          $(AllocateParentAst(field));
        $(RecoveryLabel(field));
          $(stateData.Init());
          $(stateData.ListStartPos) = $CurTextPos;
          $(stateData.ListEndPos) = $CurTextPos;
          $(AstStart(field.Rule)) = $CurTextPos;
          $(GotoParseLabel(firstRuleChild));

        $(OkLabel(field.Rule));
          $(stateData.Inc());
          $(stateData.ListEndPos) = $CurTextPos;
          $(AstPtr(field.Rule).Set(field.Rule.HeaderState, <[ N2.Internal.Parser.AstParsedState ]>));// правило полностью разобрано.
          $(AstPtr(field.Owner).Set(field, <[ $(stateData.ListEndPos) - $(stateData.ListStartPos) ]>));
          $(ifCounterEqualsToMaxThenContinue());
          $(AstStart(field.Separator)) = $CurTextPos;
          $(GotoParseLabel(firstSeparatorChild));

        $(FailLabel(field.Rule));
          when ($(AstPtr(field.Rule).Var) > 0) // АСТ создано
            $(AstPtr(field.Rule).Set(field.Rule.HeaderState, <[ parseState ]>));
          $(ifCounterLessThanMinThenFail());
          $CurTextPos = $(stateData.ListEndPos);
          $(AstPtr(field.Owner).Set(field, <[ $(stateData.ListEndPos) - $(stateData.ListStartPos) ]>));
          $(GotoContinueParseLabel(field));

        $(OkLabel(field.Separator));
          $(AstPtr(field.Separator).Set(field.Separator.HeaderState, <[ N2.Internal.Parser.AstParsedState ]>));// правило полностью разобрано.
          $hangingSeparatorCode;
          $(AstStart(field.Rule)) = $CurTextPos;
          $(GotoParseLabel(firstRuleChild));

        $(FailLabel(field.Separator));
          when ($(AstPtr(field.Separator).Var) > 0) // АСТ создано
            $(AstPtr(field.Separator).Set(field.Separator.HeaderState, <[ parseState ]>));
          $(ifCounterLessThanMinThenFail());
          $CurTextPos = $(stateData.ListEndPos);
          $(AstPtr(field.Owner).Set(field, <[ $(stateData.ListEndPos) - $(stateData.ListStartPos) ]>));
          $(GotoContinueParseLabel(field));
      ]>);
    }
  }
}
