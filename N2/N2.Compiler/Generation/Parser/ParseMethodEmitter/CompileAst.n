using N2.Typing;
using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using N2.Model;

namespace N2.Compiler
{
  internal partial class ParseMethodEmitter : MethodEmitter
  {
    public CompileAst(ruleStructure : RuleStructure.Ast) : PExpr
    {
      def code = SCG.List();
      def endLabel = Environment.MakeLabel("EndParse");
      def recoveryLabel = Environment.MakeLabel("Recovery");
      def okStateCode = int.MaxValue;
      def okStateCode = ~okStateCode;//nemerle bug
      def checkAstMemoize = if (ruleStructure.Symbol is SimpleRuleSymbol)
        <[
          when ($CurTextPos <= text.Length)
          {
            for (mutable ast = parser.memoize[$CurTextPos]; ast > 0; ast = parser.ast[ast + 1])
            {
              when (parser.ast[ast] == RuleId)
              {
                parseState = parser.ast[ast + 2];
                if (parseState == $okStateCode)
                {
                  mutable i = ast + $(ruleStructure.SizesStart);
                  mutable end = ast + $(ruleStructure.SizesEnd);
                  for (; i < end; ++i)
                    $CurTextPos += parser.ast[i];
                  $(endLabel.Goto());
                }
                else
                {
                  when (parseState < 0)
                  {
                    parseState = ~parseState;
                    $(AstPtr(ruleStructure).Var) = ast;
                    $(recoveryLabel.Goto());
                  }
                  $CurTextPos = -1;
                  $(endLabel.Goto());
                }
              }
            }
          }
        ]>
      else
        <[
          when (resultRef > 0)
          {
            $(AstPtr(ruleStructure).Var) = resultRef;
            parseState = parser.ast[resultRef + 2];
            assert(parseState < 0 && parseState != $okStateCode);
            parseState = ~parseState;
            $(recoveryLabel.Goto());
          }
        ]>;

      def setResultRef = if (ruleStructure.Symbol is ExtentionRuleSymbol)
        <[ resultRef = $(AstPtr(ruleStructure).Var); ]>
      else
        <[ {} ]>;

      code.Add(<[ _ = $(ruleStructure.SrcRule.ToString()) ]>);
      //code.AddRange(_fields.Map(field => <[ _ = $(field.Print()) ]>));

      code.Add(<[ $(AstStart(ruleStructure)) = $CurTextPos; ]>);
      code.Add(checkAstMemoize);
      code.Add(<[ $(GotoParseLabel(ruleStructure.NotMarkerFields[0])); ]>);

      //parse ok
      code.Add(<[ $(OkLabel(ruleStructure)); ]>);
      code.Add(<[ $(AstPtr(ruleStructure).Set(ruleStructure.HeaderState, <[ $okStateCode ]>)) ]>);// правило полностью разобрано.
      code.Add(setResultRef);
      code.Add(<[ $(endLabel.Goto()); ]>);

      //parse fail
      code.Add(<[ $(FailLabel(ruleStructure)); ]>);
      code.Add(<[ when (parseState > $(ruleStructure.AllocateState)) // АСТ создано
                  {
                    $(AstPtr(ruleStructure).Set(ruleStructure.HeaderState, <[ parseState ]>));
                    $setResultRef;
                  }
      ]>);
      code.Add(<[ $CurTextPos = -1; $(endLabel.Goto()); ]>);
      CompileFields(code, ruleStructure.Fields);

      code.Add(recoveryLabel.Label());
      //code.Add(<[ assert2(false) ]>);
      foreach (field in ruleStructure.NotMarkerFields)
      {
        when (field.NextSibling != null)
        {
          def skip = if (field is RuleStructureField.Predicate)
            <[ assert(false) ]>;
          else
            <[ $CurTextPos += $(AstPtr(field.Owner).GetSize(field)) ]>;
          code.Add(<[
            if (parseState >= $(field.NextSibling.EndState))
              $skip;
            else
              $(GotoRecoveryLabel(field))
          ]>);
        }
      }
      code.Add(<[ assert2(false); assert(false) ]>);

      if (ruleStructure.Symbol.LastLocation is Some(loc) when Owner.Environment.Manager.Options.EmitDebug)
      {
        //assert2(false);
        def debugCode = <[ () ]>;
        debugCode.Location = loc.NLocation();
        code.Add(<[ $(endLabel.Label()); $debugCode; $CurTextPos; ]>);
      }
      else
        code.Add(<[ $(endLabel.Label()); $CurTextPos; ]>);

      def variables = SCG.List();
      variables.Add(<[
        mutable tmpSize    : int; _ = tmpSize;
        mutable parseState : int; _ = parseState;
        mutable newPos     : int; _ = newPos;
      ]>);
      variables.AddRange(LocalVariables.Filter(_ != "resultRef").Map(name => <[ mutable $(name : dyn) ]>));

      variables.Concat(code).FlattenPExpr();
    }
  }
}
