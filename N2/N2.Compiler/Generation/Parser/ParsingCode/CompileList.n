using Nemerle;
using Nemerle.Imperative;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using N2.Model;

namespace N2.Compiler
{
  internal abstract partial class RuleParserEmitter
  {
    public CompileList(code : SCG.List[PExpr], field : RuleStructureField.List) : void
    {
      def counter     () { <[ $(CreateVariable($"counter$(field.Level)") : dyn) ]> }
      def listStartPos() { <[ $(CreateVariable($"listStartPos$(field.Level)") : dyn) ]> }
      def useCounter = field.MinCount > 0 || field.MaxCount is Some(_);
      def counterInit() { if (useCounter) <[ $(counter())  = 0 ]> else <[ {} ]> }
      def counterInc()  { if (useCounter) <[ ++ $(counter())   ]> else <[ {} ]> }
      def counterDec()  { if (useCounter) <[ -- $(counter())   ]> else <[ {} ]> }
      def ifCounterEqualsToMaxThenContinue()
      {
        if (field.MaxCount is Some(max))
          <[
            when ($(counter()) == $max)
              $(field.ParserExtention().ContinueParseLabel.Goto());
          ]>
        else
          <[ {} ]>
      }
      def ifCounterLessThanMinThenFail()
      {
        if (field.MinCount > 0)
          <[
            when ($(counter()) < $(field.MinCount))
              $(field.ParserExtention().FailParseLabel.Goto());
          ]>
        else
          <[ {} ]>
      }

      def firstChild = field.Rule.Fields[0];
      code.Add(<[
        $(field.ParserExtention().ParseLabel.Label());
          _ = $(field.SrcRule.ToString());
          $(AllocateParentAst(field));
          $(counterInit());
          $(listStartPos()) = $CurTextPos;
          $(field.Rule.ParserExtention().AstStart) = $CurTextPos;
          $(firstChild.ParserExtention().ParseLabel.Goto());

        $(field.Rule.ParserExtention().OkLabel.Label());
          $(counterInc());
          $(field.Owner.ParserExtention().AstPtr.Set(field, <[ $CurTextPos - $(listStartPos()) ]>));
          $(ifCounterEqualsToMaxThenContinue());
          $(field.Rule.ParserExtention().AstStart) = $CurTextPos;
          $(firstChild.ParserExtention().ParseLabel.Goto());

        $(field.Rule.ParserExtention().FailLabel.Label());
          $(ifCounterLessThanMinThenFail());
          $CurTextPos = $(field.Rule.ParserExtention().AstStart);
          $(field.Owner.ParserExtention().AstPtr.Set(field, <[ $CurTextPos - $(listStartPos()) ]>));
          $(field.ParserExtention().ContinueParseLabel.Goto());

        $(field.ParserExtention().RecoveryLabel.Label());
          $(counterInit());
          $(listStartPos()) = $CurTextPos;
          $(field.Rule.ParserExtention().AstStart) = $CurTextPos;
          when (true)
          {
            mutable ast;
            def listEndPos = $CurTextPos + $(field.Owner.ParserExtention().AstPtr.GetSize(field));
            mutable itemEndPos = $CurTextPos;
            while (itemEndPos != listEndPos)
            {
              $(counterInc());
              $CurTextPos = itemEndPos;
              for (ast = parser.memoize[$CurTextPos]; ast > 0; ast = parser.ast[ast + 1])
              {
                when (parser.ast[ast] == $(field.Rule.AstId : dyn))
                {
                  mutable i = ast + $(field.Rule.SizesStart);
                  mutable end = ast + $(field.Rule.SizesEnd);
                  for (; i < end; ++i)
                    itemEndPos += parser.ast[i];
                  break;
                }
              }
            }
            $(counterDec());
            _ = "Мы перед последним элеменом цикла. Начинаем парсить хвост...";
            $(firstChild.ParserExtention().ParseLabel.Goto());
          }
      ]>);
      CompileFields(code, field.Rule.Fields);
    }
  }
}
