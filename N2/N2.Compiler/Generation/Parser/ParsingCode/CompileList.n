using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using N2.Model;

namespace N2.Compiler
{
  internal abstract partial class RuleParserEmitter
  {
    public CompileList(code : SCG.List[PExpr], field : RuleStructureField.List) : void
    {
      def counter     () { <[ $(CreateVariable($"counter$(field.Level)") : dyn) ]> }
      def listStartPos() { <[ $(CreateVariable($"listStartPos$(field.Level)") : dyn) ]> }
      def useCounter = field.MinCount > 0 || field.MaxCount is Some(_);
      def counterInit() { if (useCounter) <[ $(counter())  = 0 ]> else <[ {} ]> }
      def counterInc()  { if (useCounter) <[ ++ $(counter())   ]> else <[ {} ]> }
      def ifCounterEqualsToMaxThenContinue()
      {
        if (field.MaxCount is Some(max))
          <[
            when ($(counter()) == $max)
              $(field.ContinueParseLabel.Goto());
          ]>
        else
          <[ {} ]>
      }
      def ifCounterLessThanMinThenFail()
      {
        if (field.MinCount > 0)
          <[
            when ($(counter()) < $(field.MinCount))
              $(field.FailParseLabel.Goto());
          ]>
        else
          <[ {} ]>
      }

      def firstChild = field.Rule.Fields[0];
      code.Add(<[
        $(field.ParseLabel.Label());
          $(AllocateParentAst(field));
          $(counterInit());
          $(listStartPos()) = $CurTextPos;
          $(field.Rule.AstStart) = $CurTextPos;
          $(firstChild.ParseLabel.Goto());

        $(field.Rule.OkLabel.Label());
          $(counterInc());
          $(field.Owner.AstPtr.Set(field, <[ $CurTextPos - $(listStartPos()) ]>));
          $(ifCounterEqualsToMaxThenContinue());
          $(field.Rule.AstStart) = $CurTextPos;
          $(firstChild.ParseLabel.Goto());

        $(field.Rule.FailLabel.Label());
          $(ifCounterLessThanMinThenFail());
          $CurTextPos = $(field.Rule.AstStart);
          $(field.Owner.AstPtr.Set(field, <[ $CurTextPos - $(listStartPos()) ]>));
          $(field.ContinueParseLabel.Goto());
      ]>);
      CompileFields(code, field.Rule.Fields);
    }
  }
}
