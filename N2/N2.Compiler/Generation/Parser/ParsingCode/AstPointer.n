using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using N2.Model;

namespace N2.Compiler
{
  internal interface IVariableEmitter
  {
    Environment : EmitterEnvironment { get; }
    CreateVariable(name : string) : string;
  }

  internal class AstPointer
  {
    private Structure : RuleStructure;
    private Name      : string;
    public  Var       : PExpr { get { <[ $(Name : dyn) ]> } }
    private this(ruleStructure : RuleStructure, name : string, variableEmitter : IVariableEmitter)
    {
      Structure = ruleStructure;
      Name      = variableEmitter.CreateVariable(name);
    }
    public this(ruleStructure : RuleStructure, name : string)
    {
      this(ruleStructure, $"$name$(ruleStructure.Level)", ruleStructure.VariableEmitter);
    }

    private Ref(headerField : RuleStructureHeaderField) : PExpr
    {
      if (object.ReferenceEquals(Structure, headerField.Owner))
        <[ parser.ast[$Var + $(headerField.Offset)] ]>
      else
        PExpr.Error("Type mismatch.")
    }

    public Set(headerField : RuleStructureHeaderField, value : PExpr) : PExpr
    {
      <[ $(Ref(headerField)) = $value ]>
    }

    public Get(headerField : RuleStructureHeaderField) : PExpr
    {
      <[ $(Ref(headerField)) ]>
    }

    private Ref(structField : RuleStructureField) : PExpr
    {
      if (object.ReferenceEquals(Structure, structField.Owner))
        <[ parser.ast[$Var + $(structField.Offset)] ]>
      else
        PExpr.Error("Type mismatch.")
    }

    public Set(structField : RuleStructureField, value : PExpr) : PExpr
    {
      <[ $(Ref(structField)) = $value ]>
    }

    public Get(structField : RuleStructureField) : PExpr
    {
      <[ $(Ref(structField)) ]>
    }
  }
}
