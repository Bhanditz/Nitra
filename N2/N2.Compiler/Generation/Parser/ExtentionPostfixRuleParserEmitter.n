using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using N2.Model;
using N2.Typing;

using System;
using System.Collections.Generic;
using System.Linq;

namespace N2.Compiler
{
  internal sealed class ExtentionPostfixRuleParserEmitter : RuleParserEmitter
  {
    public Rule : Rule2.Sequence { get; }

    public this(owner : SyntaxModuleEmitter, rs : ExtentionRuleSymbol)
    {
      base(owner, rs);
      this.Rule = rs.Rule;
    }

    [RecordIgnore] private mutable _parserTb : TypeBuilder;

    public override DefineParser(astSize : out int, rootAstStruct : out option[RootAstStruct2], externalCalls : List[RuleSymbol]) : void
    {
      assert(_parserTb == null);
      def implClass = <[decl:
        public class $(ruleSymbol.DecorateRuleClassName() : dyn) : N2.Internal.ExtentionRuleParser
        {
          public this(ruleId : int, grammar : N2.CompositeGrammar)
          {
            base(grammar, $(Owner.Environment.GetDescriptorClassRef(ruleSymbol.RuleSymbol)).StaticDescriptor, ruleId, char.MinValue, char.MaxValue);//TODO: Fix me.
          }
        }
      ]>;
      _parserTb = Owner.GrammarImplTB.DefineNestedType(implClass);
      MakeRuleFields(ruleSymbol, this.Rule, out astSize, out rootAstStruct, externalCalls);
    }

    public override DefineParserMethods() : void
    {
      assert(_parserTb != null);
      DefineParse(_parserTb, ruleSymbol, Rule);
      DefineInit(_parserTb);
    }

    public override Emit() : void
    {
      assert(_parserTb != null);
      _parserTb.Compile();
    }
  }
}
