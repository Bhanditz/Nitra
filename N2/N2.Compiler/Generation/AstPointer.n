using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using N2.Model;

namespace N2.Compiler
{
  public interface IVariableEmitter
  {
    CreateVariable(name : string) : string;
  }

  public interface IConstantEmitter
  {
    Environment : EmitterEnvironment { get; }
    CreateConstant(name : string, value : int) : string;
  }

  public class LocalVariableEmitter : IVariableEmitter
  {
    private _variables : SCG.HashSet[string] = SCG.HashSet();
    public CreateVariable(name : string) : string
    {
      _ = _variables.Add(name);
      name
    }

    public Define() : list[PExpr]
    {
      _variables.Map(var => <[ mutable $(var : global) ]>)
    }
  }

  public class AstPointer
  {
    public  Structure       : RuleStructure;
    private ConstantEmitter : IConstantEmitter;
    private _nameMaker      : void -> string;
    public  Name            : string { [Memoize] get { _nameMaker() } }
    public  Var             : PExpr  {           get { <[ $(Name : global) ]> } }

    private this(ruleStructure : RuleStructure, constantEmitter : IConstantEmitter, nameMaker : void -> string)
    {
      Structure       = ruleStructure;
      ConstantEmitter = constantEmitter;
      _nameMaker      = nameMaker;
    }

    public static New(ruleStructure : RuleStructure, constantEmitter : IConstantEmitter, variableEmitter : IVariableEmitter, prefix : string) : AstPointer
    {
      AstPointer(ruleStructure, constantEmitter, () => variableEmitter.CreateVariable(ruleStructure.MakeVariableName(prefix)));
    }

    public static Existing(ruleStructure : RuleStructure, constantEmitter : IConstantEmitter, name : string) : AstPointer
    {
      AstPointer(ruleStructure, constantEmitter, () => name);
    }

    private Ref(headerField : RuleStructureHeaderField) : PExpr
    {
      if (object.ReferenceEquals(Structure, headerField.Owner))
        <[ parser.ast[$Var + $(MakeOffsetRef(headerField))] ]>
      else
        PExpr.Error("Type mismatch.")
    }

    public Set(headerField : RuleStructureHeaderField, value : PExpr) : PExpr
    {
      <[ $(Ref(headerField)) = $value ]>
    }

    public Get(headerField : RuleStructureHeaderField) : PExpr
    {
      <[ $(Ref(headerField)) ]>
    }

    private Ref(structField : RuleStructureField) : PExpr
    {
      CheckTypeAndReturn(structField, <[ parser.ast[$Var + $(MakeOffsetRef(structField))] ]>)
    }

    public Set(structField : RuleStructureField, value : PExpr) : PExpr
    {
      <[ $(Ref(structField)) = $value ]>
    }

    public GetSize(structField : RuleStructureField) : PExpr
    {
//#if DEBUG
//      CheckTypeAndReturn(structField, <[ { _ = $(structField?.ToString()); parser.GetSize($Var + $(MakeOffsetRef(structField))) } ]>)
//#else
      CheckTypeAndReturn(structField, <[ parser.GetSize($Var + $(MakeOffsetRef(structField))) ]>)
//#endif
    }

    public GetRaw(structField : RuleStructureField) : PExpr
    {
      CheckTypeAndReturn(structField, Ref(structField))
    }

    public GetSkipAndSize(structField : RuleStructureField) : PExpr
    {
      CheckTypeAndReturn(structField, <[ parser.GetSkipAndSize($Var + $(MakeOffsetRef(structField))) ]>)
    }

    public GetSkip(structField : RuleStructureField) : PExpr
    {
      CheckTypeAndReturn(structField, <[ parser.GetSkip($Var + $(MakeOffsetRef(structField))) ]>)
    }

    private CheckTypeAndReturn(structField : RuleStructureField, result : PExpr) : PExpr
    {
      if (object.ReferenceEquals(Structure, structField.Owner))
        if (structField.Size == 1)
          result
        else
          PExpr.Error("Invalid field access.")
      else
        PExpr.Error("Type mismatch.")
    }

    private MakeOffsetRef(headerField : RuleStructureHeaderField) : PExpr
    {
      if (ConstantEmitter.Environment.IsDebug)
      {
        def name  = ConstantEmitter.Environment.MakeRuleStructureHeaderFieldOffsetName(headerField);
        def name2 = ConstantEmitter.CreateConstant(name, headerField.Offset);
        <[ $(name2 : global) ]>
      }
      else
        <[ $(headerField.Offset) ]>
    }

    private MakeOffsetRef(structField : RuleStructureField) : PExpr
    {
      if (ConstantEmitter.Environment.IsDebug)
      {
        def name  = ConstantEmitter.Environment.MakeRuleStructureFieldOffsetName(structField);
        def name2 = ConstantEmitter.CreateConstant(name, structField.Offset);
        <[ $(name2 : global) ]>
      }
      else
       <[ $(structField.Offset) ]>
    }
  }
}
