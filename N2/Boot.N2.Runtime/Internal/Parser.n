using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;

namespace N2.Internal
{
#if DEBUG || PARSER_DEBUG
  [System.Diagnostics.DebuggerTypeProxy(typeof(Debug.DebugParser))]
  public class Parser
#else
  public struct Parser
#endif
  {
    public mutable ast : array[int];
    public mutable memoize : array[int];
    public mutable allocated : int;
    public parserHost : ParserHost;

    public this(initalSize : int, textSize : int, parserHost : ParserHost)
    {
      this.ast        = array(initalSize);
      this.memoize    = array(textSize);
      this.allocated  = 0;
      this.parserHost = parserHost;
#if DEBUG || PARSER_DEBUG
      this.ruleCalls = SCG.List(array(textSize + 1));
#endif
    }

    private AllocateImpl(size : int) : int
    {
      def newAst = allocated;
      allocated = allocated + size;
      while (ast.Length <= allocated)
        Array.Resize(ref ast, (ast.Length * 3) / 2);

      for (mutable i = 0; i < size; ++i)
        ast[newAst + i] = -1;

      newAst
    }

    public Allocate(size : int) : int
    {
      def newAst = AllocateImpl(size);
#if DEBUG || PARSER_DEBUG
      astOffsets.Add((newAst, size, null));
#endif
      newAst
    }

#if DEBUG || PARSER_DEBUG
    public astOffsets : SCG.List[int * int * RuleDescriptor] = SCG.List();
    public ruleCalls : SCG.List[SCG.List[RuleDescriptor]];
#endif
    public Allocate(size : int, ruleId : int) : int
    {
      def newAst = AllocateImpl(size);
      ast[newAst] = ruleId;
#if DEBUG || PARSER_DEBUG
      astOffsets.Add((newAst, size, parserHost.GetRuleDescriptorById(ruleId)));
#endif
      newAst;
    }
  }
}
