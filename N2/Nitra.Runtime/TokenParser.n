using N2.Internal;
using N2.Runtime.Reflection;
using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Utility;
using Nemerle.Extensions;

using System;
using System.Collections.Generic;
using System.Linq;

namespace N2.Runtime
{
  [StructuralEquality]
  public variant TokenParser
  {
    | RegularCall    { [EqualsIgnore] Call : RegularCallDelegate; Descriptor : RegularRuleDescriptor; }
    | ExtensibleCall { RuleParser : ExtensibleRuleParser; }
    | SimpleCall     { RuleParser : SimpleRuleParser; }
    | TokenString    { Str : string; }

    public Parse(pos : int, text : string, parseResult : ParseResult) : int
    {
      match (this)
      {
        | RegularCall    as p => p.Call(pos, text)
        | ExtensibleCall as p => p.RuleParser.Parse(pos, text, parseResult)
        | SimpleCall     as p => p.RuleParser.Parse(pos, text, parseResult)
        | TokenString    as p =>
          def str = p.Str;
          if (pos + str.Length < text.Length)
          {
            for (mutable i = 0; i < str.Length; ++i)
              when (str[i] != text[pos + i])
                return -1;
            pos + str.Length
          }
          else
            -1
      }
    }

    public override ToString() : string
    {
      match (this)
      {
        | RegularCall    as p => $"regex:      $(p.Descriptor)"
        | ExtensibleCall as p => $"extensible: $(p.RuleParser.Descriptor) BP=$(p.RuleParser.BindingPower)"
        | SimpleCall     as p => $"simple:     $(p.RuleParser.Descriptor)"
        | TokenString    as p => $"string:     \"$(p.Str)\""
      }
    }
  }
}
