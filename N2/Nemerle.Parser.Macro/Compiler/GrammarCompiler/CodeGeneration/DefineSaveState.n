using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Parser.Model;

using System;
using System.Linq;
using SCG = System.Collections.Generic;

using Nemerle.Parser.Internal;

namespace Nemerle.Parser
{
  internal partial class GrammarCompiler
  {
    private DefineSaveState() : void
    {
      def extentionPoints = TypedGrammar.Rules.MapFiltered(_ is RuleDefinition.ExtentionPoint, _ :> RuleDefinition.ExtentionPoint);
      def names = extentionPoints
        .Map(extentionPoint =>
          {
            def names = DefineExtensionPointMemoizeNames(extentionPoint);
            def returnType = GetRuleNType(extentionPoint);
            [(names.PrefixRules, <[ array[ExtensionPrefixBase[$(returnType : typed)]] ]>), (names.PostfixRules, <[ array[ExtensionPostfixBase[$(returnType : typed)]] ]>)];
          })
        .Flatten();

      def saveRules = names.Map((name, _) => <[ this.$(name : dyn) = Grammar.$(name : dyn) ]>);
      def loadRules = names.Map((name, _) => <[ Grammar.$(name : dyn) = this.$(name : dyn) ]>);

      foreach ((name, type) in names)
        _ = GrammarStateTB.DefineWithSource(<[decl: private $(name : dyn) : $type; ]>);

      _ = GrammarStateTB.DefineWithSource(
        <[decl:
          public this(grammar : $(GrammarImplTB.GetMemType() : typed))
          {
            Grammar = grammar;
            { ..$saveRules }
          }
        ]>);

      _ = GrammarStateTB.DefineWithSource(
        <[decl:
          public LoadThisState() : void
          {
            ..$loadRules
          }
        ]>);

      DefineImplementation(
        <[decl:
          public SaveState() : Nemerle.Parser.Internal.IGrammarState
          {
            GrammarStateImpl(this)//TODO: $(GrammarStateTB.GetMemType() : typed)(this)
          }
        ]>);
    }
  }
}
