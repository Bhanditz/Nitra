using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Console;
using System.Linq;

using Nitra;
using Nitra.Runtime;
using Nitra.Quote;

[assembly: ImportRules(Grammar = "SplicableIniSyntax")]

module Program
{
  Main() : void
  {
    def valueX = quote <# Variable: x=10 #>;
    def valueY = quote <# Variable: y=20 #>;
    def section1 = quote <# Section:
      [Section1]
      $VariablesItem(valueX)
      $VariablesItem(valueY)
      z=30
    #>;
    def section2 = quote <# Section:
      [Section2]
      a=AA
      b=BB
    #>;
    WriteLine("INI Ast pretty print:");
    def file = quote <# Start: $SectionsItem(section1) $SectionsItem(section2) #>;
    WriteLine(file);

    quote match (file)
    {
      | <# Start: [Section1] $Variables(vars) $SectionsItem(_) #> =>
        WriteLine("Found variables in [Section1] are:");
        foreach (v in vars)
          WriteLine(v);

      | _ => ()
    }
    _ = ReadLine();
  }
}
