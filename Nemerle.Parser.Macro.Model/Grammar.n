using System;
using System.Collections.Generic;
using System.Text;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

namespace Nemerle.Parser.Model
{
  public class Grammar
  {
    private _grammar : Map[NameRef, RuleDefinition] = Map();

    [Accessor]
    private _grammarRefs : list[GrammarRef] = [];

    [Accessor]
    private _spanClasses : list[SpanClass] = [];

    [Accessor]
    private _bracketPairs : list[BracketPair] = [];

    public this()
    {
    }

    private this
      ( grammar : Map[NameRef, RuleDefinition]
      , grammarRefs : list[GrammarRef]
      , spanClasses : list[SpanClass]
      , bracketPairs : list[BracketPair]
      )
    {
      _grammar = grammar;
      _grammarRefs = grammarRefs;
      _spanClasses = spanClasses;
      _bracketPairs = bracketPairs;
    }

    public Add(ruleDefinition : RuleDefinition) : Grammar
    {
      Grammar(_grammar.Add(ruleDefinition.Name, ruleDefinition), _grammarRefs, _spanClasses, _bracketPairs);
    }

    public Add(grammarRef : GrammarRef) : Grammar
    {
      Grammar(_grammar, grammarRef :: _grammarRefs, _spanClasses, _bracketPairs);
    }

    public Add(spanClass : SpanClass) : Grammar
    {
      Grammar(_grammar, _grammarRefs, spanClass :: _spanClasses, _bracketPairs);
    }

    public Add(bracketPair : BracketPair) : Grammar
    {
      Grammar(_grammar, _grammarRefs, _spanClasses, bracketPair :: _bracketPairs);
    }

    public GetRule(name : NameRef) : RuleDefinition
    {
      if (_grammar.Contains(name))
        _grammar.Get(name);
      else
        throw GrammarException($"There is no rule \"$name\" in grammar.", name.Location);
    }

    public Count : int
    {
      get { _grammar.Count }
    }

    public Names : list[NameRef]
    {
      get { _grammar.Map((name, _) => name) }
    }

    public Rules : list[RuleDefinition]
    {
      get { _grammar.Map((_, rd) => rd) }
    }

    public TransformRules
      ( transformSimple : RuleDefinition.Simple -> Rule
      , transformExtentionPrefix : RuleDefinition.ExtentionPrefix -> Rule
      , transformExtentionPostfix : RuleDefinition.ExtentionPostfix -> Rule
      , transformRegular : RuleDefinition.Regular -> RegularRule
      ) : Grammar
    {
      _grammar.Fold(Grammar(Map(), _grammarRefs, _spanClasses, _bracketPairs), (_, rd, newGrammar) =>
      {
        def rd = match (rd : RuleDefinition)
        {
          | ExternalRule
          | ExtentionPoint  => rd
          | ExtentionPrefix as rd =>
            RuleDefinition.ExtentionPrefix
              ( rd.Location
              , rd.Id
              , rd.Name
              , rd.Attributes
              , rd.TokenFieldNames
              , rd.baseRuleName
              , transformExtentionPrefix(rd)
              )

          | ExtentionPostfix as rd =>
            RuleDefinition.ExtentionPostfix
              ( rd.Location
              , rd.Id
              , rd.Name
              , rd.Attributes
              , rd.TokenFieldNames
              , rd.baseRuleName
              , rd.bindingPower
              , transformExtentionPostfix(rd)
              )

          | Simple as rd    =>
            RuleDefinition.Simple
              ( rd.Location
              , rd.Id
              , rd.Name
              , rd.Attributes
              , rd.TokenFieldNames
              , rd.retType
              , transformSimple(rd)
              )

          | Regular as rd    =>
            RuleDefinition.Regular
              ( rd.Location
              , rd.Id
              , rd.Name
              , rd.Attributes
              , rd.TokenFieldNames
              , transformRegular(rd)
              )
        }
        newGrammar.Add(rd)
      })
    }

    /*public IsOptional(rule : Rule) : bool // TODO:Remove?
    {
      def isOptionalImpl(rule : Rule, visited : HashSet[Rule]) : bool
      {
        def isOptional = isOptionalImpl(_, visited);

        match (rule)
        {
          | Capture(_, rule1)                 => isOptional(rule1)
          | Choice(rules2)                    => rules2.Exists(isOptional)
          | Sequence(rules3)                  => rules3.ForAll(isOptional)
          | Repeat(n, _, rule5)               => n == 0 || isOptional(rule5)
          | Not                               => true
          | And                               => true
          | Scope(_, rule8)                   => isOptional(rule8)
          | Call(name, _)                     =>
            match (this.GetRule(name).GetRule())
            {
              | Some(r) =>
                if (visited.Add(r))
                  isOptional(r)
                else
                  false
              | _ => false
            }

          | Chars | Fsm                       => false
          | Cut                               => false // ???
        }
      }
      
      isOptionalImpl(rule, HashSet([rule]))
    }*/

    public override ToString() : string
    {
      def sb = StringBuilder();
      _ = sb.AppendLine($"Count:$Count");
      _ = sb.AppendLine("rules:");
      foreach((name, rule) in _grammar)
      {
        _ = sb.AppendLine($"  name:$(name)");
        _ = sb.AppendLine($"  rule type:$(rule.GetType().ToString())");
        _ = sb.AppendLine($"  rule:$(rule.ToString())");
        _ = sb.AppendLine(string.Empty);
      }

      sb.ToString()
    }
  }
}
