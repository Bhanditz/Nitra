using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nemerle.Parser.Model
{
  [Record]
  public variant RuleType
  {
    | List   { ty : RuleType; }
    | Option { ty : RuleType; }
    | Tuple  { types : list[RuleType]; }
    | PType  { ty : PExpr; }
    | NType  { ty : FixedType; }
    | Chars
    | Void

    public override ToString() : string
    {
      match (this)
      {
        | List(ty)     => $<#List[$ty]#>
        | Option(ty)   => $<#$ty?#>
        | Tuple(types) => $<#(..$types)#>
        | PType(ty)    => $<#$ty#>
        | NType(ty)    => $<#$ty#>
        | Chars        => "Chars"
        | Void         => "Void"
      }
    }
  }
}
