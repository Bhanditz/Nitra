using System;
using SCG = System.Collections.Generic;

using Nemerle;
using Nemerle.Assertions;
using Nemerle.Compiler;
using Nemerle.Macros;
using Nemerle.Utility;
using PT = Nemerle.Compiler.Parsetree;
using TT = Nemerle.Compiler.Typedtree;

namespace N2.AstUtils
{
  // Walks through AST object and collects all ancestors of `N2.Located' class
  // which name starts with "Error".
  // Code returns `SCG.List[N2.Located]' instance.
  macro CollectParseErrors(ast)
  {
    def typer = ImplicitCTX();

    def makeResult(_ : PT.PExpr, x : TT.TExpr)
    {
      def locatedType = FixedType.Class(typer.Manager.LookupTypeInfo("N2.Located", 0), []);
      def typeHandler(ty : FixedType, var : PT.PExpr) : option[PT.PExpr]
      {
        match(ty)
        {
          | Class(ti, _) when ty.TryRequire(locatedType) && ti.Name.StartsWith("Error") =>
            Some(<[ buffer.Add($var); ]>);
          | _ =>
            None()
        }
      }
      def typeWalker = TypeWalker(typeHandler);
      typeWalker.Run(x.Type);
      <[
        def buffer = System.Collections.Generic.List.[N2.Located]();
        $(typeWalker.Compile(x));
        buffer
      ]>
    }

    if(Manager().IsIntelliSenseMode)
      <[ System.Collections.Generic.List.[N2.Located]() ]>
    else
      typer.TransformWhenAllTypesWouldBeInfered(makeResult, typer.TypeExpr(ast))
  }
}