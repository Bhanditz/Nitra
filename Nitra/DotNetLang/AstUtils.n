using Nitra;
using Nitra.Declarations;
using Nitra.ProjectSystem;
using Nitra.Runtime.Binding;

using Nemerle;
using Nemerle.Imperative;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace DotNet
{
  public module AstUtils
  {
    public Append(this lst : QualifiedReference.IAstList, item : QualifiedReference) : QualifiedReference.IAstList
    {
      def newItems = array(lst.Count + 1);
      lst.CopyTo(newItems, 0);
      newItems[lst.Count] = item;
      QualifiedReference.AstList(lst.File, lst.Span, newItems)
    }

    public MakeMembersScope(currentScope : Scope, opening : list[Scope], aliases : TableScope) : Scope
    {
      def makeCurrentScope(scope : Scope) : Scope
      {
        def withAliases =
          if (aliases.Symbols.Count == 0)
            scope
          else
            scope.UnionWith(aliases);

        def withUsings =
          match (opening)
          {
            | []    => withAliases
            | [one] => one.HideWith(withAliases)
            | _     => Scope.Union(opening).HideWith(withAliases)
          };

        withUsings
      }

      if (currentScope is HidingScope as h)
        h.Hidden.HideWith(makeCurrentScope(h.Scope))
      else
        makeCurrentScope(currentScope)
    }

    public MakeEnteredScope(parentScope : Scope, parentNs : NamespaceSymbol, enteredNs : NamespaceSymbol) : Scope
    {
      def buildEnterNsScope(ns : NamespaceSymbol) : Scope
      {
        def parent = ns.Parent;
        def scope  = ns.Scope;

        if (parent : object == parentNs)
          parentScope.HideWith(scope)
        else
          buildEnterNsScope(parent).HideWith(scope)
      }
      
      buildEnterNsScope(enteredNs)
    }
    
    [Memoize]
    public TypeOverloadResolution(argumentsCount : int) : ResolutionAlgorithm[ISymbol2, ISymbol2]
    {
      def resolve(candidate : ISymbol2) : ValueOption[ISymbol2]
      {
        | symbol is BuiltInTypeAliasSymbol              when symbol.IsTypeEvaluated                       => resolve(symbol.Type)
        | symbol is GenericEntitySymbol                 when argumentsCount == symbol.TypeParametersCount => VSome(symbol)
        | _                                             when argumentsCount == 0                          => VSome(candidate)
        | _ => VNone()
      }
      resolve
    }

    public CreateMissingQualifiedReference(file : File, span : NSpan) : QualifiedReference
    {
      QualifiedReference.Simple(
        File = file,
        Span = span,
        Name = Reference.AmbiguousOrMissing(file, span))
    }

    public CreateBuiltinName(file : File, span : NSpan, name : string) : QualifiedReference
    {
      QualifiedReference.Simple(file, NSpan(span.StartPos), Reference(file, NSpan(span.StartPos), name))
    }

    public CreateTupleQualifiedReference(file : File, span : NSpan, items : QualifiedReference.IAstList) : QualifiedReference
    {
      QualifiedReference.Generic(file, span,
        CreateBuiltinName(file, NSpan(span.StartPos), "#Tuple"),
        items)
    }

    public CreateFuncQualifiedReference(file : File, span : NSpan, qualifiedReference1 : QualifiedReference, qualifiedReference2 : QualifiedReference) : QualifiedReference
    {
      def arguments = 
        match (qualifiedReference1)
        {
          | QualifiedReference.Generic(QualifiedName = QualifiedReference.Simple(Name = Reference(Text = "#Tuple")), Arguments = tupleArguments) => tupleArguments.Append(qualifiedReference2)
          | _ => QualifiedReference.AstList(qualifiedReference1.File, qualifiedReference1.Span, qualifiedReference1, qualifiedReference2)
        };

      QualifiedReference.Generic(file, span,
        CreateBuiltinName(file, NSpan(span.StartPos), "#Func"),
        arguments)
    }

    public IdToString(parsedValue : ParsedValue[int]) : ParsedValue[string]
    {
      if (parsedValue.HasValue)
        ParsedValue(parsedValue.Span, StringIndex.GetText(parsedValue.ValueOrDefault))
      else
        ParsedValue(parsedValue.Span.StartPos)
    }

    public MakeNamespaceFullName(parentFullName : string, name : string) : string
    {
      if (string.IsNullOrEmpty(parentFullName))
        name
      else
        parentFullName + "." + name
    }

    public IsSameIdentity(typeParametersCount : int, candidate : Declaration) : bool
    {
      match (candidate)
      {
        | DotNet.GenericType as t => t.TypeParameterAliases.Count == typeParametersCount
        | Namespace
        | DotNet.Type             => typeParametersCount == 0
        | _                       => false
      }
    }

    public CanMerge[T](typeParametersCount : int, candidate : Declaration) : bool
      where T : GenericType
    {
      match (candidate)
      {
        | genericType is T => genericType.TypeParameterAliases.Count == typeParametersCount
        | _ => false
      }
    }

     public CanMerge[T](symbol : GenericEntitySymbol, candidate : Declaration) : bool
      where T : GenericEntity
    {
      match (candidate)
      {
        | genericType is T => genericType.TypeParameterAliases.Count == symbol.TypeParametersCount
        | _ => false
      }
    }

    public CanMerge[T](symbol : GenericContainerTypeSymbol, candidate : Declaration) : bool
      where T : GenericContainerType
    {
      match (candidate)
      {
        | genericType is T => genericType.TypeParameterAliases.Count == symbol.TypeParametersCount && symbol.IsPartial && genericType.IsPartial.ValueOrDefault
        | _ => false
      }
    }
    
    public FilterResolved(refs : array[Ref[DeclarationSymbol]]) : array[TypeSymbol]
    {
      def lst = List(refs.Length);

      foreach (r in refs)
      {
        def r2 = r.Resolve.[TypeSymbol]();
        when (r2.IsSymbolEvaluated)
          lst.Add(r2.Symbol);
      }

      lst.ToArray()
    }

    public CreateNestedConstructedTypeSymbol(t : NestedGenericTypeSymbol, parentTypeSubst : TypeSymbol, context : DependentPropertyEvalContext) : ConstructedTypeSymbol
    {
      def resultSymbol      = NestedConstructedTypeSymbol();
      resultSymbol.Parent   = parentTypeSubst;
      resultSymbol.TypeSubst = resultSymbol;
      foreach (decl in t.Declarations)
        resultSymbol.AddDeclaration(decl);
      resultSymbol.TypeInfo = t;
      resultSymbol.Args     = t.TypeParameters.ToArray();
      resultSymbol.EvalProperties(context);
      resultSymbol
    }

    public CreateNestedConstructedTypeSymbol(file : File, span : NSpan, parentTypeSubst : TypeSymbol, typeInfo : GenericTypeSymbol, arguments : IList[TypeSymbol], context : DependentPropertyEvalContext) : ConstructedTypeSymbol
    {
      def name              = Name(file, span, "#ConstructedType");
      def decl              = NestedConstructedType(file, span, name);
      def resultSymbol      = NestedConstructedTypeSymbol();
      resultSymbol.Parent   = parentTypeSubst;
      resultSymbol.TypeSubst = resultSymbol;
      name.Symbol = resultSymbol;
      resultSymbol.AddDeclaration(decl);
      resultSymbol.TypeInfo = typeInfo;
      resultSymbol.Args     = arguments;
      resultSymbol.EvalProperties(context);
      resultSymbol
    }

    public CreateConstructedTypeSymbol(file : File, span : NSpan, typeInfo : GenericTypeSymbol, arguments : IList[TypeSymbol], context : DependentPropertyEvalContext) : ConstructedTypeSymbol
    {
      def name              = Name(file, span, "#ConstructedType");
      def decl              = TopConstructedType(file, span, name);
      def resultSymbol      = TopConstructedTypeSymbol();
      name.Symbol = resultSymbol;
      resultSymbol.AddDeclaration(decl);
      resultSymbol.TypeInfo = typeInfo;
      resultSymbol.Args     = arguments;
      resultSymbol.EvalProperties(context);
      resultSymbol
    }

    public MakeConstructedType(ast : ILocated, args : array[Ref[DeclarationSymbol]], context : DependentPropertyEvalContext) : ResolutionAlgorithm[DeclarationSymbol, DeclarationSymbol]
    {
      mutable allArgumentsResolved = true;
      def resolvedArgs = array(args.Length);
      foreach (arg in args with index)
      {
        def resolvedArg = arg.Resolve.[TypeSymbol]();
        if (resolvedArg.IsSymbolEvaluated)
          resolvedArgs[index] = resolvedArg.Symbol
        else
        {
          allArgumentsResolved = false; 
          resolvedArgs[index]  = null;
        }
      }

      fun (symbol)
      {
        def makeArguments(symbol : GenericTypeSymbol)
        {
          mutable argsWithTemplates = resolvedArgs;
          unless (allArgumentsResolved)
          {
            def typeParameters = symbol.TypeParameters;
            argsWithTemplates  = array(resolvedArgs.Length);
            foreach (arg in resolvedArgs with index)
              argsWithTemplates[index] = arg ?? typeParameters[index];
          }
          argsWithTemplates
        }

        match (symbol)
        {
          | symbol is NestedGenericTypeSymbol => VSome(CreateNestedConstructedTypeSymbol(ast.File, ast.Span, symbol.Parent.TypeSubst, symbol, makeArguments(symbol), context))
          | symbol is TopGenericTypeSymbol    => VSome(CreateConstructedTypeSymbol(ast.File, ast.Span, symbol, makeArguments(symbol), context))
          | _ => VNone()
        }
      }
    }

    public GetTypeParametersCount(declaration : Declaration) : int
    {
      (declaration :> GenericEntity).TypeParameterAliases?.Count
    }

    public GetTypeParameter(alias : TypeParameterAlias, typeParameterSymbols : IList[TypeParameterSymbol], IndexIn : int) : TypeParameterSymbol
    {
      def typeParameterSymbol = typeParameterSymbols[IndexIn];
      typeParameterSymbol.AddAliasSymbol(alias);
      typeParameterSymbol
    }

    public NoTypeParameters : array[TypeParameterSymbol] = array(0);

    public CreateTypeParameters(count : int) : array[TypeParameterSymbol]
    {
      if (count == 0)
        NoTypeParameters
      else
      {
        def result = array(count);
        for (mutable i = 0; i < result.Length; ++i)
          result[i] = TypeParameterSymbol();
        result
      }
    }

    public IsPartial(declarations : Seq[GenericContainerType]) : bool
    {
      declarations.FirstOrDefault()?.IsPartial.ValueOrDefault
    }

    public PrependScopeIfResolved(reference : Ref[ISymbol2], scopes : list[Scope]) : list[Scope]
    {
      if (reference.IsSymbolEvaluated)
        reference.Symbol.Scope :: scopes
      else
        scopes
    }
  }
}
