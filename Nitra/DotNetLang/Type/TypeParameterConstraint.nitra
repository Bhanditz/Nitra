using Nitra;
using Nitra.Declarations;

using Nemerle.Collections;

namespace DotNet
{
  abstract ast TypeParameterConstraint
  {
  stage 1:
    in    Scope                    : Scope;
    in    TypeConstraints          : TypeConstraintSet;
    inout ConstraintIndex          : int;
    in    ConstraintCount          : int;

    ConstraintIndexOut = ConstraintIndexIn + 1;

    | Ctor
      {
        TypeConstraints |= TypeConstraint.Ctor;

        when (ConstraintIndexOut != ConstraintCount)
          Error("The 'new()' constraint must be the last constraint specified.");
      }
    | Class
      {
        TypeConstraints |= TypeConstraint.Class;

        when (ConstraintIndexIn != 0)
          Error("The 'class' constraint must come before any other constraints.");
      }
    | Struct
      {
        TypeConstraints |= TypeConstraint.Struct;

        when (ConstraintIndexIn != 0)
          Error("The 'struct' constraint must come before any other constraints.");
      }
    | Base
      {
        Type.Scope = Scope;
        TypeConstraints |= TypeRef;

      stage 1:
        out TypeRef : Ref[TypeSymbol] = Type.Ref.Resolve();
        // TODO: add non-sealed check
        out IsValid : bool = TypeRef.Symbol is ClassSymbol || TypeRef.Symbol is InterfaceSymbol || TypeRef.Symbol is TypeParameterSymbol;
        out IsClassOrStructSpecified : bool = TypeConstraints.Contains(TypeConstraint.Class) || TypeConstraints.Contains(TypeConstraint.Struct);

        when (TypeRef.Symbol is ClassSymbol)
        when (IsClassOrStructSpecified)
          Error($"'$(TypeRef.Symbol.FullName)': cannot specify both a constraint class and the 'class' or 'struct' constraint.");

        when (TypeRef.Symbol is ClassSymbol)
        when (ConstraintIndexIn != 0)
        unless (IsClassOrStructSpecified)
          Error($"The class type constraint '$(TypeRef.Symbol.FullName)' must come before any other constraints.");

        unless (IsValid)
          Error($"'$(TypeRef.Symbol.FullName)' is not a valid constraint. A type used as a constraint must be an interface, a non-sealed class or a type parameter.");

        Type : QualifiedReference;
      }
  }
}
