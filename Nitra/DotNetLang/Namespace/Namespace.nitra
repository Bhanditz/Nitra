using Nitra;
using Nitra.Declarations;
using DotNet.AstUtils;

namespace DotNet
{
  abstract declaration Namespace : NamespaceMemberDeclaration
  {
    sealed symbol
    {
      Kind      = "namespace";
      SpanClass = DotNetLang.NamespaceSpanClass;
      Scope     = MemberTable;

      out MemberTable : TableScope = TableScope(ToString(), this);

      IsSameIdentity = AstUtils.IsSameIdentity(0, candidate);
      CanMerge       = candidate is Namespace || candidate is ExternalDeclaration[NamespaceSymbol];
    }
  }

  declaration ImplicitNamespace : Namespace
  {
    inout ParentPipe : NamespaceSymbol;

    DeclaredIn                      = ParentPipeIn;
    Symbol.DeclaredIn               = DeclaredIn;
    ContainingTable                 = ParentPipeIn.MemberTable;
    ParentPipeOut                   = Symbol;
  }

  abstract ast NamespaceBody : BindableAst
  {
  stage 1:
    out EnteredScope : Scope;

    Members.OpeningIn         = EmptyScope.Instance;
    Members.UsingAliasesScope = TableScope("aliases", null);
    Members.CurrentScope      = this.Scope;
    Members.Scope             = MakeMembersScope(EnteredScope, Members.OpeningOut, Members.UsingAliasesScope);

    Members         : NamespaceMember*;
  }

  declaration ExplicitNamespace : Namespace, NamespaceBody
  {
    EnteredScope                    = MakeEnteredScope(Scope, DeclaredIn, Symbol);
    Path.ParentPipeIn               = DeclaredIn;
    ContainingTable                 = Path.ParentPipeOut.MemberTable;
    Members.DeclaredIn              = Symbol;
    Symbol.DeclaredIn               = Path.ParentPipeOut;

    Path            : ImplicitNamespace*;
  }
}
