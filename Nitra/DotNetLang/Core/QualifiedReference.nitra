using Nitra;
using Nitra.Declarations;
using System.Linq;

namespace DotNet
{
  abstract ast QualifiedReference : BindableAst
  {
  stage 1:
    in  Arity   : int = 0;
    in  NameScopeFactory : ScopeFactory = ScopeFactories.Default;
    out Ref     : Ref[DeclarationSymbol];

    | Simple
      {
        Name.Scope  = NameScopeFactory(Scope);
        Ref         = Name.Ref.Resolve(AstUtils.TypeOverloadResolution(Arity));

        Name : Reference;
      }

    | Aliased
      {
        Name.Scope  = NameScopeFactory(Scope);
        Ref         = Name.Ref.Resolve(AstUtils.TypeOverloadResolution(Arity));

        Alias : Reference;
        Name  : Reference;
      }

    | Qualified
      {
        Qualifier.Arity = 0;
        Qualifier.Scope = Scope;
        Name.Scope      = NameScopeFactory(Qualifier.Ref.Scope);
        Ref             = Name.Ref.Resolve(AstUtils.TypeOverloadResolution(Arity));

        Qualifier : QualifiedReference;
        Name      : Reference;
      }

    | Generic
      {
        Arguments.Arity     = 0;
        QualifiedName.Arity = Arguments.Count;
        QualifiedName.Scope = Scope;
        QualifiedName.NameScopeFactory = NameScopeFactory;
        Arguments.Scope     = Scope;
        Ref                 = QualifiedName.Ref.Resolve(AstUtils.MakeConstructedType(this, Arguments.Ref, context));

        QualifiedName : QualifiedReference;
        Arguments     : QualifiedReference*; // TODO: change to  QualifiedReference+
      }
  }
}
