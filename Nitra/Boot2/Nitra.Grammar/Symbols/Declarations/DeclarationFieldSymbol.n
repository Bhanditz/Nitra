using Nitra.Model;
using Nitra.ProjectSystem;
using Nitra.Typing.RuleMethod;
using Nitra.Serialization;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.IO;
using SCG = System.Collections.Generic;

using G = NitraSyntaxParseTree;

namespace Nitra.Typing
{
  [Record, ExportableSymbol(ExternalDeclarationFieldSymbol), ChemicalSymbol]
  public abstract class DeclarationFieldSymbol : NitraFieldSymbol
  {
    public DeclaringDeclaration : DeclarationSymbol
    {
      get
      {
        assert(Node != null);
        match (Node.Parent.GetSymbol.[DeclarationSymbol]())
        {
          | declarationSymbol :: _ => declarationSymbol
          | []                        => assert(false)
        }
      }
    }

    Serialize(_ : BinaryWriter, _ : IMetadataWriter) : void implements IExportableSymbol.Serialize
    {
      // all data is written to .NET metadata
    }
  }

  public sealed class ParsedDeclarationFieldSymbol : DeclarationFieldSymbol
  {
    private _node : G.NitraField;

    public this(node : G.NitraField)
    {
      base(node.Name, node.Name.Value());
      _node = node;
    }

    public override Type         : NType          { get { _node.Type } }
    public override HasBody      : bool           { get { !_node.RuleMethodBody.IsEmpty() } }
    public override Body         : RuleMethodBody { get { _node.Body } }
  }

  public sealed class ExternalDeclarationFieldSymbol : DeclarationFieldSymbol
  {
    Deserialize(_ : BinaryReader, metadataReader : IMetadataReader) : void implements IImportableSymbol.Deserialize
    {
      (Type, HasBody) = DeclarationFieldReflectionUtils.ReflectProperties(metadataReader.Project :> NitraProject, this)
    }
  }
}
