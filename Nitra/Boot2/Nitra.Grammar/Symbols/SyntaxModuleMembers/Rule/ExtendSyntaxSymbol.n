using Nitra.Model;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

using G = NitraSyntaxParseTree;

namespace Nitra.Typing
{
  [Record, ExportableSymbol, ChemicalSymbol]
  public abstract class ExtendSyntaxSymbol : SingleDefSymbol, IRuleDeclarationSite
  {
    public abstract DeclarationSite : IRuleDeclarationSite { get; }
    public abstract Extend          : ExtensibleRuleRef    { get; }
    public override Description     : string               { get { "extend syntax" } }

    public GetDefaultRuleDefinitionOptions() : RuleDefinitionOptions
    {
      DeclarationSite.GetDefaultRuleDefinitionOptions()
    }
  }

  public sealed class ParsedExtendSyntaxSymbol : ExtendSyntaxSymbol
  {
    _node : G.SyntaxModuleMember.ExtendSyntax;

    public this(node : G.SyntaxModuleMember.ExtendSyntax, declarationSite : IRuleDeclarationSite)
    {
      def nameLoc =
        if (node.NameOpt.HasValue)
          node.NameOpt.Value.Item1.Location
        else
          node.BaseName.CalcLocations().Last;
      base(nameLoc);
      _node = node;
      DeclarationSite = declarationSite;
    }

    public override Extend : ExtensibleRuleRef { [Memoize] get { ExtensibleRuleRef.Create(_node.BaseName, _node.Context.Bind(_node.BaseName)) } }
    public override DeclarationSite : IRuleDeclarationSite { get; }
  }

  public sealed class ParsedExtendTokenSymbol : ExtendSyntaxSymbol
  {
    _node : G.SyntaxModuleMember.ExtendToken;

    public this(node : G.SyntaxModuleMember.ExtendToken, declarationSite : IRuleDeclarationSite)
    {
      def nameLoc =
        if (node.NameOpt.HasValue)
          node.NameOpt.Value.Item1.Location
        else
          node.BaseName.CalcLocations().Last;
      base(nameLoc);
      _node = node;
      DeclarationSite = declarationSite;
    }

    public override Extend : ExtensibleRuleRef { [Memoize] get { ExtensibleRuleRef.Create(_node.BaseName, _node.Context.Bind(_node.BaseName)) } }
    public override DeclarationSite : IRuleDeclarationSite { get; }
  }
}
