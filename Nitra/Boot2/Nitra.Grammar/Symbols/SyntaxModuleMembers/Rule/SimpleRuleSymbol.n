using Nemerle;

using Nitra.Model;
using Nitra.ProjectSystem;
using Nitra.Typing.RuleMethod;

using System;

using G = NitraSyntaxParseTree;

namespace Nitra.Typing
{
  public enum SimpleRuleKind
  {
    | Normal
    | Token
    | Void
  }

  [Record, ExportableSymbol]
  public abstract class SimpleRuleSymbol : SyntaxRuleSymbol, IRuleDeclarationSite
  {
    public abstract Kind        : SimpleRuleKind { get; }
    public override Description : string
    {
      get
      {
        match (Kind)
        {
          | Normal => "simple rule"
          | Token  => "token rule"
          | Void   => "void rule"
        }
      }
    }
    public abstract SpliceSource : SymbolRef[SimpleRuleSymbol] { get; }
  }

  [Record, ChemicalSymbol]
  public abstract class BaseSimpleRuleSymbol : SimpleRuleSymbol
  {
    public override IdInGrammar         : int        { [Memoize] get { DeclaringModule.GetNewRuleId() } }
  }

  public sealed class ParsedSimpleRuleSymbol : BaseSimpleRuleSymbol
  {
    public this(node : G.SimpleRule, declarationSite : IRuleDeclarationSite)
    {
      base(node.Name);

      _node            = node;
      DeclarationSite = declarationSite;
    }

    private _node                        : G.SimpleRule;

    public override Kind                : SimpleRuleKind              { get SimpleRuleKind.Normal }
    public override DeclarationSite     : IRuleDeclarationSite        { get; }
    public override Type                : RuleType                    { [Memoize] get { RuleType.Ref(this) } }
    public override Options             : RuleDefinitionOptions       { [Memoize] get { _node.RuleAttributes.Options(DeclarationSite.GetDefaultRuleDefinitionOptions()) } }
    public override Rule                : Rule.Sequence               {           get { _node.TypedRule } }
    public override LastLocation        : option[Location]            { [Memoize] get { Some(_node.SimpleRuleBody.GetLastLocation()) } }
    public override SpliceSource        : SymbolRef[SimpleRuleSymbol] { get; default SymbolRef.None(Location.Default, true) }
    public override Declaration         : SymbolRef[DeclarationSymbol]
    {
      get
      {
        if (_declarationMappingBody != null)
         _declarationMappingBody.Declaration
       else
         SymbolRef.None(Location.Default, true)
      }
    }

    private mutable _declarationMappingBody : DeclarationMappingBody = null;

    internal override SetDeclarationMappingBody(declarationMappingBody : DeclarationMappingBody) : void
    {
      TrySetDeclarationMappingBody(ref _declarationMappingBody, declarationMappingBody)
    }

    public override GetDeclarationMappingBody() : DeclarationMappingBody
    {
      _declarationMappingBody
    }
  }

  public sealed class ParsedTokenRuleSymbol : BaseSimpleRuleSymbol
  {
    public this(node : G.TokenRule, declarationSite : IRuleDeclarationSite)
    {
      base(node.Name);

      _node            = node;
      DeclarationSite = declarationSite;
    }

    private _node                        : G.TokenRule;

    public override Kind                : SimpleRuleKind              { get SimpleRuleKind.Token }
    public override DeclarationSite     : IRuleDeclarationSite        { get; }
    public override Type                : RuleType                    { [Memoize] get { RuleType.Ref(this) } }
    public override Options             : RuleDefinitionOptions       { [Memoize] get { _node.RuleAttributes.Options(RuleDefinitionOptions.CreateTokenRule()) } }
    public override Rule                : Rule.Sequence               {           get { _node.TypedRule } }
    public override LastLocation        : option[Location]            { [Memoize] get { Some(_node.SimpleRuleBody.GetLastLocation()) } }
    public override SpliceSource        : SymbolRef[SimpleRuleSymbol] { get; default SymbolRef.None(Location.Default, true) }
    public override Declaration         : SymbolRef[DeclarationSymbol] { get; default SymbolRef.None(Location.Default, true) }
  }

  public sealed class ParsedVoidRuleSymbol : BaseSimpleRuleSymbol
  {
    public this(node : G.VoidRule, declarationSite : IRuleDeclarationSite)
    {
      base(node.Name);
      _node = node;
      DeclarationSite = declarationSite;
    }

    private _node                        : G.VoidRule;

    public override Kind                : SimpleRuleKind               {           get SimpleRuleKind.Void }
    public override DeclarationSite     : IRuleDeclarationSite         {           get; }
    public override Type                : RuleType                     {           get { RuleType.Void() } }
    public override Options             : RuleDefinitionOptions        { [Memoize] get { _node.RuleAttributes.Options(RuleDefinitionOptions.CreateTokenRule()) } }
    public override Rule                : Rule.Sequence                {           get { _node.TypedRule } }
    public override SpliceSource        : SymbolRef[SimpleRuleSymbol]  {           get; default SymbolRef.None(Location.Default, true) }
    public override Declaration         : SymbolRef[DeclarationSymbol] {           get; default SymbolRef.None(Location.Default, true) }
  }
}
