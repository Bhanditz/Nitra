using Nitra.Runtime;
using Nitra.Model;
using Nitra.Typing.RuleMethod;

using Nemerle;

using G = NitraSyntaxParseTree;

namespace Nitra.Typing
{
  [Record, ExportableSymbol, ChemicalSymbol]
  public abstract class ExtensibleRuleSymbol : SyntaxRuleSymbol, IRuleDeclarationSite
  {
    public sealed override Type          : RuleType                        { [Memoize] get { RuleType.Ref(this) } }
    public sealed override Rule          : Rule.Sequence                   { get { assert3(false) } }
    public        abstract SplicableType : SplicableRuleType               { get; }
    public        override Description   : string                          { get "extensible rule" }
    public        abstract SpliceSource  : SymbolRef[ExtensibleRuleSymbol] { get; }

    public variant SplicableRuleType
    {
      | None
      | Splice       { Type       : RuleType }
      | OptionSplice { OptionType : RuleType }
      | LoopSplice   { LoopType   : RuleType }
    }
  }

  [Record, ChemicalSymbol]
  public abstract class BaseExtensibleRuleSymbol : ExtensibleRuleSymbol
  {
    public override IdInGrammar         : int  { [Memoize] get { DeclaringModule.GetNewRuleId() } }
  }

  public sealed class ParsedExtensibleRuleSymbol : BaseExtensibleRuleSymbol
  {
    public this(node : G.ExtensibleRule, declarationSite : IRuleDeclarationSite)
    {
      base(node.Name);
      _node = node;
      DeclarationSite = declarationSite;
    }

    private _node                    : G.ExtensibleRule;

    public override DeclarationSite : IRuleDeclarationSite            { get; }
    public override Options         : RuleDefinitionOptions           { [Memoize] get { _node.RuleAttributes.Options(DeclarationSite.GetDefaultRuleDefinitionOptions()) } }
    public override SplicableType   : SplicableRuleType               {           get { SplicableRuleType.None() } }
    public override LastLocation    : option[Location]                {           get { Some(_node.RuleBody.GetLastLocation()) } }
    public override SpliceSource    : SymbolRef[ExtensibleRuleSymbol] {           get; default SymbolRef.None(Location.Default, true) }
    public override Ast             : AstType                         {           get { _ast ?? AstType.Void(FirstLocation) } }

    private mutable _ast : AstType = null;

    internal override SetAst(ast : AstType) : void
    {
      TrySetAst(ref _ast, ast)
    }

    private mutable _syntaxMappingExpr : SyntaxMappingExpr = null;

    internal override SetSyntaxMappingExpr(syntaxMappingExpr : SyntaxMappingExpr) : void
    {
      TrySetSyntaxMappingExpr(ref _syntaxMappingExpr, syntaxMappingExpr)
    }

    public override GetSyntaxMappingExpr() : SyntaxMappingExpr
    {
      _syntaxMappingExpr
    }
  }

  public sealed class ParsedExtensibleTokenRuleSymbol : BaseExtensibleRuleSymbol
  {
    public this(node : G.ExtensibleRule, declarationSite : IRuleDeclarationSite)
    {
      base(node.Name);
      _node = node;
      DeclarationSite = declarationSite;
    }

    private _node                        : G.ExtensibleRule;

    public override DeclarationSite     : IRuleDeclarationSite            {           get; }
    public override Options             : RuleDefinitionOptions           { [Memoize] get { _node.RuleAttributes.Options(RuleDefinitionOptions.CreateTokenRule()) } }
    public override SplicableType       : SplicableRuleType               {           get SplicableRuleType.None() }
    public override LastLocation        : option[Location]                {           get { Some(_node.RuleBody.GetLastLocation()) } }
    public override SpliceSource        : SymbolRef[ExtensibleRuleSymbol] {           get; default SymbolRef.None(Location.Default, true) }
    public override Ast                 : AstType                         {           get { _ast ?? AstType.Void(FirstLocation) } }

    private mutable _ast : AstType = null;

    internal override SetAst(ast : AstType) : void
    {
      TrySetAst(ref _ast, ast)
    }

    private mutable _syntaxMappingExpr : SyntaxMappingExpr = null;

    internal override SetSyntaxMappingExpr(syntaxMappingExpr : SyntaxMappingExpr) : void
    {
      TrySetSyntaxMappingExpr(ref _syntaxMappingExpr, syntaxMappingExpr)
    }

    public override GetSyntaxMappingExpr() : SyntaxMappingExpr
    {
      _syntaxMappingExpr
    }
  }
}
