using Nitra.Model;
using Nitra.Typing.RuleMethod;
using Nitra.Utils;

using Nemerle;

using G = N2SyntaxAst;

namespace Nitra.Typing
{
  [Record, ExportableSymbol]
  public abstract class ExtensionRuleSymbol     : SyntaxRuleSymbol, IRuleDeclarationSite
  {
    public        abstract BaseRule             : SymbolRef[ExtensibleRuleSymbol] { get; }
    public        override Description          : string                          { get { "extension rule" } }
    public        override CanParseEmptyString  : bool                            { get { assert(false); } }

    public variant ExtensionRuleType
    {
      | Prefix
      | Postfix { bindingPower : int; rule : Rule.Sequence }
    }

    public ExtensionType : ExtensionRuleType
    {
      [Memoize]
      get
      {
        match (Rule)
        {
          | Sequence(Call(baseRule, bindingPower) :: rules) when BaseRule == baseRule => ExtensionRuleType.Postfix(bindingPower, Rule.Sequence(Rule, rules))
          | _                                                                         => ExtensionRuleType.Prefix()
        }
      }
    }
  }

  public sealed class ParsedExtensionRule : ExtensionRuleSymbol
  {
    public this(ast : G.ExtensionRule.Ast, declarationSite : IRuleDeclarationSite)
    {
      def (loc, name) =
        match (ast.NameOpt)
        {
          | Some((name, _)) => (name.Location, name.GetText())
          | _ =>
            def name1 = ast.RuleExpression.InferName();
            def name2 = if (IsIdentifier(name1)) ToPascalCase(name1) else "";

            when (name2 == "")
              ProjectSystem.N2Project.CurrentProject.Error(ast.Location, "Can't infer name for this rule.", -1);
            (Location(ast.Location.Source, ast.Pipe.EndPos, ast.Pipe.EndPos), name2);
        };

      base(loc, name);
      _ast = ast;
      DeclarationSite = declarationSite;
    }

    public _ast                      : G.ExtensionRule.Ast;

    public override DeclarationSite  : IRuleDeclarationSite  { get; }
    public override BaseRule         : ExtensibleRuleRef     { get { _ast.BaseRuleRef } }
    public override Type             : RuleType              { get { _ast.Type } }
    public override Options          : RuleDefinitionOptions { get { _ast.Options } }
    public override Rule             : Rule.Sequence         { get { _ast.TypedRule } }
    public override LastLocation     : option[Location]
    {
      [Memoize]  get
      {
        match (_ast.ExtensionRuleBodyOpt)
        {
          | Some(body) => Some(body.GetLastLocation())
          | _          => Some(Utils.MakeLastLocation(_ast))
        }
      }
    }
    public override IdInGrammar         : int        { [Memoize] get { DeclaringModule.GetNewRuleId() } }
    public override FirstCharLowerBound : char       {           get { FirstCharBounds[0] } }
    public override FirstCharUpperBound : char       {           get { FirstCharBounds[1] } }
    private         FirstCharBounds     : CharBounds
    {
      [Memoize] get
      {
        match (ExtensionType)
        {
          | Prefix           => EvalFirstCharBounds(Rule)
          | Postfix(_, rule) => EvalFirstCharBounds(rule)
        }
      }
    }
  }
}
