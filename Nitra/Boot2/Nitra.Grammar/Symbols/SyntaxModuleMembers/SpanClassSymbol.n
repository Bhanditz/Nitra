using Nemerle;
using Nemerle.Collections;
using Nemerle.Extensions;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra.Model;

using System;
using System.Collections.Generic;
using System.Linq;

using G = NitraSyntaxParseTree;

namespace Nitra.Typing
{
  [Record, ExportableSymbol]
  public abstract class SpanClassSymbol : SingleDefSymbol
  {
    public abstract RegularRule     : option[RegularRule] { get; }
    public override Description     : string              { get "span class" }
    public abstract DeclaringModule : SyntaxModuleSymbol  { get; }
    public          Matcher         : string -> bool
    {
      [Memoize] get
      {
        assert(Node != null);
        match (this.RegularRule)
        {
          | Some(rr) => rr.MakeMatcher(Node.Project)
          | None     => nullMatcher
        }
      }
    }

    private static nullMatcher : string -> bool = _ => false;
  }

  public sealed class ParsedSpanClassSymbol : SpanClassSymbol
  {
    private _node : G.SyntaxModuleMember.SpanClass;

    public override DeclaringModule : SyntaxModuleSymbol { get; }

    public this(node : G.SyntaxModuleMember.SpanClass, context : Context)
    {
      base(node.Name);
      _node = node;
      def syntaxModuleSymbol = context.GetContext.[SyntaxModuleContext]().SyntaxModuleSymbol;
      DeclaringModule = syntaxModuleSymbol;
    }

    public override RegularRule : option[RegularRule]        { get { _node.RegularRule() } }
  }
}
