using Nemerle;
using Nemerle.Internal;
using Nemerle.Collections;
using Nemerle.Extensions;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Text;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Typing
{
  [StructuralEquality]
  public variant DependencyPath
  {
    | This
    | Variable
      {
        Name : string;
      }
    | DeclaredProperty
      {
        Path   : DependencyPath;
        Symbol : PropertySymbol;
        [EqualsIgnore] Mask : int;

        [RecordCtor]
        public new this([MappedMember("Path")] path : DependencyPath, [MappedMember("Symbol")] symbol : PropertySymbol, [MappedMember("Mask")] mask : int)
        {
          Path   = path;
          Symbol = if (symbol.OverrideProperty is SymbolRef.Some(originalSymbol)) originalSymbol else symbol;
          Mask   = mask;
        }
      }
    | DependentProperty
      {
        Path            : DependencyPath;
        Name            : string;
        IsEvaluatedName : string;
      }
    | SimpleProperty
      {
        Path : DependencyPath;
        Name : string;
      }

    public BasePath : ValueOption[DependencyPath]
    {
      get
      {
        match (this)
        {
          | This
          | Variable => VNone()
          | DeclaredProperty(basePath, _, _)
          | DependentProperty(basePath, _, _)
          | SimpleProperty(basePath, _) => VSome(basePath)
        }
      }
    }

    public IsDependent          : bool
    {
      get
      {
        match (this)
        {
          | DeclaredProperty(_, DependentPropertySymbol, _)
          | DependentProperty => true
          | _ => false
        }
      }
    }

    public IsSinglePropertyPath : bool
    {
      get
      {
        match (this)
        {
          | DeclaredProperty(This, _, _)
          | DependentProperty(This, _, _)
          | SimpleProperty(This, _) => true
          | _ => false
        }
      }
    }

    public override ToString() : string
    {
      ToString(StringBuilder()).ToString()
    }

    public ToString(buffer : StringBuilder) : StringBuilder
    {
      match (this)
      {
        | Variable(name) => buffer.Append(name)
        | This           => buffer.Append("this");
        | DeclaredProperty(This, symbol, _) with name = symbol.Name
        | DependentProperty(This, name, _)
        | SimpleProperty(This, name) => buffer.Append(name)
        | DeclaredProperty(path, symbol, _) with name = symbol.Name
        | DependentProperty(path, name, _)
        | SimpleProperty(path, name) => path.ToString(buffer).Append('.').Append(name)
      }
    }
  }
}
