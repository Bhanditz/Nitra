using Nitra.Model;
using Nitra.Serialization;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using G = NitraSyntaxParseTree;

namespace Nitra.Typing
{
  [Record, ExportableSymbol, ChemicalSymbol]
  public abstract class OutAstPropertySymbol : DependentAstPropertySymbol
  {
    public override Direction   : DependentPropertyDirection { get DependentPropertyDirection.Out }
    public override Description : string                     { get "output declaration property" }
  }

  public sealed class ParsedOutAstPropertySymbol : OutAstPropertySymbol
  {
    private _node : G.AstMember.OutProperty;

    public this(node : G.AstMember.OutProperty)
    {
      base(node.Name, node.Name.Value());
      _node            = node;
      OverrideProperty = SymbolRef.None(_node.Name.Location, false);
      LinkedProperty   = SymbolRef.None(_node.Name.Location, false);
    }

    public override OverrideProperty : SymbolRef[AstPropertySymbol]          { get; }
    public override LinkedProperty   : SymbolRef[DependentAstPropertySymbol] { get; }
    public override Type             : NType                                 { get { _node.Type } }
    public override AstType          : AstType                               { get { _node.AstType } }
    public override Stage            : int                                   { get { _node.Stage() } }
  }

  public sealed class ParsedAutoOutAstPropertySymbol : OutAstPropertySymbol
  {
    private _node : G.AstMember.InOutProperty;

    public this(node : G.AstMember.InOutProperty)
    {
      base(node.Name, node.Name.Value() + "Out");
      _node            = node;
      OverrideProperty = SymbolRef.None(_node.Name.Location, false);
    }

    public override OverrideProperty : SymbolRef[AstPropertySymbol]          {           get; }
    public override LinkedProperty   : SymbolRef[DependentAstPropertySymbol] { [Memoize] get { SymbolRef.Some(_node.Name.Location, false, _node.InSymbol) } }
    public override Type             : NType                                 {           get { _node.Type } }
    public override AstType          : AstType                               {           get { _node.AstType } }
    public override Stage            : int                                   {           get { _node.Stage() } }
  }
}
