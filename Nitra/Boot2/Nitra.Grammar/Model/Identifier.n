using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Model
{
  public class Identifier : Nitra.Located, IComparable[Identifier]
  {
    public Value : string { get; }

    public this(location : Nitra.Location, value : string)
    {
      base(location);
      Value = value;
    }

    public this(parent : Nitra.Located, token : Nitra.NToken) { this(Nitra.Location(parent.Location.Source, token)) }
    public this(location : Nitra.Location)                 { this(location, location.GetText()) }
    public this(located  : Nitra.Located)                  { this(located,  located.GetText()) }

    public this(pexpr : PExpr)
    {
      this(pexpr.Location.NitraLocation(), pexpr.ToString())
    }

    public override ToString() : string { Value }

    public CompareTo(that : Identifier) : int { Value.CompareTo(that.Value) }

    [Nemerle.OverrideObjectEquals]
    public Equals(that : Identifier)   : bool { Value.Equals(that.Value, StringComparison.Ordinal) }
    public EqualsToName(name : string) : bool { Value.Equals(name, StringComparison.Ordinal) }

    public override GetHashCode() : int { Value.GetHashCode() }

    public static @==(name1 : Identifier, name2 : Identifier) : bool
    {
      if (name1 : object == name2)
        true
      else if (null == name1 || null == name2)
        false
      else
        name1.Equals(name2)
    }

    public static @!=(name1 : Identifier, name2 : Identifier) : bool
    {
      !(name1 == name2)
    }
  }
}
