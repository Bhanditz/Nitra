using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra.Declarations;

using System;
using System.Collections.ObjectModel;
using System.Collections.Generic;
using System.Collections.Specialized;
using System.ComponentModel;
using System.Linq;

namespace Nitra.ProjectSystem
{
  public class FsProject[TAst] : Project
    where TAst: IAst
  {
    public FsSolution : FsSolution[TAst]                   { get; internal set; }
    public FsFiles    : ObservableCollection[FsFile[TAst]] { get; private set; }

    public this([NotNull] solution : FsSolution[TAst])
    {
      base();
      FsFiles = ObservableCollection();
      FsFiles.CollectionChanged += OnCollectionChanged;
      solution.FsProjects.Add(this);
    }
    
    public this([NotNull] solution : FsSolution[TAst], [NotNull] files : Seq[FsFile[TAst]])
    {
      this(solution);
      foreach (file in files)
        this.FsFiles.Add(file);
    }

    public this([NotNull] solution : FsSolution[TAst], [NotNull] file : FsFile[TAst])
    {
      this(solution);
      this.FsFiles.Add(file);
    }

    public override Files : Seq[File]
    {
      get { this.FsFiles }
    }
    
    OnCollectionChanged(_sender : object, e : NotifyCollectionChangedEventArgs) : void
    {
      when (e.OldItems != null)
      {
        foreach(oldItem is FsFile[TAst] in e.OldItems)
        {
          oldItem.FsProject = null;
          //oldItem.PropertyChanged -= this.OnItemPropertyChanged;
        }
      }

      when (e.NewItems != null)
      {
        foreach(newItem is FsFile[TAst] in e.NewItems)
        {
          def oldProj = newItem.FsProject;
          when (oldProj != null)
            _ = oldProj.FsFiles.Remove(newItem);
          newItem.FsProject = this;
          //Add listener for each item on PropertyChanged event
          //newItem.PropertyChanged += this.OnItemPropertyChanged;         
        }
      }
    }

    //OnItemPropertyChanged(sender : object, e : PropertyChangedEventArgs) : void
    //{
    //  when (sender is FsFile as item)
    //    ModifiedItems.Add(item);
    //}
  }
}
