using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.ProjectSystem
{
  public class ConsoleCompilerMessages : TextCompilerMessages
  {
    public this()
    {
      base(Console.Out)
    }

    public this(numberPrefix : string)
    {
      base(Console.Out, numberPrefix)
    }

    protected override ReportMessage(loc : Location, msg : string, num : int, messageType : CompilerMessageType) : void
    {
      def oldColor = Console.ForegroundColor;
      try
      {
        Console.ForegroundColor = GetMessageColor(messageType);
        base.ReportMessage(loc, msg, num, messageType);
      }
      finally Console.ForegroundColor = oldColor;
    }

    protected static GetMessageColor(messageType : CompilerMessageType) : ConsoleColor
    {
      | FatalError | Error => ConsoleColor.Red
      | Warning            => ConsoleColor.Yellow
      | Hint               => ConsoleColor.Gray
    }
  }
}
