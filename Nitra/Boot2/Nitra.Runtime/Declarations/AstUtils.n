using Nitra.Declarations;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Reflection;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Internal
{
  public module AstUtils
  {
    public GetAstAmbiguities[TParseTree, TAst](ambiguities : array[TParseTree], context : AstContext) : array[TAst]
      where TParseTree   : IMappedParseTree[TAst]
      where TAst         : IAst
    {
      def result = array(ambiguities.Length);
      foreach (node in ambiguities with index)
        result[index] = node.GetAst(context);
      result
    }

    public GetAstAmbiguities[TParseTree, TAst](parseTree : IAmbiguousParseTree, context : AstContext, getAst : Func[TParseTree, AstContext, TAst]) : array[TAst]
      where TParseTree : IParseTree
      where TAst       : IAst
    {
      def result = array(parseTree.Ambiguities.Count);
      foreach (pt :> TParseTree in parseTree.Ambiguities with index)
        result[index] = getAst(pt, context);
      result
    }

    public EvalAmbiguitiesProperties(context : DependentPropertyEvalContext, ambiguities : array[IAst]) : void
    {
      foreach (ambiguity in ambiguities)
        ambiguity.EvalProperties(context);
    }

    public SetAmbiguitiesParent(parent : IAst, ambiguities : array[IAst]) : void
    {
      foreach (ambiguity in ambiguities)
        ambiguity.SetParent(parent);
    }
  }
}
