using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra.Model;
using Nitra.Typing;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Compiler
{
  internal partial class FindRecoveryPositionMethodEmitter
  {
    [Record]
    private sealed class SequenceData : SequenceDataBase[FindRecoveryPositionMethodEmitter]
    {
      [RecordIgnore] public AstPtr : AstPointer { get; private set; }

      public override Init() : void
      {
        AstPtr = AstPointer.New(Sequence, this.Emitter.ConstantEmitter, this.Emitter, "astPtr");
      }
    }

    [Record]
    private sealed class SubruleData : SubruleDataBase[FindRecoveryPositionMethodEmitter]
    {
      [RecordIgnore] public SelectStartStateLabel : PExprLabel { get; private set; }
      public override Init() : void
      {
        SelectStartStateLabel = Emitter.Environment.MakeLabel($"SelectStartStateLabel_$LabelSuffix");
      }
    }

    public CheckState(subrule : SubruleCompileInfo,  code : PExpr) : PExpr
    {
      <[
        when (startState <= $(subrule.State))
          $code;
        if (parseState <= $(subrule.State)) ret() else $CurTextPos += $(AstPtr(subrule.Owner).GetSize(subrule));
      ]>
    }

    _data : SequenceDataContainer[FindRecoveryPositionMethodEmitter, SequenceData, SubruleData];
#pragma warning disable 10003
    private FunctionName (sequence : SequenceCompileInfo) : string     { sequence.FindRecoveryPositionFunctionName           }
    private AstPtr       (sequence : SequenceCompileInfo) : AstPointer { _data.SequenceData[sequence].AstPtr            }

    private SelectStartStateLabel    (subrule : SubruleCompileInfo) : PExpr { _data.SubruleData[subrule].SelectStartStateLabel.Label()         }
    private GotoSelectStartStateLabel(subrule : SubruleCompileInfo) : PExpr { _data.SubruleData[subrule].SelectStartStateLabel.Goto()          }
#pragma warning restore
  }
}
