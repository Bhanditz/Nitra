using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;

using Nitra.Internal;
using Nitra.Model;
using Nitra.Typing;

using System;
using System.Diagnostics;
using System.Linq;
using SCG = System.Collections.Generic;

namespace Nitra.Compiler
{
  internal abstract partial class RuleParseTreeEmitter
  {
    protected DefineParseTreeApplyMethod(fields : SCG.List[FieldInfo], _nodeTb : TypeBuilder, _chemicalTb : TypeBuilder, _parsedTb : TypeBuilder) : void
    {
      def ctorArguments = SCG.List();
      ctorArguments.Add(<[ this.GetSourceSnapshotProvider() ]>);
      ctorArguments.Add(<[ this.GetLocationSpan() ]>);

      def statements = SCG.List();

      foreach (field in fields)
      {
        if (field.Rule.Type is RuleType.Chars)
          ctorArguments.Add(<[ this.$(field.Name : usesite) ]>);
        else
        {
          def tempVarName = Util.tmpname(field.Name);
          statements.Add(<[ def $(tempVarName : usesite) = visitor.Visit(this.$(field.Name : usesite)) :> $(field.Type : typed) ]>);
          statements.Add(<[ isUntouched &= ReferenceEquals($(tempVarName : usesite), this.$(field.Name : usesite)) ]>);
          ctorArguments.Add(<[ $(tempVarName : usesite) ]>);
        }
      }

      when (statements.Count > 0)
      {
        _nodeTb.DefineConditional(<[ decl:
          [Nemerle.Diagnostics.CompileStatistic("ParseTree")]
          public sealed override Apply(visitor : Nitra.ParseTreeVisitor) : Nitra.ParseTree
          {
            unchecked
            {
              visitor.Enter(this);
              mutable isUntouched = true;
              ..$statements;
              def result = if (isUntouched) this else $(_chemicalTb.GetMemType() : typed)(..$ctorArguments);
              visitor.Leave(this, result);
              result
            }
          }
        ]>);

        _chemicalTb.DefineConditional(<[ decl:
          protected override GetSourceSnapshotProvider() : Nitra.Runtime.ISourceSnapshotProvider
          {
            this.sourceProvider
          }
        ]>);

        _chemicalTb.DefineConditional(<[ decl:
          protected override GetLocationSpan() : Nitra.NSpan
          {
            this.span
          }
        ]>);

        _parsedTb.DefineConditional(<[ decl:
          protected override GetSourceSnapshotProvider() : Nitra.Runtime.ISourceSnapshotProvider
          {
            this.parseResult
          }
        ]>);

        _parsedTb.DefineConditional(<[ decl:
          protected override GetLocationSpan() : Nitra.NSpan
          {
            Nitra.NSpan(this.startPos, this.endPos)
          }
        ]>);
      }
    }
  }
}
