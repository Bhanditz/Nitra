using Nitra.Runtime;
using Nitra.Model;
using Nitra.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Compiler
{
  [Record]
  public sealed class HighlighterWalkerGenerator : WalkerGenerator
  {
    public Environment : EmitterEnvironment { get; }

    public override Name : string { get StandardWalkerTypes.Highlighter }

    public override SupportsVoidRules : bool { get true }

    public override GetSignature(_rule : RuleSymbol) : WalkerSignature
    {
      WalkerSignature(
        [
          <[ parameter: _spans    : System.Collections.Generic.List[Nitra.SpanInfo] ]>,
          <[ parameter: _startPos : int ]>,
          <[ parameter: _endPos   : int ]>
        ],
        <[ void ]>);
    }

    protected override GetRuleValue(subrule : SubruleCompileInfo, parentAstPointer : AstPointerScope) : PExpr
    {
      def spanExpr = GetSpanOf(subrule, parentAstPointer);

      match (subrule)
      {
        | Empty      as subrule => Skip(subrule, parentAstPointer)
        | SimpleCall as subrule =>
          <[
            if ($spanExpr.IntersectsWith(_startPos, _endPos))
              $(CallSimpleRule(subrule, parentAstPointer, <[ _spans ]>, <[ _startPos ]>, <[ _endPos ]>))
            else
              $(Skip(subrule, parentAstPointer))
          ]>

        | ExtensibleCall as subrule =>
          <[
            if ($spanExpr.IntersectsWith(_startPos, _endPos))
              $(CallExtensibleRule(subrule, parentAstPointer, <[ _spans ]>, <[ _startPos ]>, <[ _endPos ]>))
            else
              $(Skip(subrule, parentAstPointer))
          ]>

        | RegularCall(symbol) =>
          if (symbol.Options.SpanClassSymbol != null)
            <[
              def loc = $spanExpr;
              when (loc.IntersectsWith(_startPos, _endPos))
                _spans.Add(Nitra.SpanInfo(loc, $(MakeSpanClassFieldName(symbol.Options.SpanClassSymbol))));
              $(Skip(subrule, parentAstPointer))
            ]>
          else
            Skip(subrule, parentAstPointer)

        | TokenString =>
          def rule = subrule.SrcRule :> Rule.String;
          if (rule.SpanClassOpt is Some(spanClass))
            <[
              when ($spanExpr.IntersectsWith(_startPos, _endPos))
                _spans.Add(Nitra.SpanInfo($spanExpr, $(MakeSpanClassFieldName(spanClass))));
              $(Skip(subrule, parentAstPointer))
            ]>
          else
            Skip(subrule, parentAstPointer)

        | Option as subrule =>
          GetOptionValue(
            subrule            = subrule,
            parentAstPointer = parentAstPointer,
            hasValue         = <[ _ = $(GetRuleValue(subrule.Rule, "valuePointer")); ]>,
            noValue          = <[ () ]>)

        | List as lst when lst.IsSkipAnyPattern => Skip(subrule, parentAstPointer)
        | List as lst=>
          GetListValue(subrule            = lst,
            parentAstPointer = parentAstPointer,
            init             = <[ () ]>,
            item             = GetRuleValue(lst.Rule, "itemPointer"),
            returnCode       = <[ () ]>,
            emptyCode        = <[ () ]>);

        | ListWithSeparator as lst=>
          GetListWithSeparatorValue(
            subrule            = lst,
            parentAstPointer = parentAstPointer,
            init             = <[ () ]>,
            item             = GetRuleValue(lst.Rule, "itemPointer"),
            separator        = GetRuleValue(lst.Separator, "separatorPointer"),
            returnCode       = <[ () ]>,
            emptyCode        = <[ () ]>);

        | Marker => <[ () ]>
      }
    }

    MakeSpanClassFieldName(spanClassSymbol : SpanClassSymbol) : PExpr
    {
      assert2(spanClassSymbol != null);
      def expr = PExprUtils.FromQualifiedIdentifier(spanClassSymbol.DeclaringModule.Node.FullNameParts);
      <[ $expr.$("SpanClass_" + spanClassSymbol.Name : usesite) ]>
    }

    protected override CombineValues(_sequence : SequenceCompileInfo, arguments : list[PExpr]) : PExpr
    {
      <[ ignore(..$arguments) ]>
    }

    protected override GetSyntaxRuleValue(ruleSymbol : SyntaxRuleSymbol, astPointer : AstPointerScope) : PExpr
    {
      def sequence = ruleSymbol.Sequence;
      def statementsList = List();

      foreach (subrule in sequence.Subrules)
        statementsList.Add(GetRuleValue(subrule, astPointer));

      def statements = statementsList.NToList();

      if (ruleSymbol.Options.SpanClassSymbol != null)
        <[
          { ..$statements }
          def loc = Nitra.NSpan(__startPos, pos);
          when (loc.IntersectsWith(_startPos, _endPos))
            _spans.Add(Nitra.SpanInfo(loc, $(MakeSpanClassFieldName(ruleSymbol.Options.SpanClassSymbol))))
        ]>
      else
        <[ { ..$statements } ]>
    }

    public override PrefixAmbiguity(_ruleSymbol : ExtensibleRuleSymbol) : PExpr
    {
      <[ ignore(parseResult, endPos, ambiguities); ]>
    }

    public override PostfixAmbiguity(_ruleSymbol : ExtensibleRuleSymbol) : PExpr
    {
      <[ ignore(parseResult, endPos, ambiguities); ]>
    }

    public override PrefixMissed(_ruleSymbol : ExtensibleRuleSymbol) : PExpr
    {
      <[ ignore(parseResult, pos); ]>
    }

    public override SimpleMissed(_ruleSymbol : SimpleRuleSymbol) : PExpr
    {
      <[ ignore(parseResult, pos); ]>
    }
  }
}
