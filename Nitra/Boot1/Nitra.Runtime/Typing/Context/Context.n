using Nitra.Runtime;
using Nitra.ProjectSystem;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Diagnostics;
using System.Linq;

namespace Nitra.Typing
{
  [Record]
  [DebuggerDisplay("{DebugViewInline}")]
  public abstract class Context : IContext, ICompilerMessages
    //where TProject: Project
  {
    public Parent  : Context { get; }
    public Parents : Seq[Context]
    {
      get
      {
        def lst = List();
        def loop(context : Context) : void
        {
          | null => ()
          | _    => lst.Add(context); loop(context.Parent);
        }

        loop(this);
        lst.Reverse();
        lst
      }
    }

    public virtual Project : Project { get { Parent.Project } }

    public virtual BindMany[T](path : list[Location]) : BindingResult[T]
      where T: Symbol
    {
      Parent.BindMany(path)
    }

    public virtual BindMany[T](name : Location) : BindingResult[T]
      where T: Symbol
    {
      Parent.BindMany(name)
    }

    public DebugView : string
    {
      get { $<#..$(Parents; "\r\n"; p => $"$p        $(p.GetType().Name)")#> }
    }

    public DebugViewInline : string
    {
      get { $<#..$(Parents; "  ->  ")#> }
    }

    public Hint         (loc : Location, msg : string, num : int = -1) : void                  { Project.Hint(loc, msg, num)        }
    public Warning      (loc : Location, msg : string, num : int = -1) : void                  { Project.Warning(loc, msg, num)     }
    public Error        (loc : Location, msg : string, num : int = -1) : void                  { Project.Error(loc, msg, num)       }
    public FatalError[T](loc : Location, msg : string, num : int = -1) : T                     { Project.FatalError(loc, msg, num)  }
    public RootHint     (loc : Location, msg : string, num : int = -1) : IRootCompilerMessages { Project.RootHint(loc, msg, num)    }
    public RootWarning  (loc : Location, msg : string, num : int = -1) : IRootCompilerMessages { Project.RootWarning(loc, msg, num) }
    public RootError    (loc : Location, msg : string, num : int = -1) : IRootCompilerMessages { Project.RootError(loc, msg, num)   }
  }

  public interface IContext
  {
    Parent  : Context { get; }
    Project : Project { get; }
  }
}
