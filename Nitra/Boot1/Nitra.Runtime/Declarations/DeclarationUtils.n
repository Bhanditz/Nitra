using Nitra.Declarations;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Internal
{
  public module DeclarationUtils
  {
    public GetDeclarationAmbiguities[TParseTree, TDeclaration](parent : Declaration, ambiguities : array[TParseTree]) : array[TDeclaration]
      where TParseTree   : IDeclarableParseTree
      where TDeclaration : Declaration
    {
      def result = array(ambiguities.Length);
      foreach (node in ambiguities with index)
        result[index] = node.GetDeclaration(parent) :> TDeclaration;
      result
    }

    public MakeList[TParseTree, TDeclaration](parent : Declaration, items : ListParseTree[TParseTree]) : DeclarationList[TDeclaration]
      where TParseTree   : ParseTree, IDeclarableParseTree
      where TDeclaration : Declaration
    {
      def result = DeclarationList(parent, items.Location.Span);
      def declarations = array(items.Length);
      foreach (node in items with index)
        declarations[index] = node.GetDeclaration(result) :> TDeclaration;
      result.Initialize(declarations);
      result
    }

    public MakeOption[TParseTree, TDeclaration](parent : Declaration, item : OptionParseTree[TParseTree]) : DeclarationOption[TDeclaration]
      where TParseTree   : ParseTree, IDeclarableParseTree
      where TDeclaration : Declaration
    {
      if (item.HasValue)
      {
        def result = DeclarationOption.Some(parent, item.Location.Span);
        result.Initialize(item.Value.GetDeclaration(result) :> TDeclaration);
        result
      }
      else
        DeclarationOption.None(parent, item.Location.Span)
    }
  }
}
