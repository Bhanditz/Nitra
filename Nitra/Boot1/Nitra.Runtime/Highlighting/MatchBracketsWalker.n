using Nemerle;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra.Internal;
using Nitra.Internal.Recovery;
using Nitra.Runtime.Reflection;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Runtime.Highlighting
{
  public sealed class MatchBracketsWalker : AstWalkerBase[Context]
  {
    _stacks : Stack[Stack[NSpan]] = Stack();

    [Record]
    public struct MatchBrackets
    {
      public OpenBracket  : NSpan;
      public CloseBracket : NSpan;
    }
    
    [Record]
    public class Context
    {
      public Position : int;
      
      [RecordIgnore]
      public Brackets : List[MatchBrackets] = List();
      
      public Add(openBraceSpan : NSpan, closeBraceSpan : NSpan) : void
      {
        this.Brackets.Add(MatchBrackets(openBraceSpan, closeBraceSpan));
      }
    }

    public this()
    {
    }

    public override StartWalkSequence(_sequenceInfo : SequenceInfo) : void
    {
      _stacks.Push(Stack());
    }
    
    public override FinishWalkSequence(_sequenceInfo : SequenceInfo) : void
    {
      def braceStack = _stacks.Pop();
      assert2(braceStack.Count == 0, "Found the unpaired brackets");
    }
    
    public override OnTokenString(ruleInfo : SubruleInfo.TokenString, startPos : int, endPos : int, parseResult : ParseResult, context : Context) : void
    {
      _ = parseResult;
      when (endPos >= 0)
      {
        //ruleInfo.Descriptor
        def spanClass = ruleInfo.SpanClass;
        when (spanClass != null)
        {
          if (spanClass.Name == "OpenBrace")
            _stacks.Peek().Push(NSpan(startPos, endPos));
          else when (spanClass.Name == "CloseBrace")
          {
            def braceStack = _stacks.Peek();
            def pos = context.Position;
            
            when (braceStack.Count > 0)
            {
              def openBraceSpan = braceStack.Pop();
              when (openBraceSpan.IntersectsWith(pos) || pos >= startPos && pos <= endPos)
                context.Add(openBraceSpan, NSpan(startPos, endPos));
            }
          }
        }
      }
    }
  }
}
