using Nitra.Runtime;
using Nitra.Internal;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;

namespace Nitra.Quote
{
  internal module QuotationUtils
  {
    public GetAstTypeRef(manager : ManagerClass, ruleDescriptor : RuleDescriptor) : PExpr
    {
      def namePrefix      = "Splicable";
      def grammarFullName = ruleDescriptor.Grammar.FullName;
      assert(grammarFullName.StartsWith(namePrefix));
      def astFullName     = grammarFullName.Substring(namePrefix.Length) + "Ast." + ruleDescriptor.Name;
      PExpr.FromQualifiedIdentifier(manager, astFullName);
    }

    public delegate PostfixWalker(prefix : PExpr, prefixStartPos : int, pos : ref int, astPtr : int, ruleParser : ExtensionRuleParser, parseResult : ParseResult, builder : ISplicableAstBuilder[PExpr]) : PExpr;

    [Memoize]
    public GetPostfixWalker(ruleDescriptor : RuleDescriptor) : PostfixWalker { GetWalker(ruleDescriptor) }

    public delegate PrefixWalker(pos : ref int, astPtr : int, ruleParser : ExtensionRuleParser, parseResult : ParseResult, builder : ISplicableAstBuilder[PExpr]) : PExpr;

    [Memoize]
    public GetPrefixWalker(ruleDescriptor : RuleDescriptor) : PrefixWalker { GetWalker(ruleDescriptor) }

    private GetWalker[TDelegate](ruleDescriptor : RuleDescriptor) : TDelegate
    {
      def grammarAssembly = ruleDescriptor.Grammar.GetType().Assembly;
      def walkerType      = grammarAssembly.GetType(ruleDescriptor.Grammar.FullName + "AstWalker", throwOnError = true);
      def walkMethod      = walkerType.GetMethod(ruleDescriptor.Name.Replace('.', '_')).MakeGenericMethod(typeof(PExpr));
      Delegate.CreateDelegate(typeof(TDelegate), walkMethod) :> TDelegate
    }

    public GetText(parseResult : ParseResult, startPos : int, endPos : int) : string
    {
      parseResult.OriginalSource.Text.Substring(startPos, endPos - startPos);
    }

    public MakeLocation(offset : TextPoint, parseResult : ParseResult, startPos : int, endPos : int) : Nemerle.Compiler.Location
    {
      def source                = parseResult.OriginalSource;
      def (startLine, startCol) = TranslateLineColumn(offset, source.PositionToLineColumn(startPos));
      def (endLine, endCol)     = TranslateLineColumn(offset, source.PositionToLineColumn(endPos));
      Nemerle.Compiler.Location(source.FileIndex, startLine, startCol, endLine, endCol)
    }

    public TranslateLineColumn(offset : TextPoint, point : LineColumn) : TextPoint
    {
      if (point.Line == 1)
        TextPoint(offset.Line, offset.Column + point.Column)
      else
        TextPoint(offset.Line + point.Line - 1, point.Column)
    }
  }
}
