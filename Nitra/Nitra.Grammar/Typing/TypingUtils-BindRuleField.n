using Nitra.Model;
using Nitra.ProjectSystem;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;

namespace Nitra.Typing
{
  public partial module TypingUtils
  {
    internal BindRuleField(ruleSymbol : SyntaxRuleSymbol, loc : Location, path : list[LocatedName], compilationMessages : ICompilerMessages) : option[ParseTreePath]
    {
      def eq(a : string, b : string)
      {
        String.Equals(a, b, StringComparison.InvariantCulture)
      }

      def bind(qualifier : ParseTreePath, name : LocatedName, tail : list[LocatedName]) : option[ParseTreePath]
      {
        def failure() : option[ParseTreePath]
        {
          compilationMessages.Error(name.Location, $"Unbound name '$(name.Id)'");
          None()
        }

        def success(ruleType : RuleType) : option[ParseTreePath]
        {
          def path = ParseTreePath.Member(qualifier.Location + name.Location, qualifier, name, ruleType);
          match (tail)
          {
            | [] => Some(path)
            | nextName :: nextTail => bind(path, nextName, nextTail)
          }
        }

        match (qualifier.Type)
        {
          | Ref(symbol) =>
            def loop(rules : list[Rule])
            {
              | [] => failure()
              | rule :: _ when eq(name.Id, rule.FieldName) => success(rule.Type)
              | _ :: otherRules => loop(otherRules)
            }
            loop(symbol.NonVoidSubRules)

          | Tuple(members) =>
            def loop(members, index)
            {
              match (members)
              {
                | [] => failure()
                | member :: _ when eq(name.Id, "Item" + index.ToString()) => success(member)
                | _ :: otherMembers => loop(otherMembers, index + 1)
              }
            }
            loop(members, 1)

          | _ => failure()
        }
      }

      match (path)
      {
        | [] =>
          compilationMessages.Error(loc, "No rule field name speciefied");
          None()

        | name :: tail =>
          def thisLoc = Location(loc.Source, NSpan(loc.StartPos, loc.StartPos));
          def path    = ParseTreePath.This(thisLoc, ruleSymbol);
          bind(path, name, tail)
      }
    }
  }
}
