using Nitra.ProjectSystem;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;

namespace Nitra.Typing
{
  public partial module TypingUtils
  {
    public SetReferenceDeclarationMapping(project : NitraProject) : void
    {
      when (project.RootNamespace.Node.BindMany.[DeclarationSymbol](["Nitra", "Runtime", "Binding", "Reference"]) is [referenceSymbol])
      {
        mutable namePropertyRef;

        foreach (symbol when symbol.AssemblyId == AssemblyIds.CurrentAssembly && symbol.Options.IsIdentifier in project.RootNamespace.Node.GetDescendants.[SyntaxRuleSymbol]())
        {
          match (symbol)
          {
            | SimpleRuleSymbol
            | ExtensionRuleSymbol =>
              def exisingBody = symbol.GetDeclarationMappingBody();
              when (exisingBody == null)
              {
                when (namePropertyRef : object == null)
                {
                  def namePropertyCandidates = referenceSymbol.Node.BindMany.[DeclarationPropertySymbol]("Name");
                  def bindingResult          = BindingUtils.MakeBindingResult(symbol.FirstLocation, namePropertyCandidates);
                  namePropertyRef            = Utils.MakeSymbolRefWithMessages(bindingResult, project);
                }

                def bodyLocation         = symbol.FirstLocation;
                def declarationSymbolRef = SymbolRef.Some(symbol.FirstLocation, true, referenceSymbol);
                def namePropertyMapping  = DeclarationMappingField.Inline(symbol.FirstLocation, namePropertyRef, Util.locate(symbol.FirstLocation.NLocation(), <[ Nitra.Runtime.Binding.Name.GetOrAdd(this.GetText()) ]>));
                def newBody              = DeclarationMappingBody.PerField(bodyLocation, declarationSymbolRef, [namePropertyMapping]);
                symbol.SetDeclarationMappingBody(newBody)
              }

            | _ => ()
          }
        }
      }
    }
  }
}
