using Nitra.Model;
using Nitra.ProjectSystem;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;

using G = NitraSyntaxParseTree;

namespace Nitra.Typing
{
  public partial module TypingUtils
  {
    internal TypeAstInheritance(astInheritance : OptionParseTree[G.AstInheritance], context : Context, isDeclaration : bool) : list[AstSymbol]
    {
      if (isDeclaration)
      {
        def baseDeclarationName = ["Nitra", "Runtime", "Binding", "Declaration"];
        match (context.Project.RootNamespace.Node.BindMany.[AstSymbol](baseDeclarationName))
        {
          | [declarationSymbol] =>
            if (astInheritance.HasValue)
              declarationSymbol :: astInheritance.Value.Typing(context, isDeclaration)
            else
              [declarationSymbol]

          | []     => context.FatalError(astInheritance.Location.FromStart(), $"Base ast '..$(baseDeclarationName; \".\")' not found")
          | _ :: _ => context.FatalError(astInheritance.Location.FromStart(), $"Base ast '..$(baseDeclarationName; \".\")' is ambiguous")
        }
      }
      else
      {
        if (astInheritance.HasValue)
          astInheritance.Value.Typing(context, isDeclaration)
        else
          []
      }
    }

    internal BindAstInheritanceList(baseList : ListParseTree[G.QualifiedIdentifier], context : Context, isDeclaration : bool) : list[AstSymbol]
    {
      def result = LightList();
      foreach (identifier in baseList)
      {
        match (context.Bind.[AstSymbol](identifier))
        {
          | SymbolRef.Some(AbstractAstSymbol as astSymbol) as symbolRef =>
            if (!isDeclaration && astSymbol.IsDeclaration)
              context.Error(symbolRef, "Only abstract ast can be specified in base ast list.")
            else
              result.Add(astSymbol)

          | SymbolRef.Some as symbolRef => context.Error(symbolRef, "Only abstract declarations can be specified in base declarations list.");
          | _ => ()
        }
      }
      result.ToNList()
    }
  }
}
