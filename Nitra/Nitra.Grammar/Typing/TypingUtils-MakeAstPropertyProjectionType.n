using Nitra.Model;
using Nitra.ProjectSystem;

using Nemerle;
using Nemerle.Imperative;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;

namespace Nitra.Typing
{
  public partial module TypingUtils
  {
    internal MakeAstPropertyProjectionType(thisType : NType, projectionSource : SimpleAstPropertySymbol, baseType : AstSymbol, context : Context) : AstPropertyProjectionType
    {
      def bind(name : NType.Qualified, ctor : _ * _ -> AstPropertyProjectionType) : AstPropertyProjectionType
      {
        if (context.Bind.[AstSymbol](name) is SymbolRef.Some(targetType))
        {
          if (targetType.IsSubtypeOf(baseType))
            ctor(projectionSource, targetType)
          else
          {
            context.Error(name, $"Declaration '$(targetType.FullName)' is not subtype of declaration '$(baseType.FullName)'.");
            AstPropertyProjectionType.Error(projectionSource, null)
          }
        }
        else AstPropertyProjectionType.Error(projectionSource, null)
      }

      if (projectionSource : object != null && baseType : object != null)
      {
        match (thisType)
        {
          | OptionList(Qualified as name) => bind(name, AstPropertyProjectionType.OptionList)
          | List      (Qualified as name) => bind(name, AstPropertyProjectionType.List)
          | Option    (Qualified as name) => bind(name, AstPropertyProjectionType.Option)
          | Qualified as name             => bind(name, AstPropertyProjectionType.Single)
          | t =>
            context.Error(t, $"Invalid property type '$t'.");
            AstPropertyProjectionType.Error(projectionSource, null)
        }
      }
      else AstPropertyProjectionType.Error(projectionSource, null)
    }
  }
}
