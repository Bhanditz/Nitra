using Nitra.Model;
using Nitra.ProjectSystem;

using Nemerle;
using Nemerle.Imperative;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;

namespace Nitra.Typing
{
  public partial module TypingUtils
  {
    internal MakeDeclarationPropertyProjectionType(thisType : NType, projectionSource : SimpleDeclarationPropertySymbol, baseType : DeclarationSymbol, context : Context) : DeclarationPropertyProjectionType
    {
      def bind(name : NType.Qualified, ctor : _ * _ -> DeclarationPropertyProjectionType) : DeclarationPropertyProjectionType
      {
        if (context.Bind.[DeclarationSymbol](name) is SymbolRef.Some(targetType))
        {
          if (targetType.IsSubtypeOf(baseType))
            ctor(projectionSource, targetType)
          else
          {
            context.Error(name, $"Declaration '$(targetType.FullName)' is not subtype of declaration '$(baseType.FullName)'.");
            DeclarationPropertyProjectionType.Error(projectionSource, null)
          }
        }
        else DeclarationPropertyProjectionType.Error(projectionSource, null)
      }

      if (projectionSource : object != null && baseType : object != null)
      {
        match (thisType)
        {
          | OptionList(Qualified as name) => bind(name, DeclarationPropertyProjectionType.List)
          | List      (Qualified as name) => bind(name, DeclarationPropertyProjectionType.List)
          | Option    (Qualified as name) => bind(name, DeclarationPropertyProjectionType.Option)
          | Qualified as name             => bind(name, DeclarationPropertyProjectionType.Single)
          | t =>
            context.Error(t, $"Invalid property type '$t'.");
            DeclarationPropertyProjectionType.Error(projectionSource, null)
        }
      }
      else DeclarationPropertyProjectionType.Error(projectionSource, null)
    }
  }
}
