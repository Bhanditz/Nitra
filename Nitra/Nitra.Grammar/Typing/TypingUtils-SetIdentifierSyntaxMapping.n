using Nitra.ProjectSystem;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;

namespace Nitra.Typing
{
  public partial module TypingUtils
  {
    public SetIdentifierSyntaxMapping(project : NitraProject) : void
    {
      when (project.RootNamespace.Node.BindMany.[AstSymbol](["Nitra", "Runtime", "Binding", "Reference"]) is [referenceSymbol])
      {
        foreach (symbol when symbol.AssemblyId == AssemblyIds.CurrentAssembly && symbol.Options.IsIdentifier in project.RootNamespace.Node.GetDescendants.[SyntaxRuleSymbol]())
        {
          match (symbol)
          {
            | SimpleRuleSymbol
            | ExtensionRuleSymbol =>
              when (symbol.Ast is SymbolRef.None(IsGenerated = true))
              {
                def bodyLocation = symbol.FirstLocation;
                def astSymbolRef = SymbolRef.Some(symbol.FirstLocation, true, referenceSymbol);
                symbol.SetAst(astSymbolRef);

                def code    = <[ Nitra.Runtime.Binding.Reference.Concrete(Span = this.Span, Id = Nitra.Runtime.Binding.StringIndex.GetId(this.GetText())) ]>;
                def newBody = SyntaxMappingBody.Inline(bodyLocation, astSymbolRef, code);
                symbol.SetSyntaxMappingBody(newBody)
              }

            | _ => ()
          }
        }
      }
    }
  }
}
