using Nitra.Model;
using Nitra.ProjectSystem;
using Nitra.Serialization;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;

using G = NitraSyntaxParseTree;

namespace Nitra.Typing
{
  [Record, ExportableSymbol, ChemicalSymbol]
  public abstract class ExtensionAstSymbol : AstSymbol, IMemberSite
  {
    public override Description : string                         { get { if (IsDeclaration) "extension declaration" else "extension ast" } }
    public abstract BaseAst     : SymbolRef[ExtensibleAstSymbol] { get; }
    public override BaseAsts    : list[AstSymbol]                { get { if (BaseAst is SymbolRef.Some(s)) [s] else [] } }
  }

  public sealed class ParsedExtensionAstSymbol : ExtensionAstSymbol, IDependentPropertyAssignmentContainer
  {
    private         _node                     : G.ExtensionAst;
    private         _assignments              : SCG.List[DependentPropertyAssignment];
    private mutable _initializedOutProperties : list[DependentAstPropertySymbol];

    public this(node : G.ExtensionAst, isDeclaration : bool, cardinality : AstCardinality)
    {
      base(node.Name, node.Name.Value());
      _node                     = node;
      _assignments              = SCG.List();
      _initializedOutProperties = [];
      IsDeclaration             = isDeclaration;
      Cardinality               = cardinality;
    }

    private mutable _symbol : SymbolRef[SymbolSymbol];

    public override Symbol                   : SymbolRef[SymbolSymbol]          { get { assert(_symbol : object != null); _symbol } }
    public override IsDeclaration            : bool                             { get; }
    public override Cardinality              : AstCardinality                   { get; }
    public override BaseAst                  : SymbolRef[ExtensibleAstSymbol]   { get { _node.BaseAstRef } }
    public override InitializedOutProperties : list[DependentAstPropertySymbol] { get { _initializedOutProperties } }

    public AddAssignment(assignment : DependentPropertyAssignment) : void { AddAssignmentImpl(assignment, _assignments, ref _initializedOutProperties); }
    public GetAssignments() : Seq[DependentPropertyAssignment] { _assignments }

    protected override CreateSymbolSymbol(allBaseAsts : SCG.HashSet[AstSymbol]) : SymbolRef[SymbolSymbol]
    {
      assert(_symbol : object == null);

      _ = allBaseAsts;
      _symbol =
        if (IsDeclaration)
        {
          def symbolSymbol = ParsedSymbolSymbol.Extension(this);
          _ = Node.Parent.Parent.AddAndReport.[SymbolSymbol](symbolSymbol, FirstLocation);
          SymbolRef.Some(FirstLocation, true, symbolSymbol)
        }
        else
          SymbolRef.None(FirstLocation, true);

      _symbol
    }
  }
}
