using Nitra.Model;
using Nitra.ProjectSystem;
using Nitra.Serialization;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;

using G = NitraSyntaxParseTree;

namespace Nitra.Typing
{
  [Record, ExportableSymbol, ChemicalSymbol]
  public abstract class ExtensionAstSymbol : AstSymbol, IMemberSite
  {
    public override Description : string                         { get { if (IsDeclaration) "extension declaration" else "extension ast" } }
    public override Cardinality : AstCardinality                 { get { if (BaseAst is SymbolRef.Some(s)) s.Cardinality else AstCardinality.Single } }
    public abstract BaseAst     : SymbolRef[ExtensibleAstSymbol] { get; }
    public override BaseAsts    : list[AstSymbol]                { get { if (BaseAst is SymbolRef.Some(s)) [s] else [] } }
  }

  public sealed class ParsedExtensionAstSymbol : ExtensionAstSymbol, IDependentPropertyAssignmentContainer
  {
    private         _node                     : G.ExtensionAst;
    private         _assignments              : SCG.List[DependentPropertyAssignment];
    private mutable _initializedOutProperties : list[DependentAstPropertySymbol];

    public this(node : G.ExtensionAst)
    {
      base(node.Name, node.Name.Value());
      _node                     = node;
      _assignments              = SCG.List();
      _initializedOutProperties = [];
    }

    public override IsDeclaration            : bool                             { get { false } }
    public override BaseAst                  : SymbolRef[ExtensibleAstSymbol]   { get { _node.BaseAstRef } }
    public override InitializedOutProperties : list[DependentAstPropertySymbol] { get { _initializedOutProperties } }

    public AddAssignment(assignment : DependentPropertyAssignment) : void { AddAssignmentImpl(assignment, _assignments, ref _initializedOutProperties); }
    public GetAssignments() : Seq[DependentPropertyAssignment] { _assignments }
  }
}
