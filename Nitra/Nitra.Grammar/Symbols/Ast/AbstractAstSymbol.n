using Nitra.Model;
using Nitra.ProjectSystem;
using Nitra.Serialization;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;

using G = NitraSyntaxParseTree;

namespace Nitra.Typing
{
  [Record, ExportableSymbol, ChemicalSymbol]
  public abstract class AbstractAstSymbol : AstSymbol, INamespaceMember
  {
    public override Description : string         { get { if (IsDeclaration) "abstract declaration" else "abstract ast" } }
    public override Cardinality : AstCardinality { get { AstCardinality.Single } }
  }

  public sealed class ParsedAbstractAstSymbol : AbstractAstSymbol
  {
    private _node : G.NamespaceMember.SimpleAst;

    public this(node : G.NamespaceMember.SimpleAst, isDeclaration : bool)
    {
      base(node.Name, node.Name.Value());
      _node         = node;
      IsDeclaration = isDeclaration;
    }

    private mutable _symbol : SymbolRef[SymbolSymbol];

    public override Symbol                   : SymbolRef[SymbolSymbol]          { get { assert(_symbol : object != null); _symbol } }
    public override IsDeclaration            : bool                             { get; }
    public override BaseAsts                 : list[AstSymbol]                  { get { _node.BaseAsts } }
    public override InitializedOutProperties : list[DependentPropertySymbol] { get { [] } }

    protected override CreateSymbolSymbol() : SymbolRef[SymbolSymbol]
    {
      assert(_symbol : object == null);

      _symbol =
        CreateSymbolSymbolImpl(fun (baseSymbols)
        {
          def symbolSymbol =
            if (_node.Body.IsSymbolSealed())
              ParsedSymbolSymbol.Simple(this, baseSymbols)
            else
              ParsedSymbolSymbol.Abstract(this, baseSymbols);
          _ = Node.Parent.AddAndReport.[SymbolSymbol](symbolSymbol, FirstLocation);
          symbolSymbol
        });
      _node.Body.TypeSymbol(this, _symbol);
      _symbol
    }
  }
}
