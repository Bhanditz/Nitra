using Nitra.ProjectSystem;
using Nitra.Model;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;

using G = NitraSyntaxParseTree;

namespace Nitra.Typing
{
  internal partial module SyntaxMappingTyper
  {
    public Actual : SyntaxMappingTyper[SyntaxMappingExpr] = ActualSyntaxMappingTyper();

    private sealed class ActualSyntaxMappingTyper : SyntaxMappingTyper[SyntaxMappingExpr]
    {
      protected override GetCompilerMessages(node : G.MapSyntaxExpression) : ICompilerMessages
      {
        node.Context
      }

      protected override TypeNode(node : G.MapSyntaxExpression, ruleSymbol : SyntaxRuleSymbol, astType : AstType) : SyntaxMappingExpr
      {
        node.Expression(ruleSymbol, astType)
      }

      protected override CreateError(loc : Location) : SyntaxMappingExpr
      {
        SyntaxMappingExpr.Error(loc)
      }

      protected override CreateConvertList(loc : Location, astSymbol : AstSymbol, path : ParseTreePath, syntaxMapping : SyntaxMappingSymbol, arguments : SCG.IList[G.MapSyntaxExpression], itemPath : ParseTreePath, ruleSymbol : SyntaxRuleSymbol) : SyntaxMappingExpr
      {
        def argumentExprs = ApplyArgumentTypes(syntaxMapping.Parameters, arguments, ruleSymbol);
        SyntaxMappingExpr.ConvertList(loc, path, argumentExprs, astSymbol, itemPath)
      }

      protected override CreateConcatLists(loc : Location, astSymbol : AstSymbol, path : ParseTreePath, syntaxMapping : SyntaxMappingSymbol, arguments : SCG.IList[G.MapSyntaxExpression], itemPath : ParseTreePath, ruleSymbol : SyntaxRuleSymbol) : SyntaxMappingExpr
      {
        def argumentExprs = ApplyArgumentTypes(syntaxMapping.Parameters, arguments, ruleSymbol);
        SyntaxMappingExpr.ConcatLists(loc, path, argumentExprs, astSymbol, itemPath)
      }

      protected override CreateConcatOptions(loc : Location, astSymbol : AstSymbol, path : ParseTreePath, syntaxMapping : SyntaxMappingSymbol, arguments : SCG.IList[G.MapSyntaxExpression], itemPath : ParseTreePath, ruleSymbol : SyntaxRuleSymbol) : SyntaxMappingExpr
      {
        def argumentExprs = ApplyArgumentTypes(syntaxMapping.Parameters, arguments, ruleSymbol);
        SyntaxMappingExpr.ConcatOptions(loc, path, argumentExprs, astSymbol, itemPath)
      }

      protected override CreateConvertOption(loc : Location, astSymbol : AstSymbol, path : ParseTreePath, syntaxMapping : SyntaxMappingSymbol, arguments : SCG.IList[G.MapSyntaxExpression], valuePath : ParseTreePath, ruleSymbol : SyntaxRuleSymbol) : SyntaxMappingExpr
      {
        def argumentExprs = ApplyArgumentTypes(syntaxMapping.Parameters, arguments, ruleSymbol);
        SyntaxMappingExpr.ConvertOption(loc, path, argumentExprs, astSymbol, valuePath)
      }

      protected override CreateCall(loc : Location, path : ParseTreePath, syntaxMapping : SyntaxMappingSymbol, arguments : SCG.IList[G.MapSyntaxExpression], ruleSymbol : SyntaxRuleSymbol) : SyntaxMappingExpr
      {
        def argumentExprs = ApplyArgumentTypes(syntaxMapping.Parameters, arguments, ruleSymbol);
        SyntaxMappingExpr.Call(loc, path, argumentExprs, syntaxMapping.ReturnType)
      }

      protected override CreateAstCtor(loc : Location, astSymbol : AstSymbol, members : ListParseTree[G.MapSyntaxExpressionAstMember], closeBrace : NSpan, ruleSymbol : SyntaxRuleSymbol, compilerMessages : ICompilerMessages) : SyntaxMappingExpr
      {
        def mappingFields = SCG.List();
        foreach (member in members)
          when (member.GetSyntaxMappingField(astSymbol, ruleSymbol) is VSome(mappingField))
            mappingFields.Add(mappingField);
        TypingUtils.CheckRequiredAstProperties(astSymbol, mappingFields, Location(loc.Source, closeBrace), compilerMessages);
        SyntaxMappingExpr.AstCtor(loc, astSymbol, mappingFields.ToNList())
      }

      protected override CreateListCtor(loc : Location, astSymbol : AstSymbol, items : array[SyntaxMappingExpr]) : SyntaxMappingExpr
      {
        SyntaxMappingExpr.ListCtor(loc, astSymbol, items.ToNList())
      }

      protected override CreateOptionCtor(loc : Location, astSymbol : AstSymbol, value : ValueOption[SyntaxMappingExpr]) : SyntaxMappingExpr
      {
        SyntaxMappingExpr.OptionCtor(loc, astSymbol, if (value.HasValue) Some(value.Value) else None())
      }

      private static ApplyArgumentTypes(parameters : list[SyntaxMappingParameter], arguments : SCG.IList[G.MapSyntaxExpression], ruleSymbol : SyntaxRuleSymbol, index : int = 0) : list[SyntaxMappingExpr]
      {
        match (parameters)
        {
          | []         => []
          | p :: other => arguments[index].Expression(ruleSymbol, p.Type) :: ApplyArgumentTypes(other, arguments, ruleSymbol, index + 1)
        }
      }
    }
  }
}
