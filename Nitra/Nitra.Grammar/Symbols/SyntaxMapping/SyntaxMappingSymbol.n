using Nitra.Model;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;

using G = NitraSyntaxParseTree;

namespace Nitra.Typing
{
  [Record, ExportableSymbol]
  public abstract class SyntaxMappingSymbol : SingleDefSymbol
  {
    public static SymbolName = "GetAst";

    public override Description   : string                       { get { "map syntax" } }
    public abstract DeclaringRule : SyntaxRuleSymbol             { get; }
    public abstract Parameters    : list[SyntaxMappingParameterSymbol] { get; }
    public abstract ReturnType    : AstType                      { get; }
    public virtual  Expression    : SyntaxMappingExpr            { get { null } }
    public          HasValidSignature : bool
    {
      get { !(ReturnType.IsVoid || Parameters.Exists(p => p.Type.IsVoid)) }
    }
  }

  public sealed class ChemicalSyntaxMappingSymbol : SyntaxMappingSymbol
  {
    public this(loc : Location, declaringRule : SyntaxRuleSymbol, parameters : list[SyntaxMappingParameterSymbol], returnType : AstType)
    {
      base(loc, SymbolName);
      this.DeclaringRule = declaringRule;
      this.Parameters    = parameters;
      this.ReturnType    = returnType;
    }

    public override DeclaringRule : SyntaxRuleSymbol             { get; }
    public override Parameters    : list[SyntaxMappingParameterSymbol] { get; }
    public override ReturnType    : AstType                      { get; }
    public override Expression    : SyntaxMappingExpr            { get { assert(_expr.HasValue, "Property 'Expression' is not set."); _expr.GetValueOrDefault() } }

    private mutable _expr : ValueOption[SyntaxMappingExpr];

    public SetExpression(expr : SyntaxMappingExpr) : void
    {
      assert(!_expr.HasValue, "Property 'Expression' is already set.");
      _expr = VSome(expr);
    }
  }

  public sealed class ParsedSyntaxMappingSymbol : SyntaxMappingSymbol
  {
    public this(node : G.NamespaceMember.MapSyntax, declaringRule : SyntaxRuleSymbol)
    {
      base(node.Location, SymbolName);
      this._node         = node;
      this.DeclaringRule = declaringRule;
    }

    private _node : G.NamespaceMember.MapSyntax;

    public override DeclaringRule : SyntaxRuleSymbol             { get; }
    public override Parameters    : list[SyntaxMappingParameterSymbol] { get { _node.Parameters } }
    public override ReturnType    : AstType                      { get { _node.ReturnType } }
    public override Expression    : SyntaxMappingExpr            { get { _node.Expr } }
  }

  public sealed class ParsedInplaceSyntaxMappingSymbol : SyntaxMappingSymbol
  {
    public this(node : G.RuleBodyMember.MapSyntax, declaringRule : SyntaxRuleSymbol)
    {
      base(node.Location, SymbolName);
      this._node         = node;
      this.DeclaringRule = declaringRule;
    }

    private _node : G.RuleBodyMember.MapSyntax;

    public override DeclaringRule : SyntaxRuleSymbol             { get; }
    public override Parameters    : list[SyntaxMappingParameterSymbol] { get { _node.Parameters } }
    public override ReturnType    : AstType                      { get { _node.ReturnType } }
    public override Expression    : SyntaxMappingExpr            { get { _node.Expr } }
  }
}
