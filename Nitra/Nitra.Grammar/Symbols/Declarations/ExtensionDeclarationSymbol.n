using Nitra.Model;
using Nitra.ProjectSystem;
using Nitra.Serialization;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;

using G = NitraSyntaxParseTree;

namespace Nitra.Typing
{
  [Record, ExportableSymbol, ChemicalSymbol]
  public abstract class ExtensionDeclarationSymbol : DeclarationSymbol, IMemberSite
  {
    public override Description      : string                                 { get "declaration extension" }
    public abstract BaseDeclaration  : SymbolRef[ExtensibleDeclarationSymbol] { get; }
    public override BaseDeclarations : list[DeclarationSymbol]                { get { if (BaseDeclaration is SymbolRef.Some(s)) [s] else [] } }
  }

  public sealed class ParsedExtensionDeclarationSymbol : ExtensionDeclarationSymbol, IDependentPropertyAssignmentContainer
  {
    private         _node                     : G.DeclarationExtension;
    private         _assignments              : SCG.List[DependentPropertyAssignment];
    private mutable _initializedOutProperties : list[DependentDeclarationPropertySymbol];

    public this(node : G.DeclarationExtension)
    {
      base(node.Name, node.Name.Value());
      _node                     = node;
      _assignments              = SCG.List();
      _initializedOutProperties = [];
    }

    public override BaseDeclaration          : SymbolRef[ExtensibleDeclarationSymbol]   { get { _node.BaseDeclarationRef } }
    public override InitializedOutProperties : list[DependentDeclarationPropertySymbol] { get { _initializedOutProperties } }

    public AddAssignment(assignment : DependentPropertyAssignment) : void { AddAssignmentImpl(assignment, _assignments, ref _initializedOutProperties); }
    public GetAssignments() : Seq[DependentPropertyAssignment] { _assignments }
  }
}
