using Nitra.Model;
using Nitra.ProjectSystem;
using Nitra.Serialization;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;

using G = NitraSyntaxParseTree;

namespace Nitra.Typing
{
  [Record, ExportableSymbol, ChemicalSymbol]
  public abstract class SimpleDeclarationSymbol : DeclarationSymbol, INamespaceMember, IMemberSite
  {
    public override Description : string { get "declaration" }
  }

  public sealed class ParsedSimpleDeclarationSymbol : SimpleDeclarationSymbol, IDependentPropertyAssignmentContainer
  {
    private         _node                     : G.NamespaceMember.SimpleDeclaration;
    private         _assignments              : SCG.List[DependentPropertyAssignment];
    private mutable _initializedOutProperties : list[DependentDeclarationPropertySymbol];

    public this(node : G.NamespaceMember.SimpleDeclaration)
    {
      base(node.Name, node.Name.Value());
      _node                     = node;
      _assignments              = SCG.List();
      _initializedOutProperties = [];
    }

    public override BaseDeclarations         : list[DeclarationSymbol]                  { get { _node.BaseDeclarations } }
    public override InitializedOutProperties : list[DependentDeclarationPropertySymbol] { get { _initializedOutProperties } }

    public AddAssignment(assignment : DependentPropertyAssignment) : void { AddAssignmentImpl(assignment, _assignments, ref _initializedOutProperties); }
    public GetAssignments() : Seq[DependentPropertyAssignment] { _assignments }
  }
}
