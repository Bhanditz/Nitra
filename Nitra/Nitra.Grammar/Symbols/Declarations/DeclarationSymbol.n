using Nitra.Model;
using Nitra.Serialization;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Extensions;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Typing
{
  [Record]
  public abstract class DeclarationSymbol : SingleDefSymbol
  {
    public abstract BaseDeclarations : list[DeclarationSymbol] { get; }

    public Properties : Seq[DeclarationPropertySymbol]
    {
      get { this.Node.GetChildren.[DeclarationPropertySymbol]() }
    }

    public ParentProperty : SymbolRef[ParentDeclarationPropertySymbol]
    {
      [Memoize]
      get
      {
        def propertySymbol = AllProperties.OfType.[ParentDeclarationPropertySymbol]().FirstOrDefault();
        if (propertySymbol != null)
          SymbolRef.Some(propertySymbol.FirstLocation, true, propertySymbol)
        else
          SymbolRef.None(FirstLocation, true)
      }
    }

    public NameProperty : SymbolRef[NameDeclarationPropertySymbol]
    {
      [Memoize]
      get
      {
        def propertySymbol = AllProperties.OfType.[NameDeclarationPropertySymbol]().FirstOrDefault();
        if (propertySymbol != null)
          SymbolRef.Some(propertySymbol.FirstLocation, true, propertySymbol)
        else
          SymbolRef.None(FirstLocation, true)
      }
    }

    public AllBaseDeclarations : Seq[DeclarationSymbol]
    {
      [Memoize]
      get
      {
        if (BaseDeclarations.IsEmpty)
          []
        else
        {
          def result = HashSet(BaseDeclarations);
          foreach (s in BaseDeclarations)
            result.UnionWith(s.AllBaseDeclarations);
          result.ToArray()
        }
      }
    }

    public AllProperties : Seq[DeclarationPropertySymbol]
    {
      [Memoize]
      get
      {
        if (BaseDeclarations.IsEmpty)
          Properties
        else
        {
          def result = List(InheritedProperties);
          result.AddRange(Properties);
          result.ToArray()
        }
      }
    }

    public InheritedProperties : Seq[DeclarationPropertySymbol]
    {
      [Memoize]
      get
      {
        match (BaseDeclarations)
        {
          | []             => []
          | first :: other =>
            def result = HashSet(first.AllProperties);
            foreach (s in other)
              result.UnionWith(s.AllProperties);
            result.ToArray()
        }
      }
    }
  }
}
