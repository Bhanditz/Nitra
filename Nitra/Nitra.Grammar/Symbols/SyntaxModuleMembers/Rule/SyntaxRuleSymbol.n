using Nitra.ProjectSystem;
using Nitra.Model;
using Nitra.Typing.RuleMethod;

using Nemerle;
using System;

namespace Nitra.Typing
{
  [Record]
  public abstract class SyntaxRuleSymbol : RuleDefSymbol
  {
    public abstract Rule             : Rule.Sequence                {           get; }
    public abstract Declaration      : SymbolRef[DeclarationSymbol] {           get; }
    public          NonVoidSubRules  : list[Rule]                   { [Memoize] get { Rule.Rules.Filter(rule => !(rule.Type is RuleType.Void)) } }
    public          SubRules         : list[Rule]                   {           get { Rule.Rules } }
    public          Sequence         : SequenceCompileInfo.Root     { [Memoize] get { SequenceCompileInfo.MakeSequenceCompileInfo(this) } }
    public override IsToken          : bool                         {           get { Options.IsTokenRule } }

    protected static TrySetDeclarationMappingBody(declarationMappingBody : ref DeclarationMappingBody, newValue : DeclarationMappingBody) : void
    {
      if (declarationMappingBody != null)
      {
        using (messages = Project.CurrentProject.RootError(declarationMappingBody.Location, "Only single declaration mapping is allowed", -1))
          messages.Hint(declarationMappingBody.Location, "Fist declaration mapping", -1);
      }
      else
        declarationMappingBody = newValue
    }

    internal virtual SetDeclarationMappingBody(declarationMappingBody : DeclarationMappingBody) : void
    {
      Project.CurrentProject.Error(declarationMappingBody.Location, "Declaration mapping is not allowed here", 0)
    }

    public virtual GetDeclarationMappingBody() : DeclarationMappingBody
    {
      null
    }
  }
}
