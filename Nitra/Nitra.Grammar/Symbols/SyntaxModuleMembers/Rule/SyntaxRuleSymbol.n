using Nitra.ProjectSystem;
using Nitra.Model;
using Nitra.Typing.RuleMethod;

using Nemerle;
using System;

namespace Nitra.Typing
{
  [Record]
  public abstract class SyntaxRuleSymbol : RuleDefSymbol
  {
    public abstract Rule             : Rule.Sequence                {           get; }
    public abstract Ast              : SymbolRef[AstSymbol]         {           get; }
    public          NonVoidSubRules  : list[Rule]                   { [Memoize] get { Rule.Rules.Filter(rule => !(rule.Type is RuleType.Void)) } }
    public          SubRules         : list[Rule]                   {           get { Rule.Rules } }
    public          Sequence         : SequenceCompileInfo.Root     { [Memoize] get { SequenceCompileInfo.MakeSequenceCompileInfo(this) } }
    public override IsToken          : bool                         {           get { Options.IsTokenRule } }

    internal virtual SetAst(astSymbolRef : SymbolRef[AstSymbol]) : void
    {
      assert(Node != null);
      Node.Project.Error(astSymbolRef, "Declaration mapping is not allowed here")
    }

    protected TrySetSyntaxMappingBody(syntaxMappingBody : ref SyntaxMappingBody, newValue : SyntaxMappingBody) : void
    {
      assert(Node != null);
      if (syntaxMappingBody != null)
      {
        using (messages = Node.Project.RootError(syntaxMappingBody, "Only single declaration mapping is allowed"))
          messages.Hint(syntaxMappingBody, "Fist declaration mapping");
      }
      else
        syntaxMappingBody = newValue
    }

    internal virtual SetSyntaxMappingBody(syntaxMappingBody : SyntaxMappingBody) : void
    {
      assert(Node != null);
      Node.Project.Error(syntaxMappingBody, "Declaration mapping is not allowed here")
    }

    public virtual GetSyntaxMappingBody() : SyntaxMappingBody
    {
      null
    }
  }
}
