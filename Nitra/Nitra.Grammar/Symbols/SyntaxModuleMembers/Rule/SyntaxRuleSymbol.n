using Nitra.ProjectSystem;
using Nitra.Model;
using Nitra.Typing.RuleMethod;

using Nemerle;
using System;

namespace Nitra.Typing
{
  [Record]
  public abstract class SyntaxRuleSymbol : RuleDefSymbol
  {
    public abstract Rule             : Rule.Sequence                {           get; }
    public abstract Ast              : SymbolRef[AstSymbol]         {           get; }
    public          NonVoidSubRules  : list[Rule]                   { [Memoize] get { Rule.Rules.Filter(rule => !(rule.Type is RuleType.Void)) } }
    public          SubRules         : list[Rule]                   {           get { Rule.Rules } }
    public          Sequence         : SequenceCompileInfo.Root     { [Memoize] get { SequenceCompileInfo.MakeSequenceCompileInfo(this) } }
    public override IsToken          : bool                         {           get { Options.IsTokenRule } }

    internal virtual SetAst(astSymbolRef : SymbolRef[AstSymbol]) : void
    {
      assert(Node != null);
      Node.Project.Error(astSymbolRef, "Declaration mapping is not allowed here")
    }

    protected TrySetSyntaxMappingExpr(syntaxMappingExpr : ref SyntaxMappingExpr, newValue : SyntaxMappingExpr) : void
    {
      assert(Node != null);
      if (syntaxMappingExpr != null)
      {
        using (messages = Node.Project.RootError(syntaxMappingExpr, "Only single declaration mapping is allowed"))
          messages.Hint(syntaxMappingExpr, "Fist declaration mapping");
      }
      else
        syntaxMappingExpr = newValue
    }

    internal virtual SetSyntaxMappingExpr(syntaxMappingExpr : SyntaxMappingExpr) : void
    {
      assert(Node != null);
      Node.Project.Error(syntaxMappingExpr, "Declaration mapping is not allowed here")
    }

    public virtual GetSyntaxMappingExpr() : SyntaxMappingExpr
    {
      null
    }
  }
}
