using Nitra.Typing;
using Nitra.Model;
using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

namespace Nitra.Model
{
  [Record]
  public variant RuleStructure
  {
                   public         SrcRule          : Nitra.Model.Rule;
                   public         Fields           : array[RuleStructureField];
    [RecordIgnore] public mutable Owner            : RuleStructureField;
    [RecordIgnore] public mutable Header           : array[RuleStructureHeaderField];
    [RecordIgnore] public mutable AstId            : string;
    [RecordIgnore] public mutable Level            : int;
    [RecordIgnore] public mutable AllocateState    : int;
                   public         SizesStart       : int   { get { Fields[0].Offset } }
                   public         SizesEnd         : int   { get { Fields[Fields.Length - 1].Offset + Fields[Fields.Length - 1].Size } }
                   public         Size             : int   { get { SizesEnd } }
                   public         HeaderId         : RuleStructureHeaderField { get { Header[0] } }
                   public         HeaderNext       : RuleStructureHeaderField { get { Header[1] } }
                   public         HeaderState      : RuleStructureHeaderField { get { Header[2] } }

    | Ast
      {
                               Symbol                 : SyntaxRuleSymbol;
        [RecordIgnore] mutable InternalRuleStructures : array[RuleStructure];
        [RecordIgnore] mutable AllFields              : array[RuleStructureField];
        [RecordIgnore] mutable StateCount             : int;

        public AllNonMarkerFields : array[RuleStructureField] { [Memoize] get { AllFields.FilterToArray(f => !(f is RuleStructureField.Marker)) } }

        public CodeForStates(startState : int, mutable endState : int, skipOptionalStates : bool) : array[string]
        {
          _ = skipOptionalStates;
          
          def field1 = AllNonMarkerFields[startState];
          def owner1 = field1.Owner;
          
          when (endState < 0)
            endState = 0;
            
          def field2 = 
            if  (endState < 0)
              owner1.NonMarkerFields.Last()
            else
              AllNonMarkerFields[endState];
          def owner2 = field2.Owner;
          assert3(owner1 : object == owner2);
          mutable i = Array.IndexOf(owner1.Fields, field1);
          def endIndex = Array.IndexOf(owner2.Fields, field2);
          def result = array(endIndex - i);
          for (mutable k = 0; i <= endIndex; i++)
          {
            def field = owner1.Fields[i];
            
            when (field is RuleStructureField.Marker)
              continue;
            
            result[k] = field.SrcRule.ToString();
              
            k++;
          }

          result
        }

        public CodeForState(state : int) : string
        {
          assert(state >= 0);
          assert(state < StateCount);

          def toString(i : int, fields : array[RuleStructureField]) : string
          {
            assert2(i < fields.Length);
            assert2(i >= 0);

            def field = fields[i];

            def collectSignificantStatesStrings(k : int, fields : array[RuleStructureField]) : list[string]
            {
              when (k >= fields.Length)
                return [];

              def field = fields[k];

              if (field is RuleStructureField.Marker)
                collectSignificantStatesStrings(k + 1, fields)
              else if (field.CanParseEmptyString)
                field.SrcRule.ToString() :: collectSignificantStatesStrings(k + 1, fields)
              else
                [field.SrcRule.ToString()]
            }

            if (field.State == state)
              $<#..$(collectSignificantStatesStrings(i, fields); " ")#>
            else if (field.State < StateCount || field.State == int.MaxValue - 1)
              toString(i + 1, fields)
            else
            {
              assert3(false);
            }
          }

          toString(state, AllFields)
        }
      }

    | Option
    | List                       { public UseCounter : bool { get { (Owner :> RuleStructureField.List).UseCounter } } }
    | ListWithSeparatorRule      { public UseCounter : bool { get { (Owner :> RuleStructureField.ListWithSeparator).UseCounter } } }
    | ListWithSeparatorSeparator { public UseCounter : bool { get { (Owner :> RuleStructureField.ListWithSeparator).UseCounter } } }
    | Predicate { IsAnd : bool }

    public NonMarkerFields    : array[RuleStructureField] { [Memoize] get { Fields.FilterToArray(f => !(f is RuleStructureField.Marker)) } }
    internal static MakeRuleStructure(ruleSymbol : SyntaxRuleSymbol) : RuleStructure.Ast
    {
      def internalRuleStructures = SCG.List();
      def allFields = SCG.List();

      mutable predicates = SCG.List();
      def takePredicates() : array[RuleStructure.Predicate]
      {
        def result = predicates.ToArray();
        predicates.Clear();
        result
      }
      def convAndAppendPredicates(rule)
      {
        def rules = conv(rule);
        def tailPredicates = takePredicates();
        if (tailPredicates.Length > 0)
          rules.Append(RuleStructureField.Empty(Rule.String(Nitra.Location(rule.Location.Source, rule.Location.EndPos, rule.Location.EndPos), RuleType.Chars(), "", ""), tailPredicates));
        else
          rules;
      }
      and conv(rule : Rule) : array[RuleStructureField]
      {
        def predicate(rule, isAnd)
        {
          def externalPredicates = predicates;
          predicates = SCG.List();
          externalPredicates.Add(RuleStructure.Predicate(rule, convAndAppendPredicates(rule), isAnd));
          predicates = externalPredicates;
          array[]
        }
        match (rule)
        {
          | And                   as rule => predicate(rule.Rule, true)
          | Not                   as rule => predicate(rule.Rule, false)
          | Repeat(0, Some(1), _) as rule => array[RuleStructureField.Option           (rule, takePredicates(), RuleStructure.Option(rule.Rule, convAndAppendPredicates(rule.Rule)))]
          | Repeat                as rule => array[RuleStructureField.List             (rule, takePredicates(), RuleStructure.List(rule.Rule, convAndAppendPredicates(rule.Rule)), rule.MinCount, rule.MaxCount)]
          | RepeatWithSeparator   as rule => array[RuleStructureField.ListWithSeparator(rule, takePredicates(), RuleStructure.ListWithSeparatorRule(rule.Rule, convAndAppendPredicates(rule.Rule)), RuleStructure.ListWithSeparatorSeparator(rule.Separator, convAndAppendPredicates(rule.Separator)), rule.MinCount, rule.MaxCount, rule.HangingSeparator)]
          | String                as rule => array[RuleStructureField.TokenString      (rule, takePredicates(), rule.Str)]
          | Sequence              as rule => rule.Rules.Map(conv(_)).Flatten().NToArray();
          | Call(RuleRef.Some(ruleSymbol) as ruleRef, bindingPower) =>
            match (ruleSymbol)
            {
              | ExtensibleRuleSymbol as rs => array[RuleStructureField.ExtensibleCall(rule, takePredicates(), rs, bindingPower)]
              | SimpleRuleSymbol     as rs => array[RuleStructureField.SimpleCall    (rule, takePredicates(), rs)]
              | RegularRuleSymbol    as rs => array[RuleStructureField.RegularCall   (rule, takePredicates(), rs)]
              | MarkerSymbol         as rs => array[RuleStructureField.Marker        (rule,          array[], rs)]
              | _                          => Message.FatalError(ruleRef.NLocation(), $"ICE! Nitra typing bug: invalid symbol bound. $(ruleSymbol.GetType().Name)");
            }
          | Call(RuleRef.None as ruleRef, _) => Message.FatalError(ruleRef.NLocation(), "ICE! Nitra typing bug: not bound symbol.");
          //| Choice(rules)                 => assert3(false, "Надо реализовать");
          //| Chars(chars)                  => assert3(false, "Надо реализовать");
        }
      }
      def rule = match (ruleSymbol)
      {
        | ExtensionRuleSymbol(ExtensionType = Postfix(_, rule)) => rule
        | _ => ruleSymbol.Rule
      }
      def ast = RuleStructure.Ast(rule, convAndAppendPredicates(rule), ruleSymbol);
      mutable state = 0;
      def initStructure(structure : RuleStructure, owner : RuleStructureField, level : int, isPredicate : bool) : void
      {
        structure.Level           = level;
        structure.Owner           = owner;
        match (structure )
        {
          | RuleStructure.Ast =>
            assert(owner == null);
            structure.Header    = array[RuleStructureHeaderField.Id(structure, 0), RuleStructureHeaderField.Next(structure, 1), RuleStructureHeaderField.State(structure, 2)];
            structure.AstId     = "RuleId";

          | RuleStructure.Predicate =>
            assert(owner == null);
            structure.Header    = array[RuleStructureHeaderField.Id(structure, 0)];
            structure.AstId     = $"RuleId$(internalRuleStructures.Count)";
            internalRuleStructures.Add(structure);

          | _ =>
            assert(owner != null);
            structure.Header    = array[RuleStructureHeaderField.Id(structure, 0), RuleStructureHeaderField.Next(structure, 1), RuleStructureHeaderField.State(structure, 2)];
            structure.AstId     = $"RuleId$(internalRuleStructures.Count)";
            internalRuleStructures.Add(structure);
        }

        def fields = structure.Fields;
        def notMarkerFields = structure.NonMarkerFields;
        for (mutable i = 0; i < notMarkerFields.Length - 1; ++i)
          notMarkerFields[i].NextSibling = notMarkerFields[i + 1];
        for (mutable i = 1; i < notMarkerFields.Length; ++i)
          notMarkerFields[i].PrevSibling = notMarkerFields[i - 1];

        for (mutable i = 0; i < fields.Length; ++i)
          fields[i].Owner = structure;

        for (mutable i = 0; i < fields.Length; ++i)
        {
          initField(fields[i], level, isPredicate);
          fields[i].EndState = state;
        }

        fields[0].Offset = structure.Header.Length;
        for (mutable i = 1; i < fields.Length; ++i)
          fields[i].Offset = fields[i - 1].Offset + fields[i - 1].Size;

        match (fields.Find(field => !(field is RuleStructureField.Marker)))
        {
          | Some(field) =>
            field.AllocateAst = true;
            structure.AllocateState = field.State;

          | None()      => assert(false);
        }
      }
      and initField(field : RuleStructureField, level : int, isPredicate : bool) : void
      {
        foreach (predicate in field.HeadPredicates)
          initStructure(predicate, null, level + 1, true);

        allFields.Add(field);

        field.Level            = level;
        field.IsPredicate      = isPredicate;

        if (field is RuleStructureField.Marker)
          field.State = int.MaxValue - 1;
        else
        {
          field.State = state;
          ++state;
        }

        match (field)
        {
          | Empty                                         => field.Size = 1; field.CanParseEmptyString = true;
          | TokenString(str)                              => field.Size = 1; field.CanParseEmptyString = str == "";
          | RegularCall(sym)                              => field.Size = 1; field.CanParseEmptyString = sym.CanParseEmptyString;
          | SimpleCall(sym)                               => field.Size = 1; field.CanParseEmptyString = sym.CanParseEmptyString;
          | ExtensibleCall                                => field.Size = 1; field.CanParseEmptyString = false;
          | Option           (rule)                       => field.Size = 1; field.CanParseEmptyString = true;     initStructure(rule, field, level + 1, isPredicate);
          | List             (rule, min, _)               => field.Size = 1; field.CanParseEmptyString = min == 0; initStructure(rule, field, level + 1, isPredicate);
          | ListWithSeparator(rule, separator, min, _, _) => field.Size = 1; field.CanParseEmptyString = min == 0; initStructure(rule, field, level + 1, isPredicate); initStructure(separator, field, level + 1, isPredicate);
          | Marker                                        => field.Size = 0; field.CanParseEmptyString = true;
        }
      }
      initStructure(ast, null, 0, false);
      ast.InternalRuleStructures = internalRuleStructures.ToArray();
      ast.AllFields              = allFields.ToArray();
      ast.StateCount             = state;
      ast
    }

    public FunctionNamePostfix : string
    {
      get
      {
        match (this)
        {
          | Ast                        => ""
          | Predicate                  => $"_$(Owner.State)_Predicate"
          | ListWithSeparatorSeparator => $"_$(Owner.State)_Separator"
          | ListWithSeparatorRule      => $"_$(Owner.State)_Body"
          | List                       => $"_$(Owner.State)_Body"
          | Option                     => $"_$(Owner.State)_Body"
        }
      }
    }

    public ParseFunctionName                : string { get { $"Parse$FunctionNamePostfix" } }
    public FindRecoveryPositionFunctionName : string { get { $"FindRecoveryPosition$FunctionNamePostfix" } }

    public override ToString() : string
    {
      $<#..$(Fields; "  ")#>
    }
  }
}
