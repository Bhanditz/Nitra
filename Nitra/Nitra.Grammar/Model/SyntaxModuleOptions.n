using Nemerle;
using Nemerle.Collections;
using Nemerle.Surround;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

[assembly: DefineSurround("newSyntaxModuleOptions", false, def newOptions = SyntaxModuleOptions(this), newOptions)]

namespace Nitra.Model
{
  [Record]
  public sealed class SyntaxModuleOptions
  {
    public static Empty : SyntaxModuleOptions = SyntaxModuleOptions();

    public IsNeedSplicable  : bool { get; private set; }
    public IsExplicitSpaces : bool { get; private set; }

    public SetNeedSplicable (_loc : Located, value : bool) : SyntaxModuleOptions { surroundwith(newSyntaxModuleOptions) newOptions.IsNeedSplicable = value }
    public SetExplicitSpaces(_loc : Located, value : bool) : SyntaxModuleOptions { surroundwith(newSyntaxModuleOptions) newOptions.IsExplicitSpaces = value }

    public this() {}

    public this(original : SyntaxModuleOptions)
    {
      this( isNeedSplicable  = original.IsNeedSplicable
          , isExplicitSpaces = original.IsExplicitSpaces)
    }

    public override ToString() : string
    {
      def values = List();
      when (IsNeedSplicable)  values.Add("Splicable");
      when (IsExplicitSpaces) values.Add("ExplicitSpaces");
      $"[..$(values; \", \")]"
    }
  }
}
