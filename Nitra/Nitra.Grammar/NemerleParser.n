using Nitra.Model;
using Nitra.ProjectSystem;
using Nitra.Runtime;
using Nitra.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

using G = NitraSyntaxParseTree;

namespace Nitra
{
  public module NemerleParser
  {
    public Parse(body : G.RuleMethodBody, context : Context) : PExpr
    {
      match (body)
      {
        | G.RuleMethodBody.Block      as b => ParseImpl(b.Block, context)
        | G.RuleMethodBody.Expression as b => ParseImpl(b.Expression, context)
        | G.RuleMethodBody.Empty           => <[ () ]>
        | _                                => <[ () ]>
      }
    }

    public Parse(body : G.Expression, context : Context) : PExpr
    {
      ParseImpl(body, context)
    }

    public Parse(body : G.DependentPropertyValue, context : Context) : PExpr
    {
      match (body)
      {
        | G.DependentPropertyValue.Expression as b => ParseImpl(b.Expression, context)
        | G.DependentPropertyValue.Empty           => <[ () ]>
        | _                                        => <[ () ]>
      }
    }

    public Parse(body : G.MapSyntaxExpression, context : Context) : PExpr
    {
      match (body)
      {
        | G.MapSyntaxExpression.Block      as b => ParseImpl(b.Block, context)
        | G.MapSyntaxExpression.Expression as b => ParseImpl(b.Expression, context)
        | _                                     => <[ () ]>
      }
    }

    public ParseParameter(context : Context, name : Nitra.ParseTree, type : Nitra.ParseTree) : PParameter
    {
      match (ParseImpl(name, context))
      {
        | <[ $(n : name) ]> => <[ parameter: $n : $(ParseImpl(type, context)) ]>
        | _ => context.FatalError(name, "Expected simple name.")
      }
    }

    public Parse(parms : ListParseTree[G.RuleMethodsParam], context : Context) : list[PParameter]
    {
      def parsed = LightList();
      foreach (p in parms)
        parsed.Add(ParseParameter(context, p.Name, p.Type));
      parsed.ToNList()
    }

    public Parse(type : G.Type, context : Context) : PExpr
    {
      ParseImpl(type, context)
    }

    public Parse(type : G.QualifiedIdentifier, context : Context) : PExpr
    {
      ParseImpl(type, context)
    }

    ParseImpl(parseTree : Nitra.ParseTree, context : Context) : PExpr
    {
      def env = context.GetNemerleEnvironment();
      def simpleParse(parseTree : Nitra.ParseTree) : PExpr
      {
        def text = parseTree.Location.GetText();
        MainParser.ParseExpr(env, text, parseTree.NLocation())
      }
      def parseAndAddIt(parseTree : Nitra.ParseTree) : PExpr
      {
        def e = simpleParse(parseTree);

        def addIt(e)
        {
          | <[ value ]> => e
          | <[ $(n : name) ]> => <[ it.$(n : name)]>
          | <[ $x = $y ]> => <[ $(addIt(x)) = $(addIt(y)) ]>
          | _ => e
        }

        addIt(e)
      }

      match (parseTree)
      {
        | G.Expression(ExpressionContentsData=[G.ExpressionContent.Content1(Content=G.Content.Fold as fold)]) =>
          def collection = simpleParse(fold.Identifier);
          def expr1 = simpleParse(fold.Expression);
          def exprs = fold.Expressions.Item1.Map(parseAndAddIt);
          //exprs.Fold()
          <[ $collection.Fold($expr1, fun((it, value)) { ..$exprs }) ]>

        | _ => simpleParse(parseTree)
      }
    }
  }
}
