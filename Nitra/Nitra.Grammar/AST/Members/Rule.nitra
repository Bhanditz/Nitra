using DotNet;

using Nitra.AstUtils;
using Nitra.Runtime.Binding;

using System.Collections.Generic;

using R  = Nitra.Ast.RuleExpressions;
using RX = Nitra.Ast.RegexExpressions;

namespace Nitra.Ast
{
  abstract declaration RuleType : Type
  {
    symbol
    {
      Kind = "rule type";
    }
  }

           declaration RuleVoidType    : RuleType { }
           declaration RuleListType    : RuleType { symbol { in ElemType  : RuleTypeSymbol; } }
           declaration RuleTupleType   : RuleType { symbol { in ElemTypes : IList[RuleTypeSymbol]; } }
           declaration RuleOptionType  : RuleType { symbol { in ValueType : RuleTypeSymbol; } }
  abstract declaration RuleCharsType   : RuleType { symbol { } }
           declaration RuleLiteralType : RuleCharsType { }
}

namespace Nitra.Ast
{
  abstract declaration Rule : RuleType, SyntaxElementContainer
  {
    symbol
    {
      out MemberTable : TableScope = TableScope(this.ToString(), this);

      SpanClass = NitraLangLanguage.RuleSpanClass;
      Kind      = "syntax";
    }
  }

  abstract declaration RegexRule : RuleCharsType, Rule
  {
    symbol
    {
      Kind = "regex";
      SpanClass = NitraLangLanguage.RegexRuleSpanClass;
    }
    
    //TypeSubst = "char";

    Expression.Scope = Scope;

    Expression : RX.Expression;
  }

  abstract declaration SyntaxRule : Rule
  {
    Expression.Literals = LiteralsIn;
    Expression.Scope = Scope;

    Expression : R.Expression;
  }

  abstract declaration SimpleRule : SyntaxRule { }

  abstract declaration ExtensibleRule : Rule
  {
    Members.LiteralsIn = LiteralsIn;
    Members.ContainingTable = Symbol.MemberTable;
    Members.Scope = Scope;

    Members : RuleBodyMember*;
  }
}
