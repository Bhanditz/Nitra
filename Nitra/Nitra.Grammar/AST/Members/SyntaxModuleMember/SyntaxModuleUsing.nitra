using DotNet;

using Nitra;
using Nitra.Runtime.Binding;

using Nemerle.Collections;

namespace Nitra.Ast
{
  abstract ast SyntaxModuleUsingDirective : ScopedAst
  {
  stage 1:
    inout Literals          : Map[string, string];
    in    UsingAliasesScope : TableScope;
    in    CurrentScope      : Scope;
    inout Opening           : list[Scope];
  }

  declaration SyntaxModuleUsingAliasDirective : SyntaxModuleUsingDirective, Alias
  {
    symbol
    {
      Kind      = "alias";
      SpanClass = DotNetLangLanguage.AliasSpanClass;
    }

    ContainingTable           = UsingAliasesScope;
    NamespaceOrTypeName.Scope = CurrentScope;
    Symbol.Replacement        = NamespaceOrTypeName.Ref;

    NamespaceOrTypeName : QualifiedReference;
  }

  ast SyntaxModuleUsingOpenDirective : SyntaxModuleUsingDirective
  {
    LiteralsOut               = AstUtils.TryGetLiterals(NamespaceOrTypeName.Ref, LiteralsIn);
    ContainingTable           = null;
    NamespaceOrTypeName.Scope = CurrentScope;
    OpeningOut                = AstUtils.PrependScopeIfResolved(NamespaceOrTypeName.Ref, OpeningIn);

    unless (NamespaceOrTypeName.Ref.Symbol is NamespaceSymbol || NamespaceOrTypeName.Ref.Symbol is SyntaxModuleSymbol)
      Error("Using directive can open only namespace or syntax module.");

    NamespaceOrTypeName : QualifiedReference;
  }
}
