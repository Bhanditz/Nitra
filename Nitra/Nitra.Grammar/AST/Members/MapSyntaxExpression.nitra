using DotNet;

using Nitra.Runtime.Binding;

namespace Nitra.Ast
{
  abstract ast MapSyntaxExpression : BindableAst
  {
  stage 1:
    in AstScope : Scope;

    | This
      {
      }

    | Identifier
      {
        Reference.Scope = Scope;

        Reference : Reference;
      }

    | Ast
      {
        Ast.Scope = AstScope;
      stage 1:
        out AstTypeRef : Ref[AstTypeSymbol] = Ast.Ref.Resolve();

        Members.Scope    = Scope;
        Members.AstType  = AstTypeRef.Symbol;
        Members.AstScope = AstScope;

        Ast     : QualifiedReference;
        Members : MapSyntaxExpressionAstMember*;
      }

    | List
      {
        Items.AstScope = AstScope;
        Items.Scope    = Scope;

        Items : MapSyntaxExpression*;
      }

    | OptionSome
      {
        Value.AstScope = AstScope;
        Value.Scope    = Scope;

        Value : MapSyntaxExpression;
      }

    | OptionNone
      {
      }

    | Match
      {
        Expression.AstScope = AstScope;
        Expression.Scope    = Scope;
        Cases.AstScope      = AstScope;
        Cases.Scope         = Scope;

        Expression : MapSyntaxExpression;
        Cases      : MapSyntaxMatchCase*;
      }

    | Fold
      {
        ItemName         : Name;
        ItemExpression   : MapSyntaxExpression;

        AccumulatorName1 : Name;
        InitExpression   : MapSyntaxExpression;

        AccumulatorName2 : Reference;
        EachExpression   : MapSyntaxExpression;
      }

    | DefineVariable
      {
        VariableName : Name;
        VariableType : QualifiedReference;
        Expression   : MapSyntaxExpression;
        Body         : MapSyntaxExpression;
      }

    | TypeHint
      {
        Expression.AstScope = AstScope;
        Expression.Scope    = Scope;
        Ast.Scope           = AstScope;

        Expression : MapSyntaxExpression;
        Ast        : QualifiedReference;
      }

    | Call
      {
        Expression.AstScope = AstScope;
        Expression.Scope    = Scope;
        Arguments.AstScope  = AstScope;
        Arguments.Scope     = Scope;

        Expression : MapSyntaxExpression;
        Arguments  : MapSyntaxExpression*;
      }

    | MemberAccess
      {
        Expression.AstScope = AstScope;
        Expression.Scope    = Scope;
        // Reference.Scope = Expression.Type.Scope;

        Expression : MapSyntaxExpression;
        Reference  : Reference;
      }
  }
}
