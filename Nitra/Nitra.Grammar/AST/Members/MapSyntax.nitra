using DotNet;

using Nitra.Runtime.Binding;

namespace Nitra.Ast
{
  abstract ast MapSyntax : BindableAst
  {
    AstName.Scope = Scope;

    out ParameterScope : TableScope = TableScope("parameters", null);

  stage 1:
    out Rule    : RuleSymbol;
    out AstType : AstTypeSymbol;
    out AstRef  : Ref[AstTypeSymbol] = this.AstName.Ref.Resolve();

    Parameters.ContainingTable = ParameterScope;
    AstType       = AstRef.Symbol;
    Body.Scope    = Scope.HideWith(ParameterScope);
    Body.Rule     = Rule;
    Body.AstType  = AstType;

    Parameters : MapSyntaxParameter*;
    AstName    : QualifiedReference;
    Body       : MapSyntaxBody;
  }

  declaration MapSyntaxParameter
  {
    symbol
    {
      Kind      = "parameter";
      SpanClass = DotNetLangLanguage.ParameterSpanClass;
    }

    AstName : QualifiedReference;
  }

  ast TopMapSyntax : MapSyntax, NamespaceMember
  {
    RuleName.Scope = Scope;

  stage 1:
    out RuleRef : Ref[RuleSymbol] = this.RuleName.Ref.Resolve();

    Rule = RuleRef.Symbol;

    RuleName : QualifiedReference;
  }

  ast InlineMapSyntax : MapSyntax, RuleBodyMember
  {
    Rule = Parent;
  }

  ast ExtensionMapSyntax : BindableAst
  {
    RuleName.Scope = Scope;

  stage 1:
    out RuleRef : Ref[RuleSymbol] = this.RuleName.Ref.Resolve();

    RuleName : Reference;
    Body     : MapSyntaxBody;
  }

  abstract ast MapSyntaxBody : BindableAst
  {
  stage 1:
    in Rule    : RuleSymbol;
    in AstType : AstTypeSymbol;

    | Empty
      {
      }

    | Inline
      {
        Code : string;
      }

    | PerMember
      {
        Members.Scope   = Scope;

        Members : MapSyntaxBodyMember*;
      }

    | Expression
      {
        Expression : MapSyntaxExpression;
      }

    | Extensions
      {
        Members : ExtensionMapSyntax*;
      }
  }

  abstract ast MapSyntaxBodyMember : BindableAst
  {
  stage 1:
    in Rule    : RuleSymbol;
    in AstType : AstTypeSymbol;
  }

  abstract ast MapSyntaxExpressionAstMember : MapSyntaxBodyMember
  {
    AstProperty.Scope  = AstType.Scope;
    AstProperty : Reference;

    | Inline
      {
        Code : string;
      }

    | Expression
      {
        Expression  : MapSyntaxExpression;
      }
  }


  abstract ast ParseTreePattern
  {
    in VariableScope : TableScope;

    | Identifier
      {
        Reference : Reference;
      }

    | Ctor
      {
        Reference : Reference;
        Arguments : ParseTreePattern*;
      }

    | Tuple
      {
        Members : ParseTreePattern*;
      }

    | List1
      {
        Head : ParseTreePattern;
        Tail : ParseTreePattern;
      }

    | List2
      {
        Items : ParseTreePattern*;
      }
  }
}
