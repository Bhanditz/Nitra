using Nitra.Runtime.Binding;
using Nitra.ProjectSystem;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Globalization;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Ast
{
  public sealed class RuleFieldNameBuilder
  {
    private _table : Dictionary[int, bool * int];

    public this(names : array[ParsedValue[string]])
    {
      when (names.Length > 1)
      {
        def table = Dictionary(names.Length);
        mutable containsNonUniqeNames = false;
        foreach (name in names)
        {
          mutable data;
          def id = GetId(name);
          if (table.TryGetValue(id, out data))
          {
            containsNonUniqeNames = true;
            data = (false, 0);
          }
          else
            data = (true, 0);

          table[id] = data;
        }
        when (containsNonUniqeNames)
          _table = table;
      }
    }

    public GetName(file : File, desiredName : ParsedValue[string]) : Name
    {
      def id = GetId(desiredName);
      if (_table : object == null)
        Name(file, desiredName.Span, id)
      else
      {
        def data = _table[id];
        if (data[0])
          Name(file, desiredName.Span, id)
        else
        {
          // TODO: generated name can be non unique in some cases
          def index   = data[1] + 1;
          _table[id]  = (false, index);
          def newName = StringIndex.GetText(id) + index.ToString(CultureInfo.InvariantCulture);
          Name(file, desiredName.Span, newName)
        }
      }
    }

    private static GetId(desiredName : ParsedValue[string]) : int
    {
      def text = if (desiredName.HasValue) desiredName.Value else "Node";
      StringIndex.GetId(text)
    }
  }
}
