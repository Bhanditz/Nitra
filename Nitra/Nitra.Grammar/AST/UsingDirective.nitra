using Nitra;
using Nitra.Runtime.Binding;
using Nitra.Ast.SymbolUtils;

namespace Nitra.Ast
{
  abstract ast UsingDirective : ScopedAst, NamespaceMember
  {
  }

  declaration UsingAliasDirective : UsingDirective
  {
    symbol
    {
      Kind      = "alias";
      SpanClass = NitraLangLanguage.AliasSpanClass;
      Scope     = ReplacementScope;

      in ReplacementSymbol : ISymbol2;
      in ReplacementScope  : Scope;
      in Type              : TypeReference;
    }

    ContainingTable           = UsingAliasesScope;
    NamespaceOrTypeName.Scope = CurrentScope;
    Symbol.ReplacementSymbol  = NamespaceOrTypeName.Ref.Symbol;
    Symbol.ReplacementScope   = NamespaceOrTypeName.Ref.Scope;
    Symbol.Type               = NamespaceOrTypeName.Type;

    NamespaceOrTypeName : QualifiedReference;
  }

  ast UsingOpenDirective : UsingDirective
  {
    ContainingTable           = null;
    NamespaceOrTypeName.Scope = CurrentScope;
    OpeningOut                = SymbolUtils.PrependScopeIfResolved(NamespaceOrTypeName.Ref, OpeningIn);

    unless (NamespaceOrTypeName.Ref.Symbol is NamespaceSymbol || NamespaceOrTypeName.Ref.Symbol is SyntaxModuleSymbol)
      Error("Using directive can open only namespace or syntax module.");

    NamespaceOrTypeName : QualifiedReference;
  }
}
