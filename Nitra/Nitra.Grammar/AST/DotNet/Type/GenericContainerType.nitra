using DotNet;

using Nemerle.Collections;

using Nitra;
using Nitra.Ast;
using Nitra.Declarations;
using Nitra.Runtime.Binding;
using Nitra.Internal;

using System.Linq;
using SCG = System.Collections.Generic;

namespace DotNet
{
  // class, struct or interface
  abstract declaration GenericContainerType : GenericType
  {
    symbol
    {
      SpanClass = DotNetLangLanguage.TypeSpanClass;
      Scope     = MemberTable;

      out MemberTable     : TableScope       = TableScope(this.ToString(), this);
      out IsPartial       : bool             = Utils.IsPartial(this.Declarations);
      in  BaseTypeSet     : BaseTypeReferenceSet;

      when (BaseTypeSet.IsCycled)
        Error($"'$(self.Name)' is cycled.");
    }

    TypeBase.Scope   = TypeParameterConstraints.Scope;
    Members.Parent   = Symbol;
    Members.Scope    = Scope.HideWith(Symbol.BaseTypeSet.ParentScope)
                            .HideWith(TypeParameterAliasesScope)
                            .HideWith(Symbol.MemberTable);

    Symbol.BaseTypeSet |= SymbolUtils.FilterResolved(TypeBase.Ref);

    TypeBase                 : QualifiedReference*;
    Members                  : TypeMember*;
    IsPartial                : bool;
  }

  abstract declaration TopGenericType    : GenericType, NamespaceMemberDeclaration { }
  abstract declaration NestedGenericType : GenericType, TypeMember
  {
    symbol
    {
      TypeSubst = SymbolUtils.CreateNestedConstructedTypeSymbol(this, this.Parent.TypeSubst, context);
    }
  }

  abstract declaration Class     : GenericContainerType { symbol { Kind = "class";     } }
  abstract declaration Struct    : GenericContainerType { symbol { Kind = "struct";    } }
  abstract declaration Interface : GenericContainerType { symbol { Kind = "interface"; } }

  declaration TopClass        : Class,     TopGenericType    { symbol { IsSameIdentity = SymbolUtils.IsSameIdentity(TypeParametersCount, candidate); CanMerge = SymbolUtils.CanMerge.[TopClass       ](this, candidate); } }
  declaration NestedClass     : Class,     NestedGenericType { symbol { IsSameIdentity = SymbolUtils.IsSameIdentity(TypeParametersCount, candidate); CanMerge = SymbolUtils.CanMerge.[NestedClass    ](this, candidate); } }
  declaration TopStruct       : Struct,    TopGenericType    { symbol { IsSameIdentity = SymbolUtils.IsSameIdentity(TypeParametersCount, candidate); CanMerge = SymbolUtils.CanMerge.[TopStruct      ](this, candidate); } }
  declaration NestedStruct    : Struct,    NestedGenericType { symbol { IsSameIdentity = SymbolUtils.IsSameIdentity(TypeParametersCount, candidate); CanMerge = SymbolUtils.CanMerge.[NestedStruct   ](this, candidate); } }
  declaration TopInterface    : Interface, TopGenericType    { symbol { IsSameIdentity = SymbolUtils.IsSameIdentity(TypeParametersCount, candidate); CanMerge = SymbolUtils.CanMerge.[TopInterface   ](this, candidate); } }
  declaration NestedInterface : Interface, NestedGenericType { symbol { IsSameIdentity = SymbolUtils.IsSameIdentity(TypeParametersCount, candidate); CanMerge = SymbolUtils.CanMerge.[NestedInterface](this, candidate); } }
}
