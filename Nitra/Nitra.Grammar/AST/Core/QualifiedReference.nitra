using Nitra;
using Nitra.Runtime.Binding;
using System.Linq;

namespace DotNet
{
  abstract ast QualifiedReference
  {
    out FieldNameId : ParsedValue[int];
  stage 1:
    in  Arity   : int = 0;
    in  Scope   : Scope;
    in  NameScopeFactory : ScopeFactory = ScopeFactories.Default;
    out Ref     : Ref[ISymbol2];
    out Type    : TypeReference;

    | Simple
      {
        FieldNameId = ParsedValue(Name.Span, Name.Id);
        Name.Scope  = NameScopeFactory(Scope);
        Ref         = Name.Ref.Resolve(AstUtils.TypeOverloadResolution(Arity));
        Type        = TypeReference.Create(Ref);

        Name : Reference;
      }

    | Aliased
      {
        FieldNameId = ParsedValue(Name.Span, Name.Id);
        Name.Scope  = NameScopeFactory(Scope);
        Ref         = Name.Ref.Resolve(AstUtils.TypeOverloadResolution(Arity));
        Type        = TypeReference.Create(Ref);

        Alias : Reference;
        Name  : Reference;
      }

    | Qualified
      {
        FieldNameId  = ParsedValue(Name.Span, Name.Id);
        Qualifier.Arity = 0;
        Qualifier.Scope = Scope;
        Name.Scope      = NameScopeFactory(Qualifier.Ref.Scope);
        Ref             = Name.Ref.Resolve(AstUtils.TypeOverloadResolution(Arity));
        Type            = TypeReference.Create(Ref);

        Qualifier : QualifiedReference;
        Name      : Reference;
      }

    | Generic
      {
        FieldNameId         = QualifiedName.FieldNameId;
        Arguments.Arity     = 0;
        QualifiedName.Arity = Arguments.Count;
        QualifiedName.Scope = Scope;
        QualifiedName.NameScopeFactory = NameScopeFactory;
        Arguments.Scope     = Scope;
        Ref                 = QualifiedName.Ref;
        Type                = TypeReference.Create(Ref, Arguments.Type);

        QualifiedName : QualifiedReference;
        Arguments     : QualifiedReference*; // TODO: change to  QualifiedReference+
      }
  }
}
