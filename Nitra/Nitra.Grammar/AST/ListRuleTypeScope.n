using Nitra;
using Nitra.Declarations;
using Nitra.Serialization2;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Ast
{
  public sealed class ListRuleTypeScope : RuleTypeScope
  {
    public this(itemType : RuleTypeSymbol)
    {
      base(itemType);
    }

    protected override TryCreateMapSyntaxSymbol(itemMapSyntax : MapSyntaxSymbol, context : DependentPropertyEvalContext) : MapSyntaxSymbol
    {
      match (itemMapSyntax.ReturnType)
      {
        | ListAstTypeSymbol(IsElemTypeEvaluated = true, ElemType = AstSymbol as t)
        | OptionAstTypeSymbol(IsValueTypeEvaluated = true, ValueType = AstSymbol as t)
        | AstSymbol as t =>
          def decl          = ExternalDeclaration.[MapSyntaxSymbol](SourceSnapshot.Default.File, NSpan(), itemMapSyntax.Name);
          def listMapSyntax = decl.DefineSymbol();
          listMapSyntax.Parameters = itemMapSyntax.Parameters;
          listMapSyntax.ReturnType = AstUtils.CreateListAstType(decl, t, context);
          listMapSyntax.EvalProperties(context);
          listMapSyntax

        | _ => null
      }
    }

    public override Serialize(writer : IO.BinaryWriter, metadataWriter : MetadataWriter) : void
    {
      metadataWriter.WriteObject(_itemType, writer);
    }

    public static Deserialize(reader : IO.BinaryReader, metadataReader : MetadataReader) : this
    {
      def itemType = metadataReader.ReadObject(reader);
      ListRuleTypeScope(itemType)
    }
  }
}
