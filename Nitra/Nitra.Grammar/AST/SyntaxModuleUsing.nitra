using Nitra;
using Nitra.Runtime.Binding;
using Nitra.Ast.SymbolUtils;

using Nemerle.Collections;

namespace Nitra.Ast
{
  abstract ast SyntaxModuleUsingDirective : ScopedAst
  {
  stage 1:
    inout Literals          : Map[string, string];
    in    UsingAliasesScope : TableScope;
    in    CurrentScope      : Scope;
    inout Opening           : list[Scope];
  }

  declaration SyntaxModuleUsingAliasDirective : SyntaxModuleUsingDirective
  {
    symbol
    {
      Kind      = "alias";
      SpanClass = NitraLangLanguage.AliasSpanClass;

      in ReplacementSymbol : ISymbol2;
      in Type              : TypeReference;
    }

    ContainingTable           = UsingAliasesScope;
    NamespaceOrTypeName.Scope = CurrentScope;
    Symbol.ReplacementSymbol  = NamespaceOrTypeName.Ref.Symbol;
    Symbol.Type               = NamespaceOrTypeName.Type;

    NamespaceOrTypeName : QualifiedReference;
  }

  ast SyntaxModuleUsingOpenDirective : SyntaxModuleUsingDirective
  {
    LiteralsOut               = SymbolUtils.TryGetLiterals(NamespaceOrTypeName.Ref, LiteralsIn);
    ContainingTable           = null;
    NamespaceOrTypeName.Scope = CurrentScope;
    OpeningOut                = SymbolUtils.PrependScopeIfResolved(NamespaceOrTypeName.Ref, OpeningIn);

    unless (NamespaceOrTypeName.Ref.Symbol is NamespaceSymbol || NamespaceOrTypeName.Ref.Symbol is SyntaxModuleSymbol)
      Error("Using directive can open only namespace or syntax module.");

    NamespaceOrTypeName : QualifiedReference;
  }
}
