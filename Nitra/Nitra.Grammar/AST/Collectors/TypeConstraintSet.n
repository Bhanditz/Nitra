using Nitra;
using Nitra.Ast;
using Nitra.Declarations;
using Nitra.Runtime.Binding;
using Nitra.Serialization2;
using Nitra.Staging;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace DotNet
{
  [Flags]
  public enum TypeConstraint : byte
  {
    | None   = 0
    | Class  = 1
    | Struct = 2
    | Ctor   = 4
  }

  public sealed class TypeConstraintSet : ICollector, IEquatable[TypeConstraintSet]
  {
    private this(baseTypes : Seq[TypeReference], flags : TypeConstraint)
    {
      _baseTypes = HashSet(baseTypes);
      _flags     = flags;
    }

    public this(context : DependentPropertyEvalContext)
    {
      context.NotifyCollectorCreated(out CreatedOnStage, out CreatedOnPass);
      _baseTypes    = HashSet();
    }

    public CreatedOnStage           : int  { get; }
    public CreatedOnPass : int  { get; }
    public IsEmpty                  : bool { get { _flags == TypeConstraint.None && _baseTypes.Count == 0 } }

    private         _baseTypes : HashSet[TypeReference];
    private mutable _flags     : TypeConstraint;
    
    public SpecialConstraintMask : TypeConstraint { get { _flags } }
    
    public BaseTypes : Seq[TypeReference] { get { _baseTypes } }

    [Collect]
    public Add(baseType : TypeReference) : void
    {
      _ = _baseTypes.Add(baseType);
    }

    [Collect]
    public Add(flag : TypeConstraint) : void
    {
      _flags |= flag;
    }

    [Collect]
    public Add(set : TypeConstraintSet) : void
    {
      _baseTypes.UnionWith(set._baseTypes);
      _flags |= set._flags;
    }

    public Contains(flag : TypeConstraint) : bool
    {
      _flags %&& flag
    }

    public Contains(baseType : TypeReference) : bool
    {
      _baseTypes.Contains(baseType)
    }

    public override ToString() : string
    {
      $"$CreatedOnStage($CreatedOnPass): $_flags, ..$_baseTypes"
    }

    public Equals(other : TypeConstraintSet) : bool
      implements IEquatable[TypeConstraintSet].Equals
    {
      | null => false
      | _ => this._flags == other._flags
        && this._baseTypes.Count == other._baseTypes.Count
        && this._baseTypes.SetEquals(other._baseTypes)
    }

    public override Equals(other : object) : bool
    {
      | TypeConstraintSet as other => Equals(other)
      | _ => false
    }

    public override GetHashCode() : int
    {
      (this._baseTypes.Count << 4) | (this._flags :> int)
    }

    public Serialize(writer : IO.BinaryWriter, metadataWriter : MetadataWriter) : void
    {
      metadataWriter.WriteObjects(_baseTypes, writer);
      writer.Write(_flags :> int);
    }

    public static Deserialize(reader : IO.BinaryReader, metadataReader : MetadataReader) : this
    {
      def baseTypes = metadataReader.ReadObjects(reader);
      def flags     = reader.ReadInt32() :> TypeConstraint;
      TypeConstraintSet(baseTypes, flags)
    }
  }
}
