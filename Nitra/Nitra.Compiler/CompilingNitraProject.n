using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra.Runtime;
using Nitra.Typing;
using Nitra.ProjectSystem;

using System;
using System.Reflection;
using SCG = System.Collections.Generic;

namespace Nitra.Compiler
{
  internal sealed class CompilingNitraProject : NitraProject
  {
    public Environment : EmitterEnvironment { get; private set; }

    public override Files : Seq[File] { get { throw NotImplementedException() } }

    public override Init(referencedAssemblies : Seq[AssemblyName]) : void
    {
      base.Init(referencedAssemblies);
      Environment = EmitterEnvironment(ManagerClass.Instance, RootNamespace.Node);
      _fakeSourceSnapshots.Clear();
    }

    public override GetNemerleType(symbol : Symbol) : FixedType
    {
      | ruleSymbol        is RuleSymbol        => Environment.GetParseTreeType(ruleSymbol)
      | declarationSymbol is DeclarationSymbol => Environment.GetDeclarationType(declarationSymbol)
      | _ => throw NotSupportedException()
    }

    private _fakeSourceSnapshots : SCG.Dictionary[int, SourceSnapshot] = SCG.Dictionary();

    public override MakeFakeNitraLocation(location : Nemerle.Compiler.Location) : Nitra.Location
    {
      mutable sourceSnapshot;
      unless (_fakeSourceSnapshots.TryGetValue(location.FileIndex, out sourceSnapshot))
      {
        sourceSnapshot = SourceSnapshot("", "", location.FileIndex, location.File, array(0), 0);
        _fakeSourceSnapshots.Add(location.FileIndex, sourceSnapshot);
      }
      Nitra.Location(sourceSnapshot, NSpan(0, 0))
    }
  }
}
