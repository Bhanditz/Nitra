using Nitra.Typing;
using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using Nitra.Model;

namespace Nitra.Compiler
{
  internal partial class FindRecoveryPositionMethodEmitter : MethodEmitter
  {
    public CompileFunction(sequence : SequenceCompileInfo) : void
    {
      def body = if (Owner.Environment.IsIdeMode)
        <[ IgnoreParams(); assert(false); ]>
      else
        CompileFunctionBody(sequence);
      def function = match (sequence)
      {
        | SequenceCompileInfo.Ast(Symbol = ExtensionRuleSymbol) =>
          <[decl:
            public override FindRecoveryPosition(mutable curTextPos : int, astPtr : int, stack : Nitra.Internal.RecoveryStackFrame, parseResult : Nitra.Internal.ParseResult) : void
            {
              $body
            }
          ]>
        | SequenceCompileInfo.Ast(Symbol = SimpleRuleSymbol) =>
          <[decl:
            public override FindRecoveryPosition(mutable curTextPos : int, stack : Nitra.Internal.RecoveryStackFrame, parseResult : Nitra.Internal.ParseResult) : void
            {
              $body
            }
          ]>
        | SequenceCompileInfo.Ast => assert3(false)
        | SequenceCompileInfo.List                       as structure1 when structure1.UseCounter
        | SequenceCompileInfo.ListWithSeparatorRule      as structure2 when structure2.UseCounter
        | SequenceCompileInfo.ListWithSeparatorSeparator as structure3 when structure3.UseCounter =>
          <[decl:
            private $(FunctionName(sequence) : global)(mutable curTextPos : int, counter : int, stack : Nitra.Internal.RecoveryStackFrame, parseResult : Nitra.Internal.ParseResult) : void
            {
              $body
            }
          ]>;
        | _ =>
          <[decl:
            private $(FunctionName(sequence) : global)(mutable curTextPos : int, stack : Nitra.Internal.RecoveryStackFrame, parseResult : Nitra.Internal.ParseResult) : void
            {
              $body
            }
          ]>;
      }
      TypeBuilder.DefineConditional(function);
    }

    public CompileFunctionBody(sequence : SequenceCompileInfo) : PExpr
    {
      def setAstPtr = match (sequence)
      {
        | SequenceCompileInfo.Ast(Symbol = ExtensionRuleSymbol) =>
          <[
            def $(AstPtr(sequence).Var) = astPtr;
            def astHandle = parseResult.AstHandle_Extension(stack, this, $(AstPtr(sequence).Var), $CurTextPos);
          ]>
        | SequenceCompileInfo.Ast(Symbol = SimpleRuleSymbol) =>
          <[
            def $(AstPtr(sequence).Var) = parseResult.TryGetAst($CurTextPos, $(sequence.AstId : global));
            def astHandle = parseResult.AstHandle_Simple(this, $(AstPtr(sequence).Var), $CurTextPos);
          ]>
        | Option
        | List
        | ListWithSeparatorRule
        | ListWithSeparatorSeparator =>
          <[
            def $(AstPtr(sequence).Var) = parseResult.TryGetAst($CurTextPos, $(sequence.AstId : global));
            def astHandle = parseResult.AstHandle_Subrule(this, $(sequence.AstId : global), $(AstPtr(sequence).Var), $CurTextPos);
          ]>
        | SequenceCompileInfo.Ast => assert(false);
        | SequenceCompileInfo.Predicate => assert(false);
      }

      def fieldsCode = SCG.List();
      CompileFieldsNoneRecursive(fieldsCode, sequence);
      def failFields = sequence.NonMarkerSubrules.Skip(1).NToList();
      def endSelect = Environment.MakeLabel("EndSelect");
      def selectFailFieldMatch =
        [<[case: | $(sequence.NonMarkerSubrules.First().State) => startState = $(sequence.NonMarkerSubrules.First().State); $(endSelect.Goto());]>] +
        failFields.Map(subrule => <[case: | $(subrule.State) => $(GotoSelectStartStateLabel(subrule.PrevSibling))]>) +
        [<[case: | _ => assert3(false) ]>];
      def selectFailField = failFields.Reverse().Map(_.PrevSibling).Map(subrule => <[ $(SelectStartStateLabel(subrule)); startState = $(subrule.State); when ($(AstPtr(sequence).GetRaw(subrule)) != 0) $(endSelect.Goto()); ]>);
      <[
        _ = $(sequence.ToString());
        $setAstPtr;

        mutable startState;
        mutable parseState;
        if ($(AstPtr(sequence).Var) > 0)
        {
          parseState = $(AstPtr(sequence).Get(sequence.HeaderState))
        }
        else
        {
          parseState = $(sequence.NonMarkerSubrules.First().State);
          startState = $(sequence.NonMarkerSubrules.First().State);
          $(endSelect.Goto());
        }

        when (parseState == Nitra.Internal.ParseResult.AstParsedState)
          parseState = $(sequence.NonMarkerSubrules.Last().State);
        assert3(parseState >= 0);

        match (parseState)
        {
          ..$selectFailFieldMatch
        }
        ..$selectFailField;
        $(endSelect.Label());

        ret: { $(fieldsCode.FlattenPExpr()) }
      ]>.FlattenPExpr();
    }
  }
}
