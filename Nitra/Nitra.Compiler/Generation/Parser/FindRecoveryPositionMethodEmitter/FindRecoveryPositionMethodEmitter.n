using Nitra.Typing;
using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using Nitra.Model;

using Nemerle.Compiler;

namespace Nitra.Compiler
{
  internal partial class FindRecoveryPositionMethodEmitter : MethodEmitter
  {
    private EndLabel : PExprLabel;

    public this(owner : RuleParserEmitter, tb : TypeBuilder, ruleStructure : SequenceCompileInfo.Ast)
    {
      base(owner, ruleStructure, tb);
      _data                       = RuleStructureDataContainer(ruleStructure, this, RuleStructureData, RuleStructureFieldData);
      EndLabel                    = Environment.MakeLabel("EndParse");
    }

    public Emit() : void
    {
      CompileFuntion(SequenceCompileInfo)
    }

    public CurTextPos : PExpr { get { <[ curTextPos ]> } }

    public MakeRecoveryStackFrame(field : RuleStructureField, textPos : PExpr) : PExpr
    {
      def getCounter()
      {
        match (field.Owner.Owner)
        {
          | List              as field when field.UseCounter => <[ counter ]>
          | ListWithSeparator as field when field.UseCounter => <[ counter ]>
          | List                                             => <[ 0 ]>
          | ListWithSeparator                                => <[ 0 ]>
          | _                                                => assert(false);
        }
      }
      match (field.Owner)
      {
        | SequenceCompileInfo.Ast
        | SequenceCompileInfo.Option                    => <[ parseResult.RecoveryStackFrame_Rule(stack, $textPos, astHandle, $(field.State)); ]>
        | SequenceCompileInfo.List
        | SequenceCompileInfo.ListWithSeparatorRule     => <[ parseResult.RecoveryStackFrame_ListBody(stack, $textPos, astHandle, $(field.State), $(getCounter())); ]>
        | SequenceCompileInfo.ListWithSeparatorSeparator=> <[ parseResult.RecoveryStackFrame_ListSeparator(stack, $textPos, astHandle, $(field.State), $(getCounter())); ]>
        | SequenceCompileInfo.Predicate                 => assert(false);
      }
    }
  }
}
