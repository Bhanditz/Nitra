using Nitra.Typing;
using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using Nitra.Model;

using Nemerle.Compiler;

namespace Nitra.Compiler
{
  internal partial class ParseMethodEmitter : MethodEmitter
  {
    public this(owner : RuleParserEmitter, tb : TypeBuilder, ruleStructure : SequenceCompileInfo.Ast)
    {
      base(owner, ruleStructure, tb);
      _data         = RuleStructureDataContainer(ruleStructure, this, RuleStructureData, RuleStructureFieldData);
    }

    public Emit() : void
    {
      CompileFuntion(SequenceCompileInfo);
    }

    public CurTextPos : PExpr { get { <[ curTextPos ]> } }

    public MakeAstForSingleCallExtension(field : SubruleCompileInfo) : PExpr
    {
      match (field, field.Owner)
      {
        | (SubruleCompileInfo.SimpleCall as field, SequenceCompileInfo.Ast(Symbol = Nitra.Typing.ExtensionRuleSymbol) as structure) when field.AllocateAst && structure.Fields.Length == 1 =>
          <[
            when ($(AstPtr(structure).Var) == 0 && parseResult.TryGetAst($(AstStart(structure)), $(Owner.MakeParserName(field.Symbol) : global).RuleId) != 0)
              $(AstPtr(structure).Var) = parseResult.Allocate($(structure.Size), $(structure.AstId : global));
          ]>

        | (SubruleCompileInfo.ExtensibleCall as field, SequenceCompileInfo.Ast(Symbol = Nitra.Typing.ExtensionRuleSymbol) as structure) when field.AllocateAst && structure.Fields.Length == 1 =>
          <[
            when ($(AstPtr(structure).Var) == 0 && parseResult.TryGetAst($(AstStart(structure)), $(Owner.MakeParserName(field.Symbol, field.BindingPower) : global).PrefixId) != 0)
              $(AstPtr(structure).Var) = parseResult.Allocate($(structure.Size), $(structure.AstId : global));
          ]>

        | _ => <[ {} ]>
      }
    }

    public AllocateParentAst(field : SubruleCompileInfo) : PExpr
    {
      def structure = field.Owner;
      if (field.AllocateAst)
      {
        match (structure)
        {
          | SequenceCompileInfo.Predicate
          | SequenceCompileInfo.Ast(Symbol = Nitra.Typing.ExtensionRuleSymbol) =>
            <[
              when ($(AstPtr(structure).Var) == 0)
                $(AstPtr(structure).Var) = parseResult.Allocate($(structure.Size), $(structure.AstId : global));
            ]>

          | _ =>
            <[
              when ($(AstPtr(structure).Var) == 0)
              {
                $(AstPtr(structure).Var) = parseResult.Allocate($(structure.Size), $(structure.AstId : global));
                $(AstPtr(structure).Set(structure.HeaderNext, <[ parseResult.memoize[$(AstStart(structure))] ]>));
                parseResult.memoize[$(AstStart(structure))] = $(AstPtr(structure).Var);
              }
            ]>
        }
      }
      else
        <[ {} ]>
    }

    public RecoveryCode() : PExpr
    {
      <[
        when (parseResult.MaxFailPos < curTextPos)
          parseResult.MaxFailPos = curTextPos;
      ]>
    }
  }
}
