using Nitra.Typing;
using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using Nitra.Model;

namespace Nitra.Compiler
{
  internal partial class ParseMethodEmitter : MethodEmitter
  {
    public CompileFuntion(structure : SequenceCompileInfo) : void
    {
      def body = if (Owner.Environment.IsIdeMode)
        <[ IgnoreParams(); assert(false); ]>
      else
        CompileFuntionBody(structure);
      def function = match (structure)
      {
        | SequenceCompileInfo.Ast(Symbol = ExtensionRuleSymbol) =>
          <[decl:
            public override Parse(mutable curTextPos : int, text : string, resultRef : ref int, parseResult : Nitra.Internal.ParseResult) : int
            {
              $body
            }
          ]>
        | SequenceCompileInfo.Ast(Symbol = SimpleRuleSymbol) =>
          <[decl:
            public override Parse(mutable curTextPos : int, text : string, parseResult : Nitra.Internal.ParseResult) : int
            {
              $body
            }
          ]>
        | SequenceCompileInfo.Ast => assert3(false)
        | _ =>
          <[decl:
            private $(FunctionName(structure) : global)(mutable curTextPos : int, text : string, parseResult : Nitra.Internal.ParseResult) : int
            {
              $body
            }
          ]>;
      }
      TypeBuilder.DefineConditional(function);
    }

    public CompileFuntionBody(structure : SequenceCompileInfo) : PExpr
    {
      def code = SCG.List();
      def endLabel = Environment.MakeLabel("EndParse");
      def setResultRef = if (structure is SequenceCompileInfo.Ast(Symbol = ExtensionRuleSymbol))
        <[ resultRef = $(AstPtr(structure).Var); ]>
      else
        <[ {} ]>;
      def jumpToRecovery(structure : SequenceCompileInfo) : PExpr
      {
        def code = SCG.List();
        foreach (subrule in structure.NonMarkerFields)
          code.Add(<[
            if (parseState >= $(subrule.EndState))
              $CurTextPos += $(AstPtr(subrule.Owner).GetSize(subrule)); // skip
            else
              $(GotoRecoveryLabel(subrule))
          ]>);
        code.Add(<[ assert3(false) ]>);
        <[ { ..$code } ]>.FlattenPExpr();
      }
      def checkMemoize(structure : SequenceCompileInfo, okCode : PExpr, failCode : PExpr) : PExpr
      {
        <[
          $(AstPtr(structure).Var) = parseResult.TryGetAst($CurTextPos, $(structure.AstId : global));
          when ($(AstPtr(structure).Var) > 0)
          {
            parseState = $(AstPtr(structure).Get(structure.HeaderState));
            if (parseState == Nitra.Internal.ParseResult.AstParsedState)
            {
              $CurTextPos += parseResult.AstSize($(AstPtr(structure).Var), $(structure.SizesStart), $(structure.SizesEnd));
              $okCode;
            }
            else if (parseState < 0)
            {
              parseState = ~parseState;
              $(jumpToRecovery(structure : SequenceCompileInfo));
            }
            else
            {
              $failCode
            }
          }
        ]>
      }
      def memoizeCode = if (structure is SequenceCompileInfo.Ast as sequence)
      {
        if (sequence.Symbol is SimpleRuleSymbol)
          checkMemoize(sequence, endLabel.Goto(),
          <[
            $CurTextPos = -1;
            def maxFailPos = $(AstPtr(sequence).GetSize(sequence.NonMarkerFields.Last()));
            when (maxFailPos > parseResult.MaxFailPos)
              parseResult.MaxFailPos = maxFailPos;
            $(endLabel.Goto());
          ]>)
        else
          <[
            $(AstPtr(sequence).Var) = 0;
            when (resultRef > 0)
            {
              $(AstPtr(sequence).Var) = resultRef;
              parseState = parseResult.ast[resultRef + 2];
              when (parseState == Nitra.Internal.ParseResult.AstParsedState)
              {
                return $CurTextPos + parseResult.AstSize($(AstPtr(structure).Var), $(structure.SizesStart), $(structure.SizesEnd));
              }
              assert(parseState < 0);
              parseState = ~parseState;
              $(jumpToRecovery(sequence));
            }
          ]>;
      }
      else
        checkMemoize(structure, GotoOkLabel(structure), GotoFailLabel(structure));
      code.Add(<[
        mutable tmpSize    : int; _ = tmpSize;
        mutable parseState : int; _ = parseState;
        mutable newPos     : int; _ = newPos;
        mutable $(AstPtr(structure).Var);
        mutable $(AstStart(structure));
      ]>);
      code.Add(<[ _ = $(structure.ToString()) ]>);
      code.Add(memoizeCode);
      //code.AddRange(_fields.Map(subrule => <[ _ = $(subrule.Print()) ]>));

      code.Add(<[ $(AstStart(structure)) = $CurTextPos; ]>);
      CompileFieldsNoneRecursive(code, structure);

      //parse ok
      code.Add(OkLabel(structure));
      code.Add(AstPtr(structure).Set(structure.HeaderState, <[ Nitra.Internal.ParseResult.AstParsedState ]>));// правило полностью разобрано.
      code.Add(setResultRef);
      code.Add(endLabel.Goto());

      //parse fail
      code.Add(FailLabel(structure));
      code.Add(<[ when ($(AstPtr(structure).Var) > 0) // АСТ создано
                  {
                    $(AstPtr(structure).Set(structure.HeaderState, <[ parseState ]>));
                    $(AstPtr(structure).Set(structure.NonMarkerFields.Last(), <[ parseResult.MaxFailPos ]>));
                    $setResultRef;
                  }
      ]>);
      code.Add(<[ $CurTextPos = -1; $(endLabel.Goto()); ]>);

      def debugCode = if (structure is SequenceCompileInfo.Ast(Symbol = SyntaxRuleSymbol(LastLocation = Some(loc))) when Owner.Environment.Manager.Options.EmitDebug)
      {
        def debugCode = <[ () ]>;
        debugCode.Location = loc.NLocation();
        debugCode;
      }
      else
        <[ {} ]>;
      code.Add(<[ $(endLabel.Label()); $debugCode; $CurTextPos; ]>);

      code.FlattenPExpr();
    }
  }
}
