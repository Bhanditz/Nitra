using Nemerle;
using Nemerle.Imperative;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using Nitra.Model;

namespace Nitra.Compiler
{
  internal partial class TryParseMethodEmitter : MethodEmitter
  {
    public override CompileListWithSeparator(code : SCG.List[PExpr], subrule : SubruleCompileInfo.ListWithSeparator) : void
    {
      def hangingSeparatorCode = if (subrule.HangingSeparator)
        <[
          when ($(ListEndPos(subrule)) == $CurTextPos)
            $(GotoContinueParseLabel(subrule));
          $(ListEndPos(subrule)) = $CurTextPos;
        ]>
      else
        <[ {} ]>;

      def headPredicateCode = CompilePredicate(subrule.HeadPredicates, GotoFailParseLabel(subrule));

      def firstRuleChild = subrule.Rule.NonMarkerFields[0];
      def firstSeparatorChild = subrule.Separator.NonMarkerFields[0];
      code.Add(<[//TODO: Fix counters
        $(ParseLabel(subrule));
          _ = $(subrule.ToString());
          $headPredicateCode;
          $(ListStartPos(subrule)) = $CurTextPos;
          $(ListEndPos(subrule))   = $CurTextPos;
          $(GotoParseLabel(firstRuleChild));

        $(OkLabel(subrule.Rule));
          when (!continueList && $(EndOfFieldLevel(subrule)))
            $(_endLabel.Goto());
          when ($(ListEndPos(subrule)) == $CurTextPos)
            $(GotoContinueParseLabel(subrule));
          $(ListEndPos(subrule)) = $CurTextPos;
          $(GotoParseLabel(firstSeparatorChild));

        $(FailLabel(subrule.Rule));
          if ($(EndOfFieldLevel(subrule)))
          {
            //if (continueList)
            //{
            //  $CurTextPos = $(ListEndPos(subrule));
            //  $(AddParsedStateInfo(subrule, <[ $(ListEndPos(subrule)) - $(ListStartPos(subrule)) ]>));
            //}
            //else
              $CurTextPos = -1;
            $(_endLabel.Goto());
          }
          else
          {
            $CurTextPos = $(ListEndPos(subrule));
            $(AddParsedStateInfo(subrule, <[ $(ListEndPos(subrule)) - $(ListStartPos(subrule)) ]>));
            $(GotoContinueParseLabel(subrule));
          }

        $(OkLabel(subrule.Separator));
          when (!continueList && $(EndOfFieldLevel(subrule)))
            $(_endLabel.Goto());
          $hangingSeparatorCode;
          $(GotoParseLabel(firstRuleChild));

        $(FailLabel(subrule.Separator));
          if ($(EndOfFieldLevel(subrule)))
          {
            //if (continueList)
            //{
            //  $CurTextPos = $(ListEndPos(subrule));
            //  $(AddParsedStateInfo(subrule, <[ $(ListEndPos(subrule)) - $(ListStartPos(subrule)) ]>));
            //}
            //else
              $CurTextPos = -1;
            $(_endLabel.Goto());
          }
          else
          {
            $CurTextPos = $(ListEndPos(subrule));
            $(AddParsedStateInfo(subrule, <[ $(ListEndPos(subrule)) - $(ListStartPos(subrule)) ]>));
            $(GotoContinueParseLabel(subrule));
          }
      ]>);
    }
  }
}
