using Nitra.Internal;
using Nitra.Typing;
using Nitra.Model;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

namespace Nitra.Compiler
{
  internal sealed partial class TryParseMethodEmitter : MethodEmitter
  {
    private          _endLabel       : PExprLabel;

    public this(owner : RuleParserEmitter, tb : TypeBuilder, sequence : SequenceCompileInfo.Ast)
    {
      base(owner, sequence, tb);
      _data         = SequenceDataContainer(sequence, this, SequenceData, SubruleData);
      _endLabel     = Environment.MakeLabel("EndParse");
    }

    public Emit() : void
    {
      TypeBuilder.DefineConditional(<[decl:
        public override TryParse(frame : Nitra.Internal.RecoveryStackFrame, startState : int, mutable curTextPos : int, continueList : bool, parsedStates : SCG.List[Nitra.Internal.ParsedStateInfo], parseResult : Nitra.Internal.ParseResult) : int
        {
          def text = parseResult.Text;
          mutable startLevel;
          parsedStates.Clear();
          $(if (Environment.IsIdeMode)
            <[ ignore(recoveryStack, startState, curTextPos, parseResult, text, startLevel); -1 ]>
          else
            CompileAst(SequenceCompileInfo))
        }
      ]>);
      //TODO: Move this methods outside TryParseMethodEmitter
      DefineStateMapMethod("IsStateCanParseEmptyString", _.CanParseEmptyString);
      DefineStateMapMethod("IsVoidState",                subrule => subrule.SrcRule.Type is RuleType.Void);
      DefineStateMapMethod("IsStartState",               _.IsStart);
      DefineStateMapMethod("IsLoopSeparatorStart",       _.IsLoopSeparatorStart);
      DefineStateMapMethod("IsLoopSeparator",            _.IsLoopSeparator);
      DefineStateMapMethod("IsLoopBody",                 _.IsLoopBody);
      DefineStateMapMethod("GetStartState",               subrule : SubruleCompileInfo => subrule.Owner.NonMarkerSubrules[0].State);
      DefineStateMapMethod("GetOffsetForState",           subrule : SubruleCompileInfo => subrule.Offset);
      
      TypeBuilder.DefineConditional(<[decl: public override GetLoopBodyForSeparator(separatorId : int, bodyId : out int, firstState : out int) : bool { $(Compile_GetLoopBodyForSeparator(SequenceCompileInfo)) } ]>);
      TypeBuilder.DefineConditional(<[decl: public override GetRuleParserForState(state : int) : Nitra.Internal.IRecoveryRuleParser * int { $(Compile_GetRuleParserForState(SequenceCompileInfo)) } ]>);
      TypeBuilder.DefineConditional(<[decl: public override CodeForState(state : int) : string { $(Compile_CodeForState(SequenceCompileInfo)) } ]>);
      TypeBuilder.DefineConditional(<[decl: public override CodeForStates(startState : int, endState : int, skipOptionalStates : bool) : array[string] { $(Compile_CodeForStates(SequenceCompileInfo)) } ]>);
      TypeBuilder.DefineConditional(<[decl: public override IsTokenRule    : bool   { get { $(SequenceCompileInfo.Symbol.Options.IsTokenRule) } } ]>);
      TypeBuilder.DefineConditional(<[decl: public override Code           : string { get { $(SequenceCompileInfo.SrcRule.ToString()) } } ]>);
      TypeBuilder.DefineConditional(<[decl: public override CodeWithStates : string { get { $(SequenceCompileInfo.ToString()) } } ]>);
      TypeBuilder.DefineConditional(<[decl: public override RuleName       : string { get { $(SequenceCompileInfo.Symbol.Name) } } ]>);

      TypeBuilder.DefineConditional(<[decl: private static EmptyArray : array[Nitra.Internal.RecoveryStackFrame] = array(0) ]>);
    }

    public CurTextPos : PExpr { get { <[ curTextPos ]> } }

    Compile_GetLoopBodyForSeparator(sequence : SequenceCompileInfo.Ast) : PExpr
    {
      def loops = SCG.List();

      foreach (subrule is SubruleCompileInfo.ListWithSeparator in sequence.AllNonMarkerSubrules)
      {
        loops.Add(<[  
          when (separatorId == $(subrule.Separator.AstId : global))
          {
            bodyId = $(subrule.Rule.AstId : global);
            firstState = $(subrule.Rule.NonMarkerSubrules[0].State : int);
            return true;
          } ]>);
      }

      <[ IgnoreParams(); ..$loops; false ]>
    }

    Compile_GetRuleParserForState(sequence : SequenceCompileInfo.Ast) : PExpr
    {
      def code = SCG.List();
      foreach (subrule in sequence.AllNonMarkerSubrules)
      {
        def value = match (subrule)
        {
          | Marker
          | Empty
          | TokenString
          | RegularCall                              => <[ (null, -1) ]>
          | SimpleCall    (ruleSymbol)               => <[ ($(Owner.MakeParserName(ruleSymbol) : global), $(Owner.MakeParserName(ruleSymbol) : global).RuleId) ]>
          | ExtensibleCall(ruleSymbol, bindingPower) => <[ ($(Owner.MakeParserName(ruleSymbol, bindingPower) : global), -1) ]>
          | Option                          as subrule => <[ (this, $(subrule.Rule.AstId : global)) ]>
          | List                            as subrule => <[ (this, $(subrule.Rule.AstId : global)) ]>
          | ListWithSeparator               as subrule => <[ (this, $(subrule.Rule.AstId : global)) ]>
        }
        code.Add(<[ case: | $(subrule.State) => $value ]>);
      }

      code.Add(<[ case: | _ => assert3(false) ]>);

      <[
        match (state)
        {
          ..$code
        }
      ]>
    }

    Compile_CodeForState(sequence : SequenceCompileInfo.Ast) : PExpr
    {
      def nextField(subrule : SubruleCompileInfo) : SubruleCompileInfo
      {
        if (subrule == null)
          null
        else if (subrule.NextSibling != null)
          subrule.NextSibling
        else
          nextField(subrule.Owner.Owner)
      }

      def code = SCG.List();

      for (mutable i = 0; i < sequence.StateCount; i++)
      {
        mutable str = sequence.CodeForState(i);
        def subrule = sequence.AllNonMarkerSubrules[i];
        when (subrule.IsLoopSeparatorStart)
        {
          def afterLoopField = nextField(subrule);

          when (afterLoopField != null)
            str += ", " + sequence.CodeForState(afterLoopField.State);
        }
        code.Add(<[ case: | $i => $str ]>);
      }

      code.Add(<[ case: | _ => assert3(false) ]>);

      <[
        match (state)
        {
          ..$code
        }
      ]>
    }

    Compile_CodeForStates(sequence : SequenceCompileInfo.Ast) : PExpr
    {
      def code = SCG.List();
      def fields = sequence.AllNonMarkerSubrules;

      for (mutable i = 0; i < sequence.StateCount; i++)
      {
        def subrule = fields[i];
        def str = subrule.SrcRule.ToString();
        if (subrule.CanParseEmptyString)
          code.Add(<[ case: | $i => unless (skipOptionalStates) result.Add($str) ]>);
        else
          code.Add(<[ case: | $i => result.Add($str) ]>);
      }

      code.Add(<[ case: | _ => assert3(false) ]>);

      <[
        assert3(endState >= -1);
        _ = skipOptionalStates;
        def result = SCG.List();
        
        for (mutable i = startState; i != endState; i = GetNextState(i))
        {
          match (i)
          {
            ..$code
          }
        }
        
        result.ToArray()
      ]>
    }

    EndOfFieldLevel(subrule : SubruleCompileInfo) : PExpr
    {
      <[ startLevel > $(subrule.Level) ]>
    }

    AddParsedStateInfo(subrule : SubruleCompileInfo, size : PExpr) : PExpr
    {
      <[
        when (startLevel == $(subrule.Level))
          parsedStates.Add(Nitra.Internal.ParsedStateInfo($(subrule.State), $size));
      ]>
    }

    public RecoveryCode() : PExpr
    {
      <[
        when (parseResult.MaxFailPos < curTextPos)
          parseResult.MaxFailPos = curTextPos;
      ]>
    }
  }
}
