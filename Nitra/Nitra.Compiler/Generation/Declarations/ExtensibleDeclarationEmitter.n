using Nitra.Model;
using Nitra.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;

namespace Nitra.Compiler
{
  internal sealed class ExtensibleDeclarationEmitter : DeclarationEmitter
  {
    private         _symbol            : ExtensibleDeclarationSymbol;
    private mutable _baseTb            : TypeBuilder;
    private mutable _ambiguousTb       : TypeBuilder;
    private         _collectionEmitter : CollectionEmitter;

    public this(environment : EmitterEnvironment, symbol : ExtensibleDeclarationSymbol)
    {
      base(environment, environment.NamespaceOfSymbol(symbol));
      this._symbol = symbol;
      this._collectionEmitter = CollectionEmitter(this, symbol);
    }

    public override DefineTypes() : void
    {
      Util.locate(_symbol.FirstLocation.ToNemerleLocation(),
      {
        assert(_baseTb == null);

        def parentType = Environment.TypeCompiler.CompileParentType(_symbol);
        def baseTypes = SCG.List();
        baseTypes.Add(<[ Nitra.Declarations.AstBase[$(parentType : typed)] ]>);
        foreach (baseDeclaration in _symbol.BaseDeclarations)
          baseTypes.Add(<[ $(Environment.GetDeclarationType(baseDeclaration) : typed) ]>);
        when (_symbol.NameProperty.HasValue)
          baseTypes.Add(<[ $(Environment.DeclarationTypeVar : typed) ]>);

        _baseTb = NemerleEnv.Define(<[ decl:
          public abstract partial class $(_symbol.Name : usesite) : ..$baseTypes
          {
            protected this() { }
          }
        ]>);

        assert(_ambiguousTb == null);
        _ambiguousTb = DefineAmbiguousDeclaration(_baseTb, parentType);

        _collectionEmitter.DefineTypes(_baseTb);
      })
    }

    public override DefineMembers() : void
    {
      assert(_baseTb != null);

      def implementSimpleProperty(propertySymbol : DeclarationPropertySymbol, propertyType : FixedType) : void
      {
        def backingFieldName = Util.tmpname(propertySymbol.Name);

        _baseTb.Define(<[ decl:
          private mutable $(backingFieldName : usesite) : $(propertyType : typed)
        ]>);
        _baseTb.Define(<[ decl:
          public virtual $(propertySymbol.Name : usesite) : $(propertyType : typed)
          {
            get { this.$(backingFieldName : usesite) }
            protected set { this.$(backingFieldName : usesite) = value }
          }
        ]>);

        _ambiguousTb.Define(<[ decl:
          public override $(propertySymbol.Name : usesite) : $(propertyType : typed)
          {
            get { this.Ambiguities[0].$(propertySymbol.Name : usesite) }
          }
        ]>);
      }

      when (_symbol.NameProperty is SymbolRef.Some(propertySymbol))
      {
        def propertyType = Environment.TypeCompiler.CompileNamePropertyType(propertySymbol);
        implementSimpleProperty(propertySymbol, propertyType);
        unless (propertySymbol.Name == "Name")
        {
          _baseTb.Define(<[ decl:
            private $(Util.tmpname("Name") : usesite) : $(propertyType : typed) implements $(Environment.DeclarationTypeVar : typed).Name
            {
              get { this.$(propertySymbol.Name : usesite) }
            }
          ]>);
        }
      }

      def dependentProps = SCG.Dictionary();

      def defineSimpleProperty(propertySymbol : SimpleDeclarationPropertySymbol) : void
      {
        def propertyType = Environment.TypeCompiler.CompileSimplePropertyType(propertySymbol);
        implementSimpleProperty(propertySymbol, propertyType)
      }

      def defineDependentProperty(propertySymbol : DependentDeclarationPropertySymbol) : void
      {
        def index = dependentProps.Count;
        def bit   = _baseTb.GetBitFieldManager().Allocate();
        assert(bit.Index == index);

        def propertyRef = DependentDeclarationPropertyRef.Compiling(propertySymbol, index, bit);
        dependentProps.Add(propertySymbol, propertyRef);

        DefineDependentProperty(_baseTb, propertyRef, NemerleModifiers.Public | NemerleModifiers.Virtual);
        DefineAmbiguousDependentProperty(_ambiguousTb, propertyRef, NemerleModifiers.Public | NemerleModifiers.Override);
      }

      def defineProjectedProperty(propertySymbol : ProjectedDeclarationPropertySymbol) : void
      {
        def propertyType = Environment.TypeCompiler.CompileProjectedPropertyType(propertySymbol);
        implementSimpleProperty(propertySymbol, propertyType)
      }

      foreach (propertySymbol in _symbol.AllProperties)
      {
        | SimpleDeclarationPropertySymbol    as s => defineSimpleProperty(s)
        | DependentDeclarationPropertySymbol as s => defineDependentProperty(s)
        | ProjectedDeclarationPropertySymbol as s => defineProjectedProperty(s)
        | _                                       => ()
      }

      DefineIsPropertyEvaluatedMethod(_baseTb, NemerleModifiers.Override);
      DefineDependentPropertiesEvaluation(_baseTb, _symbol);

      _collectionEmitter.DefineMembers(dependentProps);
    }

    public override DefineNestedType(td : ClassMember.TypeDeclaration) : TypeBuilder
    {
      assert(_baseTb != null);
      _baseTb.DefineNestedType(td);
    }

    public override Emit() : void
    {
      assert(_baseTb != null);
      _baseTb.Compile();

      assert(_ambiguousTb != null);
      _ambiguousTb.Compile();

      _collectionEmitter.Emit();
    }
  }
}
