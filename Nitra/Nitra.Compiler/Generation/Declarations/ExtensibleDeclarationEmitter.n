using Nitra.Model;
using Nitra.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;

namespace Nitra.Compiler
{
  internal sealed class ExtensibleDeclarationEmitter : DeclarationEmitter
  {
    private         _symbol      : ExtensibleDeclarationSymbol;
    private mutable _baseTb      : TypeBuilder;
    private mutable _ambiguousTb : TypeBuilder;

    public this(environment : EmitterEnvironment, symbol : ExtensibleDeclarationSymbol)
    {
      base(environment, environment.NamespaceOfSymbol(symbol));
      this._symbol = symbol;
    }

    public override DefineTypes() : void
    {
      assert(_baseTb == null);

      def baseTypes = <[ Nitra.Declarations.Declaration[$(CompileNType(_symbol.ParentType))] ]>
        :: _symbol.BaseDeclarations.Map(s => <[ $(Environment.GetDeclarationType(s) : typed) ]>);

      _baseTb = NemerleEnv.Define(<[ decl:
        public abstract partial class $(_symbol.Name : usesite) : ..$baseTypes
        {
          protected this() { }
        }
      ]>);

      assert(_ambiguousTb == null);
      _ambiguousTb = DefineAmbiguousDeclaration(_baseTb, _symbol.ParentType);
    }

    public override DefineMembers() : void
    {
      assert(_baseTb != null);

      def dependentProps = SCG.List();
      def declaredProps = SCG.List();

      def defineSimpleField(fieldSymbol : SimpleDeclarationFieldSymbol) : void
      {
        def backingFieldName = Util.tmpname(fieldSymbol.Name);

        _baseTb.Define(<[ decl:
          private mutable $(backingFieldName : usesite) : $(MakeFieldType(fieldSymbol))
        ]>);
        def prop = _baseTb.DefineAndReturn(<[ decl:
          public $(fieldSymbol.Name : usesite) : $(MakeFieldType(fieldSymbol))
          {
            get { this.$(backingFieldName : usesite) }
            protected set { this.$(backingFieldName : usesite) = value }
          }
        ]>) :> IProperty;
        declaredProps.Add(prop);

        _ambiguousTb.Define(<[ decl:
          public override $(fieldSymbol.Name : usesite) : $(MakeFieldType(fieldSymbol))
          {
            get { this.Ambiguities[0].$(fieldSymbol.Name : usesite) }
          }
        ]>);
      }

      def defineDependentField(fieldSymbol : DependentDeclarationFieldSymbol) : void
      {
        def index = dependentProps.Count;
        dependentProps.Add(fieldSymbol);

        def backingFieldName = Util.tmpname(fieldSymbol.Name);

        _baseTb.Define(<[ decl:
          private mutable $(backingFieldName : usesite) : $(MakeFieldType(fieldSymbol))
        ]>);
        _baseTb.Define(<[ decl:
          [Nitra.Internal.DependentPropertyAttribute($(fieldSymbol.FullName : string), $(index : int))]
          public $(fieldSymbol.Name : usesite) : $(MakeFieldType(fieldSymbol))
          {
            get { this.$(backingFieldName : usesite) }
            set { this.$(backingFieldName : usesite) = value }
          }
        ]>);

        _ambiguousTb.Define(<[ decl:
          [Nitra.Internal.DependentPropertyAttribute($(fieldSymbol.FullName : string), $(index : int))]
          public override $(fieldSymbol.Name : usesite) : $(MakeFieldType(fieldSymbol))
          {
            get { this.Ambiguities[0].$(fieldSymbol.Name : usesite) }
            set
            {
              foreach (ambiguity in this.Ambiguities)
                ambiguity.$(fieldSymbol.Name : usesite) = value
            }
          }
        ]>);
      }

      foreach (fieldSymbol in _symbol.AllFields)
      {
        | SimpleDeclarationFieldSymbol    as s => defineSimpleField(s)
        | DependentDeclarationFieldSymbol as s => defineDependentField(s)
        | _                                    => assert(false)
      }

      DefineHasValueMethod(_baseTb, 0, dependentProps.Count);
      DefineDependentFieldsEvaluation(_baseTb, dependentProps, declaredProps, <[ () ]>);
    }

    public override DefineNestedType(td : ClassMember.TypeDeclaration) : TypeBuilder
    {
      assert(_baseTb != null);
      _baseTb.DefineNestedType(td);
    }

    public override Emit() : void
    {
      assert(_baseTb != null);
      _baseTb.Compile();

      assert(_ambiguousTb != null);
      _ambiguousTb.Compile();
    }
  }
}
