using Nitra.Model;
using Nitra.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;

namespace Nitra.Compiler
{
  internal sealed class ExtensionDeclarationEmitter : DeclarationEmitter
  {
    private         _parent : DeclarationEmitter;
    private         _symbol : ExtensionDeclarationSymbol;
    private mutable _tb     : TypeBuilder;

    public this(parent : DeclarationEmitter, symbol : ExtensionDeclarationSymbol)
    {
      base(parent.Environment, parent.NemerleEnv);
      this._parent = parent;
      this._symbol = symbol;
    }

    public override DefineTypes() : void
    {
      assert(_tb == null);
      _tb = _parent.DefineNestedType(<[ decl:
        public sealed partial class $(_symbol.Name : usesite) : $(Environment.GetDeclarationType(_symbol.BaseDeclaration.Value) : typed)
        {
          public this(parent : $(CompileNType(_symbol.ParentType)), span : $(Environment.NSpanTypeVar : typed))
          {
            this.Parent = parent;
            this.Span = span;
          }

          public override Parent : $(CompileNType(_symbol.ParentType)) { get; }
          public override Span   : $(Environment.NSpanTypeVar : typed) { get; }
        }
      ]>);
    }

    public override DefineMembers() : void
    {
      assert(_tb != null);

      def initParameters = SCG.List();
      def initStatements = SCG.List();

      def dependentProps = SCG.List();
      def declaredProps  = SCG.List();

      def defineSimpleField(fieldSymbol : SimpleDeclarationFieldSymbol) : void
      {
        if (fieldSymbol.DeclaringDeclaration == _symbol)
        {
          def backingFieldName = Util.tmpname(fieldSymbol.Name);
          initParameters.Add(<[ parameter : $(fieldSymbol.Name : usesite) : $(MakeFieldType(fieldSymbol)) ]>);
          initStatements.Add(<[ this.$(backingFieldName : usesite) = $(fieldSymbol.Name : usesite) ]>);
          _tb.DefineConditional(<[ decl:
            private mutable $(backingFieldName : usesite) : $(MakeFieldType(fieldSymbol))
          ]>);
          def prop = _tb.DefineAndReturn(<[ decl:
            public $(fieldSymbol.Name : usesite) : $(MakeFieldType(fieldSymbol))
            {
              get { this.$(backingFieldName : usesite) }
              protected set { this.$(backingFieldName : usesite) = value }
            }
          ]>) :> IProperty;
          declaredProps.Add(prop);
        }
        else
        {
          initParameters.Add(<[ parameter : $(fieldSymbol.Name : usesite) : $(MakeFieldType(fieldSymbol)) ]>);
          initStatements.Add(<[ this.$(fieldSymbol.Name : usesite) = $(fieldSymbol.Name : usesite) ]>);
        }
      }

      mutable inheritedFieldsCount;

      def defineDependentField(fieldSymbol : DependentDeclarationFieldSymbol) : void
      {
        def index = dependentProps.Count;
        dependentProps.Add(fieldSymbol);

        if (fieldSymbol.DeclaringDeclaration == _symbol)
        {
          def bit = _tb.GetBitFieldManager().Allocate();
          DefineDependentProperty(_tb, fieldSymbol, index, bit, NemerleModifiers.Public);
        }
        else
        {
          inheritedFieldsCount += 1;
        }
      }

      // inherited properties comes first!
      foreach (fieldSymbol in _symbol.AllFields)
      {
        | SimpleDeclarationFieldSymbol    as s => defineSimpleField(s)
        | DependentDeclarationFieldSymbol as s => defineDependentField(s)
        | _                                    => assert(false)
      }

      DefineInitializeMethod(_tb, initParameters, initStatements);
      DefineHasValueMethod(_tb, inheritedFieldsCount);
      DefineDependentFieldsEvaluation(_tb, dependentProps, declaredProps, <[ () ]>);
    }

    public override Emit() : void
    {
      assert(_tb != null);
      _tb.Compile();
    }
  }
}
