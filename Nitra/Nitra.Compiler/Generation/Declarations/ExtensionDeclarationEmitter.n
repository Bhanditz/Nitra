using Nitra.Model;
using Nitra.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;

namespace Nitra.Compiler
{
  internal sealed class ExtensionDeclarationEmitter : DeclarationEmitter
  {
    private         _parent : DeclarationEmitter;
    private         _symbol : ExtensionDeclarationSymbol;
    private mutable _tb     : TypeBuilder;

    public this(parent : DeclarationEmitter, symbol : ExtensionDeclarationSymbol)
    {
      base(parent.Environment, parent.NemerleEnv);
      this._parent = parent;
      this._symbol = symbol;
    }

    public override DefineTypes() : void
    {
      assert(_tb == null);
      def parentType = Environment.TypeCompiler.CompileParentType(_symbol);
      _tb = _parent.DefineNestedType(<[ decl:
        public sealed partial class $(_symbol.Name : usesite) : $(Environment.GetDeclarationType(_symbol.BaseDeclaration.Value) : typed)
        {
          public this(parent : $(parentType : typed), span : $(Environment.NSpanTypeVar : typed))
          {
            this.Parent = parent;
            this.Span = span;
          }

          public override Parent : $(parentType : typed)                   { get; }
          public override Span   : $(Environment.NSpanTypeVar : typed)     { get; }
        }
      ]>);
    }

    public override DefineMembers() : void
    {
      assert(_tb != null);

      def initParameters = SCG.List();
      def initStatements = SCG.List();

      def dependentProps = SCG.List();

      def defineSimpleProperty(propertySymbol : SimpleDeclarationPropertySymbol) : void
      {
        def propertyType = Environment.TypeCompiler.CompileSimplePropertyType(propertySymbol);
        if (propertySymbol.DeclaringDeclaration == _symbol)
        {
          def backingFieldName = Util.tmpname(propertySymbol.Name);
          initParameters.Add(<[ parameter : $(propertySymbol.Name : usesite) : $(propertyType : typed) ]>);
          initStatements.Add(<[ this.$(backingFieldName : usesite) = $(propertySymbol.Name : usesite) ]>);
          _tb.Define(<[ decl:
            private mutable $(backingFieldName : usesite) : $(propertyType : typed)
          ]>);
          _tb.Define(<[ decl:
            public $(propertySymbol.Name : usesite) : $(propertyType : typed)
            {
              get { this.$(backingFieldName : usesite) }
              private set { this.$(backingFieldName : usesite) = value }
            }
          ]>);
        }
        else
        {
          initParameters.Add(<[ parameter : $(propertySymbol.Name : usesite) : $(propertyType : typed) ]>);
          initStatements.Add(<[ this.$(propertySymbol.Name : usesite) = $(propertySymbol.Name : usesite) ]>);
        }
      }

      mutable inheritedFieldsCount;

      def defineDependentProperty(propertySymbol : DependentDeclarationPropertySymbol) : void
      {
        def index = dependentProps.Count;
        dependentProps.Add(propertySymbol);

        if (propertySymbol.DeclaringDeclaration == _symbol)
        {
          def bit         = _tb.GetBitFieldManager().Allocate();
          def propertyRef = DependentDeclarationPropertyRef.Compiling(propertySymbol, index, bit);
          DefineDependentProperty(_tb, propertyRef, NemerleModifiers.Public);
        }
        else
        {
          inheritedFieldsCount += 1;
        }
      }

      // inherited properties comes first!
      foreach (propertySymbol in _symbol.AllProperties)
      {
        | SimpleDeclarationPropertySymbol    as s => defineSimpleProperty(s)
        | DependentDeclarationPropertySymbol as s => defineDependentProperty(s)
        | _                                       => ()
      }

      DefineInitializeMethod(_tb, initParameters, initStatements);
      DefineIsPropertyEvaluatedMethod(_tb, inheritedFieldsCount);
      DefineDependentPropertiesEvaluation(_tb, _symbol);
    }

    public override Emit() : void
    {
      assert(_tb != null);
      _tb.Compile();
    }
  }
}
