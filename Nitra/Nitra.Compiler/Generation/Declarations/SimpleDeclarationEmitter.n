using Nitra.Model;
using Nitra.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;

namespace Nitra.Compiler
{
  internal sealed class SimpleDeclarationEmitter : DeclarationEmitter
  {
    private _environment       : EmitterEnvironment;
    private _declarationSymbol : SimpleDeclarationSymbol;
    private _env               : GlobalEnv;
    private mutable _tb        : TypeBuilder;

    public this(environment : EmitterEnvironment, declarationSymbol : SimpleDeclarationSymbol)
    {
      this._environment       = environment;
      this._declarationSymbol = declarationSymbol;
      this._env               = environment.NamespaceOfSymbol(declarationSymbol);
    }

    public override DefineTypes() : void
    {
      assert(_tb == null);
      _tb = _env.Define(<[ decl:
        public sealed class $(_declarationSymbol.Name : usesite) : $(_environment.DeclarationTypeVar : typed)
        {
          public override Parent : $(_environment.DeclarationTypeVar : typed) { get; }
          public override Span   : $(_environment.NSpanTypeVar : typed)       { get; }
        }
      ]>);

      def ctorParameters = SCG.List();
      ctorParameters.Add(<[ parameter: parent : $(_environment.DeclarationTypeVar : typed) ]>);
      ctorParameters.Add(<[ parameter: span   : $(_environment.NSpanTypeVar : typed)       ]>);

      def ctorStatements = SCG.List();
      ctorStatements.Add(<[ this.Parent = parent ]>);
      ctorStatements.Add(<[ this.Span   = span ]>);

      def makeType(fieldSymbol) { _environment.MakeNemerleType(this._env, fieldSymbol.TypeLocation, fieldSymbol.Type) }

      foreach (fieldSymbol in this._declarationSymbol.Fields)
      {
        if (fieldSymbol.HasBody)
        {
          this._tb.DefineConditional(<[ decl:
            public $(fieldSymbol.Name : usesite) : $(makeType(fieldSymbol))
            {
              get { $(fieldSymbol.Body.Expr) }
            }
          ]>);
        }
        else
        {
          this._tb.DefineConditional(<[ decl:
            public $(fieldSymbol.Name : usesite) : $(makeType(fieldSymbol));
          ]>);
          ctorParameters.Add(<[ parameter : $(fieldSymbol.Name : usesite) : $(makeType(fieldSymbol)) ]>);
          ctorStatements.Add(<[ this.$(fieldSymbol.Name : usesite) = $(fieldSymbol.Name : usesite) ]>);
        }
      }

      _tb.DefineConditional(<[ decl:
        public this(..$ctorParameters) { ..$ctorStatements }
      ]>);
    }

    public override Emit() : void
    {
      assert(_tb != null);
      _tb.Compile();
    }
  }
}
