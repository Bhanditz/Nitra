using Nitra.Model;
using Nitra.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Linq;
using SCG = System.Collections.Generic;

namespace Nitra.Compiler
{
  internal sealed class AbstractDeclarationEmitter : DeclarationEmitter
  {
    private         _symbol            : AbstractDeclarationSymbol;
    private mutable _tb                : TypeBuilder;
    private         _collectionEmitter : CollectionEmitter;

    public this(environment : EmitterEnvironment, symbol : AbstractDeclarationSymbol)
    {
      base(environment, environment.NamespaceOfSymbol(symbol));
      this._symbol = symbol;
      this._collectionEmitter = CollectionEmitter(this, symbol);
    }

    public override DefineTypes() : void
    {
      Util.locate(_symbol.FirstLocation.ToNemerleLocation(),
      {
        assert(_tb == null);

        def baseTypes = match (_symbol.BaseDeclarations)
        {
          | []   => [<[ $(Environment.AstTypeVar : typed) ]>]
          | many => many.Map(s => <[ $(Environment.GetDeclarationType(s) : typed) ]>)
        };

        _tb = NemerleEnv.Define(<[ decl:
          public partial interface $(_symbol.Name : usesite) : ..$baseTypes
          {
          }
        ]>);

        _collectionEmitter.DefineTypes(_tb);
      })
    }

    public override DefineMembers() : void
    {
      assert(_tb != null);
      foreach (s in _symbol.Properties)
      {
        | SimpleDeclarationPropertySymbol as propertySymbol =>
          def propertyType = Environment.TypeCompiler.CompileSimplePropertyType(propertySymbol);
          _tb.Define(<[ decl: $(propertySymbol.Name : usesite) : $(propertyType : typed) { get; } ]>)

        | ProjectedDeclarationPropertySymbol as propertySymbol =>
          def propertyType = Environment.TypeCompiler.CompileProjectedPropertyType(propertySymbol);
          _tb.Define(<[ decl: $(propertySymbol.Name : usesite) : $(propertyType : typed) { get; } ]>)

        | DependentDeclarationPropertySymbol as propertySymbol => DefineInterfaceDependentProperty(propertySymbol, _tb)
        | _ => assert(false)
      }

      def dependentProps = _symbol.AllProperties
        .OfType.[DependentDeclarationPropertySymbol]()
        .ToDictionary(p => p, DependentDeclarationPropertyRef.Reflected(_, -1));
      _collectionEmitter.DefineMembers(dependentProps);
    }

    public override Emit() : void
    {
      assert(_tb != null);
      _tb.Compile();

      _collectionEmitter.Emit();
    }
  }
}
