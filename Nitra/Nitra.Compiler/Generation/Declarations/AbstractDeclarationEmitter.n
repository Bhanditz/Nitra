using Nitra.Model;
using Nitra.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;

namespace Nitra.Compiler
{
  internal sealed class AbstractDeclarationEmitter : DeclarationEmitter
  {
    private         _symbol : AbstractDeclarationSymbol;
    private mutable _tb     : TypeBuilder;
    private mutable _listTb : TypeBuilder;

    public this(environment : EmitterEnvironment, symbol : AbstractDeclarationSymbol)
    {
      base(environment, environment.NamespaceOfSymbol(symbol));
      this._symbol = symbol;
    }

    public override DefineTypes() : void
    {
      assert(_tb == null);

      def baseTypes = match (_symbol.BaseDeclarations)
      {
        | []   => [<[ $(Environment.DeclarationTypeVar : typed) ]>]
        | many => many.Map(s => <[ $(Environment.GetDeclarationType(s) : typed) ]>)
      };

      _tb = NemerleEnv.Define(<[ decl:
        public partial interface $(_symbol.Name : usesite) : ..$baseTypes
        {
        }
      ]>);

      assert(_listTb == null);
      _listTb = DefineListProxy(_tb);
    }

    public override DefineMembers() : void
    {
      assert(_listTb != null);
      DefineListProxyMembers(_symbol, _listTb);

      assert(_tb != null);
      foreach (s in _symbol.Properties)
      {
        | SimpleDeclarationPropertySymbol as propertySymbol =>
          def propertyType = Environment.TypeCompiler.CompileSimplePropertyType(propertySymbol);
          _tb.Define(<[ decl:
            $(propertySymbol.Name : usesite) : $(propertyType : typed) { get; }
          ]>)

        | DependentDeclarationPropertySymbol as propertySymbol => DefineInterfaceDependentProperty(propertySymbol, _tb)
        | _ => assert(false)
      }
    }

    public override Emit() : void
    {
      assert(_tb != null);
      _tb.Compile();

      assert(_listTb != null);
      _listTb.Compile();
    }
  }
}
