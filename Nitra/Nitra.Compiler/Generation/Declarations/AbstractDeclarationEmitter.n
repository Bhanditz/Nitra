using Nitra.Model;
using Nitra.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;

namespace Nitra.Compiler
{
  internal sealed class AbstractDeclarationEmitter : DeclarationEmitter
  {
    private         _symbol : AbstractDeclarationSymbol;
    private mutable _tb     : TypeBuilder;

    public this(environment : EmitterEnvironment, symbol : AbstractDeclarationSymbol)
    {
      base(environment, environment.NamespaceOfSymbol(symbol));
      this._symbol = symbol;
    }

    public override DefineTypes() : void
    {
      assert(_tb == null);

      _tb = NemerleEnv.Define(<[ decl:
        public partial interface $(_symbol.Name : usesite)
        {
        }
      ]>);
    }

    public override DefineHierarchy() : void
    {
      foreach (s in _symbol.BaseDeclarations)
        _tb.AddImplementedInterface(<[ $(Environment.GetDeclarationType(s) : typed) ]>)
      otherwise
        _tb.AddImplementedInterface(<[ $(Environment.DeclarationTypeVar : typed) ]>)
    }

    public override Emit() : void
    {
      assert(_tb != null);
      _tb.Compile();
    }
  }
}
