using Nitra.Model;
using Nitra.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Linq;
using SCG = System.Collections.Generic;

namespace Nitra.Compiler
{
  internal sealed class AbstractAstEmitter : AstEmitter
  {
    private         _symbol            : AbstractAstSymbol;
    private mutable _tb                : TypeBuilder;
    private mutable _ambiguousTb       : TypeBuilder;
    private         _listEmitter       : ListAstEmitter;
    private         _optionEmitter     : OptionAstEmitter;
    private mutable _symbolTb          : TypeBuilder;

    public this(environment : EmitterEnvironment, symbol : AbstractAstSymbol)
    {
      base(environment, environment.NamespaceOfSymbol(symbol));
      this._symbol        = symbol;
      this._listEmitter   = ListAstEmitter(environment, symbol);
      this._optionEmitter = OptionAstEmitter(environment, symbol);
    }

    public override DefineTypes() : void
    {
      Util.locate(_symbol.FirstLocation.NLocation(),
      {
        assert(_tb == null);

        def baseTypes = SCG.List();
        if (_symbol.IsDeclaration)
          baseTypes.Add(<[ $(Environment.DeclarationTypeVar : typed) ]>)
        else
          baseTypes.Add(<[ $(Environment.AstTypeVar : typed) ]>);
        foreach (baseAst in _symbol.BaseAsts)
          baseTypes.Add(<[ $(Environment.GetAstType(baseAst) : typed) ]>);

        _tb = NemerleEnv.Define(<[ decl:
          public partial interface $(_symbol.Name : usesite) : ..$baseTypes
          {
          }
        ]>);

        assert(_ambiguousTb == null);
        _ambiguousTb = DefineAmbiguousAst(_tb, <[ Nitra.Declarations.AstBase ]>);

        _listEmitter.DefineTypes(_tb);
        _optionEmitter.DefineTypes(_tb);

        when (_symbol.IsDeclaration)
        {
          def baseSymbolTypes = SCG.List();
          baseSymbolTypes.Add(<[ Nitra.Runtime.Binding.ISymbol2 ]>);
          foreach (baseAst in _symbol.BaseAsts)
            baseSymbolTypes.Add(GetSymbolClassRef(baseAst));

          _symbolTb = NemerleEnv.Define(<[ decl:
            public partial interface $(Environment.GetSymbolClassName(_symbol) : usesite) : ..$baseSymbolTypes
            {
            }
          ]>);
        }
      })
    }

    public override DefineMembers() : void
    {
      assert(_tb != null);

      def missingBody = SCG.List();
      missingBody.Add(<[ this.File = file ]>);
      missingBody.Add(<[ this.Span = span ]>);

      def dependentProps = SCG.Dictionary();

      def implementSimpleProperty(propertySymbol : AstPropertySymbol, propertyType : FixedType) : string
      {
        def name = propertySymbol.Name;
        when (propertySymbol.DeclaringAst == _symbol)
          _tb.Define(<[ decl: $(name : usesite) : $(propertyType : typed) { get; } ]>);

        def backingFieldName = Util.tmpname(name);
        _ambiguousTb.Define(<[ decl: private $(backingFieldName : usesite) : $(propertyType : typed) ]>);
        _ambiguousTb.Define(<[ decl:
          public $(name : usesite) : $(propertyType : typed)
          {
            get
            {
              if (this.IsAmbiguous)
                this.Ambiguities[0].$(name : usesite)
              else
                this.$(backingFieldName : usesite)
            }
          }
        ]>);
        backingFieldName
      }

      when (_symbol.IsDeclaration)
      {
        def name             = "Name";
        def propertyType     = Environment.NameTypeVar;
        def backingFieldName = Util.tmpname(name);
        _ambiguousTb.Define(<[ decl: private $(backingFieldName : usesite) : $(propertyType : typed) ]>);
        _ambiguousTb.Define(<[ decl:
          public $(name : usesite) : $(propertyType : typed)
          {
            get
            {
              if (this.IsAmbiguous)
                this.Ambiguities[0].$(name : usesite)
              else
                this.$(backingFieldName : usesite)
            }
          }
        ]>);

        missingBody.Add(<[ this.$(backingFieldName : usesite) = Nitra.Runtime.Binding.Name.$(AmbiguousOrMissingAstName : usesite)(file, span) ]>);
      }

      def thisType = _tb.GetMemType();

      foreach (s in _symbol.AllProperties)
      {
        | SimpleAstPropertySymbol as propertySymbol =>
          def propertyType     = TypeCompiler.CompileSimplePropertyType(propertySymbol);
          def backingFieldName = implementSimpleProperty(propertySymbol, propertyType.Type);
          match (propertyType)
          {
            | Ast         =>
              if (propertyType.Type.Equals(thisType))
                missingBody.Add(<[ this.$(backingFieldName : usesite) = this ]>);
              else unless (propertySymbol.AstType.IsVoid)
                missingBody.Add(<[ this.$(backingFieldName : usesite) = $(GetMissingAstCtor(Environment, propertySymbol.AstType))(file, span) ]>);

            | ParsedValue => missingBody.Add(<[ this.$(backingFieldName : usesite) = Nitra.ParsedValue(span.StartPos) ]>);
            | Span        => missingBody.Add(<[ this.$(backingFieldName : usesite) = span ]>);
          }

        | ProjectedAstPropertySymbol as propertySymbol =>
          def propertyType     = TypeCompiler.CompileProjectedPropertyType(propertySymbol);
          def backingFieldName = implementSimpleProperty(propertySymbol, propertyType);
          if (propertyType.Equals(thisType))
            missingBody.Add(<[ this.$(backingFieldName : usesite) = this ]>);
          else when (propertySymbol.ProjectionType is Some(projectionType))
            missingBody.Add(<[ this.$(backingFieldName : usesite) = $(GetMissingAstCtor(Environment, projectionType))(file, span) ]>);

        | DependentAstPropertySymbol as propertySymbol =>
          def propertyRef = DependentAstPropertyRef.Reflected(propertySymbol, -1);
          dependentProps.Add(propertySymbol, propertyRef);

          when (propertySymbol.DeclaringAst == _symbol)
            DefineInterfaceDependentProperty(propertySymbol, _tb);

          DefineAmbiguousDependentProperty(propertyRef);

        | _ => assert(false)
      }

      _listEmitter.DefineMembers(dependentProps);
      _optionEmitter.DefineMembers(dependentProps);

      _ambiguousTb.Define(<[ decl:
        public this(file : $(Environment.FileTypeVar : typed), span : $(Environment.NSpanTypeVar : typed)) { ..$missingBody }
      ]>);

      _ambiguousTb.Define(<[ decl: 
        public override Accept(visitor : Nitra.Declarations.IAstVisitor) : void
        {
          Nitra.Internal.AstUtils.AcceptToAll(visitor, this.Ambiguities);
        }
       ]>);

       DefineResetPropertiesMethod(null, _ambiguousTb);
       DefineIsAllPropertiesEvaluatedProperty(null, _ambiguousTb);
    }

    private DefineAmbiguousDependentProperty(propertyRef : DependentAstPropertyRef) : void
    {
      def propertyType    = TypeCompiler.CompileDependentPropertyType(propertyRef.Symbol);
      def name            = propertyRef.Symbol.Name;
      def isEvaluatedName = GetIsEvaluatedPropertyName(propertyRef.Symbol);
      def mods            = AttributesAndModifiers(NemerleAttributes.Public, [MakeDependentPropertyAttribute(propertyRef.Symbol, propertyRef.Index)]);
      match (propertyRef.Symbol.Direction)
      {
        | In  =>
          def backingFieldName = Util.tmpname(propertyRef.Symbol.Name);
          def hasValueBit = _ambiguousTb.GetBitFieldManager().Allocate();
          _ambiguousTb.Define(<[ decl:  private mutable $(backingFieldName : usesite) : $(propertyType : typed) ]>);
          _ambiguousTb.Define(<[ decl:
            ..$mods $(name : usesite) : $(propertyType : typed)
            {
              get
              {
                if (this.IsAmbiguous)
                  this.Ambiguities[0].$(name : usesite)
                else
                {
                  unless ($(hasValueBit.Read()))
                    $(MakeAccessAssertion(propertyRef));
                  this.$(backingFieldName : usesite)
                }
              }
              set
              {
                if (this.IsAmbiguous)
                  foreach (ambiguity in this.Ambiguities)
                    ambiguity.$(name : usesite) = value;
                else unless ($(hasValueBit.Read()))
                {
                  this.$(backingFieldName : usesite) = value;
                  $(hasValueBit.Set());
                }
              }
            }
          ]>);

          _ambiguousTb.Define(<[ decl:
            public $(isEvaluatedName : usesite) : bool
            {
              get
              {
                if (this.IsAmbiguous)
                  this.Ambiguities[0].$(isEvaluatedName : usesite)
                else
                  $(hasValueBit.Read())
              }
            }
          ]>);

        | Out =>
          _ambiguousTb.Define(<[ decl:
            ..$mods $(name : usesite) : $(propertyType : typed)
            {
              get
              {
                if (this.IsAmbiguous)
                  this.Ambiguities[0].$(name : usesite)
                else
                  $(MakeAccessAssertion(propertyRef));
              }
            }
          ]>);

          _ambiguousTb.Define(<[ decl:
            public $(isEvaluatedName : usesite) : bool
            {
              get
              {
                if (this.IsAmbiguous)
                  this.Ambiguities[0].$(isEvaluatedName : usesite)
                else
                  false
              }
            }
          ]>);
      }
    }

    public override Emit() : void
    {
      assert(_tb != null);
      _tb.Compile();

      assert(_ambiguousTb != null);
      _ambiguousTb.Compile();

      _listEmitter.Emit();
      _optionEmitter.Emit();

      when (_symbolTb != null)
        _symbolTb.Compile();
    }
  }
}
