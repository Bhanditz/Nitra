using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra.ProjectSystem;
using Nitra.Typing;

using System;
using System.Collections.Generic;
using System.Linq;
using System.IO;

namespace Nitra.Compiler
{
  /// Node of a property dependency graph.
  internal variant PropertyDependencyNode
  {
    public DeclaredIn    : PropertyContainerSymbol;
    public Path          : DependencyPath;
    public Location      : Nemerle.Compiler.Location;
    public NitraLocation : Location { get { (DeclaredIn.Node.Project :> NitraProject).MakeNitraLocation(Location) } }

    public this(declaredIn : PropertyContainerSymbol, path : DependencyPath, location : Nemerle.Compiler.Location)
      requires !(path is DependencyPath.This)
    {
      DeclaredIn = declaredIn;
      Path       = path;
      Location   = location;
    }

    | Assign { Expr : PExpr; }
    | Use

    public IsDependent : bool                        { get { Path is DeclaredProperty(_, DependentPropertySymbol, _) } }
    public State       : PropertyDependencyNodeState { get { _state } }
    public Order       : int                         { get { _order } }

    [RecordIgnore] mutable _state : PropertyDependencyNodeState;
    [RecordIgnore] mutable _order : int = -1;
    [RecordIgnore] public DependendOn : HashSet[PropertyDependencyNode] = HashSet();

    public static Save(graphNodes : array[PropertyDependencyNode], propertyContainerSymbol : PropertyContainerSymbol) : void
    {
      assert(propertyContainerSymbol.IsAbstract);

      def result = List();
      when (graphNodes.Length > 0)
      {
        def map = Dictionary();
        foreach (node in graphNodes with i)
          map.Add(node, i);

        foreach (node in graphNodes)
        {
          mutable dependenedOn = [];
          foreach (dependency in node.DependendOn)
            dependenedOn ::= map[dependency];

          def dataNode =
            match (node)
            {
              | Assign => PropertyDependencyNodeData.Assign(node.NitraLocation, node.DeclaredIn, node.Path, dependenedOn)
              | Use    => PropertyDependencyNodeData.Use   (node.NitraLocation, node.DeclaredIn, node.Path, dependenedOn)
            };

          result.Add(dataNode);
        }
      }
      propertyContainerSymbol.SetPropertyDependency(result.ToNList())
    }

    public static Load(propertyContainerSymbol : PropertyContainerSymbol) : array[PropertyDependencyNode]
    {
      assert(propertyContainerSymbol.IsAbstract);

      def dataNodes = propertyContainerSymbol.PropertyDependency.ToArray();
      def nodes = array(dataNodes.Length);
      def project = propertyContainerSymbol.Node.Project :> NitraProject;
      foreach (n in dataNodes with i)
      {
        | Use    => nodes[i] = PropertyDependencyNode.Use(n.DeclaredIn, n.Path, n.NLocation());
        | Assign =>
          def type       = project.GetNemerleType(propertyContainerSymbol);
          def methodName = BaseAstEmitter.GetEvalMethodName(n.Path);
          def loc        = n.NLocation();
          def expr       = Util.locate(loc, <[ $(BaseAstEmitter.CompilePath(n.Path)) = $(type : typed).$(methodName : usesite)(this, context) ]>);
          nodes[i] = PropertyDependencyNode.Assign(n.DeclaredIn, n.Path, loc, expr);
      }

      foreach (n in nodes with i)
        n.DependendOn.UnionWith(dataNodes[i].DependendOn.Select(to => nodes[to]));

      nodes
    }

    public static Compare(l : PropertyDependencyNode, r : PropertyDependencyNode) : int
    {
      l.Order - r.Order
    }

#pragma warning disable 10003

    public static Visualize(symbol : PropertyContainerSymbol, nodes : Seq[PropertyDependencyNode], typeName : string) : void
    {
      _ = Sort(nodes);

      def nodeId = Dictionary();
      foreach (node in nodes with index)
        nodeId[node] = index;

      def name(node : PropertyDependencyNode) { nodeId[node]; }
      def label(node : PropertyDependencyNode) { X.DotEscape(node.ToString()) }

      def edges = List();

      foreach (node in nodes.OrderBy(n => n.Order))
        foreach (to in node.DependendOn.OrderBy(n => n.Order))
          edges.Add($<#$(name(node)) -> $(name(to))#>);

      def color(node : PropertyDependencyNode)
      {
        | Assign   => if (symbol : object == node.DeclaredIn) "blue" else "darkgreen"
        | Use as u => if (u.IsDependent) "black" else "red"
      }
      def content = $<#
        digraph $(typeName)_
        {
          rankdir=LR;
          compound=true;
          label="The properties dependency graph for $typeName";

          ..$(nodes; "\r\n            "; n => $<#$(name(n)) [label="$(label(n))" color=$(color(n)) shape=box]#>)

          ..$(edges; "\r\n            ")
        }
      #>;

      X.ExecuteDot(content);
    }

#pragma warning restore 10003

    public static Sort(graphNodes : Seq[PropertyDependencyNode]) : bool
    {
      foreach (node in graphNodes)
        node._state = PropertyDependencyNodeState.None;
      otherwise
        return false; // graph is empty

      mutable index = 0;
      mutable cycling = false;

      foreach (node in GetRoots(graphNodes))
        cycling |= node.Sort(ref index);
      otherwise
      {
        foreach (node in graphNodes)
          node._state = PropertyDependencyNodeState.Cycling;
        return true;
      }

      cycling
    }

    /// Return: true - cycling; false - OK
    public Sort(index : ref int) : bool
    {
      mutable cycling = false;
      this._state = PropertyDependencyNodeState.Cycling;

      foreach (to in DependendOn)
      {
        if (to._state == PropertyDependencyNodeState.None)
          when (to.Sort(ref index))
            cycling |= true;
        else when (to._state == PropertyDependencyNodeState.Cycling)
          cycling |= true;
      }

      if (cycling)
      {
        this._order = -1;
        true
      }
      else
      {
        this._state = PropertyDependencyNodeState.Visited;
        this._order = index;
        index++;
        false
      }
    }

    public static GetRoots(graphNodes : Seq[PropertyDependencyNode]) : Seq[PropertyDependencyNode]
    {
      def hasEdgeTo = HashSet();

      foreach (node in graphNodes)
        hasEdgeTo.UnionWith(node.DependendOn);

      foreach (node in graphNodes)
        unless (hasEdgeTo.Contains(node))
          yield node;
    }

    public static FindCycling(graphNodes : Seq[PropertyDependencyNode]) : Seq[PropertyDependencyNode]
    {
      foreach (node in graphNodes)
        when (node._state == PropertyDependencyNodeState.Cycling)
          yield node;
    }

    public override ToString() : string
    {
      _order + ": " +
      match (this)
      {
        | Assign    as x =>
          def loc = NitraLocation;
          if (loc.IsEmpty)
            x.Expr.ToString()
          else
            loc.GetText()

        | Use       as x => x.Path.ToString()
      }
    }

    private static _trimChars : array[char] = array['\n', '\r', ' ', '\t'];
  }
}
