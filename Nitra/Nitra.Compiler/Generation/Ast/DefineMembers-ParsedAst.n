using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;

using Nitra.Internal;
using Nitra.Model;
using Nitra.Typing;

using System;
using System.Diagnostics;
using System.Linq;
using SCG = System.Collections.Generic;

namespace Nitra.Compiler
{
  internal partial class RuleAstEmitter
  {
    private DefineParsedAstCtorAndFields(fields : SCG.List[FieldInfo]) : void
    {
      _parsedAstTb.DefineConditional(<[ decl: private parseResult : Nitra.ParseResult; ]>);
      _parsedAstTb.DefineConditional(<[ decl: private startPos    : int; ]>);
      _parsedAstTb.DefineConditional(<[ decl: private endPos      : int; ]>);
      _parsedAstTb.DefineConditional(<[ decl: private astPtr      : int; ]>);

      _parsedAstTb.DefineConditional(<[ decl:
        public override Location : Nitra.Location
        {
          get { Nitra.Location(this.parseResult.OriginalSource, this.startPos, this.endPos) }
        }
      ]>);

      _parsedAstTb.DefineConditional(<[ decl:
        protected override PrettyPrintSource : Nitra.IPrettyPrintSource
        {
          get { this.parseResult }
        }
      ]>);

      def astPtr = AstPointer.Existing(this.RuleSymbol.Sequence, this, "astPtr");

      def endPosExpr = this.RuleSymbol.Sequence.NonMarkerSubrules.Fold(<[ pos ]>, (subrule, acc) => <[ $acc + $(astPtr.GetSize(subrule)) ]>);

      match (this.RuleSymbol)
      {
        | SimpleRuleSymbol
        | ExtensionRuleSymbol(ExtensionType = Prefix) =>
          _parsedAstTb.DefineConditional(<[decl:
            [Nemerle.Diagnostics.CompileStatistic("Ast")]
            public this(pos : ref int, astPtr : int, parseResult : Nitra.ParseResult)
            {
              unchecked
              {
                this.parseResult = parseResult;
                this.startPos    = pos;
                this.endPos      = $endPosExpr;
                this.astPtr      = astPtr;
                pos              = this.endPos;
              }
            }
          ]>);

          _parsedAstTb.DefineConditional(<[decl:
            [Nemerle.Diagnostics.CompileStatistic("Ast")]
            public this(startPos : int, endPos : int, recoveredSubrulesIndex : int, parseResult : Nitra.ParseResult)
            {
              unchecked
              {
                this.parseResult = parseResult;
                this.startPos    = startPos;
                this.endPos      = endPos;
                this.astPtr      = -recoveredSubrulesIndex;
              }
            }
          ]>);

        | ExtensionRuleSymbol(ExtensionType = Postfix) as symbol =>
          _parsedAstTb.DefineConditional(<[ decl: private astStartPos : int; ]>);
          _parsedAstTb.DefineConditional(<[ decl: private prefix      : $(Environment.GetAstType(symbol.BaseRule.Value) : typed); ]>);
          _parsedAstTb.DefineConditional(<[ decl:
            [Nemerle.Diagnostics.CompileStatistic("Ast")]
            public this(prefix : $(Environment.GetAstType(symbol.BaseRule.Value) : typed), prefixStartPos : int, pos : ref int, astPtr : int, parseResult : Nitra.ParseResult)
            {
              unchecked
              {
                this.parseResult = parseResult;
                this.startPos    = prefixStartPos;
                this.endPos      = $endPosExpr;
                this.astPtr      = astPtr;
                this.astStartPos = pos;
                this.prefix      = prefix;
                pos              = this.endPos;
              }
            }
          ]>);

        | _ => assert(false)
      }

      def getStartPos() : PExpr
      {
        match (this.RuleSymbol)
        {
          | SimpleRuleSymbol
          | ExtensionRuleSymbol(ExtensionType = Prefix)  => <[ this.startPos ]>
          | ExtensionRuleSymbol(ExtensionType = Postfix) => <[ this.astStartPos ]>
          | _ => assert(false)
        }
      }

      foreach (field in fields)
      {
        def statements = SCG.List();

        mutable needRuleParser  : bool;
        mutable needCaching     : bool;
        mutable needPos         : bool;

        def analyze(_ : FieldExpr) : void
        {
          | Prefix         => ()
          | Tuple(items)   => needPos = true; foreach (expr in items) analyze(expr);
          | Chars(_, true) => needPos = true; needRuleParser = true;
          | Chars          => needPos = true;
          | Other(_, true) => needPos = true; needRuleParser = true; needCaching = true;
          | Other          => needPos = true;                        needCaching = true;
        }

        analyze(field.Expr);

        when (needPos)
          statements.Add(<[ mutable pos = $(getStartPos()); ]>);
        when (needRuleParser)
          statements.Add(<[ def ruleParser = parseResult.GetRuleParser(astPtr) :> $(this.RuleSymbol.GetParserClassRef()) ]>);

        def compile(_ : FieldExpr) : PExpr
        {
          | Prefix(expr)
          | Chars(expr, _)
          | Other(expr, _) => expr
          | Tuple(items)   =>
            def tupleCode = SCG.List();
            def tupleArgs = SCG.List();
            foreach (item in items)
            {
              def name = Util.tmpname("ast");
              tupleCode.Add(<[ def $(name : usesite) = $(compile(item)) ]>);
              tupleArgs.Add(<[ $(name : usesite) ]>);
            }
            <[
              def sequenceStartPos = pos;
              ..$tupleCode;
              Nitra.SequenceAst(Nitra.Location(parseResult.OriginalSource, sequenceStartPos, pos), ..$tupleArgs)
            ]>
        }

        statements.Add(compile(field.Expr));

        def code = <[
          if (this.astPtr > 0)
          {
            ..$statements
          }
          else
          {
            def record = this.parseResult.RecoveredSubrules[-this.astPtr];
            _ = record;
            System.Diagnostics.Debugger.Break();
            assert(false);
          };
        ]>;

        def codeWithCaching =
          if (needCaching)
          {
            def backingFieldName = Util.tmpname(field.Name);
            _parsedAstTb.DefineConditional(<[ decl: private mutable $(backingFieldName : usesite) : $(field.Type : typed); ]>);
            assert(!field.Type.IsValueType);
            <[
              when ($(backingFieldName : usesite) : object == null)
              {
                $(backingFieldName : usesite) = $code;
              }
              $(backingFieldName : usesite)
            ]>
          }
          else code;

        _parsedAstTb.DefineConditional(<[ decl:
          public override $(field.Name : usesite) : $(field.Type : typed)
          {
            get { unchecked ($codeWithCaching) }
          }
        ]>);
      }
    }
  }
}
