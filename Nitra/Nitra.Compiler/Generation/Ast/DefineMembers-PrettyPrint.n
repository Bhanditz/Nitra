using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;

using Nitra.Internal;
using Nitra.Model;
using Nitra.Typing;

using System;
using System.Diagnostics;
using System.Linq;
using SCG = System.Collections.Generic;

namespace Nitra.Compiler
{
  internal partial class RuleAstEmitter
  {
    private DefineAstPrettyPrint() : void
    {
      def isVoid(rule : Rule) { rule.Type is RuleType.Void }

      def makePrinter(ruleType : RuleType, title : string, astPath : PExpr, body : PExpr) : PExpr
      {
        def name    = Util.tmpname("PrettyPrint" + title);
        def astType = Environment.GetAstType(ruleType);
        _astTb.DefineConditional(<[ decl:
          private static $(name : usesite)(writer : Nitra.PrettyPrintWriter, source : Nitra.IPrettyPrintSource, ast : $(astType : typed)) : void
          {
            _ = source;
            $body
          }
        ]>);
        <[
          if ($astPath.IsAmbiguous)
            writer.AmbiguousNode($astPath :> Nitra.IAmbiguousAst, $(title : string), source, $(name : usesite))
          else
            $(name : usesite)(writer, source, $astPath);
        ]>
      }

      def isRecursiveCallFromPostfixRule(calleeRef)
      {
        match (this.RuleSymbol, calleeRef)
        {
          | (ExtensionRuleSymbol(BaseRule = SymbolRef.Some(baseRule), ExtensionType = Postfix), SymbolRef.Some(callee)) => baseRule.Equals(callee)
          | _ => false
        }
      }
      def fieldToString(rule : Rule) : PExpr
      {
        def loop(expr : PExpr, rule : Rule) : PExpr
        {
          def result =
            match (rule)
            {
              | String(str)                                     => <[ writer.String(source, $expr, $str); ]>
              | And | Not                                       => <[ () ]>
              | Sequence(rules) as sequenceRule                 =>
                def variables  = SCG.List();
                def statements = SCG.List();
                foreach (r in rules with i)
                {
                  if (isVoid(r))
                    statements.Add(loop(<[ () ]>, r));
                  else
                  {
                    def varName = Util.tmpname("item");
                    statements.Add(loop(<[ $(varName : usesite) ]>, r));
                    variables.Add(varName);
                  }
                }
                match (variables.Count)
                {
                  | 0 => <[ { ..$statements } ]>
                  | 1 =>
                    statements.Insert(0, <[ def $(variables[0] : usesite) = $expr ]>);
                    <[ { ..$statements } ]>

                  | _ =>
                    foreach(v in variables with index)
                      statements.Insert(index, <[ def $(v : usesite) = ast.$($"Item$(index + 1)" : usesite) ]>);
                    makePrinter(sequenceRule.Type, "Sequence", expr, <[ { ..$statements } ]>);
                }

              | Repeat as loop when loop.IsSkipAnyPattern                => <[ writer.Token(source, $expr, true, "SkipAnyPattern"); ]>
              | Call(SymbolRef.Some(RegularRuleSymbol as ruleSymbol), _) => <[ writer.Token(source, $expr, $(ruleSymbol.CanParseEmptyString : bool), $(ruleSymbol.Name : string)); ]>
              | Call(SymbolRef.Some(MarkerSymbol as markerSymbol), _bp) =>
                match (markerSymbol.Node.FullNameParts)
                {
                  | ["PrettyPrint",  "nl"] => <[ writer.NewLine() ]>
                  | ["PrettyPrint", "inl"] => <[ writer.NewLineAndIndent() ]>
                  | ["PrettyPrint",  "sm"] => <[ writer.Whitespace() ]>
                  | ["PrettyPrint",   "d"] => <[ writer.Unindent() ]>
                  | ["PrettyPrint",   "i"] => <[ writer.Indent() ]>
                  | _ => <[ () ]>
                }

              | Call(ruleRef, _bp) when rule.Type is RuleType.Void =>
                match (ruleRef.Value.Name)
                {
                  | "S" | "s" => <[ _ = "Whitespace"; ]> // TODO: add analysis of a space usefulness
                  | _ => <[ writer.Whitespace(); ]>
                }

              | Call(ruleRef, bp) when isRecursiveCallFromPostfixRule(ruleRef) => <[ $expr.PrettyPrint(writer, $bp); ]>
              | Call                                                           => <[ $expr.PrettyPrint(writer, 0); ]>
              | Repeat(_, _, rule) when isVoid(rule)                           => <[ writer.Whitespace(); ]>
              | Repeat(0, Some(1), rule) as optionRule                         =>
                makePrinter(optionRule.Type, "Option", expr, <[ when (ast.IsSome) $(loop(<[ ast.Item ]>, rule)) ]>);

              | Repeat(_, _, rule) as listRule                                 =>
                makePrinter(listRule.Type, "List", expr, <[ foreach (_elem in ast) $(loop(<[ _elem ]>, rule)) ]>);

              | RepeatWithSeparator(_, _, rule, separator, _) as listRule =>
                match (rule.Type, separator.Type)
                {
                  | (Void, Void) => <[ writer.Whitespace(); ]>
                  | (_, Void) =>
                    makePrinter(listRule.Type, "ListWithSeparatorOnlyItems", expr, <[
                      foreach (_elem in ast with i)
                      {
                        when (i > 0)
                          $(loop(<[ _ ]>, separator));
                        $(loop(<[ _elem ]>, rule));
                      }
                    ]>);

                  | (Void, _) =>
                    makePrinter(listRule.Type, "ListWithSeparatorOnlySeparators", expr, <[
                      foreach (_separator in ast with i)
                      {
                        $(loop(<[ _ ]>, rule));
                        $(loop(<[ _separator ]>, separator));
                      }
                      unless (ast.IsEmpty)
                        $(loop(<[ _ ]>, rule));
                    ]>);

                  | _ =>
                    makePrinter(listRule.Type, "ListWithSeparator", expr, <[
                      def elems      = ast.Item1;
                      def separators = ast.Item2;
                      foreach (elem in elems with i)
                      {
                        $(loop(<[ elem ]>, rule));
                        when (i < separators.Length)
                        {
                          def separator = separators[i];
                          $(loop(<[ separator ]>, separator));
                        }
                      }
                    ]>);
                }
            };
          result
        }
        loop(<[ this.$(rule.FieldName : usesite) ]>, rule);
      }

      def code = this.RuleSymbol.SubRules.Map(fieldToString);

      def printGarbage =
        if (this.RuleSymbol.IsToken) <[ writer.TryPrintGarbage(source, this.Location.Span); ]>
        else <[ () ]>;
      def decl =
      if (this.RuleSymbol is ExtensionRuleSymbol(ExtensionType = Postfix(BindingPower = bindingPower)))
        <[ decl:
          [Nemerle.Diagnostics.CompileStatistic("PrettyPrint")]
          public override PrettyPrint(writer : Nitra.PrettyPrintWriter, callerBindingPower : int) : void
          {
            def source = this.PrettyPrintSource;
            $printGarbage;
            //TODO: determine correct braces from grammar
            when (callerBindingPower > $bindingPower)
              writer.String(source, Nitra.NSpan(0, 1), "(");
            ..$code;
            when (callerBindingPower > $bindingPower)
              writer.String(source, Nitra.NSpan(0, 1), ")");
          }
        ]>
      else
        <[ decl:
          [Nemerle.Diagnostics.CompileStatistic("PrettyPrint")]
          public override PrettyPrint(writer : Nitra.PrettyPrintWriter, callerBindingPower : int) : void
          {
            def source = this.PrettyPrintSource;
            ignore(writer, callerBindingPower, source);
            ..$code;
          }
        ]>;
      _astTb.DefineConditional(decl);
    }

    private DefineMissingPrettyPrint() : void
    {
      _missingTb.DefineConditional(<[ decl:
        [Nemerle.Diagnostics.CompileStatistic("PrettyPrint")]
        public override PrettyPrint(writer : Nitra.PrettyPrintWriter, _callerBindingPower : int) : void
        {
          writer.MissingNode($(this.RuleSymbol.GetRuleDescriptorRef()));
        }
      ]>);
    }

    private DefineAmbiguousPrettyPrint() : void
    {
      _ambiguousTb.DefineConditional(<[ decl:
        [Nemerle.Diagnostics.CompileStatistic("PrettyPrint")]
        public override PrettyPrint(writer : Nitra.PrettyPrintWriter, _callerBindingPower : int) : void
        {
          writer.AmbiguousNode(this);
        }
      ]>);
    }
  }
}
