using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;

namespace Nitra.Serialization
{
  [Record]
  public class CollectionTypeSerializationBuilder : ComplexTypeSerializationBuilder
  {
    public override Tag : TypeTag { get { TypeTag.Collection } }

    private _itemType : TypeVar;
    
    [RecordIgnore] private mutable _itemSerializationBuilder : TypeSerializationBuilder;

    protected override CollectTypeData() : void
    {
      assert(_itemSerializationBuilder : object == null);

      _itemSerializationBuilder = Compiler.EnqueueType(_itemType.Fix());
    }

    protected override GenerateWriteBody(body : SCG.List[PExpr]) : void
    {
      assert(_itemSerializationBuilder : object != null);

      body.Add(Formatter.WriteInt32(CountElements(<[ obj ]>)));
      body.Add(<[ foreach (item in obj)
                    $(_itemSerializationBuilder.Write(<[ item ]>)); ]>);
    }

    protected override GenerateReadBody(body : SCG.List[PExpr]) : void
    {
      assert(_itemSerializationBuilder : object != null);

      body.Add(<[ def buffer = array($(Formatter.ReadInt32())); ]>);
      body.Add(<[ for(mutable i = 0; i < buffer.Length; ++i)
                    buffer[i] = $(_itemSerializationBuilder.Read(<[ "item" ]>, _itemSerializationBuilder.TagExpr)); ]>);
      body.Add(ConvertResult(<[ buffer ]>));
    }

    protected virtual CountElements(obj : PExpr) : PExpr
    {
      <[ $obj.Count ]>
    }

    protected virtual ConvertResult(obj : PExpr) : PExpr
    {
      obj
    }
  }
}
