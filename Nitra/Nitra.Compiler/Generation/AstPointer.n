using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;
using System.Linq;

using Nitra.Model;

namespace Nitra.Compiler
{
  public interface IVariableEmitter
  {
    CreateVariable(name : string) : string;
  }

  public interface IConstantEmitter
  {
    Environment : EmitterEnvironment { get; }
    CreateConstant(name : string, value : int) : string;
  }

  public class LocalVariableEmitter : IVariableEmitter
  {
    private _variables : SCG.HashSet[string] = SCG.HashSet();
    public CreateVariable(name : string) : string
    {
      _ = _variables.Add(name);
      name
    }

    public Define() : list[PExpr]
    {
      _variables.Map(var => <[ mutable $(var : global) ]>)
    }
  }

  public class AstPointer
  {
    public  Structure       : SequenceCompileInfo;
    private ConstantEmitter : IConstantEmitter;
    private _nameMaker      : void -> string;
    public  Name            : string { [Memoize] get { _nameMaker() } }
    public  Var             : PExpr  {           get { <[ $(Name : global) ]> } }

    private this(sequence : SequenceCompileInfo, constantEmitter : IConstantEmitter, nameMaker : void -> string)
    {
      Structure       = sequence;
      ConstantEmitter = constantEmitter;
      _nameMaker      = nameMaker;
    }

    public static New(sequence : SequenceCompileInfo, constantEmitter : IConstantEmitter, variableEmitter : IVariableEmitter, prefix : string) : AstPointer
    {
      AstPointer(sequence, constantEmitter, () => variableEmitter.CreateVariable(sequence.MakeVariableName(prefix)));
    }

    public static Existing(sequence : SequenceCompileInfo, constantEmitter : IConstantEmitter, name : string) : AstPointer
    {
      AstPointer(sequence, constantEmitter, () => name);
    }

    private Ref(headerField : SequenceHeaderCompileInfo) : PExpr
    {
      if (object.ReferenceEquals(Structure, headerField.Owner))
        <[ parseResult.ast[$Var + $(MakeOffsetRef(headerField))] ]>
      else
        PExpr.Error("Type mismatch.")
    }

    public Set(headerField : SequenceHeaderCompileInfo, value : PExpr) : PExpr
    {
      <[ $(Ref(headerField)) = $value ]>
    }

    public Get(headerField : SequenceHeaderCompileInfo) : PExpr
    {
      <[ $(Ref(headerField)) ]>
    }

    private Ref(structField : SubruleCompileInfo) : PExpr
    {
      CheckTypeAndReturn(structField, <[ parseResult.ast[$Var + $(MakeOffsetRef(structField))] ]>)
    }

    public Set(structField : SubruleCompileInfo, value : PExpr) : PExpr
    {
      <[ $(Ref(structField)) = $value ]>
    }

    public GetSize(structField : SubruleCompileInfo) : PExpr
    {
//#if DEBUG
//      CheckTypeAndReturn(structField, <[ { _ = $(structField?.ToString()); parseResult.GetSize($Var + $(MakeOffsetRef(structField))) } ]>)
//#else
      CheckTypeAndReturn(structField, <[ parseResult.GetSize($Var + $(MakeOffsetRef(structField))) ]>)
//#endif
    }

    public GetRaw(structField : SubruleCompileInfo) : PExpr
    {
      CheckTypeAndReturn(structField, Ref(structField))
    }

    public GetSkipAndSize(structField : SubruleCompileInfo) : PExpr
    {
      CheckTypeAndReturn(structField, <[ parseResult.GetSkipAndSize($Var + $(MakeOffsetRef(structField))) ]>)
    }

    public GetSkip(structField : SubruleCompileInfo) : PExpr
    {
      CheckTypeAndReturn(structField, <[ parseResult.GetSkip($Var + $(MakeOffsetRef(structField))) ]>)
    }

    private CheckTypeAndReturn(structField : SubruleCompileInfo, result : PExpr) : PExpr
    {
      if (object.ReferenceEquals(Structure, structField.Owner))
        if (structField.Size == 1)
          result
        else
          PExpr.Error("Invalid subrule access.")
      else
        PExpr.Error("Type mismatch.")
    }

    private MakeOffsetRef(headerField : SequenceHeaderCompileInfo) : PExpr
    {
      if (ConstantEmitter.Environment.IsDebug)
      {
        def name  = ConstantEmitter.Environment.MakeRuleStructureHeaderFieldOffsetName(headerField);
        def name2 = ConstantEmitter.CreateConstant(name, headerField.Offset);
        <[ $(name2 : global) ]>
      }
      else
        <[ $(headerField.Offset) ]>
    }

    private MakeOffsetRef(structField : SubruleCompileInfo) : PExpr
    {
      if (ConstantEmitter.Environment.IsDebug)
      {
        def name  = ConstantEmitter.Environment.MakeRuleStructureFieldOffsetName(structField);
        def name2 = ConstantEmitter.CreateConstant(name, structField.Offset);
        <[ $(name2 : global) ]>
      }
      else
       <[ $(structField.Offset) ]>
    }
  }
}
