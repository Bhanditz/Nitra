using Nitra.Runtime;
using Nitra.Model;
using Nitra.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Compiler
{
  [Record]
  public sealed class OutliningWalkerGenerator : WalkerGenerator
  {
    public Environment : EmitterEnvironment { get; }

    public override Name : string { get StandardWalkerTypes.Outlining }

    public override SupportsVoidRules : bool { get true }

    public override GetSignature(_rule : RuleSymbol) : WalkerSignature
    {
      WalkerSignature(
        [<[ parameter: _context : Nitra.Internal.OutliningWalkerContext ]>],
        <[ void ]>);
    }

    static IsBeginOutlineMarker(field : RuleStructureField) : bool
    {
      | RuleStructureField.Marker as m when m.Symbol.FullName == "Outline.outline_impl"
      | RuleStructureField.Marker as m when m.Symbol.FullName == "Outline.outline_begin"
      | RuleStructureField.Marker as m when m.Symbol.FullName == "Outline.outline_begin_before" => true
      | _ => false
    }

    static IsNeedSkipSpaces(field : RuleStructureField) : bool
    {
      | RuleStructureField.Marker as m when m.Symbol.FullName == "Outline.outline_begin_before"
      | RuleStructureField.Marker as m when m.Symbol.FullName == "Outline.outline_end_before" => true
      | _ => false
    }

    static IsHideByDefault(field : RuleStructureField) : bool
    {
      | RuleStructureField.Marker as m when m.Symbol.FullName == "Outline.outline_hiden" => true
      | _ => false
    }

    static IsImplementationMarker(field : RuleStructureField) : bool
    {
      | RuleStructureField.Marker as m when m.Symbol.FullName == "Outline.outline_impl" => true
      | _ => false
    }

    static IsEndOutlineMarker(field : RuleStructureField) : bool
    {
      | RuleStructureField.Marker as m when m.Symbol.FullName == "Outline.outline_end"
      | RuleStructureField.Marker as m when m.Symbol.FullName == "Outline.outline_end_before" => true
      | _ => false
    }

    protected override GetRuleValue(field : RuleStructureField, parentAstPointer : AstPointerScope) : PExpr
    {
      match (field)
      {
        | SimpleCall as simpleField         with (code = CallSimpleRule(simpleField, parentAstPointer, <[ _context ]>), ruleType = simpleField.Symbol.Type)
        | ExtensibleCall as extensibleField with (code = CallExtensibleRule(extensibleField, parentAstPointer, <[ _context ]>),           ruleType = extensibleField.Symbol.Type) =>
          if (ruleType is RuleType.Void)
            <[ def lastSpaceStartPos = pos;
               $code;
               when (pos > lastSpaceStartPos)
               {
                 _context.LastSpaceStartPos = lastSpaceStartPos;
                 _context.LastSpaceEndPos   = pos;
               }
            ]>
          else
            code

        | Marker =>
          if (IsBeginOutlineMarker(field))
            if (IsNeedSkipSpaces(field))
              <[
                if (_context.LastSpaceEndPos == pos) // пробельное правило было перед текущим
                  outlineSpanStart = _context.LastSpaceStartPos;
                else
                  outlineSpanStart = pos;
              ]>
            else
              <[ outlineSpanStart = pos; ]>
          else if (IsEndOutlineMarker(field))
            if (IsNeedSkipSpaces(field))
              <[
                def outlineSpanEnd = if (_context.LastSpaceEndPos == pos) _context.LastSpaceStartPos else pos;
                when (outlineSpanEnd > outlineSpanStart)
                  _context.Result.Add(Nitra.OutliningInfo(Nitra.NSpan(outlineSpanStart, outlineSpanEnd), hideByDefault, isImplementation));
                hideByDefault = false;
              ]>
            else
              <[
                when (pos > outlineSpanStart)
                  _context.Result.Add(Nitra.OutliningInfo(Nitra.NSpan(outlineSpanStart, pos), hideByDefault, isImplementation));
                hideByDefault = false;
              ]>
          else if (IsHideByDefault(field))
            <[ hideByDefault = true; ]>
          else
            <[ () ]>

        | List as lst when lst.IsSkipAnyPattern
        | Empty | TokenString | RegularCall => Skip(field, parentAstPointer)
        | Option as field =>
          GetOptionValue(
            field            = field,
            parentAstPointer = parentAstPointer,
            hasValue         = <[ _ = $(GetRuleValue(field.Rule, "valuePointer")); ]>,
            noValue          = <[ () ]>)

        | List as lst =>
          GetListValue(field            = lst,
            parentAstPointer = parentAstPointer,
            init             = <[ () ]>,
            item             = GetRuleValue(lst.Rule, "itemPointer"),
            returnCode       = <[ () ]>,
            emptyCode        = <[ () ]>);

        | ListWithSeparator as lst=>
          GetListWithSeparatorValue(
            field            = lst,
            parentAstPointer = parentAstPointer,
            init             = <[ () ]>,
            item             = GetRuleValue(lst.Rule, "itemPointer"),
            separator        = GetRuleValue(lst.Separator, "separatorPointer"),
            returnCode       = <[ () ]>,
            emptyCode        = <[ () ]>);
      }
    }

    protected override CombineValues(_ruleStructure : SequenceCompileInfo, arguments : list[PExpr]) : PExpr
    {
      <[ ignore(..$arguments) ]>
    }

    protected override GetSyntaxRuleValue(ruleSymbol : SyntaxRuleSymbol, astPointer : AstPointerScope) : PExpr
    {
      def ruleStruct = ruleSymbol.Structure;
      def outlineCount = ruleStruct.Fields.Count(IsBeginOutlineMarker);
      def statementsList = List();

      when (outlineCount > 0)
      {
        def isImplementation = ruleStruct.Fields.Exists(IsImplementationMarker);
        statementsList.Add(<[ mutable outlineSpanStart = -1; mutable hideByDefault = false; def isImplementation = $isImplementation; ]>);
        when (outlineCount > 1)
          Message.Error(ruleSymbol.FirstLocation.ToNemerleLocation(), "Currently not supported more than one outline region on rule.");
      }

      foreach (field in ruleStruct.Fields)
        statementsList.Add(GetRuleValue(field, astPointer));

      def statements = statementsList.NToList();

      <[ { ..$statements } ]>
    }

    public override PrefixAmbiguity(_ruleSymbol : ExtensibleRuleSymbol) : PExpr
    {
      <[ ignore(parseResult, endPos, ambiguities); ]>
    }

    public override PostfixAmbiguity(_ruleSymbol : ExtensibleRuleSymbol) : PExpr
    {
      <[ ignore(parseResult, endPos, ambiguities); ]>
    }

    public override PrefixMissed(_ruleSymbol : ExtensibleRuleSymbol) : PExpr
    {
      <[ ignore(parseResult, pos); ]>
    }

    public override SimpleMissed(_ruleSymbol : SimpleRuleSymbol) : PExpr
    {
      <[ ignore(parseResult, pos); ]>
    }
  }
}
