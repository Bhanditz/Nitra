using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;

using Nitra.ProjectSystem;
using Nitra.Internal;
using Nitra.Model;
using Nitra.Typing;

using System;
using System.Diagnostics;
using System.Linq;
using SCG = System.Collections.Generic;

namespace Nitra.Compiler
{
  internal abstract partial class RuleParseTreeEmitter
  {
    protected GetBaseDeclarationType(declarationSymbol : DeclarationSymbol) : PExpr
    {
      | ExtensionDeclarationSymbol(BaseDeclaration = baseDeclaration) => GetBaseDeclarationType(baseDeclaration.GetValue(Environment.Project))
      | ExtensibleDeclarationSymbol
      | SimpleDeclarationSymbol => <[ $(Environment.GetDeclarationType(declarationSymbol) : typed) ]>
      | _ => assert(false, "Invalid declaration symbol")
    }

    protected GetConcreteDeclarationType(declarationSymbol : DeclarationSymbol) : PExpr
    {
      | ExtensibleDeclarationSymbol
      | ExtensionDeclarationSymbol  => <[ $(Environment.GetDeclarationType(declarationSymbol) : typed) ]>
      | SimpleDeclarationSymbol     => <[ $(Environment.GetDeclarationType(declarationSymbol) : typed).Concrete ]>
      | _ => assert(false, "Invalid declaration symbol")
    }

    protected GetAmbiguousDeclarationType(declarationSymbol : DeclarationSymbol) : PExpr
    {
      | ExtensionDeclarationSymbol(BaseDeclaration = baseDeclaration) => GetAmbiguousDeclarationType(baseDeclaration.GetValue(Environment.Project))
      | ExtensibleDeclarationSymbol
      | SimpleDeclarationSymbol     => <[ $(Environment.GetDeclarationType(declarationSymbol) : typed).Ambiguous ]>
      | _ => assert(false, "Invalid declaration symbol")
    }

    protected DefineExtensibleRuleGetAstMethod(_baseTb : TypeBuilder) : void
    {
      assert(_baseTb != null);

      def syntaxMappingBody = this.RuleSymbol.GetSyntaxMappingBody();
      when (syntaxMappingBody != null)
      {
        def declarationSymbol   = syntaxMappingBody.Declaration.GetValue(Environment.Project);
        def parentType          = Environment.TypeCompiler.CompileParentType(declarationSymbol);
        def baseDeclarationType = GetBaseDeclarationType(declarationSymbol);
        _baseTb.DefineConditional(<[ decl:
          public abstract GetAst(parent : $(parentType : typed)) : $baseDeclarationType
        ]>);
      }
    }

    private static MakeParseTreeMemberAccess(loc : Location, path : list[LocatedName]) : PExpr
    {
      path.FoldLeft(Util.locate(loc.ToNemerleLocation(), <[ this ]> : PExpr), (item, acc) => Util.locate(item.Location.ToNemerleLocation(), <[ $acc.$(item.Id : usesite) ]>));
    }

    [Record]
    private struct BuildAstResult
    {
      public Code            : PExpr;
      public ConcreteList    : PExpr;
      public AmbiguousList   : PExpr;
      public ConcreteOption  : PExpr;
      public AmbiguousOption : PExpr;

      public this(code : PExpr)
      {
        this(
          code,
          <[ Nitra.Declarations.AstList[Nitra.Declarations.IAst] ]>,
          <[ Nitra.Declarations.AmbiguousAstList[Nitra.Declarations.IAst] ]>,
          <[ Nitra.Declarations.AstOption[Nitra.Declarations.IAst] ]>,
          <[ Nitra.Declarations.AmbiguousAstOption[Nitra.Declarations.IAst] ]>)
      }
    }

    private DefineGetAstMethod(tb : TypeBuilder, syntaxMappingBody : SyntaxMappingBody, modifiers : NemerleModifiers) : void
    {
      def declarationSymbol   = syntaxMappingBody.Declaration.GetValue(Environment.Project);
      def parentType          = Environment.TypeCompiler.CompileParentType(declarationSymbol);
      def baseDeclarationType = GetBaseDeclarationType(declarationSymbol);

      def buildAst(ruleType : RuleType, astType : NType, obj : PExpr, errorLoc : Location) : BuildAstResult
      {
        def reportError()
        {
          Environment.Project.FatalError(errorLoc, $"Cannot map '$(ruleType)' to '$(astType)'", -1)
        }
        match (ruleType, astType)
        {
          | (List(ruleItemType), List(astItemType))
          | (List(ruleItemType), OptionList(astItemType)) =>
            def parseTreeType     = Environment.GetParseTreeType(ruleType);
            def parseTreeItemType = Environment.GetParseTreeType(ruleItemType);
            def itemResult        = buildAst(ruleItemType, astItemType, <[ node ]>, errorLoc);
            def buildAstListName  = Util.tmpname("BuildAstList");
            tb.Define(<[ decl:
              private static $(buildAstListName : usesite)(lst : $(parseTreeType : typed), parent : $(parentType : typed)) : $(itemResult.ConcreteList)
              {
                def items = array(lst.Count);
                foreach (node in lst with index)
                  items[index] = $(itemResult.Code);
                $(itemResult.ConcreteList)(parent, lst.Location.Span, items)
              }
            ]>);
            BuildAstResult(<[
              def lst = $obj;
              if (lst.IsAmbiguous)
              {
                def ambiguousLst = lst :> Nitra.AmbiguousListParseTree[$(parseTreeItemType : typed)];
                def ambiguities  = array(ambiguousLst.Ambiguities.Length);
                foreach (parseTree in ambiguousLst.Ambiguities with index)
                  ambiguities[index] = $(buildAstListName : usesite)(parseTree, parent);
                $(itemResult.AmbiguousList)(ambiguities)
              }
              else
                $(buildAstListName : usesite)(lst, parent);
            ]>)

          | (Option(ruleItemType), Option(astItemType)) =>
            def parseTreeType      = Environment.GetParseTreeType(ruleType);
            def parseTreeItemType  = Environment.GetParseTreeType(ruleItemType);
            def itemResult         = buildAst(ruleItemType, astItemType, <[ opt.Value ]>, errorLoc);
            def buildAstOptionName = Util.tmpname("BuildAstOption");
            tb.Define(<[ decl:
              private static $(buildAstOptionName : usesite)(opt : $(parseTreeType : typed), parent : $(parentType : typed)) : $(itemResult.ConcreteOption)
              {
                if (opt.HasValue)
                  $(itemResult.ConcreteOption)(parent, opt.Location.Span, $(itemResult.Code))
                else
                  $(itemResult.ConcreteOption)(parent, opt.Location.Span)
              }
            ]>);
            BuildAstResult(<[
              def opt = $obj;
              if (opt.IsAmbiguous)
              {
                def ambiguousOpt = opt :> Nitra.AmbiguousOptionParseTree[$(parseTreeItemType : typed)];
                def ambiguities  = array(ambiguousOpt.Ambiguities.Length);
                foreach (parseTree in ambiguousOpt.Ambiguities with index)
                  ambiguities[index] = $(buildAstOptionName : usesite)(parseTree, parent);
                $(itemResult.AmbiguousOption)(ambiguities)
              }
              else
                $(buildAstOptionName : usesite)(opt, parent);
            ]>)

          | (Ref(SyntaxRuleSymbol(Declaration = SymbolRef.Some(sourceDeclarationSymbol))), Qualified) =>
            def targetDeclarationSymbol = Environment.TypeCompiler.TryGetDeclarationSymbol(astType);
            if (targetDeclarationSymbol != null)
            {
              if (sourceDeclarationSymbol == targetDeclarationSymbol || sourceDeclarationSymbol.AllBaseDeclarations.Contains(targetDeclarationSymbol))
              {
                def t = Environment.GetDeclarationType(targetDeclarationSymbol);
                BuildAstResult(<[ $obj.GetAst(parent) ]>,
                  <[ $(t : typed).AstList ]>, <[ $(t : typed).AmbiguousAstList ]>,
                  <[ $(t : typed).AstOption ]>, <[ $(t : typed).AmbiguousAstOption ]>)
              }
              else
                Environment.Project.FatalError(errorLoc, $"Declaration '$(sourceDeclarationSymbol.FullName)' is not subtype of declaration '$(targetDeclarationSymbol.FullName)'", -1)
            }
            else
              reportError()

          | (Tuple(members), _) =>
            if (members.MapI((i, m) => (i + 1, m)).Filter((_, m) => !(m is RuleType.Chars(Keyword = Some))) is [(index, nonKeyword)])
              buildAst(nonKeyword, astType, <[ $obj.$("Item" + index.ToString() : usesite) ]>, errorLoc)
            else
              reportError()

          | _ => reportError()
        }
      }

      def compileField(field : SyntaxMappingField) : PExpr
      {
        def declarationPropertySymbol = field.DeclarationProperty.GetValue(Environment.Project);
        Util.locate(field.Location.ToNemerleLocation(),
          match (field)
          {
            | Inline(code) => <[ $(declarationPropertySymbol.Name : usesite) = $code ]>
            | Automatic(loc, ruleFieldPath, ruleType) =>
              def astType         = declarationPropertySymbol.Type;
              def parseTreeMember = MakeParseTreeMemberAccess(loc, ruleFieldPath);
              <[ $(declarationPropertySymbol.Name : usesite) = $(buildAst(ruleType, astType, parseTreeMember, field.Location).Code) ]>
          });
      }

      Util.locate(syntaxMappingBody.Location.ToNemerleLocation(),
      {
        def code =
          match (syntaxMappingBody)
          {
            | CallMember(loc, ruleFieldPath) =>
              // TODO: make strict typing!
              def parseTreeMember = MakeParseTreeMemberAccess(loc, ruleFieldPath);
              <[ $parseTreeMember.GetAst(parent) ]>

            | PerField(fields) =>
              def concreteDeclarationType = GetConcreteDeclarationType(declarationSymbol);
              def values                  = fields.Map(compileField);
              <[
                def $("declaration" : global) = $concreteDeclarationType(parent, this.Location.Span);
                declaration.Initialize(..$values);
                declaration
              ]>

            | Inline(code) => code
          };

        def mods = AttributesAndModifiers(modifiers, []);
        tb.Define(<[ decl:
          ..$mods GetAst($("parent" : global) : $(parentType : typed)) : $baseDeclarationType
          {
            $code
          }
        ]>);
      });
    }

    protected DefineSimpleRuleGetAstMethod(_nodeTb : TypeBuilder) : void
    {
      assert(_nodeTb != null);

      def syntaxMappingBody = this.RuleSymbol.GetSyntaxMappingBody();
      when (syntaxMappingBody != null)
        DefineGetAstMethod(_nodeTb, syntaxMappingBody, NemerleModifiers.Public %| NemerleModifiers.Virtual)
    }

    protected DefineExtensionRuleGetAstMethod(_nodeTb : TypeBuilder) : void
    {
      assert(_nodeTb != null);

      def syntaxMappingBody = this.RuleSymbol.GetSyntaxMappingBody();
      when (syntaxMappingBody != null)
        DefineGetAstMethod(_nodeTb, syntaxMappingBody, NemerleModifiers.Public %| NemerleModifiers.Override)
    }

    protected DefineAmbiguousGetAstMethod(_ambiguousTb : TypeBuilder) : void
    {
      assert(_ambiguousTb != null);

      def syntaxMappingBody = this.RuleSymbol.GetSyntaxMappingBody();
      when (syntaxMappingBody != null)
      {
        def declarationSymbol        = syntaxMappingBody.Declaration.GetValue(Environment.Project);
        def parentType               = Environment.TypeCompiler.CompileParentType(declarationSymbol);
        def baseDeclarationType      = GetBaseDeclarationType(declarationSymbol);
        def ambiguousDeclarationType = GetAmbiguousDeclarationType(declarationSymbol);
        _ambiguousTb.Define(<[ decl:
          public override GetAst(parent : $(parentType : typed)) : $baseDeclarationType
          {
            def ambiguities = Nitra.Internal.AstUtils.GetAstAmbiguities(parent, this.Ambiguities);
            $ambiguousDeclarationType(ambiguities)
          }
        ]>);
      }
    }

    protected DefineMissingGetAstMethod(_missingTb : TypeBuilder) : void
    {
      assert(_missingTb != null);

      def syntaxMappingBody = this.RuleSymbol.GetSyntaxMappingBody();
      when (syntaxMappingBody != null)
      {
        def declarationSymbol   = syntaxMappingBody.Declaration.GetValue(Environment.Project);
        def parentType          = Environment.TypeCompiler.CompileParentType(declarationSymbol);
        def baseDeclarationType = GetBaseDeclarationType(declarationSymbol);
        _missingTb.Define(<[ decl:
          public override GetAst(parent : $(parentType : typed)) : $baseDeclarationType
          {
            _ = parent;
            null
          }
        ]>);
      }
    }
  }
}
