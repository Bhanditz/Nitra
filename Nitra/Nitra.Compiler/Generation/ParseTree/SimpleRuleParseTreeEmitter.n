using Nitra.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;

using System;
using SCG = System.Collections.Generic;

namespace Nitra.Compiler
{
  internal sealed class SimpleRuleParseTreeEmitter : RuleParseTreeEmitter
  {
    mutable _baseTb      : TypeBuilder;
    mutable _parsedTb    : TypeBuilder;
    mutable _chemicalTb  : TypeBuilder;
    mutable _missingTb   : TypeBuilder;
    mutable _ambiguousTb : TypeBuilder;
    mutable _nestedTb    : TypeBuilder = null;

    public this(environment : EmitterEnvironment, typeCompiler : NTypeCompiler, ruleSymbol : SimpleRuleSymbol)
    {
      base(environment, typeCompiler, ruleSymbol)
    }

    public override IsTypesDefined : bool { get { _baseTb != null } }

    [Memoize]
    public override CreateConstant(name : string, value : int) : string
    {
      assert(IsTypesDefined);
      Util.CreateConstant(_parsedTb, name, value)
    }

    public override DefineNestedType(td : ClassMember.TypeDeclaration) : TypeBuilder
    {
      DefineWrappedNestedType(ref _nestedTb, _baseTb, td)
    }

    public override Emit() : void
    {
      assert(IsTypesDefined);

      _baseTb.Compile();
      _parsedTb.Compile();
      _chemicalTb.Compile();
      _missingTb.Compile();
      _ambiguousTb.Compile();
      _nestedTb?.Compile();
    }

    public override DefineTypes(defineType : ClassMember.TypeDeclaration -> TypeBuilder) : void
    {
      assert(!IsTypesDefined);

      Util.locate(this.RuleSymbol.FirstLocation.NLocation(),
      {
        def baseTypes =
          if (this.RuleSymbol.Ast.IsSome)
            [ <[ $(Environment.ParseTreeTypeVar : typed) ]>, <[ Nitra.IMappedParseTree[$(GetGetAstMethodReturnType())] ]> ]
          else
            [ <[ $(Environment.ParseTreeTypeVar : typed) ]> ];

        _baseTb = defineType(<[decl:
          public new abstract partial class $(this.RuleSymbol.Name : usesite) : ..$baseTypes
          {
            public override RuleDescriptor : Nitra.RuleDescriptor { get { $(RuleSymbol.GetRuleDescriptorRef()) } }
          }
        ]>);

        _parsedTb = _baseTb.DefineNestedType(<[decl:
          private new sealed partial class __Parsed__Node : $(_baseTb.GetMemType() : typed), Nitra.Internal.ISequenceParseTreeLoader[Nitra.NSpan], Nitra.Internal.ISequenceParseTreeLoader[Nitra.ParseTree]
          {
          }
        ]>);

        _chemicalTb = _baseTb.DefineNestedType(<[decl:
          private new sealed partial class __Chemical__Node : $(_baseTb.GetMemType() : typed)
          {
          }
        ]>);

        _missingTb = _baseTb.DefineNestedType(<[decl:
          public new sealed partial class MissingNode : $(_baseTb.GetMemType() : typed)
          {
            public this(sourceProvider : Nitra.Runtime.ISourceSnapshotProvider, pos : int)
            {
              this.sourceProvider = sourceProvider;
              this.pos            = pos;
            }

            private sourceProvider : Nitra.Runtime.ISourceSnapshotProvider;
            private pos            : int;

            public override IsMissing : bool           { get { true } }
            public override Location  : Nitra.Location { get { Nitra.Location(this.sourceProvider, this.pos) } }
          }
        ]>);

        _ambiguousTb = _baseTb.DefineNestedType(<[decl:
          public new sealed partial class AmbiguousNode : $(_baseTb.GetMemType() : typed), Nitra.IAmbiguousParseTree
          {
            public this(ambiguities : array[$(_baseTb.GetMemType() : typed)])
            {
              this.Ambiguities = ambiguities;
            }

            public override IsAmbiguous    : bool                                   { get true }
            public override Location       : Nitra.Location                         { get { Ambiguities[0].Location } }
            public          Ambiguities    : array[$(_baseTb.GetMemType() : typed)] { get; }
            public          AmbiguityType  : Nitra.ParseTreeAmbiguityType           { get Nitra.ParseTreeAmbiguityType.Recovery }
          }
        ]>);
      });
    }

    public override DefineMembers() : void
    {
      assert(IsTypesDefined);

      Util.locate(this.RuleSymbol.FirstLocation.NLocation(),
      {
        DefineCreateMethod();

        def fields = GetFields();
        DefineParseTreeCtorAndFields(fields, _baseTb, true);
        DefineParsedNodeCtorAndFields(fields, _parsedTb);
        DefineChemicalNodeCtorAndFields(fields, _chemicalTb, _baseTb);
        DefineParseTreeApplyMethod(fields, _baseTb, _chemicalTb, _parsedTb);
        DefineSimpleRuleGetAstMethod(_baseTb);
        DefineParseTreePrettyPrint(_baseTb);
        DefineSimpleRuleMethods(_baseTb);
        DefineReadSequenceMethod(_parsedTb);

        DefineMissingGetAstMethod(_missingTb);
        DefineMissingPrettyPrint(_missingTb);
        DefineMissingRuleMethods(_missingTb);

        DefineAmbiguousGetAstMethod(_ambiguousTb);
        DefineAmbiguousPrettyPrint(_ambiguousTb);
        DefineAmbiguousRuleMethods(_ambiguousTb);
      })
    }

    private DefineCreateMethod() : void
    {
      _baseTb.DefineConditional(<[ decl:
        [Nemerle.Diagnostics.CompileStatistic("ParseTree")]
        public static CreateParseTree(pos : ref int, size : int, parseResult : Nitra.ParseResult, grammar : Nitra.CompositeGrammar) : $(_baseTb.GetMemType() : typed)
        {
          def ruleParser = grammar.GetSimpleRuleParser($(this.RuleSymbol.GetRuleDescriptorRef()));
          if (size > 0 || ruleParser.CanParseEmptyString)
          {
            def rawTreePtr = parseResult.TryGetRawTree(pos, ruleParser.RuleId);
            assert(rawTreePtr > 0);
            $(_baseTb.GetMemType() : typed).__Parsed__Node(ref pos, rawTreePtr, parseResult)
          }
          else
            $(_baseTb.GetMemType() : typed).MissingNode(parseResult, pos)
        }
      ]>);

      _baseTb.DefineConditional(<[ decl:
        [Nemerle.Diagnostics.CompileStatistic("ParseTree")]
        public static CreateParseTree(startPos : int, endPos : int, parseResult : Nitra.ParseResult, grammar : Nitra.CompositeGrammar) : $(_baseTb.GetMemType() : typed)
        {
          def ruleParser = grammar.GetSimpleRuleParser($(this.RuleSymbol.GetRuleDescriptorRef()));
          if (startPos < endPos || ruleParser.CanParseEmptyString)
          {
            match (parseResult.TryGetRecoveredSequence(startPos, endPos, ruleParser.ParsingSequence))
            {
              | null =>
                mutable pos = startPos;
                def rawTreePtr = parseResult.TryGetRawTree(pos, ruleParser.RuleId);
                def parseTree = $(_baseTb.GetMemType() : typed).__Parsed__Node(ref pos, rawTreePtr, parseResult);
                assert(pos == endPos);
                parseTree

              | Unambiguous(recoveredSubrulesIndex) => $(_baseTb.GetMemType() : typed).__Parsed__Node(startPos, endPos, recoveredSubrulesIndex, parseResult)
              | Ambiguous(indexes)                  =>
                def ambiguities = array(indexes.Length);
                foreach (recoveredSubrulesIndex in indexes with i)
                  ambiguities[i] = $(_baseTb.GetMemType() : typed).__Parsed__Node(startPos, endPos, recoveredSubrulesIndex, parseResult);
                $(_ambiguousTb.GetMemType() : typed)(ambiguities)
            }
          }
          else
            $(_baseTb.GetMemType() : typed).MissingNode(parseResult, startPos)
        }
      ]>);

      when (this.RuleSymbol.Options.IsStart)
      {
        _baseTb.DefineConditional(<[ decl:
          [Nemerle.Diagnostics.CompileStatistic("ParseTree")]
          public static CreateParseTree(parseResult : Nitra.IParseResult) : $(_baseTb.GetMemType() : typed)
          {
            def parseResult = parseResult :> Nitra.ParseResult;
            CreateParseTree(parseResult.StartPos, parseResult.ParseEnd, parseResult, parseResult.RuleParser.Grammar);
          }
        ]>);
      }
    }
  }
}
