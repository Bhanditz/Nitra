using Nitra.Model;
using Nitra.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Compiler
{
  public sealed class RawTreePointerScope : IVariableEmitter
  {
    private         _sequence        : SequenceCompileInfo;
    private         _baseName        : string;
    private         _constantEmitter : IConstantEmitter;
    private mutable _rawTreePointer  : RawTreePointer;
    private mutable _compiled        : bool;

    // stuff for RawTreePointer.New
    CreateVariable(name : string) : string implements IVariableEmitter.CreateVariable { name }

    internal this(sequence : SequenceCompileInfo, baseName : string, constantEmitter : IConstantEmitter)
    {
      _sequence        = sequence;
      _baseName        = baseName;
      _constantEmitter = constantEmitter;
    }

    internal this(astPointer : RawTreePointer)
    {
      _rawTreePointer = astPointer;
      _sequence   = astPointer.Sequence;
    }

    public Access() : RawTreePointer
    {
      assert(!_compiled);

      when (_rawTreePointer == null)
        _rawTreePointer = RawTreePointer.New(_sequence, _constantEmitter, this, _baseName);
      _rawTreePointer
    }

    public IsAccessed() : bool { _rawTreePointer != null }

    internal Compile(bodyCode : PExpr) : PExpr
    {
      assert(!_compiled);
      assert(_sequence != null);

      _compiled = true;
      if (_rawTreePointer == null)
        bodyCode
      else
        <[
          def $(_rawTreePointer.Name : global) = parseResult.GetAst(pos, ruleParser.$(_sequence.AstId : global));
          $bodyCode;
        ]>
    }
  }
}
