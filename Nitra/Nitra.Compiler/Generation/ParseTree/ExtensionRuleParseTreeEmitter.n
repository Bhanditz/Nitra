using Nitra.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;

using System;
using SCG = System.Collections.Generic;

namespace Nitra.Compiler
{
  internal sealed class ExtensionRuleParseTreeEmitter : RuleParseTreeEmitter
  {
    mutable _nodeTb     : TypeBuilder;
    mutable _parsedTb   : TypeBuilder;
    mutable _chemicalTb : TypeBuilder;
    mutable _nestedTb   : TypeBuilder;

    public this(environment : EmitterEnvironment, ruleSymbol : ExtensionRuleSymbol)
    {
      base(environment, ruleSymbol)
    }

    public override IsTypesDefined : bool { get { _nodeTb != null } }

    [Memoize]
    public override CreateConstant(name : string, value : int) : string
    {
      assert(IsTypesDefined);
      Util.CreateConstant(_parsedTb, name, value)
    }

    public override DefineNestedType(td : ClassMember.TypeDeclaration) : TypeBuilder
    {
      assert(IsTypesDefined);
      // Fix #NTR-32 by adding excess module "Nested"
      when (_nestedTb == null)
        _nestedTb = _nodeTb.DefineNestedType(<[ decl: public module Nested { } ]>);

      _nestedTb.DefineNestedType(td)
    }

    public override Emit() : void
    {
      assert(IsTypesDefined);

      _nodeTb.Compile();
      _parsedTb.Compile();
      _chemicalTb.Compile();
      _nestedTb?.Compile();
    }

    public override DefineTypes(defineType : ClassMember.TypeDeclaration -> TypeBuilder) : void
    {
      assert(!IsTypesDefined);

      Util.locate(this.RuleSymbol.FirstLocation.NLocation(),
      {
        _nodeTb = defineType(<[decl:
          public new abstract partial class $(this.RuleSymbol.Name : usesite) : $(Environment.GetParseTreeType((this.RuleSymbol :> ExtensionRuleSymbol).BaseRule.Value) : typed)
          {
            public override RuleDescriptor : Nitra.RuleDescriptor { get { $(RuleSymbol.GetRuleDescriptorRef()) } }
          }
        ]>);

        _parsedTb = defineType(<[decl:
          private new sealed partial class $("__Parsed__" + this.RuleSymbol.Name : usesite) : $(_nodeTb.GetMemType() : typed), Nitra.Internal.IAstSequenceLoader[Nitra.NSpan], Nitra.Internal.IAstSequenceLoader[Nitra.ParseTree]
          {
          }
        ]>);

        _chemicalTb = defineType(<[decl:
          private new sealed partial class $("__Chemical__" + this.RuleSymbol.Name : usesite) : $(_nodeTb.GetMemType() : typed)
          {
          }
        ]>);
      });
    }

    public override DefineMembers() : void
    {
      assert(IsTypesDefined);

      def fields = GetFields();
      DefineAstCtorAndFields(fields, _nodeTb, false);
      DefineParsedNodeCtorAndFields(fields, _parsedTb);
      DefineChemicalNodeCtorAndFields(fields, _chemicalTb, _nodeTb);
      DefineAstApplyMethod(fields, _nodeTb, _chemicalTb, _parsedTb);
      DefineExtensionRuleGetDeclarationMethod(_nodeTb);
      DefineAstPrettyPrint(_nodeTb);
      DefineExtensionRuleMethods(_nodeTb);
      DefineReadSequenceMethod(_parsedTb);
    }
  }
}
