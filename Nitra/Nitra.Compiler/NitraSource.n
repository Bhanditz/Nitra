using Nitra.Compiler;
using Nitra.Model;
using Nitra.ProjectSystem;
using Nitra.Typing;
using Nitra.Runtime;
using Nitra.Util;
using Nitra.Serialization;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Imperative;

using Nemerle.Utility;
using System;
using System.IO;
using System.Diagnostics;
using System.Linq;

using System.Reflection;
using SCG = System.Collections.Generic;
using G = NitraSyntaxParseTree;
using PExpr = Nemerle.Compiler.Parsetree.PExpr;

namespace Nitra
{
  internal class NitraSourceImpl
  {
    private Path          : string                 { get; }
    private Manager       : ManagerClass           { get; }
    private IsUnderNitraForVisualStudio : bool;

    private static RestrictedNames : SCG.HashSet[string] = SCG.HashSet(array[
      // Parse tree classes
      "Located",
      "ParseTree",
      "AmbiguousOrMissingNode",
      "Nested",

      // Parse tree Methods
      "Equals",
      "GetHashCode",
      "GetType",
      "ReferenceEquals",
      "MemberwiseClone",
      "Finalize",
      "ToString",
      "PrettyPrint",
      "GetText",
      "FirstChar",
      "IntersectsWith",
      "Intersection",
      "Apply",
      "Create",
      "GetSourceSnapshotProvider",
      "GetAst",

      // Parse tree Properties
      "Location",
      "Span",
      "File",
      "PrettyPrintSource",
      "RuleDescriptor",
      "IsMissing",
      "IsAmbiguous",
      "Ambiguities",
      "AmbiguityType",
    ]);
    public this(path : string, manager : ManagerClass)
    {
      Path    = path;
      Manager = manager;
      IsUnderNitraForVisualStudio =
        match (AppDomain.CurrentDomain.GetData("NitraForVisualStudio"))
        {
          | x is bool => x
          | _         => false
        };
      _ = UpdateCurrentProjectContext();
    }

    IsListUserDataPresent[T]() : bool
    {
      def map = Manager.UserData[typeof(SCG.List[T])];
      map != null
    }

    GetUserData[T](init : T -> void = null) : T
      where T: new(), class
    {
      def key = typeof(T);
      mutable result = Manager.UserData[key] :> T;
      when (result == null)
      {
        result = T();
        when (init != null)
          init(result);
        Manager.UserData[key] = result;
      }
      result
    }

    ResetListUserData[T]() : void
    {
      Manager.UserData[typeof(SCG.List[T])] = null
    }

    UpdateCurrentProjectContext() : CompilingNitraProject
    {
      def project = GetUserData.[CompilingNitraProject](p =>
        p.Init(Manager.ReferencedAssemblies.Select(a => a.Assembly.GetName())));
      project
    }

    public static Instance(path : string, manager : ManagerClass) : NitraSourceImpl
    {
      def projPath = manager.Options.ProjectPath;
      def projDir =
        if (File.Exists(projPath))
          Path.GetFullPath(Path.GetDirectoryName(projPath))
        else if (Directory.Exists(projPath))
          Path.GetFullPath(projPath)
        else
          Environment.CurrentDirectory;
      def path =
        if (IO.Path.IsPathRooted(path))
          path;
        else
          IO.Path.Combine(projDir, path);

      if (manager.UserData.Contains(path))
        manager.UserData[path] :> NitraSourceImpl;
      else
      {
        def impl = NitraSourceImpl(path, manager);
        manager.UserData[path] = impl;
        impl;
      }
    }

    [Record]
    private class ExportableSymbolData
    {
      public TypeBuilder          : TypeBuilder         { get; }
      public ExportableSymbolType : option[TypeBuilder] { get; }
    }

    [Record]
    private class ChemicalSymbolData
    {
      public TypeBuilder          : TypeBuilder         { get; }
      public ChemicalSymbolType   : option[TypeBuilder] { get; }
    }

    [Record]
    private class SplicableSyntaxModuleData
    {
      public Name : list[string] { get; }
    }

    public static AddExportableSymbol(tb : TypeBuilder, exportableSymbolType : option[TypeBuilder]) : void
    {
      def data = ExportableSymbolData(tb, exportableSymbolType);
      tb.Manager.GetListUserData.[ExportableSymbolData]().Add(data);
    }

    public static AddChemicalSymbol(tb : TypeBuilder, chemicalSymbolType : option[TypeBuilder]) : void
    {
      def data = ChemicalSymbolData(tb, chemicalSymbolType);
      tb.Manager.GetListUserData.[ChemicalSymbolData]().Add(data);
    }

    public static AddSplicableSyntaxModule(manager : ManagerClass, name : list[string]) : void
    {
      def data = SplicableSyntaxModuleData(name);
      manager.GetListUserData.[SplicableSyntaxModuleData]().Add(data);
    }

    public BeforeInheritance() : void
    {
      when (!IO.File.Exists(Path))
      {
        Message.Error($<#The "$Path" not exists.#>);
        return;
      }

      def makeWrapperClass(source : ISource, processingCount : int) : void
      {
        // request rebuilding type tree when code changed
        if (processingCount > 1 && Manager.IsIntelliSenseMode)
          Manager.RequestOnBuildTypesTree();
        else try
        {
          def project = UpdateCurrentProjectContext();
          def file    = InMemoryNemerleFile(project, source);
          Parse(file)
        }
        catch { | e => Message.Error("Exception: " + e.Message); }
      }
      SubscribeSourceChangedWithCounter(Manager, Location.GetFileIndex(Path), makeWrapperClass);
    }

    private Parse(file : ProjectSystem.File) : void
    {
      def source = file.GetSource();
      try
      {
        def parseResult = NitraSyntax.Start.Parse(source);
        unless (IsUnderNitraForVisualStudio)
          foreach (error in parseResult.GetErrors())
            Message.Error(error.Location.NLocation(), error.Message);

        // TODO: create parse tree when syntactic errors exist
        when (parseResult.IsSuccess)
        {
          def fileParseTree = G.Start.CreateParseTree(parseResult);
          def fileParseTrees = Manager.GetListUserData.[G.Start]();
          fileParseTrees.Add(fileParseTree);
        }
      }
      catch
      {
        | e =>
          def loc = Nitra.Location(source, 0, 1);
          Message.Error(loc.NLocation(), e.Message);
      }
    }

    private LoadParsedSymbols(project : CompilingNitraProject, fileParseTrees : SCG.List[G.Start]) : bool
    {
      def errorCount = project.ErrorCount;

      foreach (start in fileParseTrees)
        start.CollectGlobalSymbols(project.RootNamespace);

      project.ErrorCount == errorCount
    }

    private TypeSyntaxModules(project : CompilingNitraProject, fileParseTrees : SCG.List[G.Start]) : bool
    {
      def errorCount   = project.ErrorCount;

      def context = RootContext(project, project.RootNamespace, Manager.CoreEnv);

      foreach (start in fileParseTrees)
        _ = start.MakeContext(context);

      foreach (start in fileParseTrees)
        start.Typing();

      TypingUtils.CheckAstSymbolCyclicHierarchy(project, project.Environment.Root.GetDescendants.[AstSymbol]());

      foreach (start in fileParseTrees)
        start.TypeAst();

      TypingUtils.SetIdentifierSyntaxMapping(project);

      foreach (start in fileParseTrees)
        start.TypeSyntaxMappings();

      TypingUtils.ValidateSyntaxMapping(project);

      // HACK: force FSM build for each regular rule symbol (shall we make new pass in regular rule typing process?)
      foreach (symbol in project.Environment.Root.GetDescendants.[ParsedRegularRuleSymbol]())
        _ = symbol.Fsm;

      project.ErrorCount == errorCount
    }

    private CheckRestrictedNames(project : CompilingNitraProject) : bool
    {
      def errorCount   = project.ErrorCount;

      def check(loc : Location, name : string) : void
      {
        when (RestrictedNames.Contains(name))
          project.Error(loc, $"Name '$name' is restricted due to internal use");
      }

      foreach (syntaxModule in project.Environment.Root.GetDescendants.[ParsedSyntaxModuleSymbol]())
      {
        foreach (ruleSymbol in syntaxModule.Node.GetDescendants.[RuleSymbol]())
        {
          check(ruleSymbol.FirstLocation, ruleSymbol.Name);

          when (ruleSymbol is SyntaxRuleSymbol as s)
            foreach (ruleMethodSymbol in s.RuleMethods)
              check(ruleMethodSymbol.FirstLocation, ruleMethodSymbol.Name);

          match (ruleSymbol)
          {
            | SimpleRuleSymbol    as s with seq = s.NonVoidSubRules
            | ExtensionRuleSymbol as s with seq = s.NonVoidSubRules =>
              foreach (r in seq)
                check(r.Location, r.FieldName);

            | _ => ()
          }
        }
      }

      project.ErrorCount == errorCount
    }

    private CreateSplicableSyntaxModules(project : CompilingNitraProject) : bool
    {
      def errorCount = project.ErrorCount;
      def environment = project.Environment;

      def syntaxModules      = SCG.List();
      def addedSyntaxModules = SCG.HashSet();

      def addSyntaxModule(syntaxModule)
      {
        when (addedSyntaxModules.Add(syntaxModule))
        {
          def splicableSyntaxModuleName = syntaxModule.Node.Parent.FullNameParts + ["Splicable" + syntaxModule.Name];
          when (project.Environment.Root.BindMany.[SyntaxModuleSymbol](splicableSyntaxModuleName) is [])
          {
            foreach (sm in syntaxModule.ExternalImports)
              addSyntaxModule(sm);
            syntaxModules.Add(syntaxModule);
          }
        }
      }

      foreach (syntaxModule in environment.Root.GetDescendants.[SyntaxModuleSymbol]())
      {
        | ExternalSyntaxModuleSymbol => ()
        | _ =>
          when (syntaxModule.Options.IsNeedSplicable)
            addSyntaxModule(syntaxModule);
      }

      try
        foreach (data in Manager.GetListUserData.[SplicableSyntaxModuleData]())
          match (project.Environment.Root.BindMany.[SyntaxModuleSymbol](data.Name))
          {
            | [syntaxModule] => addSyntaxModule(syntaxModule)
            | _ => Message.Error($<#cant find syntax module symbol '..$(data.Name; ".")'#>)
          }
      finally ResetListUserData.[SplicableSyntaxModuleData]();

      def converter = SplicableSyntaxModuleConverter(project);
      foreach (syntaxModule in syntaxModules)
        converter.CreateSymbols(syntaxModule);
      converter.ResolveBaseRules();
      converter.RewriteRules();

      project.ErrorCount == errorCount
    }

    private MakeEmitters(project : CompilingNitraProject) : void
    {
      def environment = project.Environment;

      def syntaxModuleEmitters = Manager.GetListUserData.[SyntaxModuleEmitter]();
      foreach (syntaxModule in environment.Root.GetDescendants.[SyntaxModuleSymbol]())
      {
        | ExternalSyntaxModuleSymbol => ()
        | _ => syntaxModuleEmitters.Add(SyntaxModuleEmitter(environment, syntaxModule));
      }

      def astEmitters = Manager.GetListUserData.[AstEmitter]();
      def astEmitterParents = SCG.Dictionary();
      foreach (extendAst in environment.Root.GetDescendants.[ExtendAstSymbol]())
      {
        | ExternalExtendAstSymbol => ()
        | _ =>
          def emitter = ExtendAstEmitter(environment, extendAst);
          astEmitterParents.Add(extendAst, emitter);
          astEmitters.Add(emitter)
      }

      def visitedAstSymbols = SCG.HashSet();
      def makeAstEmitter(astSymbol : AstSymbol) : void
      {
        when (visitedAstSymbols.Add(astSymbol))
        {
          foreach (baseAst in astSymbol.BaseAsts)
            makeAstEmitter(baseAst);

          match (astSymbol)
          {
            | ExternalAbstractAstSymbol   => ()
            | AbstractAstSymbol      as s => astEmitters.Add(AbstractAstEmitter(environment, s))
            | ExternalSimpleAstSymbol     => ()
            | SimpleAstSymbol        as s => astEmitters.Add(SimpleAstEmitter(environment, s))
            | ExternalExtensionAstSymbol  => ()
            | ExtensionAstSymbol     as s =>
              def parent  = astEmitterParents[s.BaseAst.GetValue(project)];
              def emitter = ExtensionAstEmitter(parent, s);
              astEmitters.Add(emitter);

            | ExternalExtensibleAstSymbol => ()
            | ExtensibleAstSymbol    as s =>
              def emitter = ExtensibleAstEmitter(environment, s);
              astEmitterParents.Add(s, emitter);
              astEmitters.Add(emitter);

            | _ => ()
          }
        }
      }

      foreach (astSymbol in environment.Root.GetDescendants.[AstSymbol]())
        makeAstEmitter(astSymbol);

      def externalSymbolEmitters = Manager.GetListUserData.[ExternalSymbolEmitter]();
      def serializationEmitter   = Manager.CreateBinarySerializationEmitter(NitraBinarySerializationMessageEmitter(), NitraSymbolRefBinarySerializationEmitterExtension(environment));
      foreach (data in Manager.GetListUserData.[ExportableSymbolData]())
        externalSymbolEmitters.Add(ExternalSymbolEmitter(environment, data.TypeBuilder, data.ExportableSymbolType, serializationEmitter));
      ResetListUserData.[ExportableSymbolData]();

      def chemicalSymbolEmitters = Manager.GetListUserData.[ChemicalSymbolEmitter]();
      foreach (data in Manager.GetListUserData.[ChemicalSymbolData]())
        chemicalSymbolEmitters.Add(ChemicalSymbolEmitter(environment, data.TypeBuilder, data.ChemicalSymbolType));
      ResetListUserData.[ChemicalSymbolData]();
    }

    private DefineParseTree() : void
    {
      def syntaxModuleEmitters = Manager.GetListUserData.[SyntaxModuleEmitter]();
      foreach (emitter in syntaxModuleEmitters)
        emitter.DefineSimpleParseTreeTypes();
      foreach (emitter in syntaxModuleEmitters)
        emitter.DefineExtensionParseTreeTypes();
      foreach (emitter in syntaxModuleEmitters)
        emitter.DefineParseTreeMembers();
    }

    private DefineParsers() : void
    {
      def syntaxModuleEmitters = Manager.GetListUserData.[SyntaxModuleEmitter]();
      foreach (emitter in syntaxModuleEmitters)
        emitter.DefineParserTypes();
      foreach (emitter in syntaxModuleEmitters)
        emitter.DefineParserMethods();
    }

    private DefineSymbols() : void
    {
      def externalSymbolEmitters = Manager.GetListUserData.[ExternalSymbolEmitter]();
      foreach (emitter in externalSymbolEmitters)
        emitter.DefineMembers();

      def chemicalSymbolEmitters = Manager.GetListUserData.[ChemicalSymbolEmitter]();
      foreach (emitter in chemicalSymbolEmitters)
        emitter.DefineMembers();
    }

    private DefineAsts() : void
    {
      def astEmitters = Manager.GetListUserData.[AstEmitter]();
      foreach (emitter in astEmitters)
        emitter.DefineTypes();
      foreach (emitter in astEmitters)
        emitter.DefineMembers();
    }

    private EmitSymbols() : void
    {
      def externalSymbolEmitters = Manager.GetListUserData.[ExternalSymbolEmitter]();
      try
      {
        foreach (emitter in externalSymbolEmitters)
          emitter.Emit();
        when (!Manager.IsIntelliSenseMode && externalSymbolEmitters.Count > 0)
        {
          def buffer = SCG.List();
          foreach (emitter in externalSymbolEmitters)
            buffer.Add(<[ typeof($(emitter.TargetSymbolType.GetMemType() : typed)) ]>);
          Manager.Hierarchy.AddAssemblyAttribute(Manager.CoreEnv, <[ Nitra.Serialization.ExportableSymbolsAttribute(array[..$buffer]) ]>);
        }
        Manager.FinalizeBinarySerializationEmitter();
      }
      finally ResetListUserData.[ExternalSymbolEmitter]();

      def chemicalSymbolEmitters = Manager.GetListUserData.[ChemicalSymbolEmitter]();
      try
        foreach (emitter in chemicalSymbolEmitters)
          emitter.Emit();
      finally ResetListUserData.[ChemicalSymbolEmitter]();
    }

    private EmitSyntaxModules() : void
    {
      def syntaxModuleEmitters = Manager.GetListUserData.[SyntaxModuleEmitter]();
      try
      {
        foreach (emitter in syntaxModuleEmitters)
          emitter.Emit();
        when (!Manager.IsIntelliSenseMode && syntaxModuleEmitters.Count > 0)
        {
          def buffer = SCG.List();
          foreach (emitter in syntaxModuleEmitters)
            buffer.Add(<[ typeof($(emitter.GrammarTB.GetMemType() : typed)) ]>);
          Manager.Hierarchy.AddAssemblyAttribute(Manager.CoreEnv, <[ Nitra.GrammarsAttribute(array[..$(buffer.ToNList())]) ]>);
        }
      }
      finally ResetListUserData.[SyntaxModuleEmitter]();
    }

    private EmitAsts() : void
    {
      def astEmitters = Manager.GetListUserData.[AstEmitter]();
      try
        foreach (emitter in astEmitters)
          emitter.Emit();
      finally ResetListUserData.[AstEmitter]();
    }

    private ReadMetadata(project : CompilingNitraProject) : bool
    {
      def errorCount = project.ErrorCount;

      using (loader = ExternalSymbolLoader())
      {
        loader.Registry.Register(typeof(SyntaxModuleSymbol).Assembly);
        def externalSymbolPropertyReader = NitraExternalSymbolPropertyReader();

        foreach (reference in Manager.ReferencedAssemblies)
        {
          def assembly       = reference.Assembly;
          def metadataStream = assembly.GetManifestResourceStream("Nitra.Metadata");
          when (metadataStream != null)
          {
            def assemblyId = project.GetAssemblyId(assembly.GetName());
            def location   = project.MakeFakeNitraLocation(Nemerle.Compiler.Location(assembly.Location, 1, 1, 1, 1));
            def reader     = MetadataReader(metadataStream, loader.Registry, externalSymbolPropertyReader, project, assemblyId, location);
            loader.AddSource(reader, location);
          }
        }

        loader.Load();
      }

      project.ErrorCount == errorCount
    }

    private WriteMetadata(project : Project) : void
    {
      unless (Manager.IsIntelliSenseMode)
      {
        def metadataFileName = Manager.Options.OutputFileName + ".nitrametadata";
        using(symbolWriter = MetadataWriter(FileStream(metadataFileName, FileMode.Create, FileAccess.Write), project))
          symbolWriter.WriteSymbols();

        Manager.Options.EmbeddedResources ::= metadataFileName + ",Nitra.Metadata";
      }
    }

    public BeforeTypedMembers() : void
    {
      when (IsListUserDataPresent.[G.Start]())
        try
        {
          def project  = UpdateCurrentProjectContext();
          def fileParseTrees = Manager.GetListUserData.[G.Start]();

          unless (ReadMetadata(project))
            throw Recovery();
          unless (LoadParsedSymbols(project, fileParseTrees))
            throw Recovery();
          unless (TypeSyntaxModules(project, fileParseTrees))
            throw Recovery();
          unless (CheckRestrictedNames(project))
            throw Recovery();
          unless (CreateSplicableSyntaxModules(project))
            throw Recovery();

          MakeEmitters(project);

          DefineAsts();
          DefineParseTree();
          DefineParsers();

          WriteMetadata(project);
        }
        finally ResetListUserData.[G.Start]();
    }

    public WithTypedMembers() : void
    {
      DefineSymbols();

      EmitSymbols();
      EmitSyntaxModules();
      EmitAsts();
    }

    public SubscribeSourceChangedWithCounter(manager : ManagerClass, fileIndex : int, handler : ISource * int -> void) : void
    {
      mutable counter = 0;
      manager.SubscribeSourceChanged(fileIndex, source =>
      {
        counter += 1;
        handler(source, counter);
      });
    }
  }
}
