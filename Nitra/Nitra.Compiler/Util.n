using Nitra.Compiler;
using Nitra.Model;
using Nitra.Typing;

using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;

using System;
using System.Collections.Generic;
using System.Linq;

using G = NitraSyntaxAst;
using N = Nemerle.Compiler;
using SCG = System.Collections.Generic;

namespace Nitra
{
  /// <summary>
  /// Description of Util.
  /// </summary>
  public module Util
  {
    public Once[T](this source : IEnumerable[Ast]) : option[T]
      where T: Ast
    {
      def source2 = source.OfType.[T]().NToList();

      match (source2)
      {
        [x]           => Some(x)
        | null | []   => None()
        | x :: y :: _ =>
          N.Message.Error(y.Location.ToNemerleLocation(),
            "Acceptably only one entry.");
          Some(x)
      }
    }

    //TODO: fix the bug in the compiler
    //public Once[T](this source : list[Nitra.Ast]) : ValueOption[T]
    //  where T: Nitra.Ast
    //{
    //  def source2 = source.OfType.[T]();
    //
    //  match (source2)
    //  {
    //    [x]           => VSome(x)
    //    | null | []   => VNone()
    //    | x :: y :: _ =>
    //      Nemerle.Compiler.Message.Error((y : Nitra.Ast).Location.ToNemerleLocation(),
    //      "Acceptably only one entry."); VSome(x)
    //  }
    //}

    public Flatten[T](this src : Seq[option[T]]) : list[T]
    {
      def dst = List();
      foreach (Some(item) in src)
        dst.Add(item);
      dst.NToList();
    }

    public Flatten[T](this src : list[option[T]]) : list[T]
    {
      def dst = List(src.Length);
      foreach (Some(item) in src)
        dst.Add(item);
      dst.NToList();
    }

    public GraphWalk[Key, Value]( start   : IEnumerable[Key]
                                , calc    : Key * (Key -> Value) -> Value
                                , onCycle : Key -> Value)
                                : Map[Key, Value]
    {
      mutable result = Map();
      def visited = Hashtable();
      def getValue(key)
      {
        if (result.Contains(key))
          result.Get(key);
        else
        {
          def value = if (visited.ContainsKey(key))
            onCycle(key);
          else
          {
            visited.Add(key, 0);
            def value = calc(key, getValue);
            visited.Remove(key);
            value;
          }
          result = result.Replace(key, value);
          value;
        }
      }
      foreach (key in start)
        _ = getValue(key);
      result;
    }

    public ToNemerleLocation(this loc : Nitra.Location) : N.Location
    {
      def (startLine, startCol) = loc.StartLineColumn;
      def (endLine, endCol) = loc.EndLineColumn;
      N.Location(loc.Source.FileIndex, startLine, startCol, endLine, endCol)
    }

    public GetNestedSubtypes(this ti : N.LibraryReference.ExternalTypeInfo) : list[N.LibraryReference.ExternalTypeInfo]//TODO: Move to Nemerle.Compiler
    {
      def nestedTypes = ti.GetNestedTypes();
      nestedTypes.Where(nested => nested.DeclaringType?.Equals(ti)).OfType.[N.LibraryReference.ExternalTypeInfo]().NToList();
    }

    public IsVoidDeep(this ruleType : RuleType) : bool
    {
      | Void => true
      | Option(t) | List(t) => IsVoidDeep(t)
      | Tuple(types) => types.ForAll(IsVoidDeep)
      | Chars | Ref => false
    }

    public DefineConditional(this tb : N.TypeBuilder, classMember : ClassMember) : void
    {
//#if PARSER_DEBUG
      _ = tb.DefineWithSource(classMember)
//#else
//      tb.Define(classMember)
//#endif
    }

    public GetListUserData[T](this manager : N.ManagerClass) : SCG.List[T]
    {
      def key = typeof(SCG.List[T]);
      mutable syntaxModules = manager.UserData[key] :> SCG.List[T];
      when (syntaxModules == null)
      {
        syntaxModules = SCG.List();
        manager.UserData[key] = syntaxModules;
      }
      syntaxModules
    }

    public GetRuleMethodSignature(this symbol : RuleMethod.RuleMethodSymbol) : RuleMethod.RuleMethodSignature
    {
      | symbol is RuleMethod.ISignature => symbol.Signature
      | _ => assert(false)
    }

    public GetRuleMethodIsCached(this symbol : RuleMethod.RuleMethodSymbol) : bool
    {
      | symbol is RuleMethod.ICached => symbol.IsCached
      | _ => assert(false)
    }

    public MakeVariableName(this sequence : SequenceCompileInfo, prefix : string) : string
    {
      def suffix = match (sequence)
      {
        | SequenceCompileInfo.Ast(Symbol = symbol) => symbol.Name
        | _                                  => sequence.SrcRule.FieldName
      };
      prefix + sequence.Level + "_" + suffix + "_" + Nemerle.Compiler.ManagerClass.Instance.GetNewId()
    }

    public MakeVariableName(this subrule : SubruleCompileInfo, prefix : string) : string
    {
      prefix + subrule.Level + "_" + subrule.SrcRule.FieldName + "_" + Nemerle.Compiler.ManagerClass.Instance.GetNewId()
    }

    public CreateConstant(tb : TypeBuilder, name : string, value : int) : string
    {
      assert2(tb != null);
      def decl   = <[ decl: private static $(name : global) = $(value) ]>;
      _ = tb.Define(decl);
      name
    }
  }
}
