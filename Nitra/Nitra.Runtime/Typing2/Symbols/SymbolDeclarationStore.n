using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Text;

namespace Nitra.Runtime.Binding
{
  public struct SymbolDeclarationStore[TDeclaration]
    where TDeclaration : Declaration
  {
    public this(name : string)
    {
      this(StringIndex.GetId(name), null)
    }

    private this(nameId : int, store : object)
    {
      _nameId = nameId;
      _store  = store;
    }

    private _nameId : int;
    private _store  : object;

    public IsNameValid : bool   { get { _nameId > 0 } }
    public Name        : string { get { if (_nameId > 0) StringIndex.GetText(_nameId) else "<invalid name>" } }
    public InitialName : string { get { if (_nameId < 0) StringIndex.GetText(~_nameId) else Name } }

    public AddDeclaration(newDeclaration : Declaration) : SymbolDeclarationStore[TDeclaration]
    {
      assert(newDeclaration : object != null);

      def newNameId =
      {
        def newId = newDeclaration.Name.Id;

        if (_nameId == newId) _nameId
        else if (_nameId == 0) newId
        else if (_nameId < 0) _nameId
        else ~_nameId
      };

      match (newDeclaration, _store)
      {
        | (newDecl is TDeclaration, declarations is list[TDeclaration]) => SymbolDeclarationStore(newNameId, newDecl :: declarations)
        | (newDecl is TDeclaration,                                  _) => SymbolDeclarationStore(newNameId, [newDecl])
        | (newDecl,                                               null) => SymbolDeclarationStore(newNameId, newDecl)
        | _ => assert(false, $"Unable to add declaration '$newDeclaration' to symbol declaration store");
      }
    }

    public GetDeclarations() : Seq[TDeclaration]
    {
      match (_store)
      {
        | lst is list[TDeclaration] => lst
        | _ => []
      }
    }

    public GetDeclarationsUntyped() : Seq[Declaration]
    {
      match (_store)
      {
        | lst is list[TDeclaration] => lst
        | d   is Declaration        => [d]
        | _                         => []
      }
    }

    public override ToString() : string
    {
      def buffer = StringBuilder().Append(Name);
      unless (IsNameValid)
        _ = buffer.Append(" (").Append(InitialName).Append(')');
      _ = buffer.Append(": ");
      if (_store == null)
        _ = buffer.Append("<no declarations>");
      else
        _ = buffer.Append(_store);
      buffer.ToString();
    }
  }
}
