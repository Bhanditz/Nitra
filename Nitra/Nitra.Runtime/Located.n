using Nitra.Declarations;
using Nitra.ProjectSystem;

using System;
using System.Runtime;
using System.Runtime.ConstrainedExecution;
using System.Text.RegularExpressions;

using Nemerle;
using Nemerle.Assertions;
using Nemerle.Utility;
using Nemerle.Text;

using SCG = System.Collections.Generic;

namespace Nitra
{
  public abstract class Located : ILocated
  {
    public static @:(located : Located) : Location
    {
      located.Location
    }

    public abstract Location : Location { get; }
    public virtual  File     : File     { get { Location.Source.File } }
    public virtual  Span     : NSpan    { get { Location.Span } }

    public GetText() : string { Location.GetText() }

    public GetText(token : NSpan) : string
    {
      Location.Source.Text.Substring(token.StartPos, token.EndPos - token.StartPos)
    }

    public FirstChar(token : NSpan) : char
    {
      Location.Source.Text[token.StartPos]
    }

    [TargetedPatchingOptOut(AssemblyConstants.TargetedPatchingOptOutText)]
    [ReliabilityContract(Consistency.WillNotCorruptState, Cer.Success)]
    public IntersectsWith(start : int, end : int) : bool { this.Location.IntersectsWith(start, end) }
    [TargetedPatchingOptOut(AssemblyConstants.TargetedPatchingOptOutText)]
    [ReliabilityContract(Consistency.WillNotCorruptState, Cer.Success)]
    public IntersectsWith(other : NSpan)         : bool { this.Location.IntersectsWith(other) }
    [TargetedPatchingOptOut(AssemblyConstants.TargetedPatchingOptOutText)]
    [ReliabilityContract(Consistency.WillNotCorruptState, Cer.Success)]
    public Intersection(start : int, end : int)   : ValueOption[Location]
    {
      this.Location.Intersection(start, end)
    }
  }
}
