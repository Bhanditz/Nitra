using Nitra.Declarations;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Runtime.Binding
{
  [Record]
  public abstract class Symbol2
  {
    public Name : IReference { get; }
    public abstract FullName : string { get; }
    public abstract Kind : string { get; }
    [RecordIgnore] public Declarations : list [IDeclaration] { get; set; default []; }
    
    public virtual IsResolved : bool { get { false } }
    
    public virtual TryBind(reference : IReference) : Symbol2
    {
      UnresolvedSymbol(reference)
    }
    
    public override ToString() : string { Name?.Text + " (" + Kind + ")" }
  }

  [Record]
  public class UnresolvedSymbol : Symbol2
  {
    public override FullName : string { get { Name.Text } }
    public override Kind : string { get { "unresolved" } }
  }
  
  [Record]
  public abstract class HierarchicalBaseSymbol : Symbol2
  {
    public Parent : HierarchicalSymbol { get; }

    public override FullName : string
    {
      get
      {
        def loop(node : this, sb : Text.StringBuilder) : void
        {
          when (node.Parent != null)
          {
            loop(node.Parent, sb);
            when (node.Parent.Parent != null)
              _ = sb.Append('.');
            _ = sb.Append(node.Name);
          }
        }
        
        def sb = Text.StringBuilder();
        loop(this, sb);
        sb.ToString()
      }
    }
  }

    [Record]
  public class AmbiguousSymbol : Symbol2
  {
    public override FullName : string { get { Name.Text } }
    public override Kind : string { get { "ambiguous symbol" } }
    
    public Ambiguous : list[Symbol2] { get; set; }
   
    public override ToString() : string { $<#$Kind: ..$(Ambiguous; " "; _.FullName)#>  }
  }

  [Record]
  public class AmbiguousHierarchicalSymbol : HierarchicalBaseSymbol
  {
    public override Kind : string { get { "ambiguous symbol" } }
    
    public Ambiguous : list[HierarchicalSymbol] { get; set; }
   
    public override ToString() : string { $<#$Kind: ..$(Ambiguous; " "; _.FullName)#>  }
  }
  
  [Record]
  public abstract class HierarchicalSymbol : HierarchicalBaseSymbol
  {
    [RecordIgnore] public Scope : Scope.Table { get; set; default Scope.Table(); }

    
    public DefineNestedSymbol(symbol : HierarchicalSymbol) : void
    {
      mutable old;
      when (Scope.NameTable.TryGetValue(symbol.Name.Id, out old))
      {
        AstContext.CompilerMessages.Error(symbol.Name, 
          $"The $(Kind) '$(FullName)' already contains a definition for '$(symbol.Name)'");

        if (old is AmbiguousHierarchicalSymbol as amb)
          amb.Ambiguous ::= symbol;
        else
          Scope.NameTable[Name.Id] = AmbiguousHierarchicalSymbol(old.Name, symbol.Parent, [old :> HierarchicalSymbol, symbol]);

        return;
      }
          
      Scope.NameTable.Add(symbol.Name.Id, symbol);
    }

    public override IsResolved : bool { get { true } }
    
    public override TryBind(reference : IReference) : Symbol2
    {
      this.Scope.TryBind(reference)
    }
  }

  public variant Scope
  {
    | Table
      {
        public NameTable : Hashtable[int, Symbol2] { get; }
        
        public this() { NameTable = Hashtable(); }
        
        public DefineSymbol(symbol : Symbol2) : void
        {
          mutable old;
          when (this.NameTable.TryGetValue(symbol.Name.Id, out old))
          {
            def cm = AstContext.CompilerMessages;
            using(nested = cm.RootError(symbol.Name, $"The current scope already contains a definition for '$(symbol.Name)'."))
              nested.Hint(old.Name, "previous definition.");

            if (old is AmbiguousSymbol as amb)
              amb.Ambiguous ::= symbol;
            else
              this.NameTable[symbol.Name.Id] = AmbiguousSymbol(old.Name, [old, symbol]);

            return;
          }
    
          this.NameTable.Add(symbol.Name.Id, symbol);
        }

        public GetOrDefineSymbol(symbol : Symbol2) : Symbol2
        {
          mutable old;
          when (NameTable.TryGetValue(symbol.Name.Id, out old))
            return old;
          
          NameTable.Add(symbol.Name.Id, symbol);
          symbol
        }
        
        public TryGetSymbol(name : IReference) : Symbol2
        {
          mutable old;
          when (NameTable.TryGetValue(name.Id, out old))
            return old;
          
          null
        }
        
        public override ToString() : string
        {
          $<#Scope.Table: { ..$(NameTable.Keys; ", "; StringIndex.GetText) }#>
        }
      }
    | Union  { public Scopes : list[Scope] { get; } }
    | Hide   { public Scope  : Scope       { get; } public Hidden : Scope { get; } }
    | Filter { public Scope  : Scope       { get; } public Predicate : Symbol2 -> bool { get; } }
    | Nil
    
    /// Если не может связать возвращает AmbiguousSymbol или UnresolvedSymbol.
    public TryBind(reference : IReference) : Symbol2
    {
      def result = Bind(reference);
      match (result.Count)
      {
        | 0 => UnresolvedSymbol(reference)
        | 1 => result[0]
        | _ => AmbiguousSymbol(reference, result.NToList())
      }
    }
    
    public Bind(reference : IReference) : List[Symbol2]
    {
      def results = List(2);
      Bind(reference, results);
      results
    }
    
    private Bind(reference : IReference, results : List[Symbol2]) : void
    {
      match (this)
      {
        | Table as s =>
          mutable result;
          when (s.NameTable.TryGetValue(reference.Id, out result))
            results.Add(result);
          
        | Union as s => 
          foreach (scope in s.Scopes)
            scope.Bind(reference, results);

        | Hide as s => 
          def binded = results.Count;
          s.Scope.Bind(reference, results);
          when (binded == results.Count)
            s.Hidden.Bind(reference, results);
        
        | Filter as s =>
          def notFilteredResults = List();
          s.Scope.Bind(reference, notFilteredResults);
          foreach (sym in notFilteredResults)
            when (s.Predicate(sym))
              results.Add(sym);
          
        | Nil => ()
      }
    }
  }
}
