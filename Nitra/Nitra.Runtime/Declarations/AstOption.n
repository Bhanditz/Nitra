using Nitra.Runtime.Binding;
using Nitra.ProjectSystem;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Declarations
{
  public interface IAstOption[+T] : IAst
    where T : IAst
  {
    HasValue : bool  { get; }
    Value    : T     { get; }

    public static CreateSome(parent : IAst, span : NSpan, value : IAst) : this
    {
      AstOption.[T](parent, span, (value : object) :> T)
    }

    public static CreateNone(parent : IAst, span : NSpan) : this
    {
      AstOption.[T](parent, span)
    }

    public static CreateAmbiguous(ambiguities : array[IAstOption[IAst]]) : this
    {
      AmbiguousAstOption.[T]((ambiguities : object) :> array[IAstOption[T]])
    }
  }

  public class AstOption[T] : IAstOption[T]
    where T : IAst
  {
    protected _value     : T;

    public Parent      : IAst  { get; }
    public File        : File  { get { Parent.File } }
    public IsAmbiguous : bool  { get { false } }
    public Span        : NSpan { get; }
    public HasValue    : bool  { get; }
    public Value       : T
    {
      get
      {
        unless (HasValue)
          throw InvalidOperationException("Value not set");
        _value;
      }
    }

    public this (parent : IAst, span : NSpan)
    {
      Parent = parent;
      Span   = span;
    }

    public this (parent : IAst, span : NSpan, value : T)
    {
      this(parent, span);
      _value   = value;
      HasValue = true;
    }

    public virtual EvalProperties(context : DependentPropertyEvalContext) : void
    {
      when (HasValue)
        _value.EvalProperties(context);
    }

    public virtual IsPropertyEvaluated(_ : int) : bool { false }

    public ToXaml() : string
    {
      if (HasValue)
        _value.ToString()
      else
        "<Span Foreground = 'gray'>None</Span>"
    }
  }

  public class AmbiguousAstOption[T] : IAstOption[T], IAmbiguousAst
    where T : IAst
  {
    public this(ambiguities : array[IAstOption[T]])
    {
      Ambiguities = ambiguities
    }

    public Ambiguities       : array[IAstOption[T]] { get; }
    public Parent            : IAst               { get { Ambiguities[0].Parent } }
    public File              : File               { get { Ambiguities[0].File } }
    public IsAmbiguous       : bool               { get { true } }
    public Span              : NSpan              { get { Ambiguities[0].Span } }
    public HasValue          : bool               { get { Ambiguities[0].HasValue } }
    public Value             : T                  { get { Ambiguities[0].Value } }

    private AmbiguitiesImpl : array[IAst] implements IAmbiguousAst.Ambiguities { get { Ambiguities :> array[IAst] } }

    public EvalProperties(context : DependentPropertyEvalContext) : void
    {
      foreach (ambiguity in Ambiguities)
        ambiguity.EvalProperties(context);
    }

    public IsPropertyEvaluated(index : int) : bool
    {
      Ambiguities[0].IsPropertyEvaluated(index)
    }

    public ToXaml() : string { "AmbiguousOption Count: " + Ambiguities.Length }
  }
}
