using Nitra.Internal;
using Nitra.Runtime.Binding;
using Nitra.ProjectSystem;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Declarations
{
  public interface IAstOption[+T] : IAst
    where T : IAst
  {
    HasValue : bool  { get; }
    Value    : T     { get; }
  }

  public class AstOption[T] : IAstOption[T]
    where T : IAst
  {
    private mutable _parent : IAst;
    protected _value     : T;

    public Parent      : IAst  { get { _parent } }
    public File        : File  { get { Parent.File } }
    public IsAmbiguous : bool  { get { false } }
    public Span        : NSpan { get; }
    public HasValue    : bool  { get; }
    public Value       : T
    {
      get
      {
        unless (HasValue)
          throw InvalidOperationException("Value not set");
        _value;
      }
    }

    public this(span : NSpan)
    {
      Span = span;
    }

    public this(span : NSpan, value : T)
    {
      this(span);
      _value   = value;
      HasValue = true;
    }

    public virtual EvalProperties(context : DependentPropertyEvalContext) : void
    {
      when (HasValue)
        _value.EvalProperties(context);
    }

    public virtual IsPropertyEvaluated(_ : int) : bool { false }

    public SetParent(newParent : IAst) : void
    {
      assert(newParent != null);
      assert(_parent == null);

      _parent = newParent;
      when (HasValue)
        _value.SetParent(newParent);
    }

    public ToXaml() : string
    {
      if (HasValue)
        _value.ToString()
      else
        "<Span Foreground = 'gray'>None</Span>"
    }
  }

  public class AmbiguousAstOption[T] : IAstOption[T], IAmbiguousAst
    where T : IAst
  {
    public this(ambiguities : array[IAstOption[T]])
    {
      Ambiguities = ambiguities
    }

    public Ambiguities       : array[IAstOption[T]] { get; }
    public Parent            : IAst               { get { Ambiguities[0].Parent } }
    public File              : File               { get { Ambiguities[0].File } }
    public IsAmbiguous       : bool               { get { true } }
    public Span              : NSpan              { get { Ambiguities[0].Span } }
    public HasValue          : bool               { get { Ambiguities[0].HasValue } }
    public Value             : T                  { get { Ambiguities[0].Value } }

    private AmbiguitiesImpl : array[IAst] implements IAmbiguousAst.Ambiguities { get { Ambiguities :> array[IAst] } }

    public EvalProperties(context : DependentPropertyEvalContext) : void
    {
      AstUtils.EvalAmbiguitiesProperties(context, this)
    }

    public IsPropertyEvaluated(index : int) : bool
    {
      Ambiguities[0].IsPropertyEvaluated(index)
    }

    public SetParent(newParent : IAst) : void
    {
      AstUtils.SetAmbiguitiesParent(newParent, this)
    }

    public ToXaml() : string { "AmbiguousOption Count: " + Ambiguities.Length }
  }
}
