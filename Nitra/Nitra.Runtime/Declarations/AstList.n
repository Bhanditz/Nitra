using Nitra.Internal;
using Nitra.ProjectSystem;
using Nitra.Runtime.Binding;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Diagnostics;
using System.Linq;

namespace Nitra.Declarations
{
  // TODO: Добавить возможность различать + и *
  public interface IAstList[+T] : IEnumerable[T], IAst
    where T : IAst
  {
    Count             : int   { get; }
    Item[index : int] : T     { get; }
  }

  [DebuggerDisplay("Count = {Count}")]
  public class AstList[T] : IAstList[T]
    where T : IAst
  {
    public this(span : NSpan, params items : array[T])
    {
      Span   = span;
      _items = items;
    }

    private mutable _parent : IAst;

    [DebuggerBrowsable(DebuggerBrowsableState.RootHidden)]
    protected _items           : array[T];

    public Parent            : IAst  { get { _parent } }
    public File              : File  { get { Parent.File } }
    public IsAmbiguous       : bool  { get { false } }
    public Span              : NSpan { get; }
    public Count             : int   { get { _items.Length } }
    public Item[index : int] : T     { get { _items[index] } }

    public GetEnumerator() : IEnumerator[T]
    {
      (_items : IEnumerable[T]).GetEnumerator()
    }

    public virtual EvalProperties(context : DependentPropertyEvalContext) : void
    {
      foreach (item in _items)
        item.EvalProperties(context);
    }

    public virtual IsPropertyEvaluated(_ : int) : bool { false }

    public SetParent(newParent : IAst) : void
    {
      assert(newParent != null);
      assert(_parent == null);

      _parent = newParent;
      foreach (item in _items)
        item.SetParent(newParent);
    }

    public ToXaml() : string { "List Count: " + _items.Length }
  }

  public class AmbiguousAstList[T] : IAstList[T], IAmbiguousAst
    where T : IAst
  {
    public this(ambiguities : array[IAstList[T]])
    {
      Ambiguities = ambiguities
    }

    public Ambiguities       : array[IAstList[T]] { get; }
    public Parent            : IAst               { get { Ambiguities[0].Parent } }
    public File              : File               { get { Ambiguities[0].File } }
    public IsAmbiguous       : bool               { get { true } }
    public Span              : NSpan              { get { Ambiguities[0].Span } }
    public Count             : int                { get { Ambiguities[0].Count } }
    public Item[index : int] : T                  { get { Ambiguities[0][index] } }

    private AmbiguitiesImpl : array[IAst] implements IAmbiguousAst.Ambiguities { get { Ambiguities :> array[IAst] } }

    public GetEnumerator() : IEnumerator[T]
    {
      (Ambiguities[0] : IEnumerable[T]).GetEnumerator()
    }

    public virtual EvalProperties(context : DependentPropertyEvalContext) : void
    {
      AstUtils.EvalAmbiguitiesProperties(context, this)
    }

    public IsPropertyEvaluated(index : int) : bool
    {
      Ambiguities[0].IsPropertyEvaluated(index)
    }

    public SetParent(newParent : IAst) : void
    {
      AstUtils.SetAmbiguitiesParent(newParent, this)
    }

    public ToXaml() : string { "<Span Foreground='Red'>AmbiguousList Count: " + Ambiguities.Length + "</Span>" }
  }
}
