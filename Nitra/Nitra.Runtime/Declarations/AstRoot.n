using Nitra.ProjectSystem;
using Nitra.Runtime.Binding;
using Nitra.Internal;

using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Declarations
{
  public sealed class AstRoot[TContent] : IAst
    where TContent : IAst
  {
    public File        : File             { get; }
    public Span        : NSpan            { get; }
    public IsAmbiguous : bool             { get false }
    public IsMissing   : bool             { get false }
    public Content     : TContent         { get; }

    private this(file : File, span : NSpan, content : TContent)
    {
      this.File    = file;
      this.Span    = span;
      this.Content = content;
    }

    EvalProperties(context : DependentPropertyEvalContext) : void implements IDependentPropertyHost.EvalProperties
    {
      EvalProperties(context, DebugCompilerMessages())
    }

    public EvalProperties(compilerMessages : ICompilerMessages) : void
    {
      EvalProperties(DependentPropertyEvalContext(), compilerMessages)
    }

    public EvalProperties([NotNull] context : DependentPropertyEvalContext, [NotNull] compilerMessages : ICompilerMessages) : void
    {
      def oldState = AstContext.State;
      try
      {
        AstContext.State = (File, compilerMessages);

        mutable calculatedCount;
        mutable passCount = 0;
        do
        {
          context.DeferredCount = 0;
          calculatedCount = context.CalculatedCount;
          Content.EvalProperties(context);
          passCount++;
        }
        while (context.DeferredCount > 0 && calculatedCount != context.CalculatedCount);
      }
      finally AstContext.State = oldState
    }

    public IsPropertyEvaluated(index : int) : bool
    {
      _ = index;
      throw NotSupportedException();
    }

    public static Create(parseTree : IMappedParseTree[TContent]) : AstRoot[TContent]
    {
      Create(parseTree, DebugCompilerMessages())
    }

    public static Create([NotNull] parseTree : IMappedParseTree[TContent], [NotNull] compilerMessages : ICompilerMessages) : AstRoot[TContent]
    {
      def oldState = AstContext.State;
      try
      {
        def file         = (parseTree :> Located).File;
        AstContext.State = (file, compilerMessages);
        AstRoot(file, parseTree.Span, parseTree.GetAst())
      }
      finally AstContext.State = oldState
    }

    public ToXaml() : string { "<Bold>Root</Bold>" }
  }
}
