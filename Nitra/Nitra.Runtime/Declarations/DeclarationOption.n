using Nitra.Runtime.Binding;
using Nitra.ProjectSystem;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Declarations
{
  [Record]
  public variant DeclarationOption[T] : IDeclaration
    where T : IDeclaration
  {
    public          Parent      : IDeclaration { get; }
    public          Name        : Reference    { get { null } }
    public          File        : File         { get { Parent.File } }
    public          IsAmbiguous : bool         { get { false } }
    public          Span        : NSpan        { get; }
    public abstract HasValue    : bool         { get; }
    public abstract Value       : T            { get; }

    public abstract EvalProperties(context : EvalDeclarationPropertiesContext) : void;

    public IsPropertyEvaluated(index : int) : bool
    {
      _ = index;
      false
    }

    | Some
      {
        value : T;
        public override HasValue : bool { get { true } }
        public override Value    : T    { get { value } }

        public override EvalProperties(context : EvalDeclarationPropertiesContext) : void
        {
          value.EvalProperties(context)
        }
      }

    | None
      {
        public override HasValue : bool { get { false } }
        public override Value    : T    { get { throw NotSupportedException("DeclarationOption is None") } }

        public override EvalProperties(context : EvalDeclarationPropertiesContext) : void
        {
          _ = context
        }
      }
  }
}
