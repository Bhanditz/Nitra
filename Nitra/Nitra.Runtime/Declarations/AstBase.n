using Nitra.ProjectSystem;
using Nitra.Runtime.Binding;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Declarations
{
  public abstract class AstBase : IAst
  {
    protected this() { }

    public          File        : File      { get; protected set; }
    public          Span        : NSpan     { get; protected set; }
    public virtual  IsAmbiguous : bool      { get { false } }
    public virtual  IsMissing   : bool      { get { false } }

    public virtual EvalProperties(context : DependentPropertyEvalContext) : void
    {
      _ = context;
    }

    public virtual IsPropertyEvaluated(index : int) : bool
    {
      _ = index;
      throw InvalidOperationException("IsPropertyEvaluated is not defined")
    }

    public virtual ResetProperties() : void
    {
    }

    public virtual IsAllPropertiesEvaluated : bool { get { true } }

    public virtual ToXaml() : string
    {
      def type = GetType();
      def color = if (this.IsAmbiguous) "red" else "gray";
      def str = if (type.IsNested) type.DeclaringType.Name + "." + type.Name else type.Name;
      def typeStr = $"<Span Foreground = '$color'>$str</Span>";

      if (this : object is IDeclaration as decl)
      {
        def text = decl.Name.Text;
        if (!string.IsNullOrWhiteSpace(text))
          typeStr + ": " + text
        else
          typeStr
      }
      else
        typeStr
    }

    public virtual Apply(visitor : IAstVisitor) : void { _ = visitor }
  }
}
