using Nitra.Declarations;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Reflection;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Internal
{
  public module AstUtils
  {
    public GetAstAmbiguities[TParseTree, TParent, TAst](parent : TParent, ambiguities : array[TParseTree]) : array[TAst]
      where TParseTree   : IMappedParseTree[TParent, TAst]
      where TParent      : IAst
      where TAst         : IAst
    {
      def result = array(ambiguities.Length);
      foreach (node in ambiguities with index)
        result[index] = node.GetAst(parent);
      result
    }

    public GetAstOption[TParseTree, TParent, TOption, TAst](parent : TParent, opt : OptionParseTree[TParseTree]) : TOption
      where TParseTree   : IMappedParseTree[TParent, TAst]
      where TParent      : IAst
      where TOption      : IAstOption[TAst]
      where TAst         : IAst
    {
      if (opt.IsAmbiguous)
      {
        def ambiguousParseTree = opt :> IAmbiguousParseTree;
        def ambiguities        = array(ambiguousParseTree.Ambiguities.Count);
        foreach (parseTree :> OptionParseTree[TParseTree] in ambiguousParseTree.Ambiguities with index)
          ambiguities[index] = GetAstOption(parent, parseTree);
        OptionFactory[TOption, TAst].CreateAmbiguous(ambiguities)
      }
      else if (opt.HasValue)
      {
        def value = opt.Value.GetAst(parent);
        OptionFactory[TOption, TAst].CreateSome(parent, opt.Location.Span, value)
      }
      else
      {
        OptionFactory[TOption, TAst].CreateNone(parent, opt.Location.Span)
      }
    }

    private module OptionFactory[TOption, TAst]
    {
      public CreateSome : Func[IAst, NSpan, TAst, TOption];
      public CreateNone : Func[IAst, NSpan, TOption];
      public CreateAmbiguous : Func[array[TOption], TOption];

      static this()
      {
        def createSomeMethod = typeof(TOption).GetMethod("CreateSome", BindingFlags.Public %| BindingFlags.Static);
        CreateSome = Delegate.CreateDelegate(typeof(Func[IAst, NSpan, TAst, TOption]), createSomeMethod) :> Func[IAst, NSpan, TAst, TOption];

        def createNoneMethod = typeof(TOption).GetMethod("CreateNone", BindingFlags.Public %| BindingFlags.Static);
        CreateNone = Delegate.CreateDelegate(typeof(Func[IAst, NSpan, TOption]), createNoneMethod) :> Func[IAst, NSpan, TOption];

        def createAmbiguousMethod = typeof(TOption).GetMethod("CreateAmbiguous", BindingFlags.Public %| BindingFlags.Static);
        CreateAmbiguous = Delegate.CreateDelegate(typeof(Func[array[TOption], TOption]), createAmbiguousMethod) :> Func[array[TOption], TOption];
      }
    }

    public GetAstList[TParseTree, TParent, TList, TAst](parent : TParent, lst : ListParseTree[TParseTree]) : TList
      where TParseTree   : IMappedParseTree[TParent, TAst]
      where TParent      : IAst
      where TList        : IAstList[TAst]
      where TAst         : IAst
    {
      if (lst.IsAmbiguous)
      {
        def ambiguousParseTree = lst :> IAmbiguousParseTree;
        def ambiguities        = array(ambiguousParseTree.Ambiguities.Count);
        foreach (parseTree :> ListParseTree[TParseTree] in ambiguousParseTree.Ambiguities with index)
          ambiguities[index] = GetAstList(parent, parseTree);
        ListFactory[TList, TAst].CreateAmbiguous(ambiguities);
      }
      else
      {
        def declarations = array(lst.Count);
        foreach (node in lst with index)
          declarations[index] = node.GetAst(parent);
        ListFactory[TList, TAst].CreateConcrete(parent, lst.Location.Span, declarations)
      }
    }

    private module ListFactory[TList, TAst]
    {
      public CreateConcrete : Func[IAst, NSpan, array[TAst], TList];
      public CreateAmbiguous : Func[array[TList], TList];

      static this()
      {
        def createConcreteMethod = typeof(TList).GetMethod("CreateConcrete", BindingFlags.Public %| BindingFlags.Static);
        CreateConcrete = Delegate.CreateDelegate(typeof(Func[IAst, NSpan, array[TAst], TList]), createConcreteMethod) :> Func[IAst, NSpan, array[TAst], TList];

        def createAmbiguousMethod = typeof(TList).GetMethod("CreateAmbiguous", BindingFlags.Public %| BindingFlags.Static);
        CreateAmbiguous = Delegate.CreateDelegate(typeof(Func[array[TList], TList]), createAmbiguousMethod) :> Func[array[TList], TList];
      }
    }
  }
}
