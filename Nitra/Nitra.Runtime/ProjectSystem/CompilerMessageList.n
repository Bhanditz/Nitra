using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.ProjectSystem
{
  /// <summary>
  /// Description of CompilerMessageList.
  /// </summary>
  public sealed class CompilerMessageList : IRootCompilerMessages
  {
    [Record]
    private struct CompilerMessageInternal
    {
      public Type           : CompilerMessageType   { get; }
      public Location       : Location              { get; }
      public Text           : string                { get; }
      public Number         : int                   { get; }
      public NestedMessages : List[CompilerMessageInternal] { get; }

      public ToCompilerMessage() : CompilerMessage
      {
        CompilerMessage(Type, Location, Text, Number, ToCompilerMessage(NestedMessages))
      }

      public static ToCompilerMessage(messages : List[CompilerMessageInternal]) : list[CompilerMessage]
      {
        mutable result = [];
        when (messages != null)
          for(mutable i = messages.Count - 1; i >= 0; --i)
            result ::= messages[i].ToCompilerMessage();
        result
      }
    }

    public this()
    {
      this(List())
    }

    private this(messages : List[CompilerMessageInternal])
    {
      _messages = messages;
    }

    private         _messages   : List[CompilerMessageInternal];
    private mutable _isDisposed : bool;

    public GetMessages() : list[CompilerMessage]
    {
      CompilerMessageInternal.ToCompilerMessage(_messages)
    }

    public ReportMessage(messageType : CompilerMessageType, loc : Location, msg : string, num : int) : void
      requires !_isDisposed otherwise throw InvalidOperationException("Object disposed")
    {
      _messages.Add(CompilerMessageInternal(messageType, loc, msg, num, null))
    }

    public ReportRootMessage(messageType : CompilerMessageType, loc : Location, msg : string, num : int) : IRootCompilerMessages
      requires !_isDisposed otherwise throw InvalidOperationException("Object disposed")
    {
      def m = CompilerMessageInternal(messageType, loc, msg, num, List());
      _messages.Add(m);
      CompilerMessageList(m.NestedMessages)
    }

    public Dispose() : void
    {
      _isDisposed = true;
    }
  }
}
