using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.IO;

namespace Nitra.ProjectSystem
{
  public class TextCompilerMessages : ICompilerMessages
  {
    public Output       : TextWriter { get; }
    public NumberPrefix : string     { get; }

    public this(output : TextWriter, numberPrefix : string = null)
    {
      assert(output != null);

      Output       = output;
      NumberPrefix = if (string.IsNullOrEmpty(numberPrefix)) "MSG" else numberPrefix;
    }

    public Warning      (loc : Location, msg : string, num : int = -1) : void                  { ReportMessage(loc, msg, num, CompilerMessageType.Warning) }
    public Hint         (loc : Location, msg : string, num : int = -1) : void                  { ReportMessage(loc, msg, num, CompilerMessageType.Hint)    }
    public Error        (loc : Location, msg : string, num : int = -1) : void                  { ReportMessage(loc, msg, num, CompilerMessageType.Error)   }
    public FatalError[T](loc : Location, msg : string, num : int = -1) : T                     { ReportMessage(loc, msg, num, CompilerMessageType.FatalError); assert(false, msg) }
    public RootWarning  (loc : Location, msg : string, num : int = -1) : IRootCompilerMessages { ReportMessage(loc, msg, num, CompilerMessageType.Warning); FakeRootCompilerMessages(this) }
    public RootHint     (loc : Location, msg : string, num : int = -1) : IRootCompilerMessages { ReportMessage(loc, msg, num, CompilerMessageType.Hint);    FakeRootCompilerMessages(this) }
    public RootError    (loc : Location, msg : string, num : int = -1) : IRootCompilerMessages { ReportMessage(loc, msg, num, CompilerMessageType.Error);   FakeRootCompilerMessages(this) }

    protected virtual ReportMessage(loc : Location, msg : string, num : int, messageType : CompilerMessageType) : void
    {
      Output.Write(loc.ToMessageString());
      Output.Write(GetMessagePrefix(messageType));

      when (num != -1)
      {
        Output.Write(NumberPrefix);
        Output.Write(num);
        Output.Write(": ");
      }

      Output.WriteLine(msg)
    }

    protected static GetMessagePrefix(messageType : CompilerMessageType) : string
    {
      | FatalError | Error => "error: "
      | Warning            => "warning: "
      | Hint               => "hint: "
    }
  }
}
