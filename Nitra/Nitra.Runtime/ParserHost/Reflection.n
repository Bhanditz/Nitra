using Nitra.Internal;
using Nitra.Internal.Recovery;
using Nitra.Runtime;
using Nitra.Runtime.Reflection;

using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.ObjectModel;

using SCG = System.Collections.Generic;

namespace Nitra
{
  public partial class ParserHost
  {
    private static EmptyRuleApplications : ReadOnlyCollection[RuleApplication] = ReadOnlyCollection(array(0));

    public Reflection(parseResult : ParseResult, pos : int) : ReadOnlyCollection[RuleApplication]
    {
      def res = SCG.List();
      for (mutable rawTreePtr = parseResult.memoize[pos]; rawTreePtr > 0; rawTreePtr = parseResult.rawTree[rawTreePtr + 1])
      {
        def ruleId = parseResult.rawTree[rawTreePtr];
        if (_allRules[ruleId] is SimpleRuleParser)
          res.Add(RuleApplication(parseResult, pos, rawTreePtr))
        else if (_allRules[ruleId] is ExtensionRuleParser as ruleParser)
        {
          assert(ruleId != ruleParser.RuleId);
          res.Add(RuleApplication(parseResult, pos, rawTreePtr))
        }
        else if (_allRules[ruleId] is ExtensibleRuleParserData as extensibleRuleParserData)
          Reflection(extensibleRuleParserData, parseResult, pos, rawTreePtr, res)
        else
          assert(false)
      }

      ReadOnlyCollection(res.NToArray());
    }

    private Reflection(extensibleRuleParserData : ExtensibleRuleParserData, parseResult : ParseResult, pos : int, mutable rawTreePtr : int, res : SCG.List[RuleApplication]) : void
    {
      def ruleId = parseResult.rawTree[rawTreePtr];
      if (ruleId == extensibleRuleParserData.PrefixId)
      {
        rawTreePtr = parseResult.rawTree[rawTreePtr + ExtensibleRuleParser.PrefixOfs.List];
        for (; rawTreePtr > 0; rawTreePtr = parseResult.rawTree[rawTreePtr + 1])
        {
          def ruleId = parseResult.rawTree[rawTreePtr] & ExtensibleRuleParser.RawTreeMask.Id;
          if (_allRules[ruleId] is ExtensionRuleParser)
            res.Add(RuleApplication(parseResult, pos, rawTreePtr))
          else
            assert(false)
        }
      }
      else if (ruleId == extensibleRuleParserData.PostfixId)
      {
        rawTreePtr = parseResult.rawTree[rawTreePtr + ExtensibleRuleParser.PostfixOfs.List];
        for (; rawTreePtr > 0; rawTreePtr = parseResult.rawTree[rawTreePtr + 1])
        {
          def ruleId = parseResult.rawTree[rawTreePtr] & ExtensibleRuleParser.RawTreeMask.Id;
          if (_allRules[ruleId] is ExtensionRuleParser)
            res.Add(RuleApplication(parseResult, pos, rawTreePtr))
          else
            assert(false)
        }
      }
      else
        assert(false);
    }
  }
}
