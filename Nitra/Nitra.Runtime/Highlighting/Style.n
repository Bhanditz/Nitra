using Nitra.Serialization2;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Extensions;
using Nemerle.Text;
using Nemerle.Imperative;
using Nemerle.Utility;

using System;
using System.IO;

namespace Nitra
{
  [Record, StructuralEquality]
  public sealed class Style : ISerializable
  {
    public Language        : Language { get; }
    public Name            : string   { get; }
    public FullName        : string   { get; }
    public ForegroundColor : int      { get; }

    public override ToString() : string { FullName }

    public Serialize(writer : BinaryWriter, metadataWriter : MetadataWriter) : void
    {
      metadataWriter.WriteObject(Language, writer);
      writer.Write(Name);
    }

    public static Deserialize(reader : BinaryReader, metadataReader : MetadataReader) : this
    {
      def language = metadataReader.ReadObject.[Language](reader);
      def name     = reader.ReadString();
      unchecked for (mutable acc = language; acc : object != null; acc = acc.Parent)
        foreach (style when StringComparer.Ordinal.Equals(style.Name, name) in acc.GetDeclaredStyles())
          return style;

      metadataReader.Project.FatalError(metadataReader.ErrorLocation, $"Style '$name' not found in language '$language'.")
    }
  }
}
