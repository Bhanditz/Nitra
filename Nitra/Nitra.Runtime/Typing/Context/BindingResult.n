using Nitra.ProjectSystem;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.ObjectModel;

namespace Nitra.Typing
{
  public sealed class BindingResult[T] : Located
  {
    public override Location : Location { get; }

    public this(location : Location, candidates : list[T], mutable messages : ReadOnlyCollection[CompilerMessage])
    {
      Location = location;
      when (!(candidates is [_]) && messages.Count == 0)
      {
        def message = "Message not specified for unbound or ambiguous binding result.";
        assert2(false, message);
        messages = ReadOnlyCollection(array[CompilerMessage(CompilerMessageType.FatalError, Guid.Empty, location, message, -1, null)])
      }
      Candidates = candidates;
      Messages   = messages;
    }

    public Candidates : list[T]                             { get; }
    public Messages   : ReadOnlyCollection[CompilerMessage] { get; }

    public AppendCandidates(newCandidates : list[T]) : BindingResult[T]
    {
      | []    => this
      | [one] => BindingResult(Location, one :: Candidates, Messages)
      | many  => BindingResult(Location, many + Candidates, Messages)
    }
  }
}
