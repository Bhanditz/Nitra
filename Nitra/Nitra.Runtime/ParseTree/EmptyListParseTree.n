using Nemerle;
using System;
using SCG = System.Collections.Generic;

namespace Nitra
{
  public sealed class EmptyListParseTree[T] : ListParseTree[T]
    where T : IParseTree
  {
    public this(location : Location)
    {
      this.Location = location;
    }

    public override Location          : Location { get }
    public override Count             : int      { get 0 }
    public override Item[index : int] : T        { get { _ = index; throw ArgumentOutOfRangeException("index") } }

    public override UpdateItems(newItems : SCG.List[T]) : ListParseTree[T]
    {
      if (newItems.Count > 0)
        ConcreteListParseTree(this.Location, newItems)
      else
        this
    }

    protected override GetUnderlyingArray() : array[T]
    {
      null
    }

    public override Contains(item : T) : bool
    {
      IgnoreParams();
      false
    }

    public override CopyTo(@array : array[T], arrayIndex : int) : void
    {
      IgnoreParams();
    }

    public override IndexOf(item : T) : int
    {
      IgnoreParams();
      -1
    }

    public override GetSlice(index : int, count : int) : ListParseTree[T]
    {
      if (index == 0)
        if (count == 0)
          this
        else
          throw ArgumentOutOfRangeException("count")
      else
        throw ArgumentOutOfRangeException("index")
    }
  }
}
