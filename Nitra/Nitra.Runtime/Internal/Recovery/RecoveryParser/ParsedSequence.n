using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;
using Nemerle.Extensions;

using Nitra.Runtime.Reflection;

using System;
using System.Linq;

using SCG = System.Collections.Generic;

namespace Nitra.Internal.Recovery
{
  [Record, StructuralEquality]
  public class ParsedSequence
  {
    [EqualsIgnore]
    public RecoveryParser : RecoveryParser;
    public StartPos : int;
    public ParsingSequence : ParsingSequence;

    [RecordIgnore, EqualsIgnore] public Callers        : SCG.HashSet[ParseRecord]   = SCG.HashSet();
    [RecordIgnore, EqualsIgnore] public ParsedSubrules : SCG.HashSet[ParsedSubrule] = SCG.HashSet();
    [RecordIgnore, EqualsIgnore] public Ends           : SCG.HashSet[int]           = SCG.HashSet();
    
    public IsToken : bool { get { this.ParsingSequence.IsToken }}

    public IsSubruleVoid(subrule : ParsedSubrule) : bool
    {
      match (ParsingSequence.States[subrule.State])
      {
        | Simple           as state => state.RuleParser.IsVoid
        | Extensible       as state => state.RuleParser.IsVoid
        | ExtensionPrefix  as state => state.RuleParser.IsVoid
        | ExtensionPostfix as state => state.RuleParser.IsVoid
        | Subsequence      as state => state.SequenceInfo.IsVoid
        | Scan                      => false
        | Predicate                 => true
      }
    }
    
    public SubruleMandatoryTokenCount(subrule : ParsedSubrule) : int
    {
      match (ParsingSequence.States[subrule.State])
      {
        | Simple           as state => state.RuleParser.MandatoryTokens
        | Extensible       as state => state.RuleParser.MandatoryTokens
        | ExtensionPrefix  as state => state.RuleParser.MandatoryTokens
        | ExtensionPostfix          => 0
        | Subsequence      as state => state.SequenceInfo.MandatoryTokenCount
        | Scan             as state => state.Subrule.MandatoryTokenCount
        | Predicate                 => 0
      }
    }

    public GetSequencesForSubrule(parsedSubrule : ParsedSubrule) : Seq[ParsedSequence]
    {
      def res = SCG.HashSet();
      def begin = parsedSubrule.Begin;
      def end   = parsedSubrule.End;

      match (ParsingSequence.States[parsedSubrule.State])
      {
        | Simple           as state =>
          foreach (record in RecoveryParser.Records[begin])
            when (record.Sequence.StartPos == begin)
            when (record.Sequence.Ends.Contains(end))
            when (record.Sequence.ParsingSequence : object == state.RuleParser.ParsingSequence)
              _ = res.Add(record.Sequence);

        | Extensible       as state =>
          foreach (record in RecoveryParser.Records[begin])
            when (record.Sequence.StartPos == begin)
            when (record.Sequence.Ends.Contains(end))
            when (record.Sequence.ParsingSequence : object == state.RuleParser.ParsingSequence)
              _ = res.Add(record.Sequence);

        | ExtensionPrefix  as state =>
          foreach (record in RecoveryParser.Records[begin])
            when (record.Sequence.StartPos == begin)
            when (record.Sequence.Ends.Contains(end))
            when (state.RuleParser.IsExtension(record.Sequence.ParsingSequence))
              _ = res.Add(record.Sequence);

        | ExtensionPostfix as state =>
          foreach (record in RecoveryParser.Records[begin])
            when (record.Sequence.StartPos == begin)
            when (record.Sequence.Ends.Contains(end))
            when (state.RuleParser.IsExtension(record.Sequence.ParsingSequence))
              _ = res.Add(record.Sequence);

        | Subsequence      as state =>
          foreach (record in RecoveryParser.Records[begin])
            when (record.Sequence.StartPos == begin)
            when (record.Sequence.Ends.Contains(end))
            when (record.Sequence.ParsingSequence : object == state.Sequence)
              _ = res.Add(record.Sequence);

        | Scan => ()
        | Predicate => ()
      }
      res
    }
    
    public AddCaller(caller : ParseRecord) : void
    {
      when (Callers.Add(caller))
        foreach (end in Ends)
          RecoveryParser.RecordsToComplete.Push(end, caller, this);
    }

    public AddEnd(end : int) : void
    {
      when (Ends.Add(end))
        foreach (caller in Callers)
          RecoveryParser.RecordsToComplete.Push(end, caller, this);
    }

    public GetNextSubrules(subrule : ParsedSubrule) : Seq[ParsedSubrule]
    {
      GetNextSubrules(subrule, ParsedSubrules)
    }

    public GetNextSubrules(subrule : ParsedSubrule, parsedSubrules : Seq[ParsedSubrule]) : Seq[ParsedSubrule]
    {
      def state = ParsingSequence.States[subrule.State];
      parsedSubrules.Where(next => subrule.End == next.Begin && state.Next.Exists(_ == next.State) && next != subrule)
    }

    public GetPrevSubrules(subrule : ParsedSubrule) : Seq[ParsedSubrule]
    {
      GetPrevSubrules(subrule, ParsedSubrules)
    }

    public GetPrevSubrules(subrule : ParsedSubrule, parsedSubrules : Seq[ParsedSubrule]) : Seq[ParsedSubrule]
    {
      def state = ParsingSequence.States[subrule.State];
      parsedSubrules.Where(prev => prev.End == subrule.Begin && state.Prev.Exists(_ == prev.State) && prev != subrule)
    }

    public GetFirstSubrules() : Seq[ParsedSubrule]
    {
      GetFirstSubrules(ParsedSubrules)
    }

    public GetFirstSubrules(parsedSubrules : Seq[ParsedSubrule]) : Seq[ParsedSubrule]
    {
      parsedSubrules.Where(p => p.Begin == StartPos && ParsingSequence.IsStartState(p.State))
    }
    
    public GetLastSubrules() : Seq[ParsedSubrule]
    {
      GetLastSubrules(ParsedSubrules)
    }

    public GetLastSubrules(parsedSubrules : Seq[ParsedSubrule]) : Seq[ParsedSubrule]
    {
      parsedSubrules.Where(p => Ends.Contains(p.End) && ParsingSequence.States[p.State].Next.Exists(_ == -1))
    }

    public GetLastSubrules(end : int) : Seq[ParsedSubrule]
    {
      GetLastSubrules(ParsedSubrules, end)
    }

    public GetLastSubrules(parsedSubrules : Seq[ParsedSubrule], end : int) : Seq[ParsedSubrule]
    {
      parsedSubrules.Where(p => p.End == end && ParsingSequence.States[p.State].Next.Exists(_ == -1))
    }

    public GetValidSubrules() : SCG.SortedSet[ParsedSubrule]
    {
      GetValidSubrulesImpl(GetLastSubrules(), ParsedSubrules)
    }

    public GetValidSubrules(end : int) : SCG.SortedSet[ParsedSubrule]
    {
      GetValidSubrules(ParsedSubrules, end)
    }

    public GetValidSubrules(parsedSubrules : Seq[ParsedSubrule]) : SCG.SortedSet[ParsedSubrule]
    {
      GetValidSubrulesImpl(GetLastSubrules(parsedSubrules), parsedSubrules)
    }

    public GetValidSubrules(parsedSubrules : Seq[ParsedSubrule], end : int) : SCG.SortedSet[ParsedSubrule]
    {
      GetValidSubrulesImpl(GetLastSubrules(parsedSubrules, end), parsedSubrules)
    }

    private GetValidSubrulesImpl(ends : Seq[ParsedSubrule], parsedSubrules : Seq[ParsedSubrule]) : SCG.SortedSet[ParsedSubrule]
    {
      def cmp  = ParsedSubrule.ComparerEndBeginIndex();
      def res  = SCG.SortedSet(cmp);
      def ends = SCG.SortedSet(ends, cmp);
      while (ends.Count > 0)
      {
        def end = ends.Max;
        _ = ends.Remove(end);
        when (res.Add(end))
          foreach (prev in GetPrevSubrules(end, parsedSubrules))
            _ = ends.Add(prev);
      }
      res
    }

    public override ToString() : string
    {
      def sb = Text.StringBuilder();
      def startPos = this.StartPos;
      
      def name = ParsingSequence.RuleDesc + " ";

      _ = sb.Append(name);
      
      _ = sb.Append(", StartPos=");
        
      _ = sb.Append(startPos);
      _ = sb.Append(")");

      //when (IsSpeculative)
      //  _ = sb.Append(" !!! ");
        
      def text = this.RecoveryParser.ParseResult.Text;
          
      when (startPos <= text.Length)
      {
        _ = sb.Append("   «");
        def showLen = 30;
            
        when (startPos - showLen > 0)
          _ = sb.Append("…");

        _ = sb.Append(text.SafeLeftSubstring(startPos, showLen));
            
        _ = sb.Append("■");
            
        _ = sb.Append(text.SafeRightSubstring(startPos, showLen));
        
        when (startPos + showLen <= text.Length)
          _ = sb.Append("…");
              
        _ = sb.Append("»");
      }
        
      sb.ToString()
    }
  }
}
