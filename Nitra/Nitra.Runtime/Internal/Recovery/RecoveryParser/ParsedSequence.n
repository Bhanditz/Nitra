using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;
using Nemerle.Extensions;

using Nitra.Runtime.Reflection;

using System;
using System.Linq;

using SCG = System.Collections.Generic;

namespace Nitra.Internal.Recovery
{
  [Record, StructuralEquality]
  public variant ParsedSequence
  {
    [EqualsIgnore]
    public RecoveryParser : RecoveryParser;
    public StartPos : int;
    
    | Root
    | Extensible                 { RuleParser : ExtensibleRuleParser; }
    | Simple                     { Sequence : SequenceInfo; }
    | Extension                  { Sequence : SequenceInfo; }
    | Option                     { Subrule  : SubruleInfo.Option; }
    | List                       { Subrule  : SubruleInfo.List;              ListStart : int; Counter : int; }
    | ListWithSeparatorRule      { Subrule  : SubruleInfo.ListWithSeparator; ListStart : int; Counter : int; }
    | ListWithSeparatorSeparator { Subrule  : SubruleInfo.ListWithSeparator; ListStart : int; Counter : int; }

    [RecordIgnore, EqualsIgnore] public Callers        : SCG.HashSet[ParseRecord]   = SCG.HashSet();
    [RecordIgnore, EqualsIgnore] public ParsedSubrules : SCG.HashSet[ParsedSubrule] = SCG.HashSet();
    [RecordIgnore, EqualsIgnore] public Ends           : SCG.HashSet[int]           = SCG.HashSet();

    private static IncrementCounter(counter : int, min : int, max : int?) : int
    {
      if (counter <= min)
        counter + 1
      else if (max.HasValue)
      {
        if (counter <= max.Value)
          counter + 1
        else
          counter
      }
      else
        counter
    }

    public static NextSequence(this sequence : ParsedSequence.List, pos : int) : ParsedSequence.List
    {
      ParsedSequence.List(sequence.RecoveryParser, pos, sequence.Subrule, sequence.ListStart, IncrementCounter(sequence.Counter, sequence.Subrule.Min, sequence.Subrule.Max))
    }

    public static NextSequence(this sequence : ParsedSequence.ListWithSeparatorRule, pos : int) : ParsedSequence.ListWithSeparatorSeparator
    {
      ParsedSequence.ListWithSeparatorSeparator(sequence.RecoveryParser, pos, sequence.Subrule, sequence.ListStart, sequence.Counter)
    }

    public static NextSequence(this sequence : ParsedSequence.ListWithSeparatorSeparator, pos : int) : ParsedSequence.ListWithSeparatorRule
    {
      ParsedSequence.ListWithSeparatorRule(sequence.RecoveryParser, pos, sequence.Subrule, sequence.ListStart, IncrementCounter(sequence.Counter, sequence.Subrule.Min, sequence.Subrule.Max))
    }

    public static TryGetFromCache[T](this sequence : T) : T
      where T : ParsedSequence
    {
      when (sequence == null)
        return null;

      mutable old;
      if (sequence.RecoveryParser.Sequences.TryGetFirstAddedItem(sequence, out old))
        old :> T;
      else
        null;
    }

    public GetNextSubrules(subrule : ParsedSubrule) : SCG.IEnumerable[ParsedSubrule]
    {
      match (this)
      {
        | Extensible => ParsedSubrules.Where(next =>                                    subrule.End == next.Begin)
        | _          => ParsedSubrules.Where(next => subrule.Index == next.Index - 1 && subrule.End == next.Begin)
      }
    }

    public GetPrevSubrules(subrule : ParsedSubrule) : SCG.IEnumerable[ParsedSubrule]
    {
      match (this)
      {
        | Extensible => ParsedSubrules.Where(prev =>                                    prev.End == subrule.Begin)
        | _          => ParsedSubrules.Where(prev => prev.Index == subrule.Index - 1 && prev.End == subrule.Begin)
      }
    }

    public GetValidSubrules() : SCG.SortedSet[ParsedSubrule]
    {
      def cmp = ParsedSubrule.ComparerEndBeginIndex();
      def res = SCG.SortedSet(cmp);
      def ends = if (this is Extensible)
        SCG.SortedSet(ParsedSubrules.Where(p => Ends.Contains(p.End)), cmp);
      else
        SCG.SortedSet(ParsedSubrules.Where(p => p.Index == SubruleCount - 1 && Ends.Contains(p.End)), cmp);
      while (ends.Count > 0)
      {
        def end = ends.Max;
        _ = ends.Remove(end);
        when (res.Add(end))
          foreach (prev in GetPrevSubrules(end))
            _ = ends.Add(prev);
      }
      when (this is Extensible)
        _ = res.RemoveWhere(p => p.Index == 1 && p.Begin == p.End);
      res
    }

    public IsToken : bool
    {
      get
      {
        match (this)
        {
          | Extensible as seq => seq.RuleParser.IsTokenRule
          | Simple     as seq => seq.Sequence.Parser.IsTokenRule
          | _                 => false
        }
      }
    }

    public IsSubruleVoid(index : int) : bool
    {
      match (this)
      {
        | Root                              => false
        | Extensible                        => false
        | Simple                     as seq => seq.Sequence.Subrules[index].IsVoid
        | Extension                  as seq => seq.Sequence.Subrules[index].IsVoid
        | Option                     as seq => seq.Subrule.Rule.Subrules[index].IsVoid
        | List                       as seq => seq.Subrule.Rule.Subrules[index].IsVoid
        | ListWithSeparatorRule      as seq => seq.Subrule.Rule.Subrules[index].IsVoid
        | ListWithSeparatorSeparator as seq => seq.Subrule.Separator.Subrules[index].IsVoid
      }
    }

    public Id : int
    {
      get
      {
        match (this)
        {
          | Root                                   =>  -1
          | Extensible                 as sequence => sequence.RuleParser.PrefixId
          | Simple                     as sequence => sequence.Sequence.Id
          | Extension                  as sequence => sequence.Sequence.Id
          | Option                     as sequence => sequence.Subrule.Rule.Id
          | List                       as sequence => sequence.Subrule.Rule.Id
          | ListWithSeparatorRule      as sequence => sequence.Subrule.Rule.Id
          | ListWithSeparatorSeparator as sequence => sequence.Subrule.Separator.Id
        }
      }
    }

    public CanContinueFromSameSubrule : bool
    {
      get
      {
        match (this)
        {
          | Root                       => false
          | Simple                     => false
          | Extension                  => false
          | Option                     => false
          | Extensible                 => true
          | List                       => true
          | ListWithSeparatorRule      => true
          | ListWithSeparatorSeparator => true
        }
      }
    }

    public HasName : bool   { get { this is Extension || this is Simple || this is Extensible } }
    public Name    : string
    {
      get
      {
        match (this)
        {
          | Extensible as x => x.RuleParser.RuleName
          | Extension  as x => x.Sequence.Parser.RuleName
          | Simple     as x => x.Sequence.Parser.RuleName
          | _ => null
        }
      }
    }

    public GetSequenceForSubrule(parsedSubrule : ParsedSubrule) : option[ParsedSequence]
    {
      mutable sequence;
      match (GetSubrule(parsedSubrule.Index))
      {
        | Empty | Marker | RegularCall | TokenString => None()
        | ExtensibleCall    as rule =>
          if (RecoveryParser.Sequences.TryGetFirstAddedItem(ParsedSequence.Extensible(RecoveryParser, parsedSubrule.Begin, rule.RuleParser), out sequence))
            Some(sequence)
          else
            None()

        | SimpleCall        as rule =>
          if (RecoveryParser.Sequences.TryGetFirstAddedItem(ParsedSequence.Simple(RecoveryParser, parsedSubrule.Begin, rule.RuleParser.Reflection(rule.RuleParser.RuleId)), out sequence))
            Some(sequence)
          else
            None()

        | Option            as rule =>
          if (RecoveryParser.Sequences.TryGetFirstAddedItem(ParsedSequence.Option(RecoveryParser, parsedSubrule.Begin, rule), out sequence))
            Some(sequence)
          else
            None()

        | List              as rule =>
          if (RecoveryParser.Sequences.TryGetFirstAddedItem(ParsedSequence.List(RecoveryParser, parsedSubrule.Begin, rule, parsedSubrule.Begin, 1), out sequence))
            Some(sequence)
          else
            None()

        | ListWithSeparator as rule =>
          if (RecoveryParser.Sequences.TryGetFirstAddedItem(ParsedSequence.ListWithSeparatorRule(RecoveryParser, parsedSubrule.Begin, rule, parsedSubrule.Begin, 1), out sequence))
            Some(sequence)
          else
            None()
      }
    }

    public GetSequencesForSubrule(parsedSubrule : ParsedSubrule) : SCG.IEnumerable[ParsedSequence]
    {
      def res = SCG.HashSet();
      def begin = parsedSubrule.Begin;
      def end   = parsedSubrule.End;
      
      when (this is Extensible as ext)
      {
        foreach (record in RecoveryParser.Records[begin])
          when (record.Sequence.StartPos == begin)
          when (record.Sequence.Ends.Contains(end))
          when (record.Sequence is ParsedSequence.Extension as sequence)
          when (ext.RuleParser.IsExtension(sequence.Sequence.Parser))
            _ = res.Add(sequence);
            
        return res;
      }
      
      match (GetSubrule(parsedSubrule.Index))
      {
        | Empty | Marker | RegularCall | TokenString => ()
        | ExtensibleCall    as rule =>
          foreach (record in RecoveryParser.Records[begin])
            when (record.Sequence.StartPos == begin)
            when (record.Sequence.Ends.Contains(end))
            when (record.Sequence is ParsedSequence.Extensible as sequence)
            when (sequence.RuleParser : object == rule.RuleParser)
              _ = res.Add(sequence);

        | SimpleCall        as rule =>
          foreach (record in RecoveryParser.Records[begin])
            when (record.Sequence.StartPos == begin)
            when (record.Sequence.Ends.Contains(end))
            when (record.Sequence is ParsedSequence.Simple as sequence)
            when (sequence.Sequence.Parser : object == rule.RuleParser)
              _ = res.Add(sequence);

        | Option            as rule =>
          foreach (record in RecoveryParser.Records[begin])
            when (record.Sequence.StartPos == begin)
            when (record.Sequence.Ends.Contains(end))
            when (record.Sequence is ParsedSequence.Option as sequence)
            when (sequence.Subrule.Rule : object == rule.Rule)
              _ = res.Add(sequence);

        | List              as rule =>
          foreach (record in RecoveryParser.Records[begin])
            when (record.Sequence.StartPos == begin)
            when (record.Sequence.Ends.Contains(end))
            when (record.Sequence is ParsedSequence.List as sequence)
            when (sequence.Subrule.Rule : object == rule.Rule)
              _ = res.Add(sequence);

        | ListWithSeparator as rule =>
          foreach (record in RecoveryParser.Records[begin])
            when (record.Sequence.StartPos == begin)
            when (record.Sequence.Ends.Contains(end))
            {
              when (record.Sequence is ParsedSequence.ListWithSeparatorRule as sequence)
              when (sequence.Subrule.Rule : object == rule.Rule)
                _ = res.Add(sequence);
              when (record.Sequence is ParsedSequence.ListWithSeparatorSeparator as sequence)
              when (sequence.Subrule.Separator : object == rule.Separator)
                _ = res.Add(sequence);
            }
      }
      res
    }

    public GetSubrule(index : int) : SubruleInfo
    {
      match (this)
      {
        | Root                              => assert3(false)
        | Extensible                        => assert3(false)
        | Simple                     as seq => seq.Sequence.Subrules[index]
        | Extension                  as seq => seq.Sequence.Subrules[index]
        | Option                     as seq => seq.Subrule.Rule.Subrules[index]
        | List                       as seq => seq.Subrule.Rule.Subrules[index]
        | ListWithSeparatorRule      as seq => seq.Subrule.Rule.Subrules[index]
        | ListWithSeparatorSeparator as seq => seq.Subrule.Separator.Subrules[index]
      }
    }

    public SubruleCount : int
    {
      get
      {
        match (this)
        {
          | Root                              => 1
          | Extensible                        => 2
          | Simple                     as seq => seq.Sequence.Subrules.Length
          | Extension                  as seq => seq.Sequence.Subrules.Length
          | Option                     as seq => seq.Subrule.Rule.Subrules.Length
          | List                       as seq => seq.Subrule.Rule.Subrules.Length
          | ListWithSeparatorRule      as seq => seq.Subrule.Rule.Subrules.Length
          | ListWithSeparatorSeparator as seq => seq.Subrule.Separator.Subrules.Length
        }
      }
    }

    public override ToString() : string
    {
      def sb = Text.StringBuilder();
      def startPos = this.StartPos;
        
      def name = 
        match (this)
        {
          | Simple                     => "Simple     "
          | Extension                  => "Extension  "
          | Option                     => "Option     "
          | List                       => "List       "
          | ListWithSeparatorRule      => "ListRule   "
          | ListWithSeparatorSeparator => "ListSep    "
          | Root                       => "Root       "
          | Extensible                 => "Extensible "
        };

      _ = sb.Append(name);
        
      when (this.HasName)
      {
        _ = sb.Append(this.Name);
        _ = sb.Append(" = ");
      }
      match (this)
      {
        | ParsedSequence.Root            => _ = sb.Append("Root");
        | ParsedSequence.Extensible as r => _ = sb.Append("Extensible: " + r.RuleParser.RuleName );
        | _ =>
          def subruleCount = this.SubruleCount;
          
          for (mutable i = 0; i < subruleCount; i++)
          {
            def r = this.GetSubrule(i);
          
            when (i > 0)
              _ = sb.Append("  ");
            
            _ = sb.Append(r);
          }
        
          _ = sb.Append(", StartPos=");
        
          _ = sb.Append(startPos);
          _ = sb.Append(")");
      }

      //when (IsSpeculative)
      //  _ = sb.Append(" !!! ");
        
      def text = this.RecoveryParser.ParseResult.Text;
          
      when (startPos <= text.Length)
      {
        _ = sb.Append("   «");
        def showLen = 30;
            
        when (startPos - showLen > 0)
          _ = sb.Append("…");

        _ = sb.Append(text.SafeLeftSubstring(startPos, showLen));
            
        _ = sb.Append("■");
            
        _ = sb.Append(text.SafeRightSubstring(startPos, showLen));
        
        when (startPos + showLen <= text.Length)
          _ = sb.Append("…");
              
        _ = sb.Append("»");
      }
        
      sb.ToString()
    }
  }
}
