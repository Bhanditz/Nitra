using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;
using Nemerle.Extensions;

using Nitra.Runtime.Reflection;

using System;
using System.Linq;

using SCG = System.Collections.Generic;

namespace Nitra.Internal.Recovery
{
  public class RecoveryParser
  {
    public ParseResult       : ParseResult;
    public Sequences         : Hashtable[ParsedSequence, ParsedSequence] = Hashtable();
    public Records           : array[SCG.HashSet[ParseRecord]];
    public RecordsToProcess  : SCG.Stack[int * ParseRecord] = SCG.Stack(50);
    public RecordsToComplete : SCG.Stack[int * ParseRecord * ParsedSequence] = SCG.Stack(50);

    public this(parseResult : ParseResult)
    {
      Records     = array(parseResult.Text.Length + 1);
      ParseResult = parseResult;
    }

    private AddSequence(caller : ParseRecord, newSequence : ParsedSequence) : void
    {
      mutable sequence;
      when (!Sequences.TryGetValue(newSequence, out sequence))
      {
        Sequences.Add(newSequence, newSequence);
        def record = ParseRecord(newSequence, 0);
        AddRecord(newSequence.StartPos, record);
        sequence = newSequence;
      }
      when (sequence.Callers.Add(caller))
        foreach (pos in sequence.Ends)
          RecordsToComplete.Push(pos, caller, sequence);
    }

    private AddRecord(pos : int, record : ParseRecord) : void
    {
      if (record.IsComplete)
      {
        when (record.Sequence.Ends.Add(pos))
          foreach (caller in record.Sequence.Callers)
            RecordsToComplete.Push(pos, caller, record.Sequence);
      }
      else
      {
        mutable set = Records[pos];
        when (set == null)
        {
          set = SCG.HashSet();
          Records[pos] = set;
        }
        def newRecord = set.Add(record);
        when (newRecord)
          RecordsToProcess.Push((pos, record));
      }
    }

    private AddNextRecord(begin : int, end : int, record : ParseRecord) : void
    {
      _ = record.Sequence.ParsedSubrules.Add(ParsedSubrule(begin, end, record.Index));
      AddRecord(end, record.Next());
    }

    public StartParse(ruleParser : RuleParser) : void
    {
      def textPos = 0;
      match (ruleParser)
      {
        | SimpleRuleParser     as ruleParser =>
          def sequence = ruleParser.Reflection(ruleParser .RuleId);
          AddSequence(ParseRecord(ParsedSequence.Root(textPos), 0), ParsedSequence.Simple(textPos, sequence));

        | ExtensibleRuleParser as ruleParser =>
          AddSequence(ParseRecord(ParsedSequence.Root(textPos), 0), ParsedSequence.Extensible(textPos, ruleParser));

        | _ => assert3(false)
      }
      Parse();
    }

    public Parse() : void
    {
      while (RecordsToProcess.Count > 0 || RecordsToComplete.Count > 0)
      {
        while (RecordsToProcess.Count > 0)
          PredictionOrScanning(RecordsToProcess.Pop());
        while (RecordsToComplete.Count > 0)
          Completion(RecordsToComplete.Pop());
      }
    }

    public Completion(pos : int, caller : ParseRecord, sequence : ParsedSequence) : void
    {
      match (sequence)
      {
        | Root => ()
        | Simple | Extension | Option =>
          AddNextRecord(sequence.StartPos, pos, caller);

        | Extensible as sequence =>
          AddNextRecord(sequence.StartPos, pos, caller);
          AddRecord(pos, ParseRecord(sequence, 1));

        | List                       as sequence =>
          when (CounterLessThanMax(sequence.Counter, sequence.Subrule.Max))
            AddSequence(caller, ParsedSequence.List(pos, sequence.Subrule, sequence.ListStart, sequence.Counter + 1));
          when (sequence.Counter >= sequence.Subrule.Min)
            AddNextRecord(sequence.ListStart, pos, caller);

        | ListWithSeparatorRule      as sequence =>
          AddSequence(caller, ParsedSequence.ListWithSeparatorSeparator(pos, sequence.Subrule, sequence.ListStart, sequence.Counter + 1));
          when (sequence.Counter >= sequence.Subrule.Min)
            AddNextRecord(sequence.ListStart, pos, caller);

        | ListWithSeparatorSeparator as sequence =>
          when (CounterLessThanMax(sequence.Counter, sequence.Subrule.Max))
            AddSequence(caller, ParsedSequence.ListWithSeparatorRule(pos, sequence.Subrule, sequence.ListStart, sequence.Counter));
          when (sequence.Subrule.HangingSeparator && sequence.Counter >= sequence.Subrule.Min)
            AddNextRecord(sequence.ListStart, pos, caller);
      }
    }

    public PredictionOrScanning(textPos : int, record : ParseRecord) : void
    {
      def text = ParseResult.Text;
      if (record.Sequence is ParsedSequence.Extensible as sequence)
      {
        match (record.Index)
        {
          | 0 =>
            foreach (prefixRule in sequence.RuleParser.PrefixRules)
            {
              def sequence = prefixRule.Reflection(prefixRule.RuleId);
              AddSequence(record, ParsedSequence.Extension(textPos, sequence));
            }

          | 1 =>
            AddNextRecord(sequence.StartPos, textPos, record);
            foreach (postfixRule when sequence.RuleParser.FirstPostfixRuleId <= postfixRule.RuleId in sequence.RuleParser.PostfixRules)
            {
              def sequence = postfixRule.Reflection(postfixRule.RuleId);
              AddSequence(record, ParsedSequence.Extension(textPos, sequence));
            }

          | _ => assert3(false)
        }
      }
      else match (record.CurSubrule)
      {
        | Empty                     => ()
        | Marker                    => assert3(false)
        | RegularCall       as rule =>
          def endPos = rule.Descriptor.Parse(textPos, text);
          when (endPos >= 0)
            AddNextRecord(textPos, endPos, record);

        | TokenString       as rule =>
          def endPos = rule.Parse(textPos, text);
          when (endPos >= 0)
            AddNextRecord(textPos, endPos, record);

        | ExtensibleCall    as rule =>
          AddSequence(record, ParsedSequence.Extensible(textPos, rule.RuleParser));

        | SimpleCall        as rule =>
          def endPos = rule.RuleParser.Parse(textPos, text, ParseResult);
          if (endPos >= 0)
            AddNextRecord(textPos, endPos, record);
          else
          {
            def sequence = rule.RuleParser.Reflection(rule.RuleParser.RuleId);
            AddSequence(record, ParsedSequence.Simple(textPos, sequence));
          }

        | Option            as rule =>
          AddSequence(record, ParsedSequence.Option(textPos, rule));
          AddNextRecord(textPos, textPos, record);

        | List              as rule =>
          AddSequence(record, ParsedSequence.List(textPos, rule, textPos, 0));
          when (rule.Min == 0)
            AddNextRecord(textPos, textPos, record);

        | ListWithSeparator as rule =>
          AddSequence(record, ParsedSequence.ListWithSeparatorRule(textPos, rule, textPos, 0));
          when (rule.Min == 0)
            AddNextRecord(textPos, textPos, record);
      }
    }

    public static CounterLessThanMax(counter: int, max : Nullable[int]) : bool
    {
      !max.HasValue || counter < max.GetValueOrDefault()
    }

    public ToDot(_startPos : int) : SCG.List[string] * SCG.Dictionary[string, string]
    {
      assert3(false);
      /*
      def filesToOpen = SCG.List();
      def files = SCG.Dictionary();

      //def makeShortLabel(str : string ) : string { if (str.Length > 20) str.Substring(20) + "..." else str }
      def escape(x : string) : string
      {
        x.Replace("\\", <#\\#>).Replace(<#"#>, <#\"#>)
      }
      def makeTooltip(str : string ) : string { if (str.Length > 0) escape(str) else "<EMPTY>" }
      def makeClusterName(pos : int, sequence : SequenceInfo) { $"cluster_$(pos)_$(sequence.Id)" }
      def makeExtensibleClusterName(pos : int, parserData : ExtensibleRuleParserData) { $"cluster_extensible_$(pos)_$(parserData.PrefixId)" }
      def makeExtensionClusterName(extensibleClusterName : string,  pos : int) { $"cluster_extenson_$(pos)_$(extensibleClusterName)" }
      def makeNodeName(clusterName : string, pos : int, index : int) { $"$(clusterName)_$(pos)_$(index)" }
      def makeClusterNode(clusterName : string) : string { $<# $clusterName [label="#" style="invis"]; #> }
      def makeClusterRef(target : string) : string { $<#URL="$(escape(target)).svg"#> }
      def renderCluster(parsedSequence : ParsedSequence) : string * string
      {
        def clusterName = makeClusterName(parsedSequence.Start.Pos, parsedSequence.Start.Sequence);
        def edges = SCG.List();
        def nodes = SCG.List();
        def callerNodes = SCG.List();
        def label = escape(parsedSequence.Start.ToString());
        nodes.Add(makeClusterNode(clusterName));
        foreach (parseRecordStart in parsedSequence.Records)
        {
          def nodeName = makeNodeName(clusterName, parseRecordStart.Pos, parseRecordStart.Record.Index);
          when (parseRecordStart.Record.Index > 0)
          {
            def prevNodeName = makeNodeName(clusterName, parseRecordStart.PrevPos, parseRecordStart.Record.Index - 1);
            def text = makeTooltip(ParseResult.Text.Substring(parseRecordStart.PrevPos, parseRecordStart.Pos - parseRecordStart.PrevPos));
            edges.Add($<# $prevNodeName -> $nodeName [edgetooltip="$text"]; #>);
          }
          def labelText = if (parseRecordStart.Record.IsComplete) "#COMPLETE#" else parseRecordStart.Record.CurSubrule.ToString();
          def label = escape($"$(parseRecordStart.Pos) $(labelText)");
          mutable url = "";
          unless (parseRecordStart.Record.IsComplete)
            match (parseRecordStart.Record.CurSubrule)
            {
              | SimpleCall as subrule =>
                def targetClusterKey = SequenceStart(parseRecordStart.Pos, subrule.RuleParser.Reflection(subrule.RuleParser.RuleId));
                when (Ast.ContainsKey(targetClusterKey))
                  url = makeClusterRef(makeClusterName(targetClusterKey.Pos, targetClusterKey.Sequence));

              | ExtensibleCall as subrule =>
                url = makeClusterRef(makeExtensibleClusterName(parseRecordStart.Pos, subrule.RuleParser.ParserData));

              | Option as subrule =>
                url = makeClusterRef(makeClusterName(parseRecordStart.Pos, subrule.Rule));

              | List as subrule =>
                url = makeClusterRef(makeClusterName(parseRecordStart.Pos, subrule.Rule));

              | ListWithSeparator as subrule =>
                url = makeClusterRef(makeClusterName(parseRecordStart.Pos, subrule.Rule));

              | _ => ()
            }
          def color = if (string.IsNullOrEmpty(url)) "ghostwhite" else "honeydew";
          nodes.Add($<# $nodeName [label="$label" shape=box style=filled color=black fillcolor=$color $url]; #>);

          foreach (caller in parsedSequence.Callers)
          {
            def callerClusterName = match (caller)
            {
              | ParsedSequence as caller => makeClusterName(caller.Start.Pos, caller.Start.Sequence)
              | ParsedExtensions as caller => makeExtensibleClusterName(caller.Start.Pos, caller.Start.ParserData)
              | _ => assert3(false)
            }
            def callerLabel = escape(caller.ToString());
            def clusterRef = makeClusterRef(callerClusterName);
            callerNodes.Add($<# $callerClusterName [label="$callerLabel" shape=box style=filled color=black fillcolor=moccasin $clusterRef]; #>);
            callerNodes.Add($<# $callerClusterName -> $clusterName [lhead = $clusterName]; #>);
          }
        }
        def subgraph =
        $<#
          subgraph $clusterName
          {
            bgcolor=ghostwhite;
            label="$label";
            ..$(nodes; "\n            ")
            ..$(edges; "\n            ")
          }
          ..$(callerNodes; "\n            ")
        #>;
        (clusterName, subgraph)
      }
      def renderGraph(body : string) : string
      {
        $<#
digraph RecoveryParser
{
  rankdir=LR;
  compound=true;
  $body
}
        #>
      }

      foreach (sequence in Ast.Values)
        foreach (parseRecordStart when !parseRecordStart.Record.IsComplete in sequence.Records)
        {
          match (parseRecordStart.Record.CurSubrule)
          {
            | SimpleCall as subrule =>
              sequence.AddCaller(Ast.Get(SequenceStart(parseRecordStart.Pos, subrule.RuleParser.Reflection(subrule.RuleParser.RuleId))))

            | ExtensibleCall as subrule =>
              sequence.AddCaller(ExtensionsAst.Get(ExtensibleStart(parseRecordStart.Pos, subrule.RuleParser.ParserData)))

            | Option as subrule =>
              sequence.AddCaller(Ast.Get(SequenceStart(parseRecordStart.Pos, subrule.Rule)))

            | List as subrule =>
              sequence.AddCaller(Ast.Get(SequenceStart(parseRecordStart.Pos, subrule.Rule)))

            | ListWithSeparator as subrule =>
              sequence.AddCaller(Ast.Get(SequenceStart(parseRecordStart.Pos, subrule.Rule)))

            | _ => ()
          }
        }

      foreach ((sequenceStart, subrules) when !sequenceStart.Sequence.IsExtension in Ast.KeyValuePairs)
      {
        def (name, c) = renderCluster(subrules);
        files.Add(name, renderGraph(c));
      }

      foreach ((extensibleStart, subrules) in ExtensionsAst.KeyValuePairs)
      {
        def groupBySequenceStartPos = subrules.Sequences.Grouping(s => (s.Start.Pos, s));
        def extensibleClusterName = makeExtensibleClusterName(extensibleStart.Pos, extensibleStart.ParserData);
        def label = escape($"$(extensibleStart.Pos) $(extensibleStart.ParserData.Descriptor.Name)");
        def extensibleClusterSubclusters = SCG.List();
        foreach ((pos, clusters) in groupBySequenceStartPos.KeyValuePairs)
        {
          def extensionClusterName = makeExtensionClusterName(extensibleClusterName, pos);
          def extensionClusters = SCG.List();
          foreach (subCluster in clusters)
          {
            def (_, c) = renderCluster(subCluster);
            extensionClusters.Add(c);
          }
          extensibleClusterSubclusters.Add($<#
            subgraph $extensionClusterName
            {
              bgcolor=cornsilk;
              label="";
              $(makeClusterNode(extensionClusterName))
              ..$(extensionClusters; "\n              ")
            }
          #>);
        }
        files.Add(extensibleClusterName, renderGraph($<#
          subgraph $extensibleClusterName
          {
            bgcolor=ivory;
            label="$label";
            $(makeClusterNode(extensibleClusterName))
            ..$(extensibleClusterSubclusters; "\n            ")
          }
        #>));
      }

      filesToOpen.Add(files.Keys.First());
      (filesToOpen, files)*/
    }
  }
}
