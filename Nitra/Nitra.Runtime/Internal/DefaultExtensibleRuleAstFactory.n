using Nitra.Runtime;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Reflection;
using System.Reflection.Emit;

namespace Nitra.Internal
{
  public sealed class DefaultExtensibleRuleAstFactory[T] : IExtensibleRuleAstFactory[T]
  {
    public static Instance : this = DefaultExtensibleRuleAstFactory();

    private this() { }

    public CreateAmbiguous(parseResult : ParseResult, ruleDescriptor : RuleDescriptor, startPos : int, endPos : int, ambiguitys : array[T], parsers : array[ExtensionRuleParser], ambiguityType : ParseTreeAmbiguityType) : T
    {
      ignore(parseResult, ruleDescriptor, startPos, endPos, parsers);
      AmbiguousConstructor(ambiguitys, ambiguityType)
    }

    public CreateMissing(parseResult : ParseResult, ruleDescriptor : RuleDescriptor, pos : int) : T
    {
      _ = ruleDescriptor;
      MissingConstructor(parseResult, pos)
    }

    public CreatePrefix(pos : ref int, astPtr : int, ruleParser : ExtensionRuleParser, parseResult : ParseResult) : T
    {
      def astCtor = PrefixConstructor1[ruleParser.Descriptor];
      astCtor(ref pos, astPtr, parseResult)
    }

    public CreatePrefix(startPos : int, endPos : int, recoveredSubrulesIndex : int, ruleParser : ExtensionRuleParser, parseResult : ParseResult) : T
    {
      def astCtor = PrefixConstructor2[ruleParser.Descriptor];
      astCtor(startPos, endPos, recoveredSubrulesIndex, parseResult)
    }

    public CreatePostfix(prefix : T, prefixStartPos : int, pos : ref int, astPtr : int, ruleParser : ExtensionRuleParser, parseResult : ParseResult) : T
    {
      def astCtor = PostfixConstructor1[ruleParser.Descriptor];
      astCtor(prefix, prefixStartPos, ref pos, astPtr, parseResult)
    }

    public CreatePostfix(prefix : T, prefixStartPos : int, startPos : int, endPos : int, recoveredSubrulesIndex : int, ruleParser : ExtensionRuleParser, parseResult : ParseResult) : T
    {
      def astCtor = PostfixConstructor2[ruleParser.Descriptor];
      astCtor(prefix, prefixStartPos, startPos, endPos, recoveredSubrulesIndex, parseResult)
    }

    private static AmbiguousConstructor : Func[array[T], ParseTreeAmbiguityType, T] =
    {
      def ambiguousAstType = typeof(T).GetNestedType("AmbiguousAst");
      assert2(ambiguousAstType != null);

      def signature = array[typeof(array[T]), typeof(ParseTreeAmbiguityType)];
      MakeConstructorDelegate(ambiguousAstType, signature)
    };

    private static MissingConstructor : Func[ISourceSnapshotProvider, int, T] =
    {
      def missingAstType = typeof(T).GetNestedType("MissingAst");
      assert2(missingAstType != null);

      def signature = array[typeof(ISourceSnapshotProvider), typeof(int)];
      MakeConstructorDelegate(missingAstType, signature)
    };

    private delegate PrefixAstCtor1(pos : ref int, astPtr : int, parseResult : ParseResult) : T;

    private static PrefixConstructor1 : SynchronizedCache[RuleDescriptor, PrefixAstCtor1] = SynchronizedCache((ruleDescriptor) =>
    {
      def signature = array[typeof(int).MakeByRefType(), typeof(int), typeof(ParseResult)];
      MakeConstructorDelegate(ReflectionHelper.GetParsedAstType(ruleDescriptor), signature);
    });

    private delegate PrefixAstCtor2(startPos : int, endPos : int, recoveredSubrulesIndex : int, parseResult : ParseResult) : T;

    private static PrefixConstructor2 : SynchronizedCache[RuleDescriptor, PrefixAstCtor2] = SynchronizedCache((ruleDescriptor) =>
    {
      def signature = array[typeof(int), typeof(int), typeof(int), typeof(ParseResult)];
      MakeConstructorDelegate(ReflectionHelper.GetParsedAstType(ruleDescriptor), signature);
    });

    private delegate PostfixAstCtor1(prefix : T, prefixStartPos : int, pos : ref int, astPtr : int, parseResult : ParseResult) : T;

    private static PostfixConstructor1 : SynchronizedCache[RuleDescriptor, PostfixAstCtor1] = SynchronizedCache((ruleDescriptor) =>
    {
      def signature = array[typeof(T), typeof(int), typeof(int).MakeByRefType(), typeof(int), typeof(ParseResult)];
      MakeConstructorDelegate(ReflectionHelper.GetParsedAstType(ruleDescriptor), signature)
    });

    private delegate PostfixAstCtor2(prefix : T, prefixStartPos : int, startPos : int, endPos : int, recoveredSubrulesIndex : int, parseResult : ParseResult) : T;

    private static PostfixConstructor2 : SynchronizedCache[RuleDescriptor, PostfixAstCtor2] = SynchronizedCache((ruleDescriptor) =>
    {
      def signature = array[typeof(T), typeof(int), typeof(int), typeof(int), typeof(int), typeof(ParseResult)];
      MakeConstructorDelegate(ReflectionHelper.GetParsedAstType(ruleDescriptor), signature)
    });

    private static MakeConstructorDelegate[TDelegate](type : Type, signature : array[Type]) : TDelegate
    {
      def ctor = type.GetConstructor(signature);
      assert2(ctor != null);

      def dm = DynamicMethod("", typeof(T), signature, true);
      def il = dm.GetILGenerator();
      for (mutable index = 0; index < signature.Length; ++index)
        il.Emit(OpCodes.Ldarg, index);
      il.Emit(OpCodes.Newobj, ctor);
      il.Emit(OpCodes.Ret);
      dm.CreateDelegate(typeof(TDelegate)) :> TDelegate
    }
  }
}
