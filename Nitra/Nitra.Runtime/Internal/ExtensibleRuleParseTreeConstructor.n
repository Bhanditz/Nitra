using Nitra;
using Nitra.Runtime.Collections;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;

using System;
using SCG = System.Collections.Generic;

namespace Nitra.Internal
{
  public module ExtensibleRuleParseTreeConstructor
  {
    public Create[T](startPos : int, endPos : int, parseResult : ParseResult, bindingPower : int, ruleDescriptor : ExtensibleRuleDescriptor, parseTreeFactory : IExtensibleRuleParseTreeFactory[T]) : T
      where T : ParseTree
    {
      def ruleParser = parseResult.RuleParser.Grammar.GetExtensibleRuleParser(ruleDescriptor, bindingPower);
      if (startPos < endPos || ruleParser.CanParseEmptyString)
      {
        match (parseResult.TryGetRecoveredSequence(startPos, endPos, ruleParser.ParsingSequence))
        {
          | null =>
            mutable pos = startPos;
            Create(ref pos, endPos - startPos, parseResult, bindingPower, ruleDescriptor, parseTreeFactory)

          | Unambiguous(recoveredSubrulesIndex) => CreateImpl(startPos, parseResult, ruleParser, ruleDescriptor, parseTreeFactory, recoveredSubrulesIndex)
          | Ambiguous(indexes) =>
            def ambiguities = array(indexes.Length);
            foreach (recoveredSubrulesIndex in indexes with i)
              ambiguities[i] = CreateImpl(startPos, parseResult, ruleParser, ruleDescriptor, parseTreeFactory, recoveredSubrulesIndex);
            parseTreeFactory.CreateAmbiguous(parseResult, ruleDescriptor, startPos, endPos, ambiguities, null, ParseTreeAmbiguityType.Recovery);
        }
      }
      else
        parseTreeFactory.CreateMissing(parseResult, ruleDescriptor, startPos)
    }

    private CreateImpl[T](startPos : int, parseResult : ParseResult, ruleParser : ExtensibleRuleParser, ruleDescriptor : ExtensibleRuleDescriptor, parseTreeFactory : IExtensibleRuleParseTreeFactory[T], recoveredSubrulesIndex : int) : T
      where T : ParseTree
    {
      def (subruleStarts, parsingSequence) = parseResult.RecoveredSubrules[recoveredSubrulesIndex];
      def states = parsingSequence.States;

      mutable resultParseTree;

      for (mutable i = 0; i < subruleStarts.Length - 1; ++i)
      {
        def begin    = subruleStarts[i].Pos;
        def end      = subruleStarts[i + 1].Pos;
        def state    = states[subruleStarts[i].State];

        match (state)
        {
          | ExtensionPrefix =>
            assert(resultParseTree == null);

            resultParseTree =
              if (begin == end)
                parseTreeFactory.CreateMissing(parseResult, ruleDescriptor, begin);
              else
              {
                def prefixBuffer = LightList();
                def prefixParsers = LightList();

                foreach (prefix in ruleParser.PrefixRules)
                {
                  match (parseResult.TryGetRecoveredSequence(begin, end, prefix.ParsingSequence))
                  {
                    | null => ()
                    | Unambiguous(recoveredSubrulesIndex) =>
                      prefixBuffer.Add(parseTreeFactory.CreatePrefix(begin, end, recoveredSubrulesIndex, prefix, parseResult));
                      prefixParsers.Add(prefix);

                    | Ambiguous(indexes) =>
                      foreach (recoveredSubrulesIndex in indexes with n)
                      {
                        prefixBuffer.Add(parseTreeFactory.CreatePrefix(begin, end, recoveredSubrulesIndex, prefix, parseResult));
                        prefixParsers.Add(prefix);
                      }
                  }
                }

                match (prefixBuffer.Count)
                {
                  | 0 => assert(false)
                  | 1 => prefixBuffer.FirstItem
                  | _ => parseTreeFactory.CreateAmbiguous(parseResult, ruleDescriptor, begin, end, prefixBuffer.ToArray(), prefixParsers.ToArray(), ParseTreeAmbiguityType.Prefix)
                }
              };

          | ExtensionPostfix =>
            assert(resultParseTree != null);

            def postfixBuffer = LightList();
            def postfixParsers = LightList();

            foreach (postfix when (postfix.RuleId >= ruleParser.FirstPostfixRuleId) in ruleParser.PostfixRules)
            {
              match (parseResult.TryGetRecoveredSequence(begin, end, postfix.ParsingSequence))
              {
                | null => ()
                | Unambiguous(recoveredSubrulesIndex) =>
                  postfixBuffer.Add(parseTreeFactory.CreatePostfix(resultParseTree, startPos, begin, end, recoveredSubrulesIndex, postfix, parseResult));
                  postfixParsers.Add(postfix);

                | Ambiguous(indexes) =>
                  foreach (recoveredSubrulesIndex in indexes with n)
                  {
                    postfixBuffer.Add(parseTreeFactory.CreatePostfix(resultParseTree, startPos, begin, end, recoveredSubrulesIndex, postfix, parseResult));
                    postfixParsers.Add(postfix);
                  }
              }
            }

            resultParseTree =
              match (postfixBuffer.Count)
              {
                | 0 => assert(false)
                | 1 => postfixBuffer.FirstItem
                | _ => parseTreeFactory.CreateAmbiguous(parseResult, ruleDescriptor, begin, end, postfixBuffer.ToArray(), postfixParsers.ToArray(), ParseTreeAmbiguityType.Postfix)
              };

          | _ => assert(false, "ExtensionPrefix or ExtensionPostfix expected");
        }
      }

      assert(resultParseTree != null);
      resultParseTree
    }

    public Create[T](pos : ref int, size : int, parseResult : ParseResult, bindingPower : int, ruleDescriptor : ExtensibleRuleDescriptor, parseTreeFactory : IExtensibleRuleParseTreeFactory[T]) : T
    {
      def ruleParser = parseResult.RuleParser.Grammar.GetExtensibleRuleParser(ruleDescriptor, bindingPower);
      if (size > 0 || ruleParser.CanParseEmptyString)
      {
        mutable newPos;
        mutable rawTreePtr = ruleParser.FindFirstPrefix(ref pos, parseResult);
        mutable prefixValue;
        def prefixStartPos = pos;
        if (rawTreePtr > 0)
        {
          def prefixParserOffset = GetExtensionParserOffset(ruleParser.PrefixRules);

          mutable extensionParser = GetExtensionParser(parseResult, ruleParser.ParserData.PrefixParsers, rawTreePtr, prefixParserOffset);
          newPos = pos;
          prefixValue = parseTreeFactory.CreatePrefix(ref newPos, rawTreePtr, extensionParser, parseResult);
          rawTreePtr = ruleParser.FindNextPrefix(rawTreePtr, parseResult);
          when (rawTreePtr > 0)
          {
            def firstNewPos = newPos;
            def ambiguitys = SCG.List();
            ambiguitys.Add(prefixValue);
            def parsers = SCG.List();
            parsers.Add(extensionParser);
            while (rawTreePtr > 0)
            {
              def nextExtensionParser = GetExtensionParser(parseResult, ruleParser.ParserData.PrefixParsers, rawTreePtr, prefixParserOffset);
              newPos = pos;
              def _nextValue = parseTreeFactory.CreatePrefix(ref newPos, rawTreePtr, nextExtensionParser, parseResult);
              assert(newPos == firstNewPos);
              ambiguitys.Add(_nextValue);
              parsers.Add(nextExtensionParser);
              extensionParser = nextExtensionParser;
              rawTreePtr = ruleParser.FindNextPrefix(rawTreePtr, parseResult);
            }
            // prefix ambiguity
            prefixValue = parseTreeFactory.CreateAmbiguous(parseResult, ruleDescriptor, prefixStartPos, newPos, ambiguitys.ToArray(), parsers.ToArray(), ParseTreeAmbiguityType.Prefix);
          }
          pos = newPos;
        }
        else
        {
          // missing prefix
          prefixValue = parseTreeFactory.CreateMissing(parseResult, ruleDescriptor, prefixStartPos);
        }

        def postfixParserOffset = GetExtensionParserOffset(ruleParser.PostfixRules);
        mutable extensionParser;
        mutable postfixValue;
        while (pos < parseResult.ParseEnd && { rawTreePtr = ruleParser.FindFirstPostfix(ref pos, parseResult); rawTreePtr > 0 })
        {
          extensionParser = GetExtensionParser(parseResult, ruleParser.ParserData.PostfixParsers, rawTreePtr, postfixParserOffset);
          newPos = pos;
          postfixValue = parseTreeFactory.CreatePostfix(prefixValue, prefixStartPos, ref newPos, rawTreePtr, extensionParser, parseResult);
          when (newPos == pos)
            break;
          rawTreePtr = ruleParser.FindNextPostfix(rawTreePtr, parseResult);
          when (rawTreePtr > 0)
          {
            def firstNewPos = newPos;
            def ambiguitys = SCG.List();
            ambiguitys.Add(postfixValue);
            def parsers = SCG.List();
            parsers.Add(extensionParser);
            while (rawTreePtr > 0)
            {
              def nextExtensionParser = GetExtensionParser(parseResult, ruleParser.ParserData.PostfixParsers, rawTreePtr, postfixParserOffset);
              newPos = pos;
              def _nextValue = parseTreeFactory.CreatePostfix(prefixValue, prefixStartPos, ref newPos, rawTreePtr, nextExtensionParser, parseResult);
              assert(newPos == firstNewPos);
              ambiguitys.Add(_nextValue);
              parsers.Add(nextExtensionParser);
              extensionParser = nextExtensionParser;
              rawTreePtr = ruleParser.FindNextPostfix(rawTreePtr, parseResult);
            }
            // postfix ambiguity
            postfixValue = parseTreeFactory.CreateAmbiguous(parseResult, ruleDescriptor, prefixStartPos, newPos, ambiguitys.ToArray(), parsers.ToArray(), ParseTreeAmbiguityType.Postfix);
          }
          prefixValue = postfixValue;
          pos = newPos;
        }

        prefixValue
      }
      else
        parseTreeFactory.CreateMissing(parseResult, ruleDescriptor, pos)
    }

    public GetExtensionParserOffset(parsers : array[ExtensionRuleParser]) : int
    {
      //FIXME empty array
      if (parsers.Length > 0) parsers[0].RuleId else 0
    }

    public GetExtensionParser(parseResult : ParseResult, parsers : array[ExtensionRuleParser], rawTreePtr : int, offset : int) : ExtensionRuleParser
    {
      def ruleId = parseResult.rawTree[rawTreePtr + ExtensibleRuleParser.RawTreeOfs.Id] & ExtensibleRuleParser.RawTreeMask.Id;
      parsers[ruleId - offset]
    }
  }
}
