using Nitra.Internal;
using Nitra.Internal.Recovery;
using Nitra.Runtime;
using Nitra.Runtime.Reflection;

using Nemerle;
using Nemerle.Imperative;
using Nemerle.Collections;
using Nemerle.Late;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Diagnostics;
using System.Linq;
using System.Collections.ObjectModel;

using SCG = System.Collections.Generic;

namespace Nitra
{
  public partial class ParseResult
  {
    private _extensionHandleCache : SCG.Dictionary[ExtensionRuleParser * int, AstHandle.Extension] = SCG.Dictionary();
    public AstHandle_Extension(parentFrame : RecoveryStackFrame, ruleParser : ExtensionRuleParser, astPtr : int, textPos : int) : AstHandle.Extension
    {
      def parent = match (parentFrame)
      {
        | ExtensiblePrefix  as frame => frame.AstHandle
        | ExtensiblePostfix as frame => frame.AstHandle
        | _                          => assert3(false)
      }
      AstHandle_Extension(parent, ruleParser, astPtr, textPos)
    }

    public AstHandle_Extension(parent : AstHandle, ruleParser : ExtensionRuleParser, astPtr : int, textPos : int) : AstHandle.Extension
    {
      mutable handle;
      def key = (ruleParser, textPos);
      unless (_extensionHandleCache.TryGetValue(key, out handle))
      {
        handle = AstHandle.Extension(astPtr, textPos, parent, ruleParser);
        _extensionHandleCache.Add(key, handle);
        def children = match (parent)
        {
          | ExtensiblePrefix  as parent => parent.Children
          | ExtensiblePostfix as parent => parent.Children
          | _                           => assert3(false)
        }
        children.Add(handle);
        //Знаю что пузырёк
        //Но в нормальных условиях этот цикл не выполнится ни разу
        for (mutable i = children.Count - 2; i >= 0; --i)
          if (children[i].RuleParser.RuleId < children[i + 1].RuleParser.RuleId)
            break;
          else
            children[i] <-> children[i + 1];
      }
      when (astPtr > 0)
        handle.AstPtr = astPtr;
      handle
    }

    private _extensiblePrefixHandleCache : SCG.Dictionary[ExtensibleRuleParserData * int, AstHandle.ExtensiblePrefix] = SCG.Dictionary();
    public AstHandle_Prefix(ruleParserData : ExtensibleRuleParserData, mutable astPtr : int, textPos : int) : AstHandle.ExtensiblePrefix
    {
      mutable handle;
      def key = (ruleParserData, textPos);
      unless (_extensiblePrefixHandleCache.TryGetValue(key, out handle))
      {
        handle = AstHandle.ExtensiblePrefix(astPtr, textPos, ruleParserData);
        _extensiblePrefixHandleCache.Add(key, handle)
      }
      when (handle.AstPtr <= 0)
      {
        astPtr = TryGetAst(textPos, ruleParserData.PrefixId);
        when (astPtr > 0)
          handle.AstPtr = astPtr;
      }
      handle
    }

    private _extensiblePostfixHandleCache : SCG.Dictionary[ExtensibleRuleParserData * int, AstHandle.ExtensiblePostfix] = SCG.Dictionary();
    public AstHandle_Postfix(ruleParserData : ExtensibleRuleParserData, mutable astPtr : int, textPos : int) : AstHandle.ExtensiblePostfix
    {
      mutable handle;
      def key = (ruleParserData, textPos);
      unless (_extensiblePostfixHandleCache.TryGetValue(key, out handle))
      {
        handle = AstHandle.ExtensiblePostfix(astPtr, textPos, ruleParserData);
        _extensiblePostfixHandleCache.Add(key, handle)
      }
      when (handle.AstPtr <= 0)
      {
        astPtr = TryGetAst(textPos, ruleParserData.PostfixId);
        when (astPtr > 0)
          handle.AstPtr = astPtr;
      }
      handle
    }

    private _simpleHandleCache : SCG.Dictionary[int * int, AstHandle.Simple] = SCG.Dictionary();
    public AstHandle_Simple(ruleParser : SimpleRuleParser, mutable astPtr : int, textPos : int) : AstHandle.Simple
    {
      mutable handle;
      def key = (ruleParser.RuleId, textPos);
      unless (_simpleHandleCache.TryGetValue(key, out handle))
      {
        handle = AstHandle.Simple(astPtr, textPos, ruleParser);
        _simpleHandleCache.Add(key, handle)
      }
      when (handle.AstPtr <= 0)
      {
        astPtr = TryGetAst(textPos, ruleParser.RuleId);
        when (astPtr > 0)
          handle.AstPtr = astPtr;
      }
      handle
    }

    private _subruleHandleCache : SCG.Dictionary[int * int, AstHandle.Subrule] = SCG.Dictionary();
    public AstHandle_Subrule(ruleParser : IRecoveryRuleParser, ruleId : int, mutable astPtr : int, textPos : int) : AstHandle.Subrule
    {
      mutable handle;
      def key = (ruleId, textPos);
      unless (_subruleHandleCache.TryGetValue(key, out handle))
      {
        handle = AstHandle.Subrule(astPtr, textPos, ruleParser, ruleId);
        _subruleHandleCache.Add(key, handle)
      }
      when (handle.AstPtr <= 0)
      {
        astPtr = TryGetAst(textPos, ruleId);
        when (astPtr > 0)
          handle.AstPtr = astPtr;
      }
      handle
    }
  }
}
