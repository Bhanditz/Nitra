using Nemerle;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;

using Nitra.Internal.Recovery;
using Nitra.Runtime.Reflection;

namespace Nitra.Internal
{
  [Record]
  internal struct RecoveryModeAstReaderContext[TLoader, TItem]
    where TLoader : IParseTree
    where TItem : TLoader
  {
    public ItemLoader     : IAstSequenceLoader[TLoader];
    public ItemSequenceId : int;
    public ItemConverter  : IAstCast[TLoader, TItem];
  }

  internal class RecoveryModeListAstReader[TLoader, TItem] : RecoveryModeBaseListAstReader[RecoveryModeAstReaderContext[TLoader, TItem], SCG.List[TItem], ListAst[TItem]]
    where TLoader : IParseTree
    where TItem   : TLoader
  {
    public static Instance : this;

    static this() { Instance = RecoveryModeListAstReader() }

    protected sealed override MakeAmbiguousAst(ambiguities : array[ListAst[TItem]], context : RecoveryModeAstReaderContext[TLoader, TItem]) : ListAst[TItem]
    {
      _ = context;
      AmbiguousListAst(ambiguities)
    }

    protected sealed override MakeEmptyAst(parseResult : ParseResult, pos : int, context : RecoveryModeAstReaderContext[TLoader, TItem]) : ListAst[TItem]
    {
      _ = context;
      EmptyListAst(Location(parseResult, pos, pos))
    }

    protected sealed override CreateListBuffer() : SCG.List[TItem]
    {
      SCG.List()
    }

    protected override ReadListItem(stateNumber : int, parseResult : ParseResult, parsingSequence : ParsingSequence, startPos : int, endPos : int, listBuffer : SCG.List[TItem], context : RecoveryModeAstReaderContext[TLoader, TItem]) : void
    {
      _ = stateNumber;
      RecoveryModeSequenceAstReader.Instance.Read(parseResult, parsingSequence, startPos, endPos, RecoveryModeSequenceAstReaderContext(context.ItemLoader, context.ItemSequenceId))
        |> context.ItemConverter.Cast
        |> listBuffer.Add;
    }

    protected sealed override CreateListAst(parseResult : ParseResult, startPos : int, endPos : int, listBuffer : SCG.List[TItem]) : ListAst[TItem]
    {
      ConcreteListAst(Location(parseResult, startPos, endPos), listBuffer)
    }
  }
}
