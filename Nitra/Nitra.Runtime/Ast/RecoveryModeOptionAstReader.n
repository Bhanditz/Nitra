using Nemerle;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;

using Nitra.Internal.Recovery;
using Nitra.Runtime.Reflection;

namespace Nitra.Internal
{
  internal sealed class RecoveryModeOptionAstReader[TLoader, TItem] : RecoveryModeParseTreeReader[RecoveryModeAstReaderContext[TLoader, TItem], OptionAst[TItem]]
    where TLoader : IParseTree
    where TItem   : TLoader
  {
    public static Instance : this;

    static this() { Instance = RecoveryModeOptionAstReader() }

    protected override MakeEmptyAst(parseResult : Nitra.ParseResult, pos : int, context : RecoveryModeAstReaderContext[TLoader, TItem]) : OptionAst[TItem]
    {
      _ = context;
      OptionAst.None(Location(parseResult, pos, pos))
    }

    protected override MakeAmbiguousAst(ambiguities : array[Nitra.OptionAst[TItem]], context : RecoveryModeAstReaderContext[TLoader, TItem]) : Nitra.OptionAst[TItem]
    {
      _ = context;
      AmbiguousOptionAst(ambiguities)
    }

    protected override ReadAst(parseResult : Nitra.ParseResult, startPos : int, endPos : int, recoveredSubruleIndex : int, context : RecoveryModeAstReaderContext[TLoader, TItem]) : OptionAst[TItem]
    {
      def item = context.ItemLoader.ReadSequence(context.ItemSequenceId, recoveredSubruleIndex) |> context.ItemConverter.Cast;
      OptionAst.Some(Location(parseResult, startPos, endPos), item)
    }

    protected override ReadAstStandardMode(parseResult : ParseResult, startPos : int, endPos : int, context : RecoveryModeAstReaderContext[TLoader, TItem]) : OptionAst[TItem]
    {
      mutable pos = startPos;
      def item = context.ItemLoader.ReadSequence(context.ItemSequenceId, ref pos) |> context.ItemConverter.Cast;
      assert(pos == endPos);
      OptionAst.Some(Location(parseResult, startPos, endPos), item)
    }
  }
}
