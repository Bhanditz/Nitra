using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;

using System;
using System.Diagnostics;
using SCG = System.Collections.Generic;

namespace Nitra
{
  [DebuggerDisplay("Length = {Length}")]
  public sealed class ConcreteListAst[T] : ListAst[T]
    where T : IParseTree
  {
    private static applyItems : bool = CanApplyVisitor.[T]();

    public this(location : Location, items : array[T])
    {
      this.Location = location;
      this.items    = items;
    }

    public this(location : Location, items : SCG.List[T])
    {
      this(location, items.ToArray())
    }

    [DebuggerBrowsable(DebuggerBrowsableState.RootHidden)]
    private         items             : array[T];
    public override Location          : Nitra.Location     { get }
    public override Length            : int                { get items.Length }
    public override Item[index : int] : T                  { get items[index] }

    public override Apply(visitor : AstVisitor) : Ast
    {
      visitor.Enter(this);
      mutable result = this;
      mutable isDirty;
      when (applyItems)
      {
        mutable newItems;
        for (mutable i = 0; i < items.Length; ++i)
        {
          def newItem = visitor.Visit(items[i] :> Ast) :> T;
          unless (ReferenceEquals(newItem, items[i]))
          {
            isDirty = true;
            when (newItems == null)
            {
              newItems = array(items.Length);
              Array.Copy(items, newItems, items.Length);
            }
            newItems[i] = newItem;
          }
        }
        when (isDirty)
          result = ConcreteListAst(this.Location, newItems);
      }
      visitor.Leave(this, result);
      result
    }

    public override UpdateItems(newItems : SCG.List[T]) : ListAst[T]
    {
      if (newItems.Count != items.Length)
        ConcreteListAst(this.Location, newItems)
      else
      {
        for (mutable i = 0; i < items.Length; ++i)
          unless (ReferenceEquals(newItems[i], items[i]))
            return ConcreteListAst(this.Location, newItems);
        this
      }
    }

    public override GetEnumerator() : SCG.IEnumerator[T]
    {
      (items : SCG.IEnumerable[T]).GetEnumerator()
    }
  }
}
