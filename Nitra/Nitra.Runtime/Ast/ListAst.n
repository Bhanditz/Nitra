using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;

namespace Nitra
{
  public sealed class ListAst[T] : Ast where T : IAst
  {
    private static applyItems : bool = !typeof(T).IsValueType;

    public this(location : Location, items : array[T])
    {
      this.Location = location;
      this.items    = items;
    }

    private         items             : array[T];
    public override Location          : Nitra.Location     { get }
    public          Items             : SCG.IEnumerable[T] { get items }
    public          Count             : int                { get items.Length }
    public          Item[index : int] : T                  { get items[index] }

    public override Apply(visitor : AstVisitor) : Ast
    {
      visitor.Enter(this);
      mutable result = this;
      mutable isDirty;
      when (applyItems)
      {
        mutable newItems;
        for (mutable i = 0; i < items.Length; ++i)
        {
          def newItem = visitor.Visit(items[i] :> Ast) :> T;
          unless (ReferenceEquals(newItem, items[i]))
          {
            isDirty = true;
            when (newItems == null)
            {
              newItems = array(items.Length);
              Array.Copy(items, newItems, items.Length);
            }
            newItems[i] = newItem;
          }
        }
        when (isDirty)
          result = ListAst(this.Location, newItems);
      }
      visitor.Leave(result, false);
      result
    }

    public override Write(astWriter : Nitra.Serialization.IAstWriter) : void
    {
      _ = astWriter;
      throw System.NotImplementedException()
    }
  }
}
