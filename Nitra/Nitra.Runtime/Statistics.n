using Nemerle;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Diagnostics;
using System.Linq;

using NC = Nemerle.Collections;

namespace Nitra
{
  [Record]
  public variant StatisticsTask
  {
    public Id     : string { get; }
    public Title  : string { get; }
    public abstract Total : TimeSpan { get; }
      
    | Single
      {
        [RecordIgnore] private _timer : Stopwatch = Stopwatch();
        public Elapsed : TimeSpan { get { _timer.Elapsed } }
        public override Total : TimeSpan { get { Elapsed } }

        public Start() : void { _timer.Start(); }
        public Stop()  : void { _timer.Stop(); }
      }
        
    | Container
      {
        [RecordIgnore] private _tasks : Dictionary[string, StatisticsTask] = Dictionary();
        [RecordIgnore] public  Tasks  : ICollection[StatisticsTask] { get { _tasks.Values } }
      
        public override Total : TimeSpan
        {
          get
          {
            mutable result;
            foreach (t in _tasks.Values)
              result += t.Total;
            result
          }
        }
          
        public Items[id : string] : StatisticsTask
        {
          get
          {
            mutable result;
            _ = _tasks.TryGetValue(id, out result);
            result
          }
        }
          
        public AddSubtask(task : StatisticsTask) : void
        {
          _tasks.Add(task.Id, task);
        }
      }
    }
}
