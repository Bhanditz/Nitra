using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra.ClientServer.Messages;
using Nitra.ClientServer.Server.ProjectSystem;

using System;
using System.IO;
using System.Threading;
using System.Globalization;
using System.Collections.Generic;
using System.Collections.Concurrent;
using System.Linq;

namespace Nitra.ClientServer.Server
{
  class Router
  {
    public Logger : TextWriter;
    _mainQueue: BlockingCollection[RouterMessage] = BlockingCollection();
    _parseQueue: BlockingCollection[ParserMessage] = BlockingCollection();
    _mainThread: Thread;
    _parseWorkers: list[ParseWorker];
    _cts = CancellationTokenSource();
    mutable _stopped: bool;
    mutable _solution : Solution;

    public this()
    {
      Logger = Console.Out;
      _mainThread = Thread(RouterThreadMain);
      _mainThread.Start();
      _parseWorkers = $[1..Environment.ProcessorCount].Map(_ => ParseWorker(_parseQueue));
    }

    RouterThreadMain() : void
    {
      def thread = Thread.CurrentThread;
      thread.Name = "ServerPipeReader";
      thread.CurrentCulture = CultureInfo.InvariantCulture;

      try Router()
      catch
      {
        | OperationCanceledException => ()
        | e => Error("Error: " + e)
      }
    }
    
    Router() : void
    {
      foreach(msg in _mainQueue.GetConsumingEnumerable(_cts.Token))
      {
        | serverMsg is ServerMessage =>
          Log("Got \"" + serverMsg.GetType().Name + "\"");
          match (serverMsg)
          {
            | ServerMessage.CheckVersion(assemblyVersionGuid) =>
              if (Message.AssemblyVersionGuid == assemblyVersionGuid)
                Log("Client version checked.");
              else
              {
                def asm = typeof(Message);
                Error($"Incorrect version of messaging assembly: '$(asm.Assembly.Location)'.");
                Environment.Exit(42);
              }
            | SolutionStartLoading(fullPath)                  => assert2(_solution == null); _solution = Solution(fullPath, Logger);
            | SolutionLoaded                                  => _solution.Open();
            | SolutionUnloaded                                => assert2(_solution != null); _solution = null;
            | ProjectStartLoading(id, fullPath)               => _ = Project(_solution, fullPath, id);
            | ProjectLoaded      (id)                         => _solution[id].Open();
            | ProjectUnloaded    (id)                         => _solution.RemoveProject(id);
            | ProjectRaname      (oldId, newId, newPath)      => Log("Not supported yet.");
            | ReferenceLoaded    (projectId, fullPath, kind)  => _solution[projectId].AddLib(fullPath, kind);
            | ReferenceUnloaded  (projectId, fullPath, kind)  => _solution[projectId].RemoveLib(fullPath, kind);
            | FileLoaded         (projectId, fullPath, id)    => _ = File(id, fullPath, _solution[projectId]);
            | FileInMemoryLoaded (projectId, id, name, text)  => _ = File(id, name, _solution[projectId], text);
            | FileUnloaded       (id)                         => _solution.GetFile(id).Parent.RemoveFile(id);
            | FileRanamed        (oldId, newId, newPath)      => Log("Not supported yet.");
            | FileChanged        (id, change)                 => _solution.GetFile(id).Change(change)
            | FileChangedBatch   (id, changes)                => _solution.GetFile(id).Change(changes)
            | Shutdown                                        => _cts.Cancel()
            | _                                               => assert2(false)
          }
        | _ => assert2(false)
      }
    }
    
    public Wait() : void
    {
      unless (_stopped)
      {
        _mainThread.Join();
        _cts.Dispose();
        foreach(worker in _parseWorkers)
          worker.Dispose();
        _parseQueue.Dispose();
        _stopped = true;
      }
    }
    
    public AddMessage(msg : RouterMessage): void
    {
      unless (_stopped)
        _mainQueue.Add(msg)
    }
    
    Error(msg : string) : void
    {
      Console.ForegroundColor = ConsoleColor.Red;
      Console.WriteLine(msg);
      Console.ResetColor();
    }
    
    Log(msg : string) : void { Console.WriteLine(msg) }
  }
}
