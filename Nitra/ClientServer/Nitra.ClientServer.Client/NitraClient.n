using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra.ClientServer.Messages;

using System;
using System.Collections.Concurrent;
using System.Console;
using System.Collections.Generic;
using System.Collections.Immutable;
using System.Diagnostics;
using System.Globalization;
using System.IO;
using System.IO.Pipes;
using System.Linq;
using System.Reflection;
using System.Threading;

namespace Nitra.ClientServer.Client
{
  using Nitra.ClientServer.Messages.AsyncServerMessage;
  
  public sealed class NitraClient : IDisposable
  {
    public  StringManager    : StringManager;
            MaintPipeName    : string = Guid.NewGuid().ToString();
            _mainPipeClient  : NamedPipeClientStream;
            _serverProc      : Process;
            _mainPipeWriter  : BinaryWriter;
    mutable _isDisposed      : bool;
    public  ResponseMap      : ConcurrentDictionary[int, Action[AsyncServerMessage]] = ConcurrentDictionary();
            _mainPipeReader  : BinaryReader;
    
    public this([NotNull] stringManager : StringManager)
    {
      StringManager             = stringManager;
      def executingAssemblyPath = Assembly.GetExecutingAssembly().Location;
      def assembliesPath        = Path.GetDirectoryName(executingAssemblyPath);
      def serverPath            = Path.Combine(assembliesPath, "Nitra.ClientServer.Server.exe");
      _serverProc               = Process.Start(serverPath, MaintPipeName);
      assert2(_serverProc != null);
      Log("Server started.");
      _mainPipeClient           = NamedPipeClientStream(".", MaintPipeName, PipeDirection.InOut); 
      _mainPipeWriter           = BinaryWriter(_mainPipeClient);
      _mainPipeReader           = BinaryReader(_mainPipeClient);
      _mainPipeClient.Connect();
      Log("Connected to server started.");

      def readerThread          = Thread(AsyncResponseReaderThreadMain);
      readerThread.IsBackground = true;
      readerThread.Start();
    }
    
    AsyncResponseReaderThreadMain() : void
    {
      def responsePipet  = NamedPipeClientStream(".", MaintPipeName + Constants.AsyncResponsePipeSuffix, PipeDirection.In); 
      def asyncResponseReader = BinaryReader(responsePipet);
      responsePipet.Connect();
      
      while (!_isDisposed && responsePipet.IsConnected)
      {
        try
        {
          def msg = Deserializer.Deserialize.[AsyncServerMessage](asyncResponseReader);
          Trace.WriteLine(msg);
          
          mutable handler;
          if (ResponseMap.TryGetValue(msg.FileId, out handler))
          {
            handler(msg);
          }
          else if (ResponseMap.TryGetValue(-1, out handler))
          {
            handler(msg);
          }
          else
          {
            //assert2(false);
          }
        }
        catch
        {
          | _ is EndOfStreamException =>
            Trace.WriteLine("AsyncResponseReader disconnected.");
            return;
            
          | e => Trace.WriteLine("AsyncResponseReader exception: " + e);
        }
      }
    }

    public Send([NotNull] msg : ClientMessage) : void
    {
      when (!_mainPipeClient.IsConnected)
      {
        assert2(true);
      }
      msg.Serialize(_mainPipeWriter);
    }
    
    public Receive[T]() : T
      where T: ServerMessage
    {
      def msg = Deserializer.Deserialize.[T](_mainPipeReader);
      msg
    }
    
    public Flush() : void
    {
      _mainPipeWriter.Flush();
    }
    
    public Dispose() : void
    {
      when (_isDisposed)
        return;
        
      Send(Messages.ClientMessage.Shutdown());
      
      _mainPipeClient.Dispose();
      _isDisposed = true;
      GC.SuppressFinalize(this);
    }
    
    protected override Finalize() : void
    {
      Dispose();
    }
    
    Log(msg : string, params args: array[object]) : void
    {
      WriteLine(msg, args);
    }
  }
}
