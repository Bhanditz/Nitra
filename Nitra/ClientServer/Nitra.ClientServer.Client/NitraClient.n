using System.Reflection;
using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra.ClientServer.Messages;

using System;
using System.Collections.Generic;
using System.Diagnostics;
using System.IO;
using System.IO.Pipes;
using System.Linq;

namespace Nitra.ClientServer.Client
{
  using Nitra.ClientServer.Messages.ServerMessage;
  
  public sealed class NitraClient : IDisposable
  {
    public StringManager : StringManager;
    public PipeName      : string = Guid.NewGuid().ToString();
    
    mutable _isDisposed : bool;
    
    _serverProc : Process;
    _pipeServer : NamedPipeServerStream;
    _writer     : BinaryWriter;
    
    public this(stringManager : StringManager)
    {
      StringManager = stringManager;
      def executingAssemblyPath = Assembly.GetExecutingAssembly().Location;
      def assembliesPath        = Path.GetDirectoryName(executingAssemblyPath);
      def serverPath            = Path.Combine(assembliesPath, "Nitra.ClientServer.Server.exe");
      def serverProc            = Process.Start(serverPath, PipeName);
      assert(serverProc != null);
      _serverProc = serverProc;
      _pipeServer = NamedPipeServerStream(PipeName, PipeDirection.InOut);
      _pipeServer.WaitForConnection();
      _writer = BinaryWriter(_pipeServer);
      Send(Messages.ServerMessage.CheckVersion(Message.AssemblyVersionGuid));
    }
    
    SendProjectStartLoading(fullPath : string)                                        : void { Send(ProjectStartLoading(StringManager[fullPath], fullPath)) }
    SendProjectLoaded      (fullPath : string)                                        : void { Send(ProjectLoaded(StringManager[fullPath])) }
    SendProjectUnloaded    (fullPath : string)                                        : void { Send(ProjectUnloaded(StringManager[fullPath])) }
    SendProjectRaname      (oldPath : string, newPath : string)                       : void { Send(ProjectRaname(StringManager[oldPath], StringManager[newPath], newPath)) }
    SendReferenceLoaded    (projectId : int, fullPath : string, kind : ReferenceKind) : void { Send(ReferenceLoaded(projectId, fullPath, kind)) }
    SendReferenceUnloaded  (projectId : int, fullPath : string, kind : ReferenceKind) : void { Send(ReferenceUnloaded(projectId, fullPath, kind)) }
    SendFileLoaded         (projectId : int, fullPath : string)                       : void { Send(FileLoaded(projectId, fullPath, StringManager[fullPath])) }
    SendFileUnloaded       (fullPath : string)                                        : void { Send(FileUnloaded(StringManager[fullPath])) }
    SendFileRanamed        (oldPath : string, newPath : string)                       : void { Send(FileRanamed(StringManager[oldPath], StringManager[newPath], newPath)) }
    SendFileInMemoryLoaded (projectId : int, name : string, content : string)         : void { Send(FileInMemoryLoaded(projectId, StringManager[name], name, content)) }
    
    public Send([NotNull] msg : ServerMessage) : void
    {
      assert2(_pipeServer.IsConnected);
      msg.Serialize(_writer);
      _writer.Flush();
    }
    
    public Dispose() : void
    {
      when (_isDisposed)
        return;
        
      Send(Messages.ServerMessage.Shutdown());
      
      _pipeServer.Dispose();
      _isDisposed = true;
      GC.SuppressFinalize(this);
    }
    
    protected override Finalize() : void
    {
      Dispose();
    }
  }
}
