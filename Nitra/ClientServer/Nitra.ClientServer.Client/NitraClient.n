using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra.ClientServer.Messages;

using System;
using System.Collections.Generic;
using System.Diagnostics;
using System.IO;
using System.IO.Pipes;
using System.Linq;

namespace Nitra.ClientServer.Client
{
  using Nitra.ClientServer.Messages.ServerMessage;
  
  public sealed class NitraClient : IDisposable
  {
    public PathManager : PathManager = PathManager();
    public PipeName    : string = Guid.NewGuid().ToString();
    
    mutable _isDisposed : bool;
    
    _serverProc : Process;
    _pipeServer : NamedPipeServerStream;
    _writer     : BinaryWriter;
    
    public this()
    {
      def serverProc = Process.Start("PipeServer.exe", PipeName);
      assert(serverProc == null);
      _serverProc = serverProc;
      _pipeServer = NamedPipeServerStream(PipeName, PipeDirection.InOut);
      _pipeServer.WaitForConnection();
      _writer = BinaryWriter(_pipeServer);
    }
    
    SendLoadProject       (path : string, projectDir : string)  : void { Send(LoadProject(PathManager[path], path, projectDir)) }
    SendUnloadProject     (path : string)                       : void { Send(UnloadProject(PathManager[path])) }
    SendRanameProject     (oldPath : string, newPath : string)  : void { Send(RanameProject(PathManager[oldPath], PathManager[newPath], newPath)) }
    SendLoadReference     (path : string, kind : ReferenceKind) : void { Send(LoadReference(path, kind)) }
    SendUnloadReference   (path : string, kind : ReferenceKind) : void { Send(UnloadReference(path, kind)) }
    SendLoadFile          (path : string)                       : void { Send(LoadFile(path, PathManager[path])) }
    SendUnloadFile        (path : string)                       : void { Send(UnloadFile(PathManager[path])) }
    SendRanameFile        (oldPath : string, newPath : string)  : void { Send(RanameFile(PathManager[oldPath], PathManager[newPath], newPath)) }
    SendLoadInMemoryFile  (name : string, content : string)     : void { Send(LoadInMemoryFile(PathManager[name], name, content)) }
    SendUnloadInMemoryFile(path : string)                       : void { Send(UnloadInMemoryFile(PathManager[path])) }
    
    public Send([NotNull] msg : ServerMessage) : void
    {
      msg.Serialize(_writer);
      _writer.Flush();
    }
    
    public Dispose() : void
    {
      when (_isDisposed)
        return;
        
      _pipeServer.Dispose();
      
      _isDisposed = true;
      GC.SuppressFinalize(this);
    }
    
    protected override Finalize() : void
    {
      Dispose();
    }
  }
}
