using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Reflection;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Lifting
{
  [MacroUsage(MacroPhase.BeforeInheritance, MacroTargets.Assembly)]
  macro LiftRule(id : string, assemblyName : string, grammarName : string, ruleName : string)
  {
    LiftImpl.LiftRule(id, assemblyName, grammarName, ruleName)
  }

  macro Lift(body, params args : array[PExpr]) : PExpr
    syntax ("lift", "(", args, ")", body)
  {
    match (args.ToNList())
    {
      | [<[ $(id : string) ]>] =>
        def typer = Macros.ImplicitCTX();
        LiftImpl.Lift(id, <[ Nitra.Runtime.ChemicalAstContext() ]>, body, typer)

      | [<[ $(id : string) ]>, context] =>
        def typer = Macros.ImplicitCTX();
        LiftImpl.Lift(id, context, body, typer)

      | _ => Message.FatalError("Expected string rule id and optional ast context expression")
    }
  }

  module LiftImpl
  {
    public LiftRule(id : string, assemblyName : string, grammarName : string, ruleName : string) : void
    {
      def assemblies = ManagerClass.Instance.ReferencedAssemblies.Map(_.Assembly);
      match (assemblies.Find(a => a.GetName().Name == assemblyName))
      {
        | Some(a) =>
          foreach (g when g.FullName == grammarName in GrammarDescriptor.GetDescriptors(a))
            foreach (r is StartRuleDescriptor when r.Name == ruleName in g.Rules)
            {
              if (Parsers.ContainsKey(id))
                Message.FatalError($"Lift rule '$id' already registered")
              else
              {
                def parserHost = ParserHost(() => assemblies);
                Parsers.Add(id, parserHost.DoParsing(_, r))
              }
            }
            otherwise Message.FatalError($"Rule '$ruleName' not found in grammar '$grammarName' from assembly '$a'");
          otherwise Message.FatalError($"Grammar '$grammarName' not found in assembly '$a'");

        | None => Message.FatalError($"Assembly '$assemblyName' is not refrenced");
      }
    }

    public Lift(id : string, context : PExpr, body : PExpr, typer : Typer) : PExpr
    {
      mutable parseCallback;
      if (Parsers.TryGetValue(id, out parseCallback))
      {
        match (body)
        {
          | Literal(Literal.String(RawString = rawString)) when rawString.StartsWith("<#") =>
            def originalSource = SourceSnapshot(rawString, body.Location.FileIndex, Location.GetFileName(body.Location.FileIndex));
            def sourceToParse  = originalSource.WithTextSubstring(2, originalSource.Text.Length - 4); // cut string braces <# #>
            def offset         = body.Location.Begin;
            def parseResult    = parseCallback(sourceToParse);
            unless (parseResult.IsSuccess)
            {
              foreach (e in parseResult.GetErrors())
              {
                def begin = PExprSplicableAstBuilder.TranslateLineColumn(offset, e.Location.StartLineColumn);
                def end   = PExprSplicableAstBuilder.TranslateLineColumn(offset, e.Location.EndLineColumn);
                Message.Error(Nemerle.Compiler.Location(e.Location.Source.FileIndex, begin, end), e.Message);
              }
            }

            match (context)
            {
              | <[ $(ctx : name) ]> =>
                <[ $(parseResult.CreateSplicableAst.[PExpr](PExprSplicableAstBuilder(typer.Manager, typer.Env, offset, () => <[ $(ctx : name) ]>))) ]>

              | expr =>
                <[
                  def $("context" : dyn) = $expr;
                  $(parseResult.CreateSplicableAst.[PExpr](PExprSplicableAstBuilder(typer.Manager, typer.Env, offset, () => <[ $("context" : dyn) ]>)))
                ]>
            }

          | _ => Message.FatalError(body.Location, "Recursive string literal <#...#> expected");
        }
      }
      else
        Message.FatalError($"Lift rule '$id' is not registered")
    }

    private Parsers : Dictionary[string, SourceSnapshot -> Nitra.ParseResult]
    {
      mutable key : object = object();
      get
      {
        if (ManagerClass.Instance.UserData.Contains(key))
          ManagerClass.Instance.UserData[key] :> Dictionary[_, _]
        else
        {
          def table = Dictionary();
          ManagerClass.Instance.UserData.Add(key, table);
          table
        }
      }
    }
  }
}
