using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using SCG = System.Collections.Generic;

using Nitra;
using Nitra.Quote;
using CSharp.StatementsParseTree;

[assembly: ImportRules(Grammar = "CSharp.SplicableStatements", GrammarAlias = "stmt")]
[assembly: ImportRules(Grammar = "CSharp.SplicableExpressions", GrammarAlias = "expr")]

internal sealed class CSharpVisitor : ParseTreeVisitor
{
  [Record]
  private struct VisitorContext
  {
    public Statements : SCG.List[Statement];
  }

  _contexts : SCG.Stack[VisitorContext] = SCG.Stack();
  _parents  : SCG.Stack[ParseTree]      = SCG.Stack();

  public override Enter(ast : ParseTree)        : void { _parents.Push(ast); }

  public override Leave(_ : ParseTree, _ : ParseTree) : void { _ = _parents.Pop(); }

  public override Visit(ast : ParseTree) : ParseTree
  {
    quote match (ast)
    {
      | statementList is ListParseTree[Statement] =>
        def newStatements = SCG.List(statementList.Count);
        _contexts.Push(VisitorContext(newStatements));
        foreach (item in statementList)
          newStatements.Add(Visit(item) :> Statement);
        _ = _contexts.Pop();
        statementList.UpdateItems(newStatements)

      | expressionStatement is EmbeddedStatement.ExpressionStatement when _parents.Peek() is IndentedEmbeddedStatement =>
        def newStatements = SCG.List();
        _contexts.Push(VisitorContext(newStatements));
        def newExpressionStatement = expressionStatement.Apply(this) :> EmbeddedStatement;
        _ = _contexts.Pop();
        if (newStatements.Count > 0)
        {
          newStatements.Add(quote <# stmt::Statement: $EmbeddedStatement(newExpressionStatement) #>);
          quote <# EmbeddedStatement: { $Statements(newStatements.ToNList()) } #>
        }
        else newExpressionStatement;

      | <# Expression: $(expr1).Where($Name(item) => $Expression(pred)) #> when _contexts.Count > 0 with context = _contexts.Peek() =>
        def varName = NameGenerator.GenerateName("buffer");
        context.Statements.Add(quote <# Statement:
          List<object> $Name(DeclarationName.CreateParseTree(context, false, varName)) = new List<object>();
        #>);
        context.Statements.Add(quote <# Statement:
          foreach (var $Name(DeclarationName.CreateParseTree(context, false, item.IdentifierBodyData)) in $Expression(expr1))
            if ($BooleanExpression(pred))
              $Identifier(Identifier.CreateParseTree(context, false, varName)).Add($Identifier(item));
        #>);
        quote <# Expression: $Identifier(Identifier.CreateParseTree(context, false, varName)) #>;

      | <# Expression: !false #> => quote <# Expression: true #>
      | x => x.Apply(this)
    }
  }
}

internal module NameGenerator
{
  private mutable _id : int = 0;

  public GenerateName(body : string) : string
  {
    _id += 1;
    "__N_" + body + "_" + _id
  }
}
