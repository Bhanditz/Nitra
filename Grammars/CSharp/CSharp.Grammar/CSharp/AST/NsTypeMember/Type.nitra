using CSharp.Symbols;
using Nitra;
using Nitra.Runtime.Binding;
using Nemerle.Collections;

namespace CSharp
{
  declaration TypeParameter
  {
    inout Scope : Scope;
    ScopeOut = ScopeIn;
    out Symbol : Symbol2 = Name.Symbol;
    Name.Symbol = TypeParameterSymbol(this.Name, this.Variance, ScopeIn);

    Variance : Variance;
    // TODO: Attributes
  }

  declarations Type : NamespaceMember, TypeMember
  {
    | Class
      {
        out Symbol : ClassSymbol = ClassSymbol.Create(this);

        Members.Parent         = Symbol;
        TypeParameters.ScopeIn = this.Symbol.Scope;
        TypeBase.Scope         = Scope.Hide(TypeParameters.ScopeOut, this.Scope);
      //Members.Scope          = TypeBase.что-то там вычисляем для эмуляции наследования;

        TypeBase : QualifiedReference*;
        Members : TypeMember*;
        TypeParameters : TypeParameter*;
      }

    | Struct
      {
        out Symbol : StructSymbol = StructSymbol.Create(this);
        
        Members.Parent        = Symbol;

        Members : TypeMember*;
      }

    | Interface
      {
        out Symbol : InterfaceSymbol = InterfaceSymbol.Create(this);

        Members.Parent        = Symbol;

        Members : TypeMember*;
      }

    | Delegate
      {
        out Symbol : DelegateSymbol = null;
      }

    | Enum
      {
        out Symbol : EnumSymbol = null;
      }
  }

  map syntax Types.TypeParameter -> TypeParameter
  {
    Name -> Name;
    Variance = 
      if (VarianceAnnotationOpt.HasValue)
        ParsedValue(VarianceAnnotationOpt.Value.Annotation, if (VarianceAnnotationOpt.Value.Annotation.Length == 2) Variance.Covariant else Variance.Contravariant)
      else ParsedValue(VarianceAnnotationOpt.Span, Variance.None);
  }

  map syntax Types.TypeParameters -> TypeParameter*
  {
    TypeParameters.Item1
  }


  map syntax TopDeclarations.TypeDeclaration -> Type
  {
    | Class
      {
        Name                            -> Name;
        TypeBody.TypeMemberDeclarations -> Members;
        match(TypeBaseOpt)       { Some(value) -> value | None() -> [] } -> TypeBase;
        match(TypeParametersOpt) { Some(value) -> value | None() -> [] } -> TypeParameters;
      }
    | Struct
      {
        Name                            -> Name;
        TypeBody.TypeMemberDeclarations -> Members;
      }
    | Interface
      {
        Name -> Name;
        TypeBody.TypeMemberDeclarations -> Members;
      }
    | Enum
      {
        Name                                                         -> Name;
        //EnumBody.EnumMemberDeclarations.EnumMemberDeclarations.Item1 -> Members;
      }
    | Delegate
      {
        Name -> Name;
      }
  }

  map syntax TopDeclarations.TypeBase -> QualifiedReference*
  {
    AnyTypes.Item1
  }
}
