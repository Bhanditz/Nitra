using CSharp.Utils;
using Nemerle.Collections;

using Nitra;
using Nitra.Declarations;
using Nitra.Runtime.Binding;
using Nitra.Internal;

using System.Linq;
using SCG = System.Collections.Generic;

namespace CSharp
{
  // class, struct or interface
  abstract declaration GenericContainerType : GenericType
  {
    symbol
    {
      out MemberTable     : TableScope       = TableScope(this.ToString());
      out IsPartial       : bool             = SymbolUtils.IsPartial(this.Declarations);
      in  BaseTypeSet     : BaseTypeReferenceSet;

      when (BaseTypeSet.IsCycled)
        Error($"'$(self.Name)' is cycled.");
    }

    TypeBase.Scope   = TypeParameterConstraints.Scope;
    Members.Parent   = Symbol;
    Members.Scope    = Scope.HideWith(Symbol.BaseTypeSet.ParentScope)
                            .HideWith(TypeParameterAliasesScope)
                            .HideWith(Symbol.MemberTable);

    Symbol.BaseTypeSet |= TypeBase.Type;

    TypeBase                 : QualifiedReference*;
    Members                  : TypeMember*;
    IsPartial                : bool;
  }

  declaration TopClass        : GenericContainerType, NamespaceMemberDeclaration { symbol { Kind = "class";     SpanClass = NitraCSharp.TypeSpanClass; FullName = SymbolUtils.MakeFullName(this); } }
  declaration NestedClass     : GenericContainerType, TypeMember                 { symbol { Kind = "class";     SpanClass = NitraCSharp.TypeSpanClass; FullName = SymbolUtils.MakeFullName(this); } }
  declaration TopStruct       : GenericContainerType, NamespaceMemberDeclaration { symbol { Kind = "struct";    SpanClass = NitraCSharp.TypeSpanClass; FullName = SymbolUtils.MakeFullName(this); } }
  declaration NestedStruct    : GenericContainerType, TypeMember                 { symbol { Kind = "struct";    SpanClass = NitraCSharp.TypeSpanClass; FullName = SymbolUtils.MakeFullName(this); } }
  declaration TopInterface    : GenericContainerType, NamespaceMemberDeclaration { symbol { Kind = "interface"; SpanClass = NitraCSharp.TypeSpanClass; FullName = SymbolUtils.MakeFullName(this); } }
  declaration NestedInterface : GenericContainerType, TypeMember                 { symbol { Kind = "interface"; SpanClass = NitraCSharp.TypeSpanClass; FullName = SymbolUtils.MakeFullName(this); } }
}
