using CSharp.Utils;
using Nemerle.Collections;

using Nitra;
using Nitra.Declarations;
using Nitra.Runtime.Binding;
using Nitra.Internal;

using System.Linq;
using SCG = System.Collections.Generic;

namespace CSharp
{
  abstract declaration Enum : Type
  {
    symbol
    {
      Kind      = "enum";
      SpanClass = NitraCSharpLanguage.TypeSpanClass;
      Scope     = MemberTable;

      out MemberTable : TableScope = TableScope(this.ToString(), this);
    }

    Members.Parent  = Symbol;
    Members.Scope   = Scope.HideWith(Symbol.MemberTable);
    EnumBase.Scope  = Scope;

    EnumBase  : QualifiedReference?;
    Members   : EnumMember*;
  }

  declaration TopEnum : Enum, NamespaceMemberDeclaration
  {
    symbol
    {
      IsSameIdentity = SymbolUtils.IsSameIdentity(0, candidate);
      CanMerge       = candidate is TopEnum;
    }

    ContainingTable = Parent.MemberTable;
  }

  declaration NestedEnum : Enum, TypeMember
  {
    symbol
    {
      IsSameIdentity = SymbolUtils.IsSameIdentity(0, candidate);
      CanMerge       = candidate is NestedEnum;
    }

    ContainingTable = Parent.MemberTable;
  }
}
