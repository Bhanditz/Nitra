using Nitra;
using Nitra.Runtime.Binding;

namespace CSharp
{
  abstract declaration Member : TypeMember
  {
    symbol
    {
      in Flags : ModifierSet;
    }

    | Constructor
      {
        symbol
        {
          Kind      = "constructor";
          SpanClass = NitraCSharpLanguage.MethodSpanClass;
          FullName  = SymbolUtils.MakeFullName(this);

          out ParameterScope : TableScope = TableScope("parameters");
        }

        Symbol.Flags              |= Modifiers.Flags;
        Modifiers.Flags            = ModifierSet(context);
        Parameters.ContainingTable = Symbol.ParameterScope;
        Parameters.CurrentScope    = Scope;
        Attributes.NameScope       = Scope;

        Parameters  : FormalParameter*;
        Attributes  : Attribute*;
        Modifiers   : Modifier*;
        Initializer : ConstructorInitializer?;
      }
    | Destructor
      {
        symbol
        {
          Kind      = "destructor";
          SpanClass = NitraCSharpLanguage.MethodSpanClass;
          FullName  = SymbolUtils.MakeFullName(this);

          out ParameterScope : TableScope = TableScope("parameters");
        }

        Symbol.Flags              |= Modifiers.Flags;
        Modifiers.Flags            = ModifierSet(context);
        Parameters.ContainingTable = Symbol.ParameterScope;
        Parameters.CurrentScope    = Scope;
        Attributes.NameScope       = Scope;

        Parameters : FormalParameter*;
        Attributes : Attribute*;
        Modifiers  : Modifier*;
      }
    | Method : GenericEntity
      {
        symbol
        {
          Kind      = "method";
          SpanClass = NitraCSharpLanguage.MethodSpanClass;
          FullName  = SymbolUtils.MakeFullName(this);

          out ParameterScope : TableScope = TableScope("parameters");
          in  ReturnType     : TypeReference;
        }

        Symbol.Flags              |= Modifiers.Flags;
        Modifiers.Flags            = ModifierSet(context);
        InterfaceType.Scope        = Scope;
        ReturnType.Scope           = TypeParameterConstraints.Scope;
        Parameters.ContainingTable = Symbol.ParameterScope;
        Parameters.CurrentScope    = TypeParameterConstraints.Scope;
        Symbol.ReturnType          = ReturnType.Type;
        Attributes.NameScope       = Scope;

        InterfaceType            : QualifiedReference?;
        IsPartial                : bool;
        Parameters               : FormalParameter*;
        ReturnType               : QualifiedReference;
        Attributes               : Attribute*;
        Modifiers                : Modifier*;
      }
    | Property
      {
        symbol
        {
          Kind      = "property";
          SpanClass = NitraCSharpLanguage.PropertySpanClass;
          FullName  = SymbolUtils.MakeFullName(this);

          in Type : TypeReference;
        }

        Symbol.Flags        |= Modifiers.Flags;
        Modifiers.Flags      = ModifierSet(context);
        InterfaceType.Scope  = Scope;
        Type.Scope           = Scope;
        Symbol.Type          = Type.Type;
        Accessors.Scope      = Scope;
        Attributes.NameScope = Scope;

        InterfaceType : QualifiedReference?;
        Type          : QualifiedReference;
        Accessors     : PropertyAccessor*;
        Attributes    : Attribute*;
        Modifiers     : Modifier*;
      }
    | Event
      {
        symbol
        {
          Kind      = "event";
          SpanClass = NitraCSharpLanguage.PropertySpanClass;
          FullName  = SymbolUtils.MakeFullName(this);

          in Type : TypeReference;
        }

        Symbol.Flags               |= Header.Modifiers.Flags;
        Header.Modifiers.Flags      = ModifierSet(context);
        InterfaceType.Scope         = Scope;
        Header.Type.Scope           = Scope;
        Symbol.Type                 = Header.Type.Type;
        Accessors.Scope             = Scope;
        Header.Attributes.NameScope = Scope;

        InterfaceType : QualifiedReference?;
        Header        : EventHeader;
        Accessors     : EventAccessor*;
      }
    | Indexer
      {
        symbol
        {
          Kind      = "indexer";
          SpanClass = NitraCSharpLanguage.PropertySpanClass;
          FullName  = SymbolUtils.MakeFullName(this);

          out ParameterScope : TableScope = TableScope("parameters");
          in  Type           : TypeReference;
        }

        Symbol.Flags              |= Modifiers.Flags;
        Modifiers.Flags            = ModifierSet(context);
        InterfaceType.Scope        = Scope;
        Type.Scope                 = Scope;
        Parameters.ContainingTable = Symbol.ParameterScope;
        Parameters.CurrentScope    = Scope;
        Symbol.Type                = Type.Type;
        Accessors.Scope            = Scope;
        Attributes.NameScope       = Scope;

        Parameters    : FormalParameter*;
        Type          : QualifiedReference;
        InterfaceType : QualifiedReference?;
        Accessors     : PropertyAccessor*;
        Attributes    : Attribute*;
        Modifiers     : Modifier*;
      }
    | Constant
      {
        symbol
        {
          Kind      = "constant";
          SpanClass = NitraCSharpLanguage.ConstantSpanClass;
          FullName  = SymbolUtils.MakeFullName(this);

          in Type : TypeReference;
        }

        Symbol.Flags               |= Header.Modifiers.Flags;
        Header.Modifiers.Flags      = ModifierSet(context);
        Header.Type.Scope           = Scope;
        Symbol.Type                 = Header.Type.Type;
        Header.Attributes.NameScope = Scope;

        Header : ConstantHeader;
      }
    | Field
      {
        symbol
        {
          Kind      = "field";
          SpanClass = NitraCSharpLanguage.FieldSpanClass;
          FullName  = SymbolUtils.MakeFullName(this);

          in Type : TypeReference;
        }

        Symbol.Flags               |= Header.Modifiers.Flags;
        Header.Modifiers.Flags      = ModifierSet(context);
        Header.Type.Scope           = Scope;
        Symbol.Type                 = Header.Type.Type;
        Header.Attributes.NameScope = Scope;

        Header : FieldHeader;
      }
    | FixedSizeBuffer
      {
        symbol
        {
          Kind      = "fixed size buffer";
          SpanClass = NitraCSharpLanguage.FieldSpanClass;
          FullName  = SymbolUtils.MakeFullName(this);

          in Type : TypeReference;
        }

        Symbol.Flags               |= Header.Modifiers.Flags;
        Header.Modifiers.Flags      = ModifierSet(context);
        Header.Type.Scope           = Scope;
        Symbol.Type                 = Header.Type.Type;
        Header.Attributes.NameScope = Scope;

        Header : FixedSizeBufferHeader;
      }
    | OperatorUnary
      {
        symbol
        {
          Kind      = "operator";
          SpanClass = Language.OperatorSpanClass;
          FullName  = SymbolUtils.MakeFullName(this);

          out ParameterScope : TableScope = TableScope("parameters");
          in  ReturnType     : TypeReference;
        }

        Symbol.Flags             |= Modifiers.Flags;
        Modifiers.Flags           = ModifierSet(context);
        ReturnType.Scope          = Scope;
        Parameter.ContainingTable = Symbol.ParameterScope;
        Parameter.CurrentScope    = Scope;
        Symbol.ReturnType         = ReturnType.Type;
        Attributes.NameScope      = Scope;

        ReturnType : QualifiedReference;
        Parameter  : FormalParameter;
        Attributes : Attribute*;
        Modifiers  : Modifier*;
      }
    | OperatorBinary
      {
        symbol
        {
          Kind      = "operator";
          SpanClass = Language.OperatorSpanClass;
          FullName  = SymbolUtils.MakeFullName(this);

          out ParameterScope : TableScope = TableScope("parameters");
          in  ReturnType     : TypeReference;
        }

        Symbol.Flags              |= Modifiers.Flags;
        Modifiers.Flags            = ModifierSet(context);
        ReturnType.Scope           = Scope;
        Parameter1.ContainingTable = Symbol.ParameterScope;
        Parameter1.CurrentScope    = Scope;
        Parameter2.ContainingTable = Symbol.ParameterScope;
        Parameter2.CurrentScope    = Scope;
        Symbol.ReturnType          = ReturnType.Type;
        Attributes.NameScope       = Scope;

        ReturnType : QualifiedReference;
        Parameter1 : FormalParameter;
        Parameter2 : FormalParameter;
        Attributes : Attribute*;
        Modifiers  : Modifier*;
      }
    | OperatorCnv
      {
        symbol
        {
          Kind      = "operator";
          SpanClass = Language.OperatorSpanClass;
          FullName  = SymbolUtils.MakeFullName(this);

          out ParameterScope : TableScope = TableScope("parameters");
          in  ReturnType     : TypeReference;
        }

        Symbol.Flags             |= Modifiers.Flags;
        Modifiers.Flags           = ModifierSet(context);
        ReturnType.Scope          = Scope;
        Parameter.ContainingTable = Symbol.ParameterScope;
        Parameter.CurrentScope    = Scope;
        Symbol.ReturnType         = ReturnType.Type;
        Attributes.NameScope      = Scope;

        ReturnType : QualifiedReference;
        Parameter  : FormalParameter;
        Attributes : Attribute*;
        Modifiers  : Modifier*;
        Kind       : OperatorKind;
      }
  }

  abstract ast ConstructorInitializer
  {
    // TODO: Map arguments
    | This
    | Base
  }

  ast FieldHeader
  {
    Type       : QualifiedReference;
    Attributes : Attribute*;
    Modifiers  : Modifier*;
  }

  ast ConstantHeader
  {
    Type       : QualifiedReference;
    Attributes : Attribute*;
    Modifiers  : Modifier*;
  }

  ast EventHeader
  {
    Type       : QualifiedReference;
    Attributes : Attribute*;
    Modifiers  : Modifier*;
  }

  ast FixedSizeBufferHeader
  {
    Type       : QualifiedReference;
    Attributes : Attribute*;
    Modifiers  : Modifier*;
  }

  declaration FormalParameter
  {
    symbol
    {
      Kind      = "parameter";
      SpanClass = NitraCSharpLanguage.ParameterSpanClass;
      FullName  = SymbolUtils.MakeFullName(this);

      in Type     : TypeReference;
      in Modifier : ParameterModifier;
    }

    in CurrentScope : Scope;

    Type.Scope           = CurrentScope;
    Symbol.Type          = Type.Type;
    Symbol.Modifier      = Modifier.ValueOrDefault;
    Attributes.NameScope = CurrentScope;

    Type       : QualifiedReference;
    Attributes : Attribute*;
    Modifier   : ParameterModifier;
  }

  abstract declaration PropertyAccessor
  {
    in Scope : Scope;

    Attributes.NameScope = Scope;

    Attributes : Attribute*;
    Modifiers  : Modifier*;
    // TODO: Map body
    | Get
    | Set
  }

  abstract declaration EventAccessor
  {
    in Scope : Scope;

    Attributes.NameScope = Scope;

    Attributes : Attribute*;
    // TODO: Map body
    | Add
    | Remove
  }
}
