using Nitra;
using Nitra.Declarations;
using Nitra.Internal;
using Nitra.ProjectSystem;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra.Runtime.Binding;
using System;
using System.Collections.Generic;
using System.Linq;

namespace CSharp
{
  public partial interface BindableAst : IDependentPropertyContainer, IAst { }
  
  public partial interface NamespaceBody : IDependentPropertyContainer, IAst, BindableAst {}
  
  public partial class CompilationUnit : AstBase, IProjectSupport, NamespaceBody
  {
    public RefreshProject(files : Seq[File]) : void
    {
      def files         = files.ToArray();
      def context       = DependentPropertyEvalContext();
      def rootNamespace = NamespaceSymbol();
      rootNamespace.MemberScope = TableScope("<root namespace>");
      rootNamespace.Parent = null;
      //rootNamespace.EvalProperties(context);

      CreateBuiltInSymbols(rootNamespace, context);

      foreach (file in files)
        when (file.Ast is CompilationUnit as cu)
          cu.RootNamespace = rootNamespace;
      AstUtils.EvalProperties(context, files, "SymbolHierarchy", 0);

      def rootScope = rootNamespace.Scope;
      foreach (file in files)
        when (file.Ast is CompilationUnit as cu)
          cu.Scope = rootScope;
      AstUtils.EvalProperties(context, files, "Scopes", 1);

      AstUtils.EvalProperties(context, files, "Type bodies binding", 2);
    }

    private static CreateBuiltInSymbols(rootNamespace : NamespaceSymbol, context : DependentPropertyEvalContext) : void
    {
      def initNamespace(symbol : NamespaceSymbol)
      {
        symbol.MemberScope = TableScope(symbol.ToString());
      }

      def initType(symbol : GenericContainerTypeSymbol, baseType : TypeSymbol)
      {
        symbol.MemberScope         = TableScope(symbol.ToString());
        symbol.IsPartial           = false;
        symbol.BaseTypeSet         = BaseTypeReferenceSet(context);
        symbol.TypeParametersCount = 0;
        symbol.Flags               = ModifierSet(context);

        symbol.Flags.Add(Modifiers.Public);
        when (baseType : object != null)
        {
          def r = Ref.Some(SourceSnapshot.Default.File, NSpan(), baseType);
          symbol.BaseTypeSet.AddParent(TypeReference(r.File, r.Span, r, []));
        }
      }

      def systemNamespace = CreateBuiltInSymbol.[NamespaceSymbol](rootNamespace, "System", context, initNamespace);
      def objectClass     = CreateBuiltInSymbol.[TopClassSymbol](systemNamespace, "Object", context, initType(_, null));
      def valueTypeStruct = CreateBuiltInSymbol.[TopStructSymbol](systemNamespace, "ValueType", context, initType(_, objectClass));

      def initClass(symbol : TopClassSymbol) : void { initType(symbol, objectClass) }
      def initStruct(symbol : TopStructSymbol) : void { initType(symbol, valueTypeStruct) }

      _ = CreateBuiltInSymbol.[TopStructSymbol](systemNamespace, "Void", context, initStruct);
      _ = CreateBuiltInSymbol.[TopStructSymbol](systemNamespace, "Boolean", context, initStruct);
      _ = CreateBuiltInSymbol.[TopStructSymbol](systemNamespace, "Byte", context, initStruct);
      _ = CreateBuiltInSymbol.[TopStructSymbol](systemNamespace, "SByte", context, initStruct);
      _ = CreateBuiltInSymbol.[TopStructSymbol](systemNamespace, "Int16", context, initStruct);
      _ = CreateBuiltInSymbol.[TopStructSymbol](systemNamespace, "UInt16", context, initStruct);
      _ = CreateBuiltInSymbol.[TopStructSymbol](systemNamespace, "Int32", context, initStruct);
      _ = CreateBuiltInSymbol.[TopStructSymbol](systemNamespace, "UInt32", context, initStruct);
      _ = CreateBuiltInSymbol.[TopStructSymbol](systemNamespace, "Int64", context, initStruct);
      _ = CreateBuiltInSymbol.[TopStructSymbol](systemNamespace, "UInt64", context, initStruct);
      _ = CreateBuiltInSymbol.[TopStructSymbol](systemNamespace, "Int64", context, initStruct);
      _ = CreateBuiltInSymbol.[TopStructSymbol](systemNamespace, "UInt64", context, initStruct);
      _ = CreateBuiltInSymbol.[TopStructSymbol](systemNamespace, "Single", context, initStruct);
      _ = CreateBuiltInSymbol.[TopStructSymbol](systemNamespace, "Double", context, initStruct);
      _ = CreateBuiltInSymbol.[TopStructSymbol](systemNamespace, "Decimal", context, initStruct);
      _ = CreateBuiltInSymbol.[TopStructSymbol](systemNamespace, "Char", context, initStruct);
      _ = CreateBuiltInSymbol.[TopClassSymbol](systemNamespace, "String", context, initClass);
      _ = CreateBuiltInSymbol.[TopClassSymbol](rootNamespace, "#Dynamic", context, initClass);
    }

    private static CreateBuiltInSymbol[TSymbol](parent : NamespaceSymbol, name : string, context : DependentPropertyEvalContext, init : TSymbol -> void) : TSymbol
      where TSymbol : NamespaceMemberDeclarationSymbol, new()
    {
      def declaration = BuiltInSymbolDeclaration.[TSymbol](name);
      def symbol      = parent.MemberScope.Define.[TSymbol](declaration, context);
      symbol.Parent   = parent;
      init(symbol);
      symbol
    }
  }
}
