using Nitra;
using Nitra.Runtime.Binding;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace CSharp
{
  module SymbolUtils
  {
    public PrependScopeIfResolved(reference : Ref[ISymbol2], scopes : list[Scope]) : list[Scope]
    {
      if (reference.IsSymbolEvaluated)
        reference.Symbol.Scope :: scopes
      else
        scopes
    }
    
    public IsPartial(symbol : GenericContainerTypeSymbol) : bool
    {
      symbol.Declarations.First().IsPartial.ValueOrDefault
    }

    public IsSameIdentity(typeParametersCount : int, candidate : Declaration) : bool
    {
      match (candidate)
      {
        | CSharp.GenericType as t => t.TypeParameterAliases.Count == typeParametersCount
        | CSharp.Namespace
        | CSharp.Type             => typeParametersCount == 0
        | _                       => false
      }
    }

    public CanMerge[T](typeParametersCount : int, candidate : Declaration) : bool
      where T : GenericType
    {
      match (candidate)
      {
        | genericType is T => genericType.TypeParameterAliases.Count == typeParametersCount
        | _ => false
      }
    }

    public CanMerge[T](symbol : GenericContainerTypeSymbol, candidate : Declaration) : bool
      where T : GenericContainerType
    {
      match (candidate)
      {
        | genericType is T => genericType.TypeParameterAliases.Count == symbol.TypeParametersCount && symbol.IsPartial && genericType.IsPartial.ValueOrDefault
        | _ => false
      }
    }
    
    public MakeFullName(node : ISymbol2) : string
    {
      def loop(node : ISymbol2, sb : Text.StringBuilder) : bool
      {
        match (node)
        {
          | NamespaceMemberDeclarationSymbol as y when y.Parent == null => return false; // root
          | TypeMemberSymbol                 as x with (parent = x.Parent : ISymbol2)
          | NamespaceMemberDeclarationSymbol as y with (parent = y.Parent : ISymbol2) =>
            when (loop(parent, sb))
              _ = sb.Append('.');

          | _ => ()
        }

        _ = sb.Append(node.Name);
        true
      }
        
      def sb = Text.StringBuilder();
      _ = loop(node, sb);
      sb.ToString()
    }
  }
}
