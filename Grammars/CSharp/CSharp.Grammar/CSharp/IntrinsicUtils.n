using Nitra;
using Nitra.Declarations;
using Nitra.Runtime.Binding;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace CSharp
{
  internal module IntrinsicUtils
  {
    private CreateReference(span : NSpan, text : string) : Reference
    {
      Reference.Concrete(Span = span, Id = StringIndex.GetId(text))
    }

    public CreateOpenTypeArgument(span : NSpan) : QualifiedReference
    {
      QualifiedReference.Simple(Span = span, Name = CreateReference(span, "#"))
    }

    public CreateQualifiedReference(span : NSpan, params parts : array[string]) : QualifiedReference
    {
      assert(parts.Length > 0);

      mutable acc = QualifiedReference.Simple(
        Span = span,
        Name = CreateReference(span, parts[0]));

      for(mutable i = 1; i < parts.Length; ++i)
        acc = QualifiedReference.Qualified(
          Span      = span,
          Qualifier = acc,
          Name      = CreateReference(span, parts[i]));

      acc
    }

    public CreatePredefinedTypeReference(span : NSpan, text : string) : QualifiedReference
    {
      match (text)
      {
        | "void"    => CreateQualifiedReference(span, "System", "Void")
        | "object"  => CreateQualifiedReference(span, "System", "Object")
        | "bool"    => CreateQualifiedReference(span, "System", "Bool")
        | "string"  => CreateQualifiedReference(span, "System", "String")
        | "char"    => CreateQualifiedReference(span, "System", "Char")
        | "byte"    => CreateQualifiedReference(span, "System", "Byte")
        | "sbyte"   => CreateQualifiedReference(span, "System", "SByte")
        | "short"   => CreateQualifiedReference(span, "System", "Int16")
        | "ushort"  => CreateQualifiedReference(span, "System", "UInt16")
        | "int"     => CreateQualifiedReference(span, "System", "Int32")
        | "uint"    => CreateQualifiedReference(span, "System", "UInt32")
        | "long"    => CreateQualifiedReference(span, "System", "Int64")
        | "ulong"   => CreateQualifiedReference(span, "System", "UInt64")
        | "float"   => CreateQualifiedReference(span, "System", "Single")
        | "double"  => CreateQualifiedReference(span, "System", "Double")
        | "decimal" => CreateQualifiedReference(span, "System", "Decimal")
        | "dynamic" => CreateQualifiedReference(span, "#Dynamic")
        | _         => assert(false)
      }
    }
  }
}
