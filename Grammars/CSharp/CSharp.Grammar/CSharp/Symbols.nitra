using Nitra;
using Nitra.Declarations;
using Nitra.Runtime.Binding;
using Nitra.Internal;
using System.Linq;

namespace CSharp
{
  ast Project
  {
    CompilationUnits : CompilationUnit*;
  }

  asts CompilationUnit
  {
    | CSharp
      {
        Externs         : ExternAliasDirective*;
        UsingDirectives : UsingDirective*;
        Members         : Top*;
      }
  }

  asts QualifiedReference
  {
    | Simple
      {
        Name  : Reference;
      }
    | Aliased
      {
        Alias : Reference;
        Name  : Reference;
      }
    | Qualified
      {
        Qualifier : QualifiedReference;
        Name      : Reference;
      }
    | Generic
      {
        Name      : QualifiedReference;
        Arguments : QualifiedReference+;
      }
  }

  map syntax CsIdentifiers.QualifiedAlias      -> Reference          = Alias.GetAst();
  map syntax CsIdentifiers.QualifiedIdentifier -> QualifiedReference
  {
    | (None,    (first :: other, _)) = other.Aggregate(first.CreateSimple(),            (acc, item) => item.CreateQualified(acc));
    | (Some(a), (first :: other, _)) = other.Aggregate(first.CreateAliased(a.GetAst()), (acc, item) => item.CreateQualified(acc));
    | (_,       ([],             _)) = IntrinsicUtils.CreateQualifiedReference(QualifiedIdentifierParts.Span, "#Missing");
  }

  map syntax Types.TypeName -> QualifiedReference
  {
    | QualifiedName  = QualifiedIdentifier.GetAst();
    | PredefinedType = IntrinsicUtils.CreatePredefinedTypeReference(PredefinedType, GetText(PredefinedType));
  }

  map syntax Types.AnyNonArrayType -> QualifiedReference
  {
    | (typeName, None) = typeName.GetAst();
    | (typeName, Some) -> Generic
      {
        Name      = IntrinsicUtils.CreateQualifiedReference(Span, "System", "Nullable");
        Arguments = QualifiedReference.AstList(Span, typeName.GetAst());
      }
  }

  map syntax Types.TypeSpecifier -> QualifiedReference
  {
    | PointerSpecifier = IntrinsicUtils.CreateQualifiedReference(Span, "#Pointer");
    | RankSpecifier =
      {
        def rank = RankSpecifier.RankSeparators.Count + 1;
        IntrinsicUtils.CreateQualifiedReference(Span, "#Array" + rank.ToString())
      };
  }

  map syntax Types.AnyType -> QualifiedReference =
  {
    mutable acc = AnyNonArrayType.GetAst();

    foreach (ts in TypeSpecifiers)
    {
      acc = QualifiedReference.Generic(
        Span      = acc.Span + ts.Span,
        Name      = ts.GetAst(),
        Arguments = QualifiedReference.AstList(acc.Span, acc))
    }

    acc
  };

  ast ExternAliasDirective
  {
    Name   : Reference;
  }

  asts UsingDirective
  {
    | Alias
      {
        Name                : Reference;
        NamespaceOrTypeName : QualifiedReference;
      }
    | Open
      {
        NamespaceOrTypeName : QualifiedReference;
      }
  }

  declarations Top
  {
    | Namespace
      {
        Path            : Reference*;
        ExternAlias     : ExternAliasDirective*;
        UsingDirectives : UsingDirective*;
        Members         : Top*;
      }
    | Type
      {
        Members   : Top*;
      }
    | Member
      {
      }
  }

  map syntax Main.CompilationUnit -> CompilationUnit.CSharp
  {
    ExternAliasDirectives       -> Externs;
    UsingDirectives             -> UsingDirectives;
    NamespaceMemberDeclarations -> Members;
  }

  map syntax TopDeclarations.ExternAliasDirective -> ExternAliasDirective
  {
    Name -> Name;
  }

  map syntax TopDeclarations.UsingDirective -> UsingDirective
  {
    | Alias
      {
        Name          -> Name;
        QualifiedName -> NamespaceOrTypeName;
      }
    | Open
      {
        QualifiedName -> NamespaceOrTypeName;
      }
  }

  map syntax TopDeclarations.NamespaceMemberDeclaration -> Top
  {
    | Namespace
      {
        Name            = NamespaceName.Identifiers.Item1.Last().GetAst();
        Path            = null;
        ExternAliasDirectives       -> ExternAlias;
        UsingDirectives             -> UsingDirectives;
        NamespaceMemberDeclarations -> Members;
      }
    | Type = TypeDeclaration.GetAst();
  }

  map syntax TopDeclarations.TypeDeclaration -> Top
  {
    | Class -> Type
      {
        Name                            -> Name;
        TypeBody.TypeMemberDeclarations -> Members;
      }
    | Struct -> Type
      {
        StructHead.Name                 -> Name;
        TypeBody.TypeMemberDeclarations -> Members;
      }
    | Interface -> Type
      {
        Name -> Name;
        TypeBody.TypeMemberDeclarations -> Members;
      }
    | Enum -> Type
      {
        Name                                                         -> Name;
        EnumBody.EnumMemberDeclarations.EnumMemberDeclarations.Item1 -> Members;
      }
    | Delegate -> Type
      {
        Name    -> Name;
        Members = Top.AstList(Location.Span);
      }
  }

  map syntax TopDeclarations.TypeMemberDeclaration -> Top
  {
    | NestedType = TypeDeclaration.GetAst();
    | Method          -> Member { Name = IntrinsicUtils.CreateReference(this.Location.Span, "#NotImplemented"); }
    | Property        -> Member { Name = IntrinsicUtils.CreateReference(this.Location.Span, "#NotImplemented"); }
    | Field           -> Member { Name = IntrinsicUtils.CreateReference(this.Location.Span, "#NotImplemented"); }
    | Constant        -> Member { Name = IntrinsicUtils.CreateReference(this.Location.Span, "#NotImplemented"); }
    | Constructor     -> Member { Name = IntrinsicUtils.CreateReference(this.Location.Span, "#NotImplemented"); }
    | Event           -> Member { Name = IntrinsicUtils.CreateReference(this.Location.Span, "#NotImplemented"); }
    | SimpleEvent     -> Member { Name = IntrinsicUtils.CreateReference(this.Location.Span, "#NotImplemented"); }
    | Indexer         -> Member { Name = IntrinsicUtils.CreateReference(this.Location.Span, "#NotImplemented"); }
    | Destructor      -> Member { Name = IntrinsicUtils.CreateReference(this.Location.Span, "#NotImplemented"); }
    | FixedSizeBuffer -> Member { Name = IntrinsicUtils.CreateReference(this.Location.Span, "#NotImplemented"); }
    | Operator1       -> Member { Name = IntrinsicUtils.CreateReference(this.Location.Span, "#NotImplemented"); }
    | Operator2       -> Member { Name = IntrinsicUtils.CreateReference(this.Location.Span, "#NotImplemented"); }
  }

  map syntax TopDeclarations.EnumMemberDeclaration -> Top.Member
  {
    Name = IntrinsicUtils.CreateReference(this.Location.Span, "#NotImplemented");
  }
}
