using Nitra;
using Nitra.Declarations;
using Nitra.Runtime.Binding;
using Nitra.Internal;
using System.Linq;

namespace CSharp
{
  ast Project
  {
    CompilationUnits : CompilationUnit*;
  }

  asts CompilationUnit
  {
    | CSharp
      {
        Externs         : ExternAliasDirective*;
        UsingDirectives : UsingDirective*;
        Members         : NamespaceMember*;
      }
  }

  asts QualifiedReference
  {
    | Simple
      {
        Name  : Reference;
      }
    | Aliased
      {
        Alias : Reference;
        Name  : Reference;
      }
    | Qualified
      {
        Qualifier : QualifiedReference;
        Name      : Reference;
      }
    | Generic
      {
        Name      : QualifiedReference;
        Arguments : QualifiedReference+;
      }
  }

  map syntax CsIdentifiers.QualifiedAlias      -> Reference          = Alias.GetAst();
  map syntax CsIdentifiers.QualifiedIdentifier -> QualifiedReference
  {
    | (None,    (first :: other, _)) = other.Aggregate(first.CreateSimple(),            (acc, item) => item.CreateQualified(acc));
    | (Some(a), (first :: other, _)) = other.Aggregate(first.CreateAliased(a.GetAst()), (acc, item) => item.CreateQualified(acc));
    | (_,       ([],             _)) = IntrinsicUtils.CreateQualifiedReference(QualifiedIdentifierParts.Span, "#Missing");
  }

  map syntax Types.TypeName -> QualifiedReference
  {
    | QualifiedName  = QualifiedIdentifier.GetAst();
    | PredefinedType = IntrinsicUtils.CreatePredefinedTypeReference(PredefinedType, GetText(PredefinedType));
  }

  map syntax Types.AnyNonArrayType -> QualifiedReference
  {
    | (typeName, None) = typeName.GetAst();
    | (typeName, Some) -> Generic
      {
        Name      = IntrinsicUtils.CreateQualifiedReference(Span, "System", "Nullable");
        Arguments = QualifiedReference.AstList(Span, typeName.GetAst());
      }
  }

  map syntax Types.TypeSpecifier -> QualifiedReference
  {
    | PointerSpecifier = IntrinsicUtils.CreateQualifiedReference(Span, "#Pointer");
    | RankSpecifier =
      {
        def rank = RankSpecifier.RankSeparators.Count + 1;
        IntrinsicUtils.CreateQualifiedReference(Span, "#Array" + rank.ToString())
      };
  }

  map syntax Types.AnyType -> QualifiedReference =
  {
    mutable acc = AnyNonArrayType.GetAst();

    foreach (ts in TypeSpecifiers)
    {
      acc = QualifiedReference.Generic(
        Span      = acc.Span + ts.Span,
        Name      = ts.GetAst(),
        Arguments = QualifiedReference.AstList(acc.Span, acc))
    }

    acc
  };

  ast ExternAliasDirective
  {
    Name   : Reference;
  }

  asts UsingDirective
  {
    | Alias
      {
        Name                : Reference;
        NamespaceOrTypeName : QualifiedReference;
      }
    | Open
      {
        NamespaceOrTypeName : QualifiedReference;
      }
  }

  abstract declaration NamespaceMember
  {
  }

  abstract declaration TypeMember
  {
  }

  declaration Namespace : NamespaceMember
  {
    Path            : Reference*;
    ExternAlias     : ExternAliasDirective*;
    UsingDirectives : UsingDirective*;
    Members         : NamespaceMember*;
  }

  declarations Type : NamespaceMember, TypeMember
  {
    | Struct    { Members : TypeMember*; }
    | Class     { Members : TypeMember*; }
    | Interface { Members : TypeMember*; }
    | Delegate
    | Enum
  }

  declaration MemberStub : TypeMember
  {
  }

  map syntax Main.CompilationUnit -> CompilationUnit.CSharp
  {
    ExternAliasDirectives       -> Externs;
    UsingDirectives             -> UsingDirectives;
    NamespaceMemberDeclarations -> Members;
  }

  map syntax TopDeclarations.ExternAliasDirective -> ExternAliasDirective
  {
    Name -> Name;
  }

  map syntax TopDeclarations.UsingDirective -> UsingDirective
  {
    | Alias
      {
        Name          -> Name;
        QualifiedName -> NamespaceOrTypeName;
      }
    | Open
      {
        QualifiedName -> NamespaceOrTypeName;
      }
  }

  map syntax TopDeclarations.NamespaceMemberDeclaration -> NamespaceMember
  {
    | Namespace -> Namespace
      {
        Name            = NamespaceName.Identifiers.Item1.Last().GetAst();
        Path            = Reference.AstList(NamespaceName.Identifiers.Item1.Span, NamespaceName.Identifiers.Item1.Take(NamespaceName.Identifiers.Item1.Count - 1).Select(pt => pt.GetAst()).ToArray());
        ExternAliasDirectives       -> ExternAlias;
        UsingDirectives             -> UsingDirectives;
        NamespaceMemberDeclarations -> Members;
      }
    | Type = TypeDeclaration.GetAst();
  }

  map syntax TopDeclarations.TypeDeclaration -> Type
  {
    | Class
      {
        Name                            -> Name;
        TypeBody.TypeMemberDeclarations -> Members;
      }
    | Struct
      {
        StructHead.Name                 -> Name;
        TypeBody.TypeMemberDeclarations -> Members;
      }
    | Interface
      {
        Name -> Name;
        TypeBody.TypeMemberDeclarations -> Members;
      }
    | Enum
      {
        Name                                                         -> Name;
        //EnumBody.EnumMemberDeclarations.EnumMemberDeclarations.Item1 -> Members;
      }
    | Delegate
      {
        Name -> Name;
      }
  }

  map syntax TopDeclarations.TypeMemberDeclaration -> TypeMember
  {
    | NestedType = TypeDeclaration.GetAst();
    | Method          -> MemberStub { Name = Reference(this.Span, "#NotImplemented"); }
    | Property        -> MemberStub { Name = Reference(this.Span, "#NotImplemented"); }
    | Field           -> MemberStub { Name = Reference(this.Span, "#NotImplemented"); }
    | Constant        -> MemberStub { Name = Reference(this.Span, "#NotImplemented"); }
    | Constructor     -> MemberStub { Name = Reference(this.Span, "#NotImplemented"); }
    | Event           -> MemberStub { Name = Reference(this.Span, "#NotImplemented"); }
    | SimpleEvent     -> MemberStub { Name = Reference(this.Span, "#NotImplemented"); }
    | Indexer         -> MemberStub { Name = Reference(this.Span, "#NotImplemented"); }
    | Destructor      -> MemberStub { Name = Reference(this.Span, "#NotImplemented"); }
    | FixedSizeBuffer -> MemberStub { Name = Reference(this.Span, "#NotImplemented"); }
    | Operator1       -> MemberStub { Name = Reference(this.Span, "#NotImplemented"); }
    | Operator2       -> MemberStub { Name = Reference(this.Span, "#NotImplemented"); }
  }

  //map syntax TopDeclarations.EnumMemberDeclaration -> Top.Member
  //{
  //  Name = Reference(this.Span, "#NotImplemented");
  //}
}
