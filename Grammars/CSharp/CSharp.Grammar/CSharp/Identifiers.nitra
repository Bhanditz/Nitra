using Nitra;
using Nitra.Declarations;
using Nitra.Runtime.Binding;
using Nemerle;

namespace CSharp
{
  syntax module CsIdentifiers
  {
    using Outline;
    using PrettyPrint;
    using StandardSpanClasses;
    using Identifiers;
    using Whitespaces;

    using CSharp.TokenNames;
    using Literals;
    using Types;

    keyword regex ['A'..'Z', 'a'..'z', '_'..'_']+ rule S;

  // Contextual keywords:
  // "add" | "alias" | "ascending" | "assembly" | "by" | "descending" | "dynamic" | "equals" | "field" | "from" | "get" | "global"!!! | "group" |
  // "into" | "join" | "let" | "method" | "module" | "on" | "orderby" | "param" | "partial" | "property" | "remove" | "select" | "set" | "type" |
  // "typevar"!!! | "value" | "var" | "where" | "yield"

    regex KeywordToken
                  =   "abstract" | "as"         | "base"      | "bool"      | "break"     | "byte"     | "case"   | "catch"       | "char"      | "checked"
                    | "class"    | "const"      | "continue"  | "decimal"   | "default"   | "delegate" | "do"     | "double"      | "else"      | "enum"
                    | "event"    | "explicit"   | "extern"    | "false"     | "finally"   | "fixed"    | "float"  | "for"         | "foreach"   | "goto"
                    | "if"       | "implicit"   | "in"        | "int"       | "interface" | "internal" | "is"     | "lock"        | "long"      | "namespace"
                    | "new"      | "null"       | "object"    | "operator"  | "out"       | "override" | "params" | "private"     | "protected" | "public"
                    | "readonly" | "ref"        | "return"    | "sbyte"     | "sealed"    | "short"    | "sizeof" | "stackalloc"  | "static"    | "string"
                    | "struct"   | "switch"     | "this"      | "throw"     | "true"      | "try"      | "typeof" | "uint"        | "ulong"     | "unchecked"
                    | "unsafe"   | "ushort"     | "using"     | "virtual"   | "void"      | "volatile" | "while"
                    | "__arglist";

    alias Name                = Identifier;
    alias QualifiedName       = QualifiedIdentifier;
    alias Char                = Any;

    [Keyword]
    token Keyword = Name=KeywordToken !IdentifierPartCharacters;

    /////////////////////////////////////////////////////////
    // Identifiers

    [Identifier]
    token Identifier = !Keyword At="@"? IdentifierBody;

    [ExplicitSpaces]
    syntax QualifiedIdentifier  = QualifiedAlias? (QualifiedIdentifierPart; s "." s)+; // TODO: In C# spec. it's "namespace-or-type-name". Rename it!

    [ExplicitSpaces]
    syntax QualifiedIdentifierContinuation = (QualifiedIdentifierPart; s "." s)+;

    [ExplicitSpaces]
    syntax NamespaceName        = (Identifier; s "." s)+;

    [ExplicitSpaces]
    syntax QualifiedIdentifierPartTypeArguments
    {
      [SpanClass(OpenBrace)]
      regex OpenBrace = "<";

      [SpanClass(CloseBrace)]
      regex CloseBrace = ">";

      CreateTypeArguments(context : AstContext) : QualifiedReference.IAstList;

      | TypeArgumentList  = s OpenBrace s (AnyType; s "," s sm)+ CloseBrace
        {
          override CreateTypeArguments
          {
            def args = array(AnyTypes.Item1.Count);
            foreach (anyType in AnyTypes.Item1 with index)
              args[index] = anyType.GetAst(context);
            QualifiedReference.AstList(Span, args)
          }
        }

      | OpenTypeArguments = s OpenBrace s RankSeparator*         CloseBrace
        {
          override CreateTypeArguments
          {
            _ = context;
            def args = array(RankSeparators.Count + 1);
            args[0] = IntrinsicUtils.CreateOpenTypeArgument(NSpan(OpenBrace.EndPos, OpenBrace.EndPos));
            foreach (rs in RankSeparators with index)
              args[index + 1] = IntrinsicUtils.CreateOpenTypeArgument(NSpan(rs.Location.EndPos, rs.Location.EndPos));
            QualifiedReference.AstList(Span, args)
          }
        }
    }

    [ExplicitSpaces]
    syntax QualifiedIdentifierPart = Identifier QualifiedIdentifierPartTypeArguments?
    {
      CreateSimple(context : AstContext) : QualifiedReference
      {
        TryCreateGeneric(
          QualifiedReference.Simple(
            Span = Identifier.Span,
            Name = Identifier.GetAst(context)),
          context)
      }

      CreateAliased(aliasAst : Reference, context : AstContext) : QualifiedReference
      {
        TryCreateGeneric(
          QualifiedReference.Aliased(
            Span  = aliasAst.Span + Identifier.Span,
            Alias = aliasAst,
            Name  = Identifier.GetAst(context)),
          context)
      }

      CreateQualified(qualifier : QualifiedReference, context : AstContext) : QualifiedReference
      {
        TryCreateGeneric(
          QualifiedReference.Qualified(
            Span      = qualifier.Span + Identifier.Span,
            Qualifier = qualifier,
            Name      = Identifier.GetAst(context)),
          context)
      }

      TryCreateGeneric(name : QualifiedReference, context : AstContext) : QualifiedReference
      {
        if (QualifiedIdentifierPartTypeArgumentsOpt.HasValue)
          QualifiedReference.Generic(
            Span      = name.Span + QualifiedIdentifierPartTypeArgumentsOpt.Span,
            Name      = name,
            Arguments = QualifiedIdentifierPartTypeArgumentsOpt.Value.CreateTypeArguments(context))
        else
          name
      }
    }

    syntax QualifiedAlias = Alias=Identifier "::";


    // same as 'QualifiedIdentifierPart' but avoids capturing type arguments of method name
    [ExplicitSpaces]
    syntax TypeMemberIdentifierPart  = Identifier (QualifiedIdentifierPartTypeArguments !"(")?;
    syntax TypeMemberIdentifier      = QualifiedAlias? (TypeMemberIdentifierPart; ".")+; // TODO: Maybe a bug. It's should end with Identifier

    regex IdentifierStartCharacter  = LetterCharacter | "_" | UnicodeEscapeSequence;
    regex IdentifierPartCharacters  = LetterCharacter | DecimalDigitCharacter | ConnectingCharacter | CombiningCharacter | FormattingCharacter | UnicodeEscapeSequence;
    regex IdentifierBody            = IdentifierStartCharacter IdentifierPartCharacters*;

    // Identifiers
    /////////////////////////////////////////////////////////
  }
}
