namespace Kotlin
{
  syntax module TopLevel
  {
    using Nitra.Core;

    using Kotlin.Classes;
    using Kotlin.ClassMembers;
    using Kotlin.Modifiers;
    using Kotlin.Expressions;

    [StartRule]
    syntax KotlinFile = Preamble ToplevelObject*;

    [StartRule]
    syntax Script = Preamble Expression*;

    syntax Preamble = FileAnnotations? PackageHeader? Import*
    {
      syntax FileAnnotations = FileAnnotation*;
      syntax FileAnnotation = "@" "file" ":" entries
      {
        syntax entries
        {
          | AnnotationEntries = "[" AnnotationEntry+ "]"
          | AnnotationEntry
        }
      }

      syntax PackageHeader = Modifiers "package" (SimpleName; ".")+ SEMI?;
    }

    syntax Import = "import" (SimpleName; ".")+ AliasOrContent? SEMI?
    {
      syntax AliasOrContent
      {
        | Alias = "as" SimpleName
        | Content = "." "*"
      }
    }

    syntax ToplevelObject
    {
      | Package
      | Class
      | Object
      | Function
      | Property
    }

    syntax Package = "package" (SimpleName; ".")+ "{" Import* ToplevelObject* "}";
  }
}