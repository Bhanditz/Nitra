using Nitra.Runtime;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Console;
using System.Linq;

using Nitra;

module Program
{
  parserHost : ParserHost = ParserHost();

  Main () : void
  {
    def commandPrompt = "input>";
    def eval(text : string) : void
    {
      def error(msg : string, pos : int) : void
      {
        WriteLine(string(' ', pos + commandPrompt.Length) + "^");
        WriteLine(msg);
      }
      def writeColored(color : ConsoleColor, text : string, pos : int, length : int)
      {
        def originalColor = ForegroundColor;
        ForegroundColor = color;
        Write(text.Substring(pos, length));
        ForegroundColor = originalColor;
      }

      def source = SourceSnapshot(text);
      def parseResult = CalcGrammar.start(source, parserHost);
      try
      {
        Write("Highlighting: ");
        def spans = List();
        def text = source.Text;
        CalcGrammarHighlighterWalkers.start(parseResult, spans, 0, text.Length);
        mutable pos = 0;
        foreach (span in spans)
        {
          when (span.SpanClass : object == StandardSpanClasses.SpanClass_Number)
          {
            writeColored(ConsoleColor.Gray, text, pos, span.Location.StartPos - pos);
            writeColored(ConsoleColor.Magenta, text, span.Location.StartPos, span.Location.Length);
            pos = span.Location.EndPos;
          }
          when (span.SpanClass : object == StandardSpanClasses.SpanClass_Operator)
          {
            writeColored(ConsoleColor.Gray, text, pos, span.Location.StartPos - pos);
            writeColored(ConsoleColor.Cyan, text, span.Location.StartPos, span.Location.Length);
            pos = span.Location.EndPos;
          }
        }
        writeColored(ConsoleColor.Gray, text, pos, text.Length - pos);
        WriteLine();

        def ast = CalcGrammarAstWalkers.start(parseResult);
        WriteLine($<#Result: ..$(ast.Value(); ", ")#>);
        WriteLine($"Pretty print: $ast");
      }
      catch
      {
        | e is WalkerException => error(e.Message, e.Pos);
      }
      WriteLine();
    }
    def inputLoop() : void
    {
      Write(commandPrompt);
      def text = ReadLine();
      unless (string.IsNullOrEmpty(text))
      {
        eval(text);
        inputLoop()
      }
    }
    inputLoop()
  }
}
