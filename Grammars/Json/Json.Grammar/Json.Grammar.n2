//using N2;

//using Nemerle.Collections;
//using Nemerle.Imperative;
//using Nemerle.Compiler;

//using System.Collections.Generic;
//using System.Text;
//using System.Linq;

namespace N2.Tests
{
  syntax module JsonParser
  {
    using PrettyPrint;
    using TokenNames;
    using StandardSpanClasses;
    using Whitespaces;
    using Identifiers;
    using CStyleComments;

    braces "(", ")";
    braces "{", "}";
    braces "[", "]";

    [StartRule, ExplicitSpaces]
    syntax Start = s Value !Any;

    ///////////////////////////////////////////////////////////////////////////
    // Whitespaces

    extend syntax IgnoreToken
    {
      | [SpanClass(Comment)] SingleLineComment;
      | [SpanClass(Comment)] MultiLineComment;
    }

    ///////////////////////////////////////////////////////////////////////////
    // Identifier

    token KeywordToken = "true" | "false" | "null";
    [ExplicitSpaces] syntax Keyword = KeywordToken !IdentifierPartCharacters;
    [ExplicitSpaces] syntax Identifier = !Keyword IdentifierBody;

    ///////////////////////////////////////////////////////////////////////////
    // Strings

    token HexDigit                  = ['0'..'9', 'a'..'f', 'A'..'F'];
    token EscChar                   = '\\' | '/' | 'b' | 'f' | 'n' | 'r'| 't' | 'u' HexDigit HexDigit HexDigit HexDigit;

    token ReservedStringChar1       = '\"' | '\\';
    token EscChar1                  = '\"' | EscChar;
    token Esc1                      = '\\' EscChar1;
    [SpanClass(String), ExplicitSpaces]
    syntax StringLiteral1 = "\"" StringPart1* "\"";

    [ExplicitSpaces] syntax StringPart1
    {
      | EscStringPart   = Esc1;
      | OtherStringPart = Chars=(!ReservedStringChar1 Any)+
    }

    token ReservedStringChar2       = '\'' | '\\';
    token EscChar2                  = '\'' | EscChar;
    token Esc2                      = '\\' EscChar2;

    [ExplicitSpaces] syntax StringPart2
    {
      | EscStringPart   = Esc2;
      | OtherStringPart = Chars=(!ReservedStringChar2 Any)+;
    }

    [SpanClass(String), ExplicitSpaces]
    syntax StringLiteral2 = "\'" StringPart2* "\'";

    ///////////////////////////////////////////////////////////////////////////
    // Numbers

    token Digits   = ['0'..'9']+;
    token Integer  = '0' | ['1'..'9'] ['0'..'9']*;
    token Exponent = ("E" | "e") ("-" | "+")? Digits;
 
    [ExplicitSpaces]
    syntax Fraction = "." Digits ;

    [ExplicitSpaces]
    syntax Number = Minus="-"? Integer Fraction? Exponent?;

    ///////////////////////////////////////////////////////////////////////////
    // Value

    syntax Value
    {
      | Identifier
      | StringLiteral1
      | StringLiteral2
      | Number
      //| [ExplicitSpaces] Number = Integer Fraction? Exponent? s;
      //| [ExplicitSpaces] NumberNeg = Minus="-" Integer Fraction? Exponent? s;
      | Object =  "{" inl Properties=(Property; "," nl)* nl d "}";
      | Array  = "[" (Value; "," sm)*  "]";
      | "true"
      | "false"
      | "null"
    }

    syntax Property
    {
      | StringLiteral1 ":" sm Value;
      | StringLiteral2 ":" sm Value;
      | Identifier     ":" sm Value;
    }
  }
}