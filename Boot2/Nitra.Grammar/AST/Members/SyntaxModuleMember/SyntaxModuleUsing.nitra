using DotNet;

using Nitra;
using Nitra.Declarations;

using Nemerle.Collections;

namespace Nitra.Ast
{
  abstract ast SyntaxModuleUsingDirective : ScopedAst
  {
  stage 1:
    inout Literals          : Map[string, string];
    in    UsingAliasesScope : TableScope;
    in    CurrentScope      : Scope;
    inout Opening           : Scope;
  }

  declaration SyntaxModuleUsingAliasDirective : SyntaxModuleUsingDirective, Alias
  {
    symbol
    {
      Kind      = "alias";
      SpanClass = DotNetLang.AliasSpanClass;
    }

    ContainingTable           = UsingAliasesScope;
    NamespaceOrTypeName.Scope = CurrentScope;
    Symbol.Replacement        = NamespaceOrTypeName.Ref;

    NamespaceOrTypeName : QualifiedReference;
  }

  ast SyntaxModuleUsingOpenDirective : SyntaxModuleUsingDirective
  {
    LiteralsOut               = AstUtils.TryGetLiterals(NamespaceOrTypeName.Ref, LiteralsIn);
    ContainingTable           = null;
    NamespaceOrTypeName.Scope = CurrentScope;
    OpeningOut                = OpeningIn.UnionWith(NamespaceOrTypeName.Ref.Scope);

    unless (NamespaceOrTypeName.Ref.Symbol is NamespaceSymbol || NamespaceOrTypeName.Ref.Symbol is SyntaxModuleSymbol)
      Error("Using directive can open only namespace or syntax module.");

    NamespaceOrTypeName : QualifiedReference;
  }
}
