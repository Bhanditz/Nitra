using Nitra.Model;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

namespace Nitra.Typing
{
  [Record]
  public variant DependentStatement : Located
  {
    public override Location : Location         { get; }
    public          Path     : list[Identifier] { get; }

    | Assign
      {
        Expression : PExpr;
      }
    | Call
      {
        MethodName : Identifier;
        Arguments  : list[PExpr];
        NemerleEnv : GlobalEnv;
        Conditions : list[DependentStatementCondition];
      }
    | Add
      {
        Expression : PExpr;
      }

    public override ToString() : string
    {
      match (this)
      {
        | Assign(expr)                =>         $<#..$(Path; "."; _.GetText()) = $expr#>
        | Call(name, exprs, _, conds) => $<#..$conds..$(Path; "."; _.GetText()).$name(..$(exprs; ", "))#>
        | Add(expr)                   =>         $<#..$(Path; "."; _.GetText()) |= $expr#>
      }
    }
  }

  [Record]
  public variant DependentStatementCondition : Located
  {
    public override Location : Location { get; }

    | When
      {
        Expression : PExpr;
      }
    | Unless
      {
        Expression : PExpr;
      }

    public override ToString() : string
    {
      match (this)
      {
        | When(expr)   => $"when ($expr) "
        | Unless(expr) => $"unless ($expr) "
      }
    }
  }
}
