using Nemerle.Parser;

syntax module CalcGrammar
{
  option EmitDebugSources = true;

  using IncGrammar;
  using NumParser;

  token any = ['\u0000'..'\uFFFF'];
  syntax s : void = ' '*;

  [StartRule, Ast(expr)]
  syntax start : Ast = s expr !any;

  [StartRule, Ast()]
  syntax expr : Ast;

  [Ast(l, expr, r)] syntax rounds is expr = '('s expr ')'s;
  [Ast(l, expr, r)] syntax seq is expr = '{'s expr* '}'s;

  [Ast(num)]        syntax num is expr = number s;

  [Ast(op, expr)]   syntax neg is expr = '-'s expr : 100;

  [Ast(op, expr)]   syntax prefixDec is expr = "--"s expr : 200;
  [Ast(expr, op)]   syntax postfixDec is expr = expr : 200 "--"s;

  [Ast(l, op, r)]   syntax add is expr = expr : 10 '+'s expr : 10;
  [Ast(l, op, r)]   syntax sub is expr = expr : 10 '-'s expr : 10;
  [Ast(l, op, r)]   syntax mul is expr = expr : 20 '*'s expr : 20;
  [Ast(l, op, r)]   syntax div is expr = expr : 20 '/'s expr : 20;
  [Ast(l, op, r)]   syntax mod is expr = expr : 20 '%'s expr : 20;
  [Ast(l, op, r)]   syntax pow is expr = expr : 31 '^'s expr : 30;

  [Ast(cond, q, l, colon, r)]   syntax cond is expr = expr : 301 '?'s expr ':'s expr;

  [Ast(l, CoalescingOp, r)]     syntax coalescing  is expr = expr : 401 "??"s expr : 400;
}

syntax module IncGrammar
{
  using cp = CalcGrammar;

  [Ast(op, expr)] syntax plus is cp.expr = '+'cp.s cp.expr : 100;
  [Ast(op, expr)] syntax prefixInc is cp.expr = "++"cp.s cp.expr : 200;
  [Ast(expr, op)] syntax postfixInc is cp.expr = cp.expr : 200 "++"cp.s;
}
