using Nemerle.Parser;

syntax module CalcGrammar
{
  token field OpenBracket  = "(";
  token field CloseBracket = ")";
  token field OpenBrace    = "{";
  token field CloseBrace   = "}";
  token field Op           = "+", "++", "-", "--", "*", "/", "?", ":", "^", "%", "??";

  span class Number;
  span class Operator     = ['+', '-', '/', '*', '^', '?', ':', '%']+;
  span class OpenBracket  = "(" | "{";
  span class CloseBracket = ")" | "}";

  brackets "(", ")";
  brackets "{", "}";

  option EmitDebugSources = true;

  using IncGrammar;
  using NumParser;

  token any = ['\u0000'..'\uFFFF'];
  syntax s : void = ' '*;

  [StartRule, Ast(_)]
  syntax start : Ast = s expr !any;

  [StartRule, Ast()]
  syntax expr : Ast;

  [Ast(_, _, _)] syntax rounds is expr = '('s expr ')'s;
  [Ast(_, _, _)] syntax seq    is expr = '{'s expr* '}'s;

  [SpanClass(Number)]
  [Ast(_)]        syntax num is expr = number s;

  [Ast(_, _)]   syntax neg is expr = '-'s expr : 100;

  [Ast(_, _)]   syntax prefixDec is expr = "--"s expr : 200;
  [Ast(_, _)]   syntax postfixDec is expr = expr : 200 "--"s;

  // We can redefine names for fields in any time.
  token field Operator = "+", "++", "-", "--", "*", "/", "?", ":", "^", "%", "??";

  [Ast(_, _, _)]   syntax add is expr = expr : 10 '+'s expr : 10;
  [Ast(_, _, _)]   syntax sub is expr = expr : 10 '-'s expr : 10;
  [Ast(_, _, _)]   syntax mul is expr = expr : 20 '*'s expr : 20;
  [Ast(_, _, _)]   syntax div is expr = expr : 20 '/'s expr : 20;
  [Ast(_, _, _)]   syntax mod is expr = expr : 20 '%'s expr : 20;
  [Ast(_, _, _)]   syntax pow is expr = expr : 31 '^'s expr : 30;

  [Ast(_, _, _, _, _)]   syntax cond is expr = expr : 301 '?'s expr ':'s expr;

  [Ast(_, _, _)]     syntax coalescing  is expr = expr : 401 "??"s expr : 400;
}

syntax module IncGrammar
{
  using cp = CalcGrammar;
  using CalcGrammar;

  [Ast(_, _)] syntax plus is cp.expr = '+'cp.s cp.expr : 100;
  [Ast(_, _)] syntax prefixInc is cp.expr = "++"cp.s cp.expr : 200;
  [Ast(_, _)] syntax postfixInc is cp.expr = cp.expr : 200 "++"cp.s;
}
