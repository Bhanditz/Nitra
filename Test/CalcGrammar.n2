using Nemerle.Parser;

syntax module CalcGrammar
{
  token field OpenBrace    = "(";
  token field CloseBrace   = ")";
  token field OpenBrace    = "{";
  token field CloseBrace   = "}";
  token field Op           = "+", "++", "-", "--", "*", "/", "?", ":", "^", "%", "??";

  span class Number;
  span class Operator     = ['+', '-', '/', '*', '^', '?', ':', '%']+;
  span class OpenBrace  = "(" | "{";
  span class CloseBrace = ")" | "}";

  braces "(", ")";
  braces "{", "}";

  option EmitDebugSources = true;

  using IncGrammar;
  using NumParser;

  token any = ['\u0000'..'\uFFFF'];
  void s = ' '*;

  [StartRule]
  syntax start       = s expr !any;

  [StartRule]
  syntax expr;

  syntax rounds is expr = '('s expr ')'s;
  syntax seq    is expr = '{'s expr* '}'s;

  [SpanClass(Number)]
  syntax num is expr = number s;

  syntax neg is expr = '-'s expr { precedence 100; }

  syntax prefixDec is expr = "--"s expr { precedence 200; }
  syntax postfixDec is expr = expr "--"s { precedence 200; }

  // We can redefine names for fields in any time.
  token field Operator = "+", "++", "-", "--", "*", "/", "?", ":", "^", "%", "??";

  syntax add is expr = expr '+'s expr { precedence 10; }
  syntax sub is expr = expr '-'s expr { precedence 10; }
  syntax mul is expr = expr '*'s expr { precedence 20; }
  syntax div is expr = expr '/'s expr { precedence 20; }
  syntax mod is expr = expr '%'s expr { precedence 20; }
  syntax pow is expr = expr '^'s expr { precedence 30 right-associative; }

  [Ast(Condition, _, _, Colon, _)]
  syntax cond is expr = expr : 301 '?'s expr ':'s expr;

  syntax coalescing  is expr = expr : 401 "??"s expr : 400;
}

syntax module IncGrammar
{
  using cp = CalcGrammar;
  using CalcGrammar;

  syntax plus is cp.expr = '+'cp.s cp.expr : 100;
  syntax prefixInc is cp.expr = "++"cp.s cp.expr : 200;
  syntax postfixInc is cp.expr = cp.expr : 200 "++"cp.s;
}
