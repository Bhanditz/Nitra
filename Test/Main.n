using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Parser;
using Nemerle.Imperative;

using System;
using System.Collections.Generic;
using SCG = System.Collections.Generic;
using System.Console;
using System.Linq;

variant Ast : Located
{
  | PrefixOp  { name : string; v : Ast; }
  | PostfixOp { name : string; v : Ast; }
  | Num       { n : string; }
  | Rounds    { ast : Ast; }
  | Op        { l : Ast; name : string; r : Ast; }
}

[ParserGrammar(Options = EmitDebugSources,
  grammar
  {
    using IncParser;
    using NumParser;

    any = ['\u0000'..'\uFFFF'];
    s : void = ' '*;

    [StartRule]
    start : Ast = s expr !any;

    [StartRule]
    expr : Ast;

    [Ast(l, expr, r)] rounds is expr = '('s expr ')'s;

    [Ast(num)]        num is expr = number s;

    [Ast(op, expr)]   neg is expr = '-'s expr : 100;

    [Ast(op, expr)]   prefixDec is expr = "--"s expr : 200;
    [Ast(expr, op)]   postfixDec is expr = expr : 200 "--"s;

    [Ast(l, op, r)]   add is expr = expr : 10 '+'s expr : 10;
    [Ast(l, op, r)]   sub is expr = expr : 10 '-'s expr : 10;
    [Ast(l, op, r)]   mul is expr = expr : 20 '*'s expr : 20;
    [Ast(l, op, r)]   div is expr = expr : 20 '/'s expr : 20;
    [Ast(l, op, r)]   mod is expr = expr : 20 '%'s expr : 20;
    [Ast(l, op, r)]   pow is expr = expr : 31 '^'s expr : 30;
  }
)]
abstract class CalcParser
{
  rounds(_ : NToken, f : Ast, _ : NToken) : Ast { Ast.Rounds(f) }

  num(n : string) : Ast { Ast.Num(n) }

  neg(op : NToken, v : Ast) : Ast { Ast.PrefixOp(GetText(op), v) }

  prefixDec(op : NToken, v : Ast) : Ast { Ast.PrefixOp(GetText(op), v) }
  postfixDec(v : Ast, op : NToken) : Ast { Ast.PostfixOp(GetText(op), v) }

  add(l : Ast, op : NToken, r : Ast) : Ast { Ast.Op(l, GetText(op), r) }
  sub(l : Ast, op : NToken, r : Ast) : Ast { Ast.Op(l, GetText(op), r) }
  mul(l : Ast, op : NToken, r : Ast) : Ast { Ast.Op(l, GetText(op), r) }
  div(l : Ast, op : NToken, r : Ast) : Ast { Ast.Op(l, GetText(op), r) }
  mod(l : Ast, op : NToken, r : Ast) : Ast { Ast.Op(l, GetText(op), r) }
  pow(l : Ast, op : NToken, r : Ast) : Ast { Ast.Op(l, GetText(op), r) }
}

[ParserGrammar(Options = EmitDebugSources,
  grammar
  {
    using CalcParser;
    //using cp = CalcParser;

    plus is expr = '+'s expr : 100;
    prefixInc is expr = "++"s expr : 200;
    postfixInc is expr = expr : 200 "++"s;
  }
)]
abstract class IncParser
{
  plus(op : NToken, v : Ast) : Ast { Ast.PrefixOp(GetText(op), v) }
  prefixInc(op : NToken, v : Ast) : Ast { Ast.PrefixOp(GetText(op), v) }
  postfixInc(v : Ast, op : NToken) : Ast { Ast.PostfixOp(GetText(op), v) }
}

module Program
{
  Main() : void
  {
    match (IncParser.GrammarImpl())
    {
      | IncParser =>
      ()
    }
    def print(text)
    {
      def parser = CalcParser.GrammarImpl();
      match (parser.ParseStart(text))
      {
        | None      =>
          WriteLine($"Fail: \"$text\"");
          def (pos, rules) = parser.Parser.GetErrors();
          foreach (rule in rules)
            WriteLine($"  $pos $(rule.Grammar.Name).$(rule.Name)");
        | Some(ast) =>
          def printAst(ast)
          {
            match (ast : Ast)
            {
              | Rounds(ast)      => $"( $(printAst(ast)) )"
              | Num(num)         => $"$num"
              | PrefixOp(op, v)  => $"[ $op $(printAst(v)) ]"
              | PostfixOp(op, v) => $"[ $(printAst(v)) $op ]"
              | Op(l, op, r)     => $"[ $(printAst(l)) $op $(printAst(r)) ]"
            }
          }
          WriteLine($"Ok:   \"$text\" AST: $(printAst(ast))");
      }
    }
    print("1+2+3");
    print("1+2*3");
    print("1*2-3");
    print("1^2^3");
    print("1*2^3");
    print("1^2*3");
    print("-1+2+3");
    print("1+-2*3");
    print("1*2--3");
    print("1*(2--3)");
    print("1*2- -3");
    print("1*(2- -3)");
    print("1^2^-3");
    print("1*-2^3");
    print("1*-(2^3)");
    print("(1*-2)^3");
    print("-1^2*3");

    print("1-----3");
    print("1----3");
    print("1---3");
    print("1--3");
    print("1-3");
    print("1+2-----3");
    print("1+2----3");
    print("1+2---3");
    print("1+2--3");
    print("1+2-3");

    print("---3");
    print("----3");

    print("+3");
    print("++3");
    print("+++3");
    print("++++3");
    print("+++++3");
    print("1+3");
    print("1++3");
    print("1+++3");
    print("1++++3");
    print("1+++++3");
    print("1.23-4.3");
    print("a+3");

    _ = ReadKey();
  }
}