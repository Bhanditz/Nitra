using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra.ClientServer.Client;
using Nitra.ClientServer.Messages;
using System;
using System.Collections.Generic;
using System.Collections.ObjectModel;
using System.IO;
using System.Linq;

namespace Nitra.ViewModels
{
  public class SolutionVm : FolderVm[ProjectVm], IClientHost, IDisposable
  {
    mutable         _client        : NitraClient;
    mutable         _nextProjectId : int;
    mutable         _nextTestId    : int;
    public override Hint           : string { get { "Solution" }}

    public this(suite: SuiteVm, folder: string)
    {
      base(suite, folder);
      LoadProjects();
      when (IsSingleFileTest)
        IsExpanded = false;
    }
    
    public GetNextProjectId() : int 
    { 
      def res = _nextProjectId;
      _nextProjectId += 1;
      res
    }

    public GetNextTestId() : int 
    { 
      def res = _nextTestId;
      _nextTestId += 1;
      res
    }
    
    public IsSingleFileTest : bool { get { Children.Count == 1 && Children[0].Children.Count == 1 } }
    
    public override Name : string { get { Path.GetFileName(FullPath) + "  (Solution)" }}
    
    LoadProjects() : void
    {
      Directory
        .GetDirectories(FullPath)
        .OrderBy(x => x)
        .Select(ProjectVm(Suite, this, _, GetNextProjectId()))
        .Iter(Children.Add)
    }
    
    // IClientHost
    public Activate(client : NitraClient) : void
    {
      _client = client;
      client.Send(ServerMessage.SolutionStartLoading(FullPath));
      foreach(project in Children)
      {
        client.Send(ServerMessage.ProjectStartLoading(project.Id, project.FullPath));
        
        foreach(test in project.Children)
          client.Send(ServerMessage.FileLoaded(project.Id, test.FullPath, test.Id));
        
        client.Send(ServerMessage.ProjectLoaded(project.Id));
      }
      client.Send(ServerMessage.SolutionLoaded(FullPath))
    }

    // IClientHost
    public Deactivate() : void
    {
      Suite.Client.Send(ServerMessage.SolutionUnloaded());
      _client = null;
    }
    
    public Dispose() : void
    {
      foreach (project in Children)
        project.Dispose();
      Children.Clear();
      _client = null
    }
  }
}